;POCASH / POR  D-11
;
;
;	THIS PROGRAM CALCULATES THE CASH REQUIREMENTS FOR 30 60 AND 90 DAYS
;
;
RECORD APOPEN	
	.INCLUDE 'DEF:RD017A.DEF'

RECORD POLINE	
	.INCLUDE 'DEF:RD152A.DEF'

RECORD CUTOFS
	CUTOF0	,D6
	CUTOF1	,D6
	CUTOF2	,D6
	CUTOF3	,D6

RECORD TOTALS
	IN30DA	,D9
	IN60DA	,D9
	IN90DA	,D9
	AFTER	,D9

RECORD PEREND
	PEREN1	,D6
	PEREN2	,D6
	PEREN3	,D6

RECORD
	BRACKS	,A25,	']]]]]]]]]]]]]]]]]]]]]]]]]'
	COST	,D9
	DATE	,D6
	ENTRY	,A20
	INXCTL	,D1
	LOKCTL	,D1
	OWED	,D8
	READ	,D1,	0
	RECNUM	,D5
	SWITCH	,D1
	V	,D1
	WKDATE	,D6

PROC
	XCALL TERID (V)
	V = 1
	XCALL WATE (1,V)

	SWITCH = 1
	XCALL FILES (1,'I',017,SWITCH)
	IF (SWITCH.EQ.9) GO TO INUSE1

	SWITCH = 1
	XCALL FILES (2,'SI',152,SWITCH)
	IF (SWITCH.EQ.9) GO TO INUSE2
BEGIN,
	TOTALS =
	CUTOFS =
	XCALL OUTPT (1,1,3,'CASH REQUIREMENTS INQUIRY',V)
	XCALL OUTPT (06,20,0,'PLEASE ENTER:',V)
	XCALL OUTPT (08,25,0,'AGING DATE',V)
	XCALL OUTPT (12,21,0,'PERIOD       CASH REQUIRED   PERIOD ENDS',V)

	XCALL INPUT (08,37,06,00,'DE',ENTRY,INXCTL,V)
	GO TO (EXIT), INXCTL - 1
	DATE = ENTRY
	IF (DATE.EQ.0) XCALL RDATE (DATE)
	XCALL DSPLY (6,8,37,DATE,2,V)

	CUTOF0(1,2) = DATE(5,6)
	CUTOF0(3,6) = DATE(1,4)
	XCALL GDATE (DATE,30,PEREN1)
	XCALL GDATE (DATE,60,PEREN2)
	XCALL GDATE (DATE,90,PEREN3)

	CUTOF1(1,2) = PEREN1(5,6)
	CUTOF1(3,6) = PEREN1(1,4)

	CUTOF2(1,2) = PEREN2(5,6)
	CUTOF2(3,6) = PEREN2(1,4)

	CUTOF3(1,2) = PEREN3(5,6)
	CUTOF3(3,6) = PEREN3(1,4)

	RECNUM = 1
APLOOP,
	WKDATE =
	INCR RECNUM
	XCALL IO (1,APOPEN,RECNUM,READ,LOKCTL)
	IF (ADESC.EQ.BRACKS) GO TO STRLIN
	WKDATE(1,2) = ADUEDT(5,6)
	WKDATE(3,6) = ADUEDT(1,4)
	IF (WKDATE.LT.CUTOF0) GO TO APLOOP
	OWED = AINVAM - APAID - ADSTKN
	IF (WKDATE.GE.CUTOF0.AND.WKDATE.LE.CUTOF1) IN30DA = IN30DA + OWED
	IF (WKDATE.GT.CUTOF1.AND.WKDATE.LE.CUTOF2) IN60DA = IN60DA + OWED
	IF (WKDATE.GT.CUTOF2.AND.WKDATE.LE.CUTOF3) IN90DA = IN90DA + OWED
	IF (WKDATE.GT.CUTOF3) AFTER = AFTER + OWED
	GO TO APLOOP

STRLIN,
LNLOOP,
	WKDATE =
	LOKCTL = 1
	XCALL IOS (2,POLINE,READ,LOKCTL)
	IF (LOKCTL.NE.0) GOTO SHOW
	WKDATE(1,2) = LDTEPM(5,6)
	WKDATE(3,6) = LDTEPM(1,4)
	IF (LDTEPM.EQ.0) WKDATE(1,2) = LDTERQ(5,6)
	IF (LDTEPM.EQ.0) WKDATE(3,6) = LDTERQ(1,4)

	IF (WKDATE.LT.CUTOF0) GO TO LNLOOP
	IF (LRLNUM.EQ.0.AND.LBLKTP.NE.' ') GO TO LNLOOP   ;EXCLUDE BLANKET P/O S

	COST = ((LQTYOR - LQTYIV)*LACCST)#2
	IF (LACCST.EQ.0) COST = ((LQTYOR - LQTYIV)*LEXCST)#2

	IF (WKDATE.GE.CUTOF0.AND.WKDATE.LE.CUTOF1) IN30DA = IN30DA + COST
	IF (WKDATE.GT.CUTOF1.AND.WKDATE.LE.CUTOF2) IN60DA = IN60DA + COST
	IF (WKDATE.GT.CUTOF2.AND.WKDATE.LE.CUTOF3) IN90DA = IN90DA + COST
	IF (WKDATE.GT.CUTOF3) AFTER = AFTER + COST
	GO TO LNLOOP
SHOW,
	XCALL OUTPT (14,18,0,'0 TO 30 DAYS',V)
	ENTRY(1,13) = IN30DA,'Z,ZZZ,ZXX.XX-'
	XCALL OUTPT (14,34,0,ENTRY(1,13),V)
	ENTRY(1,8) = PEREN1,'XX/XX/XX'
	XCALL OUTPT (14,52,0,ENTRY(1,8),V)

	XCALL OUTPT (16,17,0,'31 TO 60 DAYS',V)
	ENTRY(1,13) = IN60DA,'Z,ZZZ,ZXX.XX-'
	XCALL OUTPT (16,34,0,ENTRY(1,13),V)
	ENTRY(1,8) = PEREN2,'XX/XX/XX'
	XCALL OUTPT (16,52,0,ENTRY(1,8),V)

	XCALL OUTPT (18,17,0,'61 TO 90 DAYS',V)
	ENTRY(1,13) = IN90DA,'Z,ZZZ,ZXX.XX-'
	XCALL OUTPT (18,34,0,ENTRY(1,13),V)
	ENTRY(1,8) = PEREN3,'XX/XX/XX'
	XCALL OUTPT (18,52,0,ENTRY(1,8),V)

	XCALL OUTPT (20,17,0,'AFTER 90 DAYS',V)
	ENTRY(1,13) = AFTER,'Z,ZZZ,ZXX.XX-'
	XCALL OUTPT (20,34,0,ENTRY(1,13),V)

	XCALL MESAG (' ',2)
	GO TO BEGIN

;**********************************************************************

EXIT,
	SWITCH = 4
	XCALL FILES (1,'I',017,SWITCH)
INUSE2,
	SWITCH = 4
	XCALL FILES (2,'SI',152,SWITCH)
INUSE1,
	SWITCH = 1
	XCALL PGCHN ('PO:POMENU',SWITCH)


END

