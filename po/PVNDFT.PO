;  PVNDFT / POR - D11
;
;
;		PROGRAM TO ENTER DEFAULT VALUES FOR THE ENTRY OF
;		PURCHASING DATA IN THE VENDOR MASTER FILE
;

RECORD RD011B
	.INCLUDE 'DEF:RD011B.DEF'

RECORD RD153B	
	.INCLUDE 'DEF:RD153B.DEF'

RECORD RD153A	
	.INCLUDE 'DEF:RD153A.DEF'

RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2

RECORD TBLKEY
	KTBLNO	,D2
	KCODNO	,A2

RECORD KAY
	BLANKS	,A30
	BSMID	,D5
	CNGCTL	,D1
	ENTRY	,A2
	INXCTL	,D1
	LOKCTL	,D1
	KSPVIA	,A15
	KTERMS	,A10
	KFOB	,A9
	KSHTO	,A30
	KCOLPP	,A1
	NOABRT	,D1,	1
	READ	,D1,	0
	WRITE	,D1,	1
	SELECT	,D1
	SRCCTL	,D1
	SWITCH	,D1
	WHATNO	,D1
	V	,D1



PROC

	XCALL TERID (V)

;;;	SWITCH = 3
;;;	XCALL FILES (1,'U',11,SWITCH)	;PROTECT
;;;	IF (SWITCH.EQ.9) GO TO EXIT
	SWITCH = 5
	XCALL FILES (1,'U',11,SWITCH)	;OPEN
	IF (SWITCH.EQ.9) GO TO CLOSE1

	SWITCH = 1
	XCALL FILES (2,'I',153,SWITCH)	;PORTBL FILE
	IF (SWITCH.EQ.9) GO TO CLOSE1


	LOKCTL = NOABRT
	XCALL IO (2,RD153B,1,READ,LOKCTL)

;**********************************************************************

DFTMNU,
	XCALL OUTPT (2,1,2,'DEFAULT ENTRY',1)
	XCALL OUTPT (5,20,0,'PLEASE SELECT APPLICATION',V)
	XCALL OUTPT (6,25,0,'1. ADD DEFAULTS',V)
	XCALL OUTPT (7,25,0,'2. CHANGE/INQUIRE DEFAULTS',V)

	CTL = '05,47,01,00,#E'
	CALL INPUT
	GO TO (DFTMNU,CLOSE2),INXCTL
	SELECT = ENTRY
	IF (SELECT.EQ.0.OR.SELECT.GT.2) GO TO DFTMNU

;**********************************************************************
BEGIN,
	CNGCTL =
	UNLOCK 1

	LOKCTL = NOABRT
	XCALL IO (1,RD011B,1,READ,LOKCTL)
	UNLOCK 1

	IF(SELECT.EQ.1) XCALL OUTPT (4,13,2,
&	'PLEASE ENTER DEFAULT VALUES FOR THE FOLLOWING FIELDS:',V)
	IF (SELECT.EQ.2) XCALL OUTPT (2,1,2,'CHANGE/INQUIRE DEFAULTS',1)
	IF (SELECT.EQ.2) XCALL OUTPT (4,20,0,'DEFAULT VALUES:',V)

	XCALL OUTPT ( 5,25,0,'1. SHIP VIA',V)
	XCALL OUTPT ( 6,25,0,'2. TERMS',V)
	XCALL OUTPT ( 7,25,0,'3. FOB',V)
	XCALL OUTPT ( 8,25,0,'4. USER CODE',V)
	XCALL OUTPT ( 9,25,0,'5. SHIP-TO',V)
	XCALL OUTPT (10,25,0,'6. COLL/PPD',V)
	GO TO (ONE,VALUES), SELECT
ONE,
	CTL = '05,41,01,01,AE'
	CALL INPUT
	GO TO (BEGIN,DFTMNU),INXCTL
	KTBLNO = '03'
	KCODNO = ENTRY
	CALL TBLFND
	IF (SRCCTL) GO TO ONE
	DFSVIA = ENTRY
	KSPVIA = TDESC
	XCALL OUTPT (5,44,0,'=',V)
	XCALL OUTPT (5,47,0,KSPVIA,V)
	GO TO (ANYCN),CNGCTL
TWO,
	CTL = '06,41,01,00,A '
	CALL INPUT
	GO TO (BEGIN),INXCTL
	KTBLNO = '04'
	KCODNO = ENTRY
	CALL TBLFND
	IF (SRCCTL) GO TO TWO
	DFTRMS= ENTRY
	XCALL OUTPT (6,44,0,'=',V)
	XCALL OUTPT (6,47,0,TDESC,V)
	GO TO (ANYCN),CNGCTL

THREE,
	CTL = '07,41,01,00,A '
	CALL INPUT
	GO TO (BEGIN),INXCTL
	KTBLNO = '05'
	KCODNO = ENTRY
	CALL TBLFND
	IF (SRCCTL) GO TO THREE
	DFTFOB= ENTRY
	XCALL OUTPT (7,44,0,'=',V)
	XCALL OUTPT (7,47,0,TDESC,V)
	GO TO (ANYCN),CNGCTL
FOUR,
	CTL = '08,41,02,01,A '
	CALL INPUT
	GO TO (BEGIN),INXCTL
	DFUSRC = ENTRY
	GO TO (ANYCN),CNGCTL

FIVE,
	CTL = '09,41,02,00,A '
	CALL INPUT
	GO TO (BEGIN),INXCTL
	KTBLNO = '01'
	KCODNO = ENTRY
	CALL TBLFND
	IF (SRCCTL) GO TO FIVE
	DFSHTO = ENTRY
	XCALL OUTPT (9,44,0,'=',V)
	XCALL OUTPT (9,47,0,TDESC,V)
	GO TO (ANYCN),CNGCTL
SIX,
	CTL = '10,41,01,01,A '
	CALL INPUT
	GO TO (BEGIN),INXCTL
	DFCOLP = ENTRY
	IF(DFCOLP.NE.'C' .AND. DFCOLP.NE.'P') GOTO SIX
	GO TO (ANYCN),CNGCTL
;**********************************************************************
ANYCN,
	XCALL ANYCN (CNGCTL,WHATNO)
	GOTO (PROCES,CNGBR),CNGCTL+1
CNGBR,
	GO TO (ONE,TWO,THREE,FOUR,FIVE,SIX),WHATNO
	GO TO ANYCN

;**********************************************************************
PROCES,
WRAPUP,
	LOKCTL = NOABRT
	XCALL IO (1,RD011B,1,WRITE,LOKCTL)
	GO TO DFTMNU

CLOSE2,
	XCALL FILES (2,'I',153,4)
CLOSE1,
	XCALL FILES (1,'U',11,4)
EXIT,
	XCALL PGCHN ('PO:PVNMNT',1)

;**********************************************************************
VALUES,
	IF (DFSVIA.EQ.BLANKS.AND.DFTRMS.EQ.BLANKS.AND.DFTFOB.EQ.
&		BLANKS) GO TO MSG1
	XCALL OUTPT (5,41,0,DFSVIA,V)
	XCALL OUTPT (6,41,0,DFTRMS,V)
	XCALL OUTPT (7,41,0,DFTFOB,V)
	XCALL OUTPT (8,41,0,DFUSRC,V)
	XCALL OUTPT (9,41,0,DFSHTO,V)
	XCALL OUTPT(10,41,0,DFCOLP,V)

	KTBLNO =  03
	KCODNO =  DFSVIA
	CALL TBLFND
	KSPVIA = TDESC

	KTBLNO = 04
	KCODNO = DFTRMS
	CALL TBLFND
	KTERMS = TDESC

	KTBLNO = 05
	KCODNO = DFTFOB
	CALL TBLFND
	KFOB = TDESC

	KTBLNO = 01
	KCODNO = DFSHTO
	CALL TBLFND
	KSHTO = TDESC

	KCOLPP = DFCOLP



	XCALL OUTPT (5,44,0,'=',V)
	XCALL OUTPT (6,44,0,'=',V)
	XCALL OUTPT (7,44,0,'=',V)
	XCALL OUTPT (8,44,0,'=',V)
	XCALL OUTPT (9,44,0,'=',V)
	XCALL OUTPT(10,44,0,'=',V)

	XCALL OUTPT (5,47,0,KSPVIA,V)
	XCALL OUTPT (6,47,0,KTERMS,V)
	XCALL OUTPT (7,47,0,KFOB,V)
	XCALL OUTPT (8,47,0,DFUSRC,V)
	XCALL OUTPT (9,47,0,KSHTO,V)
	XCALL OUTPT(10,47,0,KCOLPP,V)

	GO TO ANYCN

;**********************************************************************
MSG1,
	XCALL MESAG ('CODE DEFAULTS HAVE NOT YET BEEN ADDED',2)
	GO TO DFTMNU
;**********************************************************************
TBLFND,
	SRCCTL = 2
	XCALL SERCH (2,RD153A,TBLKEY,1,4,ORG153,BSMID,SRCCTL,4,6,11,0,0,0,0)
	IF (SRCCTL.EQ.0) RETURN
	XCALL MESAG ('CODE IS NOT ON FILE',2)
	IF (SELECT.EQ.2) RD153A =
	RETURN

;**********************************************************************
INPUT,
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,V)
	RETURN

;**********************************************************************

END

