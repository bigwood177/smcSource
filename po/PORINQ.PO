;PORINQ.PO
;
;
	.DEFINE POOLSIZE	,25000
	.DEFINE WNDCHNL		,15
	.DEFINE MAXWINS		,10
	.INCLUDE 'WND:WINDOWS.DEF'


RECORD PORHDR		
	.INCLUDE 'DEF:RD151A.DEF'

RECORD	PORLIN
	.INCLUDE 'DEF:RD152A.DEF'

RECORD	VENMAS
	.INCLUDE 'DEF:RD011A.DEF'

RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'

RECORD TBLCTL	
	.INCLUDE 'DEF:RD153B.DEF'

RECORD TABLE
	TNO	,D2
	CDE	,A2
	SQ	,D1
	DSC	,A30

RECORD PBLKEY
	TKEYNO	,D2
	TKEYCD	,A2
	TKEYSQ	,D1

RECORD	SVLINE
	SVL_CODE	,A1
		,A3,	' = '
	SVL_SCAC	,A4
		,A1
	SVL_NAME	,A15

RECORD	POP
	.INCLUDE 'DEF:POP1.DEF'

RECORD	POPA
	.INCLUDE 'DEF:POP1A.DEF'

RECORD	POPAR
	.INCLUDE 'DEF:poPOP.DEF'

RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,D2

RECORD	WVARS
	W_ID	,D4
	WND_1	,D4

RECORD	WN_NAME
		,A6,	'PORINQ'

RECORD	FUNKEY
	.INCLUDE 'DEF:FUNKEY.DEF'

RECORD	CHANNEL
	CHN011	,D3
	CHN021	,D3
	CHN022	,D3
	CHN153	,D3
	CHN182	,D3



RECORD	VARS
	ITEM	,A15
	POAMT	,D10
	RKEY	,A4
	XDATE	,D8
	BSMID	,D5
	SRCCTL	,D1
	SAVRFA	,A6
	GOT_PO	,D1	;1 = PO FOUND
	START_NAME	,A25
	ALPHA	,A5
	TSTAT	,D1
	F_KEY	,D3
	TODAY	,D8
	TODAA	,A10		;FORMAT XX/XX/XXXX
	BLANKS	,A30
	PONUM	,D8
	VENDR	,A4
	BUYER	,A2
	JOB	,D6
	PODTE	,D8
	I	,D6
	AI	,D6
	nbr	,d3
	OPNOK	,D1
	LOKCTL	,D1
	ENTRY	,A30
	INXCTL	,D1
	CNGCTL	,D1
	WHATNO	,D2
	READ	,D1,0
	WRITE	,D1,1
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	V = 1
xcall outpt(1,1,2,' ',1)


	CALL OPENS
	IF (.NOT. OPNOK) GOTO ENDOFF
	CALL INIT_WINDOW

BEGIN,
	W_ID = WND_1

	CNGCTL =		
	XCALL W_DISP(WND_1,WD_CLEAR)
	XCALL W_DISP(WND_1,WD_POS,1,1,'PURCHASE ORDER INQUIRY')
	XCALL W_DISP(WND_1,WD_POS,4,2,'1. P/O NO')
	XCALL W_DISP(WND_1,WD_POS,12,2,'2. VENDOR NO')
	XCALL W_DISP(WND_1,WD_POS,13,2,'   CONTACT')
	XCALL W_DISP(WND_1,WD_POS,14,2,'3. REQ DATE')
	XCALL W_DISP(WND_1,WD_POS,16,2,'   SHIP TO')
	XCALL W_DISP(WND_1,WD_POS,18,2,'4. BUYER')
	XCALL W_DISP(WND_1,WD_POS,20,2,'   SHIP-VIA')
	XCALL W_DISP(WND_1,WD_POS,22,2,'   FAX PO? (N)')
	XCALL W_DISP(WND_1,WD_POS,4,34,'   COL/PPD')
	XCALL W_DISP(WND_1,WD_POS,6,33,'   TERMS')
	XCALL W_DISP(WND_1,WD_POS,7,33,'    FOB')
	XCALL W_DISP(WND_1,WD_POS,8,33,' 5. JOB')
	XCALL W_DISP(WND_1,WD_POS,10,33,'    CONFIRM (Y)')
	XCALL W_DISP(WND_1,WD_POS,04,54,'    PRINT PRICE (Y)')

PONUM,
	CTL = '04,20,06,00,#E'
	CALL INPUT
	GOTO (BEGIN,ENDOFF), INXCTL
	PONUM(1,6) = ENTRY(1,6)
	PONUM(7,8) = 00
	IF (PONUM .NE. 0) 
		BEGIN
		CALL GET_THIS_PO
		IF (GOT_PO) 
			BEGIN
			CALL DISP_PO
			GOTO BEGIN
			END
		END
	GOTO (ANYCNG),CNGCTL

VENDR,
	XCALL W_DISP(WND_1,WD_POS,24,2,WD_CLR,WDC_EOL,'<F1> = ALPHA LOOK-UP')
	CTL = '12,20,04,00,A '
	XCALL WINPT (W_ID,ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,F_KEY)
	GO TO (BEGIN), INXCTL
	USING F_KEY SELECT
	(F_01),	BEGIN
		CALL ALPHA_LOOKUP
		XCALL W_DISP(WND_1,WD_POS,24,1,WD_CLR,WDC_EOL)
		GOTO (BEGIN,BEGIN),INXCTL
		END
	ENDUSING
	XCALL W_DISP(WND_1,WD_POS,24,1,WD_CLR,WDC_EOL)

	IF (ENTRY.EQ.BLANKS) GO TO PODTE
	VENDR = ENTRY(1,4)
	XCALL FRMAT (VENDR,4)
	XCALL W_DISP(WND_1,WD_POS,12,20,VENDR)

	XCALL ISIO (CHN011, VENMAS, VENDR, READ, LOKCTL)
	IF (LOKCTL .NE. 0)
		BEGIN
		XCALL WNMSG (WND_1,24,'VENDOR NOT ON FILE',1)
		GOTO BEGIN
		END

	GOTO (ANYCNG),CNGCTL

PODTE,
	CTL = '14,20,08,00,D '
	PODTE =
	CALL INPUT
	GO TO (BEGIN), INXCTL
	PODTE = ENTRY
	IF (PODTE.EQ.0) 
		BEGIN
		PODTE = TODAY
		XCALL W_DISP(WND_1,WD_POS,ROW,COL,TODAA)
		END
	GOTO (ANYCNG),CNGCTL
BUYER,
	CTL = '18,20,02,00,A '
	CALL INPUT
	GO TO (BEGIN), INXCTL
	XCALL W_DISP(WND_1,WD_POS,18,20,BUYER)
	BUYER = ENTRY(1,2)
	GO TO (ANYCNG), CNGCTL

JOB,
	CTL = '08,49,06,00,# '
	CALL INPUT
	GOTO (BEGIN), INXCTL
	JOB = ENTRY(1,6)
	GOTO (ANYCNG), CNGCTL
ANYCNG,
	XCALL WANCN (W_ID,24,CNGCTL,WHATNO)
	GOTO (PROCES, CNGBR), CNGCTL+1
CNGBR,
	GOTO (PONUM, VENDR, PODTE, BUYER, JOB), WHATNO
	GOTO ANYCNG

PROCES,
	IF (PONUM .GT. 0) 
		BEGIN
		CALL GET_THIS_PO
		IF (GOT_PO) 
			BEGIN
			CALL DISP_PO
			GOTO BEGIN
			END
		END

	CALL GET_MATCHING_POS
	IF (NUMARA .LE. 0) GOTO BEGIN

	XCALL TTSTS(TSTAT)
	IF (TSTAT) XCALL W_DISP(w_id,WD_READS,alpha)


	DLINE = 'PO #    JOB #  BUYER  VEND  ITEM #           TOTAL AMT'
POP_PO,
	XCALL POP1(POP)
	USING P_ACTION SELECT
	(0),	GOTO BEGIN		;NOTHING SELECTED
	(1,4),	BEGIN			;SELECTED
		SAVRFA = PARRFA(PI)
		READ(CHN021, PORHDR,RFA:SAVRFA)
		CALL DISP_PO
		END
	(2),	GOTO BEGIN		;INSERT
	(),	GOTO BEGIN
	ENDUSING

;PO #    JOB #  BUYER  VEND  ITEM #           TOTAL AMT
;ZZZZZZ ZZZZZX    AA   AAAA  AAAAAAAAAAAAAAA ZZZ,ZZX.XX
;12345678901234567890123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5         6         7

	GOTO POP_PO

ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('PO:POMENU',1)
	STOP

;================================================
GET_THIS_PO,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	
	LOKCTL = 1
	XCALL ISIO (CHN021, PORHDR, PONUM, READ, LOKCTL)
	IF (LOKCTL .EQ. 0)
	THEN GOT_PO = 1
	ELSE GOT_PO = 0

	RETURN
;------------------------------------------------
DISP_PO,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL W_DISP(WND_1,WD_POS,12,20,HVENDR)

	LOKCTL = 1
	XCALL ISIO (CHN011, VENMAS, HVENDR, READ, LOKCTL)

	IF (LOKCTL.EQ.0) GO TO FND
	NAME = 'VENDOR NOT ON FILE'
	GO TO NOTFND
FND,
NOTFND,
	XCALL W_DISP(WND_1,WD_POS,12,26,NAME)
	XDATE(1,4) = HPODTE(5,8)
	XDATE(5,8) = HPODTE(1,4)
	ENTRY(1,10) = XDATE,	'XX/XX/XXXX'
	XCALL W_DISP(WND_1,WD_POS,13,20,HCONTC)

	XCALL W_DISP(WND_1,WD_POS,14,20,ENTRY(1,10) )
	XCALL W_DISP(WND_1,WD_POS,16,20,HSHPTO)
	XCALL W_DISP(WND_1,WD_POS,18,20,HBUYER)
	CLEAR PBLKEY
	TKEYNO = 2
	TKEYCD = HBUYER
	CALL GTBL2
	XCALL W_DISP(WND_1,WD_POS,18,24,DSC)

	XCALL W_DISP(WND_1,WD_POS,20,20,HSHPVI)
	CLEAR COPTBL
	TBLCOD = 'P1'
	P1_CODE = HSHPVI
	XCALL ISIO (CHN182, COPTBL, TBL_KEY, READ, LOKCTL)
	IF (LOKCTL .NE. 0)
	THEN	XCALL WNMSG (WND_1, 24, 'CODE NOT IN "P1" TABLE',1)
	ELSE	BEGIN
		SVL_SCAC = P1_SCAC
		CLEAR COPTBL
		TBLCOD = 'SC'
		SC_SCAC = SVL_SCAC
		XCALL ISIO (CHN182, COPTBL, TBL_KEY, READ, LOKCTL)
		IF (LOKCTL.NE.0) 
		THEN	XCALL WNMSG (WND_1, 24, 'SCAC NOT IN "SC" TABLE',1)
		ELSE	XCALL W_DISP(WND_1,WD_POS,20,24,SC_NAME)
		END

	XCALL W_DISP(WND_1,WD_POS,22,20,HACKNW)
	XCALL W_DISP(WND_1,WD_POS,04,49,HCOLPP)
	XCALL W_DISP(WND_1,WD_POS,06,49,HTERMS)
	CLEAR PBLKEY
	TKEYNO = 4
	TKEYCD = HTERMS
	CALL GTBL2
	XCALL W_DISP(WND_1,WD_POS,06,51,DSC)


	XCALL W_DISP(WND_1,WD_POS,07,49,HFOB)
	CLEAR PBLKEY
	TKEYNO = 5
	TKEYCD = HFOB
	CALL GTBL2
	XCALL W_DISP(WND_1,WD_POS,07,51,DSC)

	ENTRY(1,6) = HJOB,	'ZZZZZZ' [LEFT]
	XCALL W_DISP(WND_1,WD_POS,08,49,ENTRY(1,6))

	XCALL W_DISP(WND_1,WD_POS,10,49,HCONFR)
	XCALL W_DISP(WND_1,WD_POS,04,74,HPRTPR)
;;;	XCALL WNMSG (WND_1, 24, 'THIS IS A PO',1)
	XCALL W_UPDT

	CALL GET_LINES

	IF (ANUMARA .LE. 0) 
		BEGIN
		XCALL WNMSG (W_ID, 24, 'NO LINE ITEMS',1)
		RETURN
		END

	XCALL TTSTS(TSTAT)
	IF (TSTAT) XCALL W_DISP(w_id,WD_READS,alpha)

	ADLINE = 'ITEM #            DESCRIPTION                UM   Q-ORD  Q-RCVD       COST'
	XCALL POP1(POPA)


;ITEM #          DESCRIPTION                  UM   Q-ORD  Q-RCVD        COST
;AAAAAAAAAAAAAAA AAAAAAAAAAAAAAAAAAAAAAAAAAAA AA ZZZ,ZZX ZZZ,ZZX ZZ,ZZX.XXXX
;12345678901234567890123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5         6         7
	RETURN

;------------------------------------------------------
GTBL2,
	BSMID = 1
	SRCCTL = 2
	XCALL SERCH (CHN153,TABLE,PBLKEY,1,5,ORG153,BSMID,SRCCTL,4,6,10,0,0,0,0)
	RETURN
;------------------------------------------------------



GET_MATCHING_POS,	;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR I, NUMARA

	IF (VENDR .NE. BLANKS)
		BEGIN
		RKEY = 'VEN'
		FIND (CHN021, PORHDR, VENDR, KRF:1) [ERR=GMP_LOOP]
		GOTO GMP_LOOP
		END
	IF (BUYER .NE. BLANKS)
		BEGIN
		RKEY = 'BUY'
		FIND (CHN021, PORHDR, BUYER, KRF:2) [ERR=GMP_LOOP]
		GOTO GMP_LOOP
		END
	IF (JOB .NE. 0)
		BEGIN
		RKEY = 'JOB'
		FIND (CHN021, PORHDR, JOB, KRF:3) [ERR=GMP_LOOP]
		GOTO GMP_LOOP
		END
	IF (PODTE .NE. 0)
		BEGIN
		RKEY = 'DATE'
		FIND (CHN021, PORHDR, PODTE, KRF:4) [ERR=GMP_LOOP]
		GOTO GMP_LOOP
		END
GMP_LOOP,
	XCALL IOS (CHN021, PORHDR, READ, LOKCTL)
	IF (LOKCTL .NE. 0) GOTO GMP_EOF
	USING RKEY SELECT
	('VEN'),	IF (HVENDR .NE. VENDR) GOTO GMP_EOF
	('BUY'),	IF (HBUYER .NE. BUYER) GOTO GMP_EOF
	('JOB'),	IF (HJOB .NE. JOB) GOTO GMP_EOF
	('DATE'),	IF (HPODTE .NE. PODTE) GOTO GMP_EOF
	ENDUSING

	IF (VENDR.NE.BLANKS .AND. HVENDR.NE.VENDR) GOTO GMP_LOOP
	IF (JOB.GT.0 .AND. HJOB.NE.JOB) GOTO GMP_LOOP
	IF (BUYER.NE.BLANKS .AND. HBUYER.NE.BUYER) GOTO GMP_LOOP
	IF (PODTE.GT.0 .AND. HPODTE.NE.PODTE) GOTO GMP_LOOP

	XCALL GETRFA (CHN021, SAVRFA)

	CALL GET_LINES

	CLEAR DLINE
	DLINE(1,6) = HPONUM, 'ZZZZZX'
	DLINE(8,13) = HJOB,	'ZZZZZX'
	DLINE(18,19) = HBUYER
	DLINE(23,26) = HVENDR
	DLINE(29,43) = ITEM 
	DLINE(45,54) = POAMT#2,	'ZZZ,ZZX.XX'
	INCR I
	PARRY(I) = DLINE
	PARRFA(I) = SAVRFA
	IF (I .LT. MAXARA) GOTO GMP_LOOP

GMP_EOF,
	NUMARA = I
	RETURN
;------------------------------------------------

GET_LINES,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR AI
	CLEAR POAMT, ITEM
	FIND (CHN022, PORLIN, HPONUM) [ERR=GL_LOOP]
GL_LOOP,
	XCALL IOS (CHN022, PORLIN, READ, LOKCTL)
	IF (LOKCTL .NE. 0) GOTO GL_EOF
	IF (LPONUM .NE. HPONUM) GOTO GL_EOF
	IF (ITEM .EQ. BLANKS) ITEM = LITMNO
	POAMT = POAMT + (LQTYOR*LEXCST)

	CLEAR ADLINE
	ADLINE (1,15) = LITMNO
	ADLINE (17,42) = LITMDS
	ADLINE (44,45) = LITMUM
	ADLINE (47,53) = LQTYOR,	'ZZZ,ZZX'
	ADLINE (55,61) = LQTYRC,	'ZZZ,ZZX'
	ADLINE (63,73) = LEXCST,	'ZZ,ZZX.XXXX'
	INCR AI
	IF (AI .LE. AMAXARA) APARRY(AI) = ADLINE
	GOTO GL_LOOP
GL_EOF,
	ANUMARA = AI
	RETURN
;---------------------------------------------------

ALPHA_LOOKUP,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		;;; ALPHA VENDOR LOOK-UP
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL W_DISP(W_ID,WD_POS,24,2,WD_CLR,WDC_EOL,'VEND NAME:')
	CTL = '24,13,25,00,AE'
	CALL INPUT
	GOTO (ALPHA_RETURN,ALPHA_RETURN),INXCTL
	START_NAME = ENTRY(1,25)
	FIND (CHN011,VENMAS,START_NAME,KRF:1) [ERR=MORE_NAMES]
	CLEAR LOKCTL
MORE_NAMES,
	CALL GET_NAMES
	XCALL TTSTS(TSTAT)
	IF (TSTAT) XCALL W_DISP(W_ID,WD_READS,ENTRY)
	IF (PNUMARA .GT. 0)
	THEN	BEGIN
		PDLINE = '  VEND #  NAME'
		XCALL poPOP (POPAR)
		END
	ELSE	BEGIN
		XCALL WNMSG(W_ID,24,'NO MORE NAMES',1)
		INXCTL = 2
		RETURN
		END

	CASE PP_ACTION OF
	BEGINCASE
	0:	INXCTL = 2		;<END>
	1:	BEGIN
		PDLINE = PPARRY(PPI)
		ENTRY = PDLINE(1,4)
		CLEAR INXCTL		
		END
	4:	GOTO MORE_NAMES
	5:	BEGIN
		CALL PG_UP
		GOTO MORE_NAMES
		END
	6:	GOTO MORE_NAMES
	ENDCASE
ALPHA_RETURN,
	RETURN
;------------------------------------------------

GET_NAMES,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		;;; GET NEXT 20 NAMES FROM VENALP
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR I
	XCALL IOS (CHN011,VENMAS,READ,LOKCTL)
	WHILE (LOKCTL.EQ.0 .AND. I.LT.PMAXARA)
		BEGIN	
		INCR I
		XCALL GETRFA (CHN011,PPARRFA(I))
		CLEAR PDLINE
		PDLINE(1,4) = VENNO
		PDLINE(8,32) = NAME
		PPARRY(I) = PDLINE
	
		PDLINE (1,25) = ADD1
		PDLINE (26,46) = ADD2
		PDLINE (47,61) = CITY
		PDLINE (63,64) = STATE
		PDLINE (66,75) = ZIP
		PP_ADD(I) = PDLINE
		XCALL IOS (CHN011,VENMAS,READ,LOKCTL)
		END

	PNUMARA = I
	IF (I.EQ.0 .OR. I.EQ.PMAXARA) RETURN
	
	FOR I FROM PNUMARA+1 THRU PMAXARA	CLEAR PPARRY(I)
	RETURN
;------------------------------------------------
PG_UP,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; prev page
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	XCALL POSRFA (CHN011, PPARRFA(1)) 	;1ST RECORD
	FOR I FROM 1 THRU PMAXARA
		BEGIN
		READS(CHN011,VENMAS,PU_EOF,REVERSE) [ERR=PU_EOF]
		END

PU_EOF,
	RETURN
;------------------------------------------------

INPUT,		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL WINPT (W_ID,ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL)
	RETURN
;----------------------------------------------

OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK
	CLEAR CHN011, CHN021, CHN022, CHN153
	
	SWITCH = 5
	XCALL FILES (1,'SI',11,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN011 = 1

	SWITCH = 5
;;;	XCALL FILES (21,'SI',21,SWITCH)
;;;	IF (SWITCH .EQ. 9) RETURN
	OPEN (21,SI,'TST:POHHDR.TSM')
	CHN021 = 21

	SWITCH = 5
;;;	XCALL FILES (22,'SI',22,SWITCH)
;;;	IF (SWITCH .EQ. 9) RETURN
	OPEN (22,SI,'TST:POHLIN.TSM')
	CHN022 = 22

	SWITCH = 5
	XCALL FILES (153,'I',153,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN153 = 153

	XCALL IO (CHN153, TBLCTL, 1, READ, LOKCTL)

	SWITCH = 5
	XCALL FILES (17, 'SI', 182, SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN182 = 17


	OPNOK = 1
	RETURN
;----------------------------------------------

CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	IF (CHN011) CLOSE CHN011
	IF (CHN021) CLOSE CHN021
	IF (CHN022) CLOSE CHN022
	IF (CHN153) CLOSE CHN153
	IF (CHN182) CLOSE CHN182

	RETURN
;----------------------------------------------

INIT_WINDOW,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		;;; SET UP SCREEN 1 WINDOW
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;	xcall u_start("LIB:newlib",1,0,,24,80,,100)
	xcall u_start("LIB:newlib",1,0,,24,80,,200)
	XCALL W_PROC(WP_CREATE,WND_1,WN_NAME,24,80)
	XCALL W_PROC(WP_PLACE,WND_1,1,1)	
	XCALL W_DISP(WND_1,WD_CLEAR)
	xcall u_logwnd(wnd_1)

	W_ID = WND_1


;; POP info...
	MAXARA = 20
	PLEN = 55
	NUMROW = 10
	WX = 
	WY = 
	POP_WID = 'INQPO'
	POP_TITLE = 'PURCHASE ORDERS'

;; POPA info...
	aMAXARA = 20
	aPLEN = 74
	aNUMROW = 7
	aWX = 16
	aWY = 1
	aPOP_WID = 'INQPL'
	aPOP_TITLE = 'PO LINES'

	PMAXARA = 10		;9-19-97 NOT MORE THAN 1 FULL WINDOW
	PPLEN = 34
	PNUMROW = 10
	PWX = 8
	PWY = 7
	PPOP_WID(1,5) = "POPOP"
	PPOP_TITLE = "VENDOR NAMES"

	RETURN
;-------------------------------------------------------------------
END


