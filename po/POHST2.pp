;  POHST2 / POR - D11
;
;		AUTHOR: 29-OCT-93  DKS
;
;		PURCHASE ORDER HISTORY REPORT BY PART NUMBER
;
;		TEST REPORT TO SHOW VENDOR ITEM NUMBER
;
;
RECORD PORHDR
	.INCLUDE 'DEF:RD151A.DEF'

RECORD POLINE
	.INCLUDE 'DEF:RD152A.DEF'

RECORD ITMMAS			
	.INCLUDE 'DEF:RD041A.DEF'

RECORD ITMCTL			
	.INCLUDE 'DEF:RD041B.DEF'

RECORD ITMIDX			
	.INCLUDE 'DEF:RD042A.DEF'

RECORD PORTMP
	.INCLUDE 'DEF:PORTM2.DEF'

RECORD POITEM
		,A11,'DPO:PORTM2.'
	TEXT	,A3

RECORD KEYPON
	KEY1	,D6
	KEY2	,D2

RECORD PLINE
 		,A132

RECORD LINE1,X
	KIMNUM	,A15
		,A5
	KIMDES	,A30
		,A82
RECORD LINE2,X
		,A3
	KPONUM	,A6
		,A5
	KVENDR	,A4
		,A2
	KPOQTY	,A7
	      	,A4
	KPODTE	,A8
		,A5
	KFDATE	,A8
		,A2
	KEDATE	,A8
		,A2
	KPUNIT	,A11
		,A1
	KVPART	,A20
		,A36

RECORD DTAIL1
		,A11,	'PART-NUMBER'
		,A9
		,A11,	'DESCRIPTION'
		,A105
RECORD DTAIL2
		,A6
		,A3,	'P/O'
		,A24
		,A3,	'P/O'
		,A11
		,A13,	'DELIVERY DATE'
		,A8
		,A8,	'UNIT P/O'
		,A59
RECORD DTAIL3
		,A6
		,A3,	'NO.'
		,A4
		,A6,	'VENDOR'
		,A5
		,A3,	'QTY'
		,A6
		,A4,	'DATE'
		,A8
		,A16,	' FIRST      LAST'
		,A9
		,A4,	'COST'
		,A57
RECORD LEGEND
		,A1
		,A22,'REQUEST P.O. DATE FROM'
		,A3
	DTE1	,A8
		,A3
		,A2,'TO'
		,A3
	DTE2	,A8
		,A86

RECORD
	CNGCTL	,D1
	WHATNO	,D1
	DKEY	,D11
	TNUM	,D3
	TMPREC	,D5
	BLANKS	,A15
	BRACKS	,A30,	']]]]]]]]]]]]]]]]]]]]]]]]]]]]]]'
	MASK	,A11,'ZZZZ,ZZX.XX'
	BSMID	,D5
	DSPSW	,D1
	EOF	,D1
	IDFLAG	,D1
	LINCNT	,D2,	60
	LINCTL	,D1
	LOKCTL	,D1
	LPSW	,D1
	NOABRT	,D1,	1
	PGCNT	,D3,	000
	PRTCTL	,D3
	PRTTYP	,A1
	READ	,D1,	0
	WRITE	,D1,1
	RPTNUM	,D3
	SPLFIL	,A14
	SRCOPT	,D1
	SRCCTL	,D1
	SWITCH	,D1
	TITLE	,A30,	'PURCHASE ORDER HISTORY BY PART'
	TODAY	,D6
	V	,D1
	WTARG	,D1,	2
	XRECNO	,D5
	LRECNO	,D5
	NORECS	,D5
	INXCTL	,D1
	ENTRY	,A15
	ALL	,A10
	TOTQTY	,D7
	ASOFDT	,A8
	SAVITM	,A15
	SAVPO	,D6
	STDATE	,D6
	ENDATE	,D6
	HDATE	,D6
	SDDATE	,D6
	EDDATE	,D6
	SITEM	,A15
	EITEM	,A15	
	

PROC
	XCALL TERID (V)
	XCALL TNMBR(TNUM)
	TEXT=TNUM,'XXX'
	XCALL OUTPT (2,1,2,'P.O. HISTORY REPORT BY ITEM',1)
GETDAT,
	XCALL OUTPT (6,20,2,'PLEASE ENTER STARTING REQUEST P/O DATE ',V)
	XCALL INPUT (6,61,6,0,'DE',ENTRY,INXCTL,V)
	GO TO (GETDAT,ENDOFF), INXCTL
	HDATE = ENTRY (1,6)
	IF (HDATE.EQ.0) XCALL RDATE (HDATE)
	IF (HDATE.EQ.0) GO TO GETDAT
	STDATE(1,2)=HDATE(5,6)
	STDATE(3,6)=HDATE(1,4)
	DTE1 = HDATE, 'XX/XX/XX'
	ASOFDT = HDATE, 'XX/XX/XX'
	XCALL OUTPT (6,61,0,ASOFDT,V)
GETDT2,
	XCALL OUTPT (7,20,2,'PLEASE ENTER ENDING REQUEST P/O DATE',V)
	XCALL INPUT (7,61,6,0,'D ',ENTRY,INXCTL,V)
	GO TO (GETDAT,GETDAT), INXCTL
	HDATE = ENTRY (1,6)
	IF (HDATE.EQ.0) XCALL RDATE (HDATE)
	IF (HDATE.EQ.0) GO TO GETDT2
	ENDATE(1,2)=HDATE(5,6)
	ENDATE(3,6)=HDATE(1,4)
	DTE2 = HDATE, 'XX/XX/XX'
	ASOFDT = HDATE, 'XX/XX/XX'
	XCALL OUTPT (7,61,0,ASOFDT,V)

STITM,
	XCALL OUTPT (9,20,2,'PLEASE ENTER STARTING PART #',V)
	XCALL INPUT (9,50,15,0,'A ',ENTRY,INXCTL,V)
	GO TO (STITM,GETDT2), INXCTL
	SITEM = ENTRY (1,15)
ENITM,
	XCALL OUTPT (10,20,2,'PLEASE ENTER ENDING PART #',V)
	XCALL INPUT (10,50,15,0,'A ',ENTRY,INXCTL,V)
	GO TO (ENITM,STITM), INXCTL
	EITEM = ENTRY (1,15)
	IF (EITEM.EQ.BLANKS) CALL ALLITM
	GOTO START
ALLITM,
	ALL = 'ALL PARTS'
	SITEM=
	EITEM='[[['
	XCALL OUTPT(9,50,0,ALL,V)
	RETURN 

START,
	CNGCTL=2
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (GETDAT),CNGCTL
	XCALL WATE (4,V)
	XCALL RDATE (TODAY)
	SPLFIL(5,6) = 'ND'

	LPSW = 1
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO EXIT
	IF (LPSW.EQ.2) WTARG = 4
	XCALL WATE (WTARG,V)
OPNFIL,
	SWITCH = 1
	XCALL FILES (3,'SI',151,SWITCH)		;PORHDR FILE
	IF (SWITCH.EQ.9) GO TO INUSE1

	SWITCH = 1
	XCALL FILES (4,'SI',152,SWITCH)		;PORLIN FILE
	IF (SWITCH.EQ.9) GO TO INUSE2

	SWITCH = 1
	XCALL FILES (6,'I',042,SWITCH)		;ITMIDX FILE
	IF (SWITCH.EQ.9) GO TO INUSE3

	SWITCH = 1
	XCALL FILES (7,'I',041,SWITCH)		;ITMMAS FILE
	IF (SWITCH.EQ.9) GO TO INUSE6

	OPEN (2,O,POITEM)			;open temporary file

	LOKCTL = 1
	XCALL IO (7,ITMCTL,1,READ,LOKCTL)

	XCALL WATE (4,V)
	XCALL OUTPT (2,1,0,'P.O. HISTORY REPORT BY ITEM',1)
	XCALL OUTPT (2,45,0,'CREATING DPO:PORTM2 - "POHSTI.DBR"',V)
PROCES,
	TMPREC=1
	PORTMP=
	LOKCTL = 1
	XCALL IO (2,PORTMP,TMPREC,WRITE,LOKCTL)	;write a blank control record

;read p.o header file to get p.o.'s for the date range entered
REREAD,
	LOKCTL = NOABRT
	XCALL IOS (3,PORHDR,READ,LOKCTL)
	IF (LOKCTL.NE.0) GOTO ENDFIL
	IF (HPOSTS.EQ.'C' .OR. HPOSTS.EQ.'X') GOTO REREAD
	HDATE(1,2) = HPODTE(5,6)
	HDATE(3,6) = HPODTE(1,4)
	IF (HDATE.LT.STDATE.OR.HDATE.GT.ENDATE) GOTO REREAD

;find all line items for the valid p.o.
REPEAT,
	KEY1 = HPONUM
	KEY2 = HRLNUM
	LOKCTL = 1
	XCALL ISIO (4,POLINE,KEYPON,READ,LOKCTL)
CONTIN,
	IF (LOKCTL.NE.0) GOTO REREAD
	IF (LPONUM.LT.HPONUM) GO TO NXTREC
	IF (LPONUM.GT.HPONUM) GO TO REREAD
	IF (LRLNUM.LT.HRLNUM) GO TO NXTREC
	IF (LRLNUM.GT.HRLNUM) GO TO REREAD
	IF (LITMNO.LT.SITEM.OR.LITMNO.GT.EITEM) GOTO NXTREC
	GOTO GETITM
NXTREC,
	LOKCTL = 1
	XCALL IOS (4,POLINE,READ,LOKCTL)
	GOTO CONTIN

;now find item in inventory master to get standard cost
GETITM,
	ITMMAS =
	BSMID = 1
	SRCCTL = 1
	SRCOPT = 4
	XCALL SERCH (6,ITMIDX,LITMNO,1,15,ORG041,BSMID,SRCCTL,SRCOPT,16,20,0,0,0,0)

	IF (SRCCTL.EQ.0) GO TO ITMFND
	ITMMAS=
	GOTO ADDTMP
ITMFND,
	XCALL IO (7,ITMMAS,IRC041,READ,LOKCTL)
	IF (LOKCTL.EQ.1) ITMMAS=

;now add a record for each valid line item on each p.o. to temporary file
ADDTMP,
	PORTMP=
	TACCT(1,2) = LPONCD(1)	
	TPODTE = HDATE			;PUT IN YY/DD/MM FORMAT FOR SORTING
	TPONUM = HPONUM
	TRLNUM = HRLNUM
	TPOTYP = HPOTYP
	TPOSTS = HPOSTS
	TVENDR = HVENDR
	TITMNO = LITMNO
	TITMDS = LITMDS
	TSTCST = SPCCST
	TEXCST = LEXCST
	TACCST = LACCST
	TQTYOR = LQTYOR
	TQTYRC = LQTYRC
	TDTERQ = LDTERQ
	TDTERC = LDTERC
	TINSTS = LINSTS
	TVITEM = LVITMN
	LOKCTL = 1
	INCR TMPREC
	XCALL IO (2,PORTMP,TMPREC,WRITE,LOKCTL)
	GOTO NXTREC

ENDFIL,
	PORTMP(1,30) = BRACKS
	PORTMP(31,60) = BRACKS
	PORTMP(61,90) = BRACKS
	PORTMP(91,120) = BRACKS
	PORTMP(121,124) = BRACKS
	LOKCTL = 1
	INCR TMPREC
	XCALL IO (2,PORTMP,TMPREC,WRITE,LOKCTL)

	CLOSE 2
	XCALL WATE (4,V)
	XCALL OUTPT(2,45,0,'SORTING DPO:PORTM2 - "POHSTI.DBR"',V)
;;Sort the p.o. line item file by item into a temporary file for this report
	SORT (IN=POITEM, OUT=POITEM, RECORD=PORTMP,
&		KEY=(TITMNO,TPODTE/R,TPONUM) )

	OPEN (2,I,POITEM)			;open temporary file
PRTRPT,
	SAVITM = 
	SAVPO = 0
	XRECNO=1
	XCALL WATE (2,V)
PRLOOP,
	INCR XRECNO
	LOKCTL = NOABRT
	XCALL IO (2,PORTMP,XRECNO,READ,LOKCTL)
	IF (PORTMP(1,9).EQ.BRACKS) GOTO DONE
	IF (SAVITM.NE.TITMNO) CALL NEWITM
	IF (SAVPO.NE.TPONUM) CALL NEWPO
	KPONUM = TPONUM
	KVENDR = TVENDR
	KPOQTY = TQTYOR,'ZZZ,ZZX'
	HDATE(1,4) = TPODTE(3,6)
	HDATE(5,6) = TPODTE(1,2)
	KPODTE = HDATE,'XX/XX/XX'
	KFDATE = TDTERQ,'XX/XX/XX'
	KEDATE = TDTERQ,'XX/XX/XX'
	KPUNIT = TEXCST/100,MASK
	KVPART = TVITEM

	TOTQTY = TOTQTY + TQTYOR
	HDATE(1,2) = TDTERQ(5,6)
	HDATE(3,6) = TDTERQ(1,4)
	IF (HDATE.LT.SDDATE .OR. SDDATE.EQ.0) SDDATE = HDATE
	IF (HDATE.GT.EDDATE) EDDATE = HDATE
	GOTO PRLOOP

NEWITM,
	IF (SAVITM.EQ.BLANKS) GOTO ENDITM
	CALL NEWPO
	XCALL LINFD(1)
	LINCNT = LINCNT + 1
ENDITM,
	IF (EOF) RETURN
	PLINE = DTAIL1
	CALL PRINT
	KIMNUM = TITMNO
	KIMDES = TITMDS
	CALL PRINT
	XCALL LINFD(1)
	LINCNT = LINCNT + 1
	PLINE = DTAIL2
	CALL PRINT
	PLINE = DTAIL3
	CALL PRINT
	SAVITM = TITMNO
	SAVPO = 0
	RETURN

NEWPO,
	IF (SAVPO.EQ.0) GOTO ENDPO
	KPOQTY = TOTQTY,'ZZZ,ZZX'
	HDATE(1,4) = SDDATE(3,6)
	HDATE(5,6) = SDDATE(1,2)
	KFDATE = HDATE,'XX/XX/XX'
	HDATE(1,4) = EDDATE(3,6)
	HDATE(5,6) = EDDATE(1,2)
	KEDATE = HDATE,'XX/XX/XX'	
	CALL PRINT
ENDPO,
	SAVPO = TPONUM
	TOTQTY = 0
	SDDATE = 0
	EDDATE = 0
	RETURN
DONE,
	EOF = 1
	CALL NEWITM
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	XCALL WATE (3,V)
	GO TO EXIT
;**********************************************************************
NONE,
	XCALL MESAG ('?ERROR IN TEMPORARY FILE - PORTMP.',1)
	GO TO EXIT
;**********************************************************************
PRINT,
	PRTCTL = 132
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,'NO HDR',' ',' ',
&		LEGEND,'NO LEGEND',' ',LINCTL,132,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;**********************************************************************
CLOSES,
	SWITCH = 4
	XCALL FILES (7,'I',041,SWITCH)
INUSE6,
	SWITCH = 4
	XCALL FILES (6,'I',042,SWITCH)
INUSE3,
	SWITCH = 4
	XCALL FILES (4,'SI',152,SWITCH)
INUSE2,
	SWITCH = 4
	XCALL FILES (3,'SI',151,SWITCH)
INUSE1,
ENDOFF,
	XCALL PGCHN ('PO:VAMENU',1)

;**********************************************************************
EXIT,
	CLOSE 2
 	XCALL DELET(2,POITEM)
	GOTO CLOSES
;**********************************************************************

END

