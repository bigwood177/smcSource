;P1TMNT.CP
;	COP TABLE MAINTENANCE
;	SHIP-VIA/SCAC-CODE TABLE
;
RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'


RECORD	CHANNEL
	CHN182	,D2
	CH182A	,D2	;2ND CHANNEL

RECORD	PRINT
	TITLE	,A*,	'SHIP VIA - SCAC CODE TABLE'
	HD	,A6,	'NO HDR'
	LG	,A9,	'NO LEGEND'
	PLINE	,A80
	PRNTON	,D1
	LINCNT	,D2,60
	PGCNT	,D6
	LPSW	,D2
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	PRTCTL	,D3,080
	LPARG	,D1
	PRNTSW	,D1
	PRTCTR	,D1


RECORD	HD1
		,A*,	'SHIP-VIA  SCAC'

RECORD	VARS
	ALPHA	,A12
	OPNOK	,D1
	BLANKS	,A15
	SCAC	,A4
	SHPVIA	,A1
	ST_SHP	,A1
	EN_SHP	,A1
	PGM	,D1
	ROW	,D2
	ENTRY	,A30
	INXCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	STORE	,D1,2
	DELETE	,D1,3
	LOKCTL	,D1
	WHATNO	,D2
	SELECT	,D1
	CNGCTL	,D1
	I	,D3
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'SHIP VIA - SCAC CODE TABLE',1)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO CLOSE

MENU,
	XCALL OUTPT (1,1,2,'SHIP VIA - SCAC CODE TABLE',1)
	XCALL OUTPT (3,9,0,'PLEASE SELECT APPLICATION',1)
	XCALL OUTPT (5,15,0,'1. TABLE MAINTENANCE',1)
	XCALL OUTPT (6,15,0,'2. PRINT TABLE',1)
MINPUT,
	XCALL INPUT (3,36,1,1,'#E',ENTRY,INXCTL,1)
	GOTO (MINPUT,ENDOFF), INXCTL
	PGM = ENTRY(1,1)
	GOTO (DISPLA,PRINT_TABLE),PGM
	GOTO MINPUT

DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,'SHIP VIA - SCAC CODE TABLE',1)
	XCALL OUTPT ( 4,4,0,'1. SHIP VIA ',1)
	XCALL OUTPT ( 6,4,0,'2. SCAC CODE',1)
SHPVIA,
	XCALL INPUT (4,24,01,00,'AE',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	SHPVIA = ENTRY(1,1)
CHKTBL,
	CLEAR TBL_KEY
	TBL_KEY(1,2) = 'P1'
	P1_CODE = SHPVIA
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .EQ. 0)
		BEGIN
		SELECT = 2
		CALL DSPREC
		GOTO ANYCNG
		END

	SELECT = 1			;ADD MODE
	CLEAR COPTBL
SCAC,
	XCALL INPUT (6,24,04,00,'AE',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	SCAC = ENTRY(1,4)
	CLEAR TBL_KEY
	TBL_KEY(1,2) = 'SC'
	SC_SCAC = SCAC
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .NE. 0) 
		BEGIN
		XCALL MESAG ('SCAC CODE NOT IN TABLE',1)
		GOTO SCAC
		END
	XCALL OUTPT (6,30,1,SC_NAME,1)


ANYCNG,
	XCALL OUTPT (24,1,1,'FIELD # TO CHANGE            <TAB> = DELETE',1)
	XCALL INPUT (24,20,02,00,'#T',ENTRY,INXCTL,1)
	IF (INXCTL .EQ. 3)
		BEGIN
		XCALL OUTPT (24,1,1,'DELETE, ARE YOU SURE ?',1)
		XCALL INPUT (24,24,01,01,'YN',ENTRY,INXCTL,1)
		GOTO (ANYCNG),INXCTL-1
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,DELETE,LOKCTL)
		GOTO DISPLA
		END

	WHATNO = ENTRY(1,2)
	IF (WHATNO .EQ. 0)
	THEN 	CLEAR CNGCTL
	ELSE 	CNGCTL = 1

	GOTO (PROCES,CNGBR),CNGCTL+1
CNGBR,
	GOTO (SHPVIA, SCAC),WHATNO
	GOTO ANYCNG

PROCES,
	CASE SELECT OF
	BEGINCASE
	1:	BEGIN
		clear coptbl		;5-21-12
		TBLCOD = 'P1'
		P1_CODE = SHPVIA
		P1_SCAC = SCAC
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,STORE,LOKCTL)
		END
	2:	XCALL ISIO (CHN182,COPTBL,TBL_KEY,WRITE,LOKCTL)
	ENDCASE
	GOTO DISPLA

ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('PO:TBLMNU',1)
	STOP


DSPREC,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; DISPLAY RECORD
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL OUTPT (4,24,1,P1_CODE,1)
	XCALL OUTPT (6,24,1,P1_SCAC,1)

	CLEAR TBL_KEY
	TBL_KEY(1,2) = 'SC'
	SC_SCAC = P1_SCAC
	XCALL ISIO (CH182A,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .NE. 0) 
		BEGIN
		SC_NAME = "* NOT ON FILE *"
		END

	XCALL OUTPT (6,30,1,SC_NAME,1)

;re-read original record...

	CLEAR TBL_KEY		
	TBL_KEY(1,2) = 'P1'
	P1_CODE = SHPVIA
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	RETURN
;-----------------------------------

;===================================================================
PRINT_TABLE,
;===================================================================
	LINCNT = 66
	PGCNT  = 0
	
PDISP,
	CNGCTL = 
	XCALL OUTPT (1,1,2,'PRINT SHIP VIA - SCAC CODE TABLE',1)
	XCALL OUTPT (4,4,0,'1. STARTING SHPVIA',1)
	XCALL OUTPT (6,4,0,'2. ENDING   SHPVIA',1)
ST_SHP,
	XCALL INPUT (4,27,01,00,'AE',ENTRY,INXCTL,1)
	GOTO (PDISP,PDONE),INXCTL
	ST_SHP = ENTRY(1,1)
	IF (ST_SHP .EQ. ' ')
		BEGIN
		EN_SHP = 'Z'
		XCALL OUTPT (4,25,1,'ALL',1)
		XCALL OUTPT (6,25,1,' ',1)
		GOTO P_ANY
		END
	GOTO (P_ANY),CNGCTL
EN_SHP,
	XCALL INPUT (6,25,01,00,'A ',ENTRY,INXCTL,1)
	GOTO (PDISP),INXCTL
	EN_SHP = ENTRY(1,1)
	IF (EN_SHP .EQ. ' ')
		BEGIN
		EN_SHP = ST_SHP
		XCALL OUTPT (6,25,0,EN_SHP,1)
		END
P_ANY,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (P_PRINT,P_CNGBR),CNGCTL + 1
P_CNGBR,
	GOTO (ST_SHP,EN_SHP),WHATNO
	GOTO P_ANY

P_PRINT,
	CLEAR TBL_KEY
	TBLCOD = 'P1'
	P1_CODE = ST_SHP
	FIND (CHN182,COPTBL,TBL_KEY)[ERR=PLOOP]

PLOOP,
	XCALL IOS (CHN182,COPTBL,READ,LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF
	IF (TBLCOD .NE. 'P1') GOTO EOF
	IF (P1_CODE .LT. ST_SHP) GOTO PLOOP
	IF (P1_CODE .GT. EN_SHP) GOTO EOF


	PLINE (3,3) = P1_CODE	;P1_CODE
	PLINE (10,11) = P1_SCAC

	CLEAR TBL_KEY
	TBL_KEY(1,2) = 'SC'
	SC_SCAC = P1_SCAC
	XCALL ISIO (CH182A,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .NE. 0) 
		BEGIN
		SC_NAME = "* NOT ON FILE *"
		END
	PLINE (13,28) = SC_NAME

;SHPVIA  SCAC
;  XX     AA
;12345678901234567890123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5         6         7         8
	CALL PRINT
	GOTO PLOOP

EOF,
	IF (PRNTON.EQ.1)  XCALL LPOFF(LPSW,SPLFIL,PGCNT)
	PRNTON  = 0

PDONE,
	GOTO MENU

PRINT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (PRNTON .EQ. 0) CALL PRNTON
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HD1,HD,HD
&		,LG,LG,LG,0,080,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;-------------------------------------------------------------
PRNTON,
	SPLFIL (5,6) = 'EF'
	LPSW = 1		;PRINT,SPOOL, OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTON = 1
	RETURN
;-------------------------------------------------------------
;===================================================================


OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (1,'SU',182,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN182 = 1

	SWITCH = 5
	XCALL FILES (2,'SI',182,SWITCH)
	IF (SWITCH .EQ. 9) RETURN

	CH182A = 2

	OPNOK = 1
	RETURN
;----------------------------------------------------

CLOSE,
	CLOSE CHN182
	RETURN
;----------------------------------------------------
	
	

