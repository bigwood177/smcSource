;PARTN1.POR
;		5-MAY-1992
;		VIPART NOW ISAM W/ 2 KEYS:
;		  1. NUAIRE PART # + VENDOR #
;		  2. VENDORS PART #
;
;
;
;
;
RECORD VIPART
	.INCLUDE 'DEF:RD061A.DEF'

RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
	
RECORD	VARS
	ORGKEY	,A19
	KITM	,A20
	KVEN	,D4
	KEY	,A19
	OPTION	,D1
	ENTRY	,A20
	INXCTL  ,D1
	CNGCTL	,D1
	WHATNO	,D2
	DECMAL	,D18
	SELECT	,D1
	FULL	,D1
	MAXREC	,D5
	SRCCTL	,D1
	SWITCH	,D1,	1
	V	,D1
	READ	,D1,	0			;IO READ FLAG FOR I-O
	WRITE	,D1,	1			;IO WRITE
	DELET	,D1,	4
	STORE	,D1,	3
	LOKCTL	,D1				;RECORD-LOCKED INDICATOR
PROC
	XCALL TERID (V)
OPENS,
	XCALL WATE (4,V)
	SWITCH = 1

	XCALL FILES (1,'SU',61,SWITCH)		;FILE # 61 -- VIPART FILE
	IF (SWITCH.EQ.9) GO TO CLOSE

DISPLA,
	SELECT =
	XCALL OUTPT(1,1,2,'VENDOR/INVENTORY PART CROSS REFERENCE',V)
	XCALL OUTPT (10,22,2,'1. VENDOR PART #',1)
	XCALL OUTPT (12,22,0,'2. VENDOR #',1)
	XCALL OUTPT (14,22,0,'3. NUAIRE''S PART #',1)
PARTNO,
	CTL = '10,43,20,01,AE'
	CALL INPUT
	GO TO (DISPLA,ENDOFF), INXCTL
	VPART = ENTRY
	IF (VPART.EQ.' ') GO TO PARTNO
	GO TO (ANYCNG), CNGCTL
	KITM = VPART

	XCALL ISIO (1,VIPART,VPKEY2,READ,LOKCTL)
	IF (LOKCTL .EQ. 0) GOTO DSPREC		;EXISTING RECORD

	SELECT = 1				;ELSE .. "ADD" MODE
	VPART = KITM				;RESTORE OLD KEY VALUES
VENNO,
	CTL = '12,43,04,01,# '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	XCALL FRMAT (ENTRY(1,4),4)
	IVEN = ENTRY (1,4)
	IF (IVEN.EQ.'    ') GO TO VENNO

VENPRT,
	CTL = '14,43,20,01,A '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	IPART = ENTRY

ANYCNG,
	CNGCTL =
	XCALL OUTPT (24,1,1,'FIELD #               [TAB] = DELETE',1)
	XCALL INPUT (24,10,01,00,'#T',ENTRY,INXCTL,1)
	IF (INXCTL .EQ. 3) 
		BEGIN
		XCALL OUTPT (24,1,1,'DELETE, ARE YOU SURE ?',1)
		XCALL INPUT (24,25,01,01,'YN',ENTRY,INXCTL,1)
		GOTO (DELETE,ANYCNG),INXCTL
		END
	WHATNO = ENTRY(1,1)
	IF (WHATNO .EQ. 0) GOTO PROCES

	GO TO (PARTNO,VENNO,VENPRT), WHATNO
	GO TO ANYCNG
	

PROCES,
	XCALL WATE (3,V)
	GO TO (ADD,CHANGE), SELECT

DSPREC,
	SELECT = 2			;CHANGE MODE
	ORGKEY = VPKEY1			;SAVE ORIGINAL KEY

	XCALL OUTPT (14,43,1,IPART,1)
	CTL = '12,43,05'
	DECMAL = IVEN
	CALL DSPNUM
	XCALL OUTPT (10,43,1,VPART,1)
	GOTO ANYCNG

ADD,
	XCALL ISIO (1,VIPART,VPKEY1,STORE,LOKCTL)
	IF (LOKCTL .NE. 0)
		BEGIN
		XCALL MESAG ('"ISAM STORE ERROR"',2)
		GOTO ENDOFF
		END
	GOTO DISPLA

CHANGE,
	IF (VPKEY1 .NE. ORGKEY) GOTO ADD
	XCALL ISIO (1,VIPART,VPKEY1,WRITE,LOKCTL)
	IF (LOKCTL .NE. 0)
		BEGIN
		XCALL MESAG ('"ISAM WRITE ERROR"',2)
		GOTO ENDOFF
		END
	GO TO DISPLA

DELETE,
	XCALL ISIO (1,VIPART,VPKEY2,DELET,LOKCTL)
	IF (LOKCTL .NE. 0)
		BEGIN
		XCALL MESAG ('"ISAM DELETE ERROR"',2)
		GOTO ENDOFF
		END
	
	GOTO DISPLA

ENDOFF,
CLOSE2,
	XCALL WATE (4,V)
	XCALL FILES (1,'SU',61,4)
CLOSE,
	XCALL PGCHN ('PO:VIMENU',1)

FULL,
	XCALL MESAG
&      ('THE "VIPART" FILE IS FULL. EXPAND FILE BEFORE ADDING MORE PARTS',2)
	GOTO ENDOFF
INPUT,
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,1)
	RETURN
DSPNUM,
	OPTION = 1
	GO TO CALDSP
DSPDTE,
	OPTION = 2
	GO TO CALDSP
DSPDLR,
	OPTION = 3
CALDSP,
	XCALL DSPLY (MAX,ROW,COL,DECMAL,OPTION,1)
	RETURN
END
