SUBROUTINE BYITM 	;isam
;
;
;
;		SUBROUTINE TO ACCEPT RANGES FOR SCHEDULED RECEIPTS REPORT
;			BY ITEM.
;
RANGES		,A		;ALL RANGES
;**********************************************************************
RECORD RANGER
	STAMAJ	,A15		;STARTING VALUE OF MAJOR KEY
	ENDMAJ	,A15		;ENDING VALUE OF MAJOR KEY
	STADAT	,D6		;STARTING PROMISED DATE
	ENDDAT	,D6		;ENDING PROMISED DATE
	LTDAYS	,D3		;AT LEAST X DAYS LATE
	CLOSED	,D1		;1= INCLUDE CLOSED LINES, 2 = EXCLUDE.
	ALLRCV	,D1		;1 = INCLUDE 100% RECEIVED LINES, 2 = EXCLUDE.
	OBUYER	,A2		;ONLY INCLUDE THIS BUYER (BLANK=INCLUDE ALL)
	ONOTFY	,A3		;ONLY INCLUDE THIS NOTIFY (BLANK=INCLUDE ALL)
	OMOVTO	,A3		;ONLY INCLUDE THIS MOVE-TO (BLANK=INCLUDE ALL)
	OSHPTO	,A2		;ONLY INCLUDE THIS SHIP-TO (BLANK=INCLUDE ALL)
	OUSERC	,A2		;ONLY INCLUDE THIS USER CODE (BLANK=INCLUDE ALL)
	GETOUT	,D1		;1 = RETURN TO RANGE SELECTION
;**********************************************************************
RECORD	ITMMAS
	.INCLUDE 'DEF:RD041A.def'
RECORD INVCTL,X
	.INCLUDE 'DEF:RD041B.def'

RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2

RECORD
	BLANKS	,A10
	BRACKS	,A30,	']]]]]]]]]]]]]]]]]]]]]]]]]]]]]]'
	BSMID	,D5
	CNGCTL	,D1
	DSPSW	,D1
	ENTRY	,A30
	EOF	,D1
	EPO	,D1
	HDATE	,D6
	HRECNO	,D5
	INXCTL	,D1
	LINCNT	,D2,	60
	LINCTL	,D1
	LOKCTL	,D1
	LPSW	,D1
	LRECNO	,D5
	MUSTPT	,D1
	MSGCTL	,D1
	N	,D1
	PGCNT	,D3,	000
	PGNUM	,D2
	PRTCTL	,D3
	PRTTYP	,A1
	READ	,D1,	0
	RECNO	,D5
	RPTNUM	,D3
	SPLFIL	,A14
	SRCOPT	,D1
	SRCCTL	,D1
	SWITCH	,D1
	TITLE	,A2
	TODAY	,D6
	V	,D1
	VADR	,4A30
	WHATNO	,D2
	RJUST	,D1
	LSTART	,A15
	LEND	,A15

PROC
	XCALL TERID (V)
	RANGER = RANGES
	GETOUT =
	CNGCTL =

	SWITCH = 1
	XCALL FILES (10,'SI',41,SWITCH)
	IF (SWITCH.EQ.9) GO TO EXIT

	ITEMNO = '!!!!!!!!!!!!!!!'
	LOKCTL = 1
	XCALL ISIO (10,INVCTL,ITEMNO,READ,LOKCTL)
	RJUST = JSTIFY
	XCALL FILES (10,'SI',41,4)

BEGIN,
	XCALL OUTPT (2,1,2,'SCHEDULED RECEIPTS BY ITEM',1)
	XCALL OUTPT (3,8,0,'1. STARTING ITEM NUMBER',V)
	XCALL OUTPT (4,8,0,'2. ENDING ITEM NUMBER',V)
	XCALL OUTPT (5,8,0,'3. STARTING PROMISED DATE ',V)
	XCALL OUTPT (6,8,0,'4. ENDING PROMISED DATE ',V)
	XCALL OUTPT (7,8,0,'5. AT LEAST X DAYS LATE ',V)
	XCALL OUTPT (8,8,0,'6. INCLUDE CLOSED LINES ? ',V)
	XCALL OUTPT (9,8,0,'7. INCLUDE 100% RECEIVED ? ',V)
	XCALL OUTPT (5,48,0,'8. BUYER ',V)
	XCALL OUTPT (6,48,0,'9. NOTIFY ',V)
	XCALL OUTPT (7,47,0,'10. MOVE TO ',V)
	XCALL OUTPT (8,47,0,'11. SHIP TO ',V)
	XCALL OUTPT (9,47,0,'12. USER CODE ',V)

STITM,
	CTL = '03,36,15,00,AE'
	CALL INPUT
	GO TO (BEGIN,EXIT), INXCTL
	IF (ENTRY.EQ.BLANKS) GO TO ALLITM
	IF (RJUST) XCALL FRMAT (ENTRY(1,15),15)
	STAMAJ = ENTRY
	LSTART = STAMAJ
	IF (RJUST) XCALL LEFTJ (LSTART(1,15),15)
	XCALL OUTPT (ROW,COL,0,LSTART,V)
	IF (ENDMAJ.EQ.']]]]]]]]]]]]]]]') GO TO ENDIT2
	IF (CNGCTL.NE.1) GO TO ENDIT2
	IF (ENDMAJ.LT.STAMAJ) GO TO ITMMSG
	GO TO ANYCNG
ENDITM,
	IF (ENDMAJ.EQ.']]]]]]]]]]]]]]]') GO TO STITM
ENDIT2,
	CTL = '04,36,15,00,A '
	CALL INPUT
	IF (INXCTL.EQ.1) GO TO BEGIN
	IF (ENTRY.EQ.BLANKS) ENTRY = STAMAJ
	IF (RJUST) XCALL FRMAT (ENTRY(1,15),15)
	ENDMAJ = ENTRY
	LEND = ENDMAJ
	IF (RJUST) XCALL LEFTJ (LEND(1,15),15)
	XCALL OUTPT (ROW,COL,0,LEND,V)
	IF (ENDMAJ.LT.STAMAJ) GO TO ITMMSG
ENDIT3,
	GO TO (ANYCNG), CNGCTL
STDTE,
	CTL = '05,36,06,00,D '
	CALL INPUT
	IF (INXCTL.EQ.1) GO TO BEGIN
	IF (ENTRY.EQ.BLANKS) GO TO ALLDTE
	HDATE = ENTRY(1,6)
	STADAT(1,2) = HDATE(5,6)
	STADAT(3,6) = HDATE(1,4)
	IF (ENDDAT.EQ.999999) GO TO ENDTE2
	IF (CNGCTL.NE.1) GO TO ENDTE2
	IF (ENDDAT.LT.STADAT) GO TO DATMSG
	GO TO ANYCNG
ENDTE,
	IF (ENDDAT.EQ.999999) GO TO STDTE
ENDTE2,
	CTL = '06,36,06,00,D '
	CALL INPUT
	IF (INXCTL.EQ.1) GO TO BEGIN
	IF (ENTRY.EQ.BLANKS) GO TO SAMEDA
	HDATE = ENTRY(1,6)
	ENDDAT(1,2) = HDATE(5,6)
	ENDDAT(3,6) = HDATE(1,4)
	IF (ENDDAT.LT.STADAT) GO TO DATMSG
ENDTE3,
	GO TO (ANYCNG),CNGCTL
LATE,
	CTL = '07,36,03,00,# '
	CALL INPUT
	IF (INXCTL.EQ.1) GO TO BEGIN
	LTDAYS = ENTRY
	XCALL DSPLY (MAX,ROW,COL,LTDAYS,1,V)
	GO TO (ANYCNG), CNGCTL

CLOSLN,
	CTL = '08,36,01,00,YY'
	CALL INPUT
	IF (INXCTL.EQ.0.OR.INXCTL.GE.3) GO TO CLOSLN
	CLOSED = INXCTL
	GO TO (ANYCNG), CNGCTL

RCVDLN,
	CTL = '09,36,01,00,YY'
	CALL INPUT
	IF (INXCTL.EQ.0.OR.INXCTL.GE.3) GO TO RCVDLN
	ALLRCV = INXCTL
	GO TO (ANYCNG), CNGCTL

IBUYER,
	CTL = '05,62,02,00,A '
	CALL INPUT
	IF (INXCTL.EQ.1) GO TO BEGIN
	IF (ENTRY.EQ.BLANKS) GO TO ALLBYR
	OBUYER = ENTRY
	XCALL OUTPT (ROW,COL,0,OBUYER,V)
IBUY2,
	GO TO (ANYCNG), CNGCTL

INOTFY,
	CTL = '06,62,03,00,A '
	CALL INPUT
	IF (INXCTL.EQ.1) GO TO BEGIN
	IF (ENTRY.EQ.BLANKS) GO TO ALLNFY
	ONOTFY = ENTRY
	XCALL OUTPT (ROW,COL,0,ONOTFY,V)
INOT2,
	GO TO (ANYCNG), CNGCTL

IMOVTO,
	CTL = '07,62,03,00,A '
	CALL INPUT
	IF (INXCTL.EQ.1) GO TO BEGIN
	IF (ENTRY.EQ.BLANKS) GO TO ALLMOV
	OMOVTO = ENTRY
	XCALL OUTPT (ROW,COL,0,OMOVTO,V)
IMOV2,
	GO TO (ANYCNG), CNGCTL

ISHPTO,
	CTL = '08,62,02,00,A '
	CALL INPUT
	IF (INXCTL.EQ.1) GO TO BEGIN
	IF (ENTRY.EQ.BLANKS) GO TO ALLSHP
	OSHPTO = ENTRY
	XCALL OUTPT (ROW,COL,0,OSHPTO,V)
ISHP2,
	GO TO (ANYCNG), CNGCTL

IUSERC,
	CTL = '09,62,02,00,A '
	CALL INPUT
	IF (INXCTL.EQ.1) GO TO BEGIN
	IF (ENTRY.EQ.BLANKS) GO TO ALLUSR
	OUSERC = ENTRY
	XCALL OUTPT (ROW,COL,0,OUSERC,V)
IUSE2,
	CNGCTL =
ANYCNG,
	WHATNO =
	XCALL ANYCN (CNGCTL,WHATNO)
	GO TO (EXIT,LEAVE), CNGCTL+2
CNGBR,
	GO TO (STITM,ENDITM,STDTE,ENDTE,LATE,CLOSLN,RCVDLN,IBUYER,
&		INOTFY,IMOVTO,ISHPTO,IUSERC), WHATNO
BADCNG,
	CNGCTL = 3
	GO TO ANYCNG
;**********************************************************************
ALLITM,
	XCALL OUTPT (3,36,1,'ALL ',V)
	XCALL OUTPT (4,36,1,'    ',V)
	STAMAJ =
	ENDMAJ = ']]]]]]]]]]]]]]]'
	GO TO ENDIT3
;**********************************************************************
ITMMSG,
	XCALL MESAG ('STARTING/ENDING ITEM NUMBERS ARE OUT OF ORDER',1)
	ENDMAJ = ']]]]]]]]]]]]]]]'
	GO TO STITM
;**********************************************************************
ALLDTE,
	XCALL OUTPT (5,36,0,'ALL   ',V)
	XCALL OUTPT (6,36,0,'      ',V)
	STADAT = 0
	ENDDAT = 999999
	GO TO ENDTE3
;**********************************************************************
DATMSG,
	XCALL MESAG ('STARTING/ENDING PROMISED DATES ARE OUT OF ORDER',1)
	ENDDAT = 999999
	GO TO STDTE
;**********************************************************************
SAMEDA,
	IF (STADAT.EQ.0) GO TO STDTE
	ENDDAT = STADAT
	ENTRY(1,5) = STADAT(3,6), 'XX/XX'
	ENTRY(6,8) = STADAT(1,2), '/XX'
	XCALL OUTPT (ROW,COL,0,ENTRY(1,8),V)
	GO TO (ANYCNG), CNGCTL
	GO TO LATE
;**********************************************************************
ALLBYR,
	XCALL OUTPT (ROW,COL,0,'ALL',V)
	OBUYER =
	GO TO IBUY2
;**********************************************************************
ALLNFY,
	XCALL OUTPT (ROW,COL,0,'ALL',V)
	ONOTFY =
	GO TO INOT2
;**********************************************************************
ALLMOV,
	XCALL OUTPT (ROW,COL,0,'ALL',V)
	OMOVTO =
	GO TO IMOV2
;**********************************************************************
ALLSHP,
	XCALL OUTPT (ROW,COL,0,'ALL',V)
	OSHPTO =
	GO TO ISHP2
;**********************************************************************
ALLUSR,
	XCALL OUTPT (ROW,COL,0,'ALL',V)
	OUSERC =
	GO TO IUSE2
;**********************************************************************
EXIT,
	GETOUT = 1
LEAVE,
	RANGES = RANGER
	RETURN
;**********************************************************************

INPUT,
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,V)
	RETURN
;**********************************************************************

END

