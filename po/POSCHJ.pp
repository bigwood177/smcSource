;  POSCHJ / POR - D11
;
;
;
;		PRINT SCHEDULED RECEIPTS BY JOB REPORT
;
;
RECORD HEADER
	.INCLUDE 'DEF:RD151A.DEF'

RECORD POLINE	
	.INCLUDE 'DEF:RD152A.DEF'

RECORD VENDOR
	.INCLUDE 'DEF:RD011A.DEF'

RECORD DUMVEN
	.INCLUDE 'DEF:RD011B.DEF'

RECORD VENIDX	
	.INCLUDE 'DEF:RD012A.DEF'

RECORD INDEX
	.INCLUDE 'DEF:RD155A.DEF'

RECORD INVCTL
	.INCLUDE 'DEF:RD041B.DEF'

RECORD PLINE
		,A132

RECORD LINE1,X
	KJBNUM	,A6
		,A2
	KIMNUM	,A15
		,A17
	KVDNUM	,A4
		,A2
	KVDNAM	,A25
		,A2
	KPONUM	,A6
	KDASH	,A1
	KRLNUM	,A2
		,A2
	KPODAT	,A8
		,A2
	KQYORD	,A6
		,A2
	KQYRCV	,A6
		,A2
	KRQDAT	,A8
		,A1
	KIMLAT	,A1
	KPMDAT	,A8
		,A2
	KCXCOD	,A1
		,A1

RECORD LINE2,X
		,A8
	KIMDES	,A30
		,A94
RECORD LINE3,X
		,A80
	KIMTOT	,A12
		,A1
	KQOTOT	,A7
		,A1
	KQRTOT	,A7
		,A24

RECORD HDR1
		,A1
		,A3,	'JOB'
		,A4
		,A11,	'ITEM-NUMBER'
		,A20
		,A6,	'VENDOR'
		,A31
		,A3,	'P/O'
		,A7
		,A3,	'P/O'
		,A7
		,A3,	'QTY'
		,A5
		,A3,	'QTY'
		,A3
		,A18,	'REQUESTED PROMISED'
		,A4

RECORD HDR2
		,A6,	'NUMBER'
		,A2
		,A16,	'ITEM-DESCRIPTION'
		,A15
		,A19,	'NUMBER  VENDOR-NAME'
		,A17
		,A6,	'NUMBER'
		,A5
		,A4,	'DATE'
		,A4
		,A14,	'ORDERED  RCVED'
		,A4
		,A4,	'DATE'
		,A6
		,A10,	'DATE   C/X'

RECORD LEGEND
		,A107
		,A13,	'ITEM LATE = *'
		,A12

RECORD
	ACCMQO	,D7
	ACCMQR	,D7
	BLANKS	,A10
	BRACKS	,A30,	']]]]]]]]]]]]]]]]]]]]]]]]]]]]]]'
	BSMID	,D5
	CNTR	,D3
	DSPSW	,D1
	EOF	,D1
	HDATE	,D6
	IDFLAG	,D1
	LINCNT	,D2,	60
	LINCTL	,D1
	LOKCTL	,D1
	LPSW	,D1
	NOABRT	,D1,	1
	OLDITM	,A15
	OLDJOB	,A6
	OLDVEN	,A4
	PGCNT	,D3,	000
	PRTCTL	,D3
	PRTTYP	,A1
	READ	,D1,	0
	RPTNUM	,D3
	SPLFIL	,A14
	SRCOPT	,D1
	SRCCTL	,D1
	SWITCH	,D1
	TITLE	,A25,	'SCHEDULED RECEIPTS BY JOB'
	TODAY	,D6
	V	,D1
	WTARG	,D1,	2
	XRECNO	,D5
	YTODAY	,D6
	RJUST	,D1

PROC
	XCALL TERID (V)
	XCALL OUTPT (2,1,2,'SCHEDULED RECEIPTS BY JOB',1)
	XCALL WATE (4,V)
	XCALL RDATE (TODAY)
	SPLFIL(5,6) = 'N2'

	SWITCH = 1
	XCALL FILES (10,'I',41,SWITCH)
	IF (SWITCH.EQ.9) GO TO INUSE1
	LOKCTL = 1
	XCALL IO (10,INVCTL,1,READ,LOKCTL)
	RJUST = JSTIFY
	XCALL FILES (10,'I',41,4)

	SWITCH = 1
	XCALL FILES (1,'SI',151,SWITCH)		; FILE 151, PORHDR.
	IF (SWITCH.EQ.9) GO TO INUSE1

	SWITCH = 1
	XCALL FILES (2,'SI',152,SWITCH)		; FILE 152, PORLIN.
	IF (SWITCH.EQ.9) GO TO INUSE2

	SWITCH = 5
	XCALL FILES (3,'I',011,SWITCH)		; FILE 011, VENMAS.
	IF (SWITCH.EQ.9) GO TO INUSE3

	LOKCTL = NOABRT
	XCALL IO (3,DUMVEN,1,READ,LOKCTL)

	SWITCH = 5
	XCALL FILES (4,'I',012,SWITCH)		; FILE 012, VENIDX.
	IF (SWITCH.EQ.9) GO TO INUSE4

	SWITCH = 5
	XCALL FILES (5,'I',155,SWITCH)		; FILE 155, POSIDX
	IF (SWITCH.EQ.9) GO TO INUSE5

				;	PRINT CONTROL ROUTINE
PROCES,
	CALL READIX		;READ FIRST INDEX & MATCHING LINE RECORDS
	IF (EOF) GO TO NONE

	LPSW = 1
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO EXIT
	IF (LPSW.EQ.2) WTARG = 4
	XCALL WATE (WTARG,V)

	CALL JOBS		;PRINT ALL JOBS (ENTIRE REPORT)

	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	XCALL WATE (3,V)
	GO TO EXIT
;**********************************************************************
NONE,
	XCALL MESAG ('?REPORT INDEX ERROR IN POSCHI.',1)
	GO TO EXIT
;**********************************************************************
;**********************************************************************
;	EACH PASS PRINTS SCHEDULED RECEIPTS FOR ONE JOB
;**********************************************************************
JOBS,
	OLDJOB = LJOBNO
	KJBNUM = LJOBNO
	IF (KJBNUM.EQ.BLANKS) KJBNUM = 'NO JOB'

	CALL ITEMS		; PRINT ALL ITEMS UNDER THIS JOB

	IF (EOF) RETURN
	GO TO JOBS
;**********************************************************************
;	EACH PASS PRINTS ONE ITEM-GROUP FOR ONE JOB
;**********************************************************************
ITEMS,
	OLDITM = ITMKEY
	IDFLAG = 1		; ITEM DESCRIPTION FLAG
	KIMNUM = LITMNO
	IF (RJUST) XCALL LEFTJ (KIMNUM(1,15),15)
	CNTR =
	ACCMQO =
	ACCMQR =

	CALL VENDRS		; PRINT ALL VENDORS FOR THIS ITEM

	IF (CNTR.LE.1.AND.KIMDES.EQ.BLANKS) GO TO SKIP2
	IF (CNTR.LE.1) GO TO SKIP1
	KIMTOT = 'ITEM TOTALS:'
	KQOTOT = ACCMQO,'ZZZZZZX'
	KQRTOT = ACCMQR,'ZZZZZZX'
SKIP1,
	CALL PRINT
SKIP2,
	XCALL LINFD (1)
	INCR LINCNT
	IF (EOF.EQ.1.OR.OLDJOB.NE.LJOBNO) RETURN
	GO TO ITEMS
;**********************************************************************
;	EACH PASS PRINTS ONE VENDOR-GROUP UNDER ONE ITEM
;**********************************************************************
VENDRS,
	OLDVEN = VENKEY
	VENDOR =
	SRCOPT = 4
	BSMID = 1
	SRCCTL = 1
	XCALL SERCH (4,VENIDX,VENKEY,1,4,ORG011,BSMID,SRCCTL,SRCOPT,6,10,
&			0,0,0,0)
	IF (SRCCTL.EQ.0) GO TO FOUND
	NAME = 'VENDOR NOT ON FILE'
	GO TO VENDR2
FOUND,
	LOKCTL = NOABRT
	XCALL IO (3,VENDOR,IRC011,READ,LOKCTL)
VENDR2,
	KVDNUM = VENKEY
	KVDNAM = NAME

	CALL RECRDS		; PRINT ALL RECORDS, THIS VENDOR

	IF (EOF.EQ.1.OR.ITMKEY.NE.OLDITM.OR.LJOBNO.NE.OLDJOB) RETURN
	GO TO VENDRS
;**********************************************************************
;	EACH PASS PRINTS ONE RECORD WITHIN AN VENDOR-GROUP
;**********************************************************************
RECRDS,
	KPONUM = LPONUM,'ZZZXXX'
	KDASH = '-'
	KRLNUM = LRLNUM,'XX'
	KPODAT = IPODAT,'XX/XX/XX'

	KQYORD = LQTYOR,'ZZZZZX'
	ACCMQO = ACCMQO + LQTYOR
	KQYRCV = LQTYRC,'ZZZZZX'
	ACCMQR = ACCMQR + LQTYRC

	KRQDAT = LDTERQ,'XX/XX/XX'
	IF (LDTEPM.NE.0) KPMDAT = LDTEPM,'XX/XX/XX'
	IF (LDTEPM.EQ.0) KPMDAT = '  NONE  '
	KCXCOD = LCXCDE

	IF (LDTEPM.EQ.0) LDTEPM = LDTERQ
	YTODAY(1,2) = TODAY(5,6)
	YTODAY(3,6) = TODAY(1,4)
	HDATE(1,2) = LDTEPM(5,6)
	HDATE(3,6) = LDTEPM(1,4)
	IF (LDTEPM.NE.0) GO TO RECRD2
	HDATE(1,2) = LDTERQ(5,6)
	HDATE(3,6) = LDTERQ(1,4)
RECRD2,
	IF (YTODAY.GT.HDATE) KIMLAT = '*'

	CALL PRINT
	INCR CNTR

	IF (IDFLAG.EQ.1) KIMDES = LITMDS
	IDFLAG =

	CALL READIX
	IF (VENKEY.NE.OLDVEN) RETURN
	IF (ITMKEY.NE.OLDITM) RETURN
	IF (LJOBNO.NE.OLDJOB) RETURN
	IF (EOF) RETURN
	GO TO RECRDS
;**********************************************************************
;	READ ONE INDEX RECORD AND CORRESPONDING PORLIN RECORD
;**********************************************************************
READIX,
	EOF =
REREAD,
	LOKCTL = NOABRT
	XCALL IOS (5,INDEX,READ,LOKCTL)
	IF (LOKCTL.NE.0) GOTO ENDFIL

	LOKCTL = NOABRT
	XCALL ISIO (2,POLINE,POSKEY,READ,LOKCTL)
	IF (LOKCTL.NE.0) GOTO REREAD
	RETURN
ENDFIL,
	EOF = 1
	RETURN
;**********************************************************************
PRINT,
	PRTCTL = 132
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,'NO HDR',
&		LEGEND,'NO LEGEND',' ',LINCTL,132,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;**********************************************************************
CLOSES,
	XCALL FILES (1,'SI',151,4)
	XCALL FILES (2,'SI',152,4)
	XCALL FILES (3,'I',011,4)
	XCALL FILES (4,'I',012,4)
	XCALL FILES (5,'I',155,4)
	RETURN
;**********************************************************************
INUSE5,
	XCALL FILES (4,'I',012,4)
INUSE4,
	XCALL FILES (3,'I',011,4)
INUSE3,
	XCALL FILES (2,'SI',152,4)
INUSE2,
	XCALL FILES (1,'SI',151,4)
INUSE1,
	XCALL PGCHN ('PO:POMENU',1)
;**********************************************************************
EXIT,
	CALL CLOSES
	XCALL PGCHN ('PO:POSCRG',1)
;**********************************************************************

END

