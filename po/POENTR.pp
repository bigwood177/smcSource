;  POENTR / POR - D11
;
;
;
RECORD PORHDR
	.INCLUDE 'DEF:RD151A.DEF'

RECORD POLINE
	.INCLUDE 'DEF:RD152A.DEF'

RECORD CSTMAS			
	.INCLUDE 'DEF:RD121A.DEF'

RECORD CSTCTL			
	.INCLUDE 'DEF:RD121B.DEF'

RECORD CSTIDX			
	.INCLUDE 'DEF:RD122A.DEF'

RECORD PORTMP
	.INCLUDE 'DEF:PORTMP.DEF'

RECORD POITEM
		,A11,'DPO:PORTMP.'
	TEXT	,A3

RECORD KEYPON
	KEY1	,D6
	KEY2	,D2

RECORD PLINE
 		,A132

RECORD LINE1,X
		,A1
	KGLACT	,A4
		,A4
	KIMNUM	,A15
		,A2
	KIMDES	,A30
		,A2
	KPONUM	,A6
		,A2
	KDELDT	,A8
		,A1
	KPOQTY	,A7
	      	,A1
	KUCOST	,A11
		,A1
	KUSTD	,A11
		,A1
	KPEXT	,A11
		,A1
	KDIFF	,A11
		,A2

RECORD HDR1
		,A6,	'LEDGER'
		,A84
		,A4,	'UNIT'
		,A8
		,A4,	'UNIT'
		,A6
		,A5,	"EXT'D"
		,A6
		,A5,	"EXT'D"
		,A6
RECORD HDR2
		,A7,	'ACCOUNT'
		,A52
		,A3,	'P/O'
		,A4
		,A8,	'DELIVERY'
		,A5
		,A3,	'P/O'
		,A8
		,A3,	'P/O'
		,A9
		,A3,	'STD'
		,A7
		,A3,	'P/O'
		,A8
		,A8,	'VARIANCE'
		,A2
RECORD HDR3
		,A3,	'NO.'
		,A6
		,A11,	'PART-NUMBER'
		,A6
		,A11,	'DESCRIPTION'
		,A22
		,A3,	'NO.'
		,A6
		,A4,	'DATE'
		,A7
		,A3,	'QTY'
		,A8
		,A4,	'COST'
		,A8
		,A4,	'COST'
		,A6
		,A4,	'COST'
		,A3
		,A13,	'(-) FAVORABLE'

RECORD LEGEND
		,A1
		,A22,'REQUEST P.O. DATE FROM'
		,A3
	DTE1	,A8
		,A3
		,A2,'TO'
		,A3
	DTE2	,A8
		,A86

RECORD DASHES
		,A50,	'--------------------------------------------------'
		,A50,	'--------------------------------------------------'
		,A32,	'--------------------------------'

RECORD
	CNGCTL	,D1
	WHATNO	,D1
	DKEY	,D11
	TNUM	,D3
	TMPREC	,D5
	SAVGL	,A4
	GLEXT	,D10
	GLSTD	,D10
	ACCEXT	,D10
	ACCSTD	,D10
	EXTCST	,D10
	EXTSTD	,D10
	BLANKS	,A15
	BRACKS	,A30,	']]]]]]]]]]]]]]]]]]]]]]]]]]]]]]'
	MASK	,A11,'ZZZZ,ZZX.XX'
	MASK2	,A11,'ZZZ,ZZX.XX-'
	BSMID	,D5
	CNTR	,D3
	DSPSW	,D1
	EOF	,D1
	IDFLAG	,D1
	LINCNT	,D2,	60
	LINCTL	,D1
	LOKCTL	,D1
	LPSW	,D1
	NOABRT	,D1,	1
	PGCNT	,D3,	000
	PRTCTL	,D3
	PRTTYP	,A1
	READ	,D1,	0
	WRITE	,D1,1
	RPTNUM	,D3
	SPLFIL	,A14
	SRCOPT	,D1
	SRCCTL	,D1
	SWITCH	,D1
	TITLE	,A33,	'DAILY PURCHASE ORDER ENTRY REPORT'
	TODAY	,D6
	V	,D1
	WTARG	,D1,	2
	XRECNO	,D5
	LRECNO	,D5
	NORECS	,D5
	INXCTL	,D1
	ENTRY	,A15
	ALL	,A10
	ASOFDT	,A8
	STDATE	,D6
	ENDATE	,D6
	HDATE	,D6
	SITEM	,A15
	EITEM	,A15	
	

PROC
	XCALL TERID (V)
	XCALL TNMBR(TNUM)
	TEXT=TNUM,'XXX'
	XCALL OUTPT (2,1,2,'DAILY P.O. ENTRY REPORT',1)
GETDAT,
	XCALL OUTPT (6,20,2,'PLEASE ENTER STARTING REQUEST P/O DATE ',V)
	XCALL INPUT (6,61,6,0,'DE',ENTRY,INXCTL,V)
	GO TO (GETDAT,ENDOFF), INXCTL
	HDATE = ENTRY (1,6)
	IF (HDATE.EQ.0) XCALL RDATE (HDATE)
	IF (HDATE.EQ.0) GO TO GETDAT
	STDATE(1,2)=HDATE(5,6)
	STDATE(3,6)=HDATE(1,4)
	DTE1 = HDATE, 'XX/XX/XX'
	ASOFDT = HDATE, 'XX/XX/XX'
	XCALL OUTPT (6,61,0,ASOFDT,V)
GETDT2,
	XCALL OUTPT (7,20,2,'PLEASE ENTER ENDING REQUEST P/O DATE',V)
	XCALL INPUT (7,61,6,0,'D ',ENTRY,INXCTL,V)
	GO TO (GETDAT,GETDAT), INXCTL
	HDATE = ENTRY (1,6)
	IF (HDATE.EQ.0) XCALL RDATE (HDATE)
	IF (HDATE.EQ.0) GO TO GETDT2
	ENDATE(1,2)=HDATE(5,6)
	ENDATE(3,6)=HDATE(1,4)
	DTE2 = HDATE, 'XX/XX/XX'
	ASOFDT = HDATE, 'XX/XX/XX'
	XCALL OUTPT (7,61,0,ASOFDT,V)

START,
	CNGCTL=2
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (GETDAT),CNGCTL
	XCALL WATE (4,V)
	XCALL RDATE (TODAY)
	SPLFIL(5,6) = 'NC'

	LPSW = 1
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO EXIT
	IF (LPSW.EQ.2) WTARG = 4
	XCALL WATE (WTARG,V)
OPNFIL,
	SWITCH = 1
	XCALL FILES (3,'SI',151,SWITCH)		;PORHDR FILE
	IF (SWITCH.EQ.9) GO TO INUSE1

	SWITCH = 1
	XCALL FILES (4,'SI',152,SWITCH)		;PORLIN FILE
	IF (SWITCH.EQ.9) GO TO INUSE2

	SWITCH = 1
	XCALL FILES (6,'I',122,SWITCH)		;CSTIDX FILE
	IF (SWITCH.EQ.9) GO TO INUSE3

	SWITCH = 1
	XCALL FILES (7,'I',121,SWITCH)		;CSTMAS FILE
	IF (SWITCH.EQ.9) GO TO INUSE6

	OPEN (2,O,POITEM)			;open temporary file

	LOKCTL = 1
	XCALL IO (7,CSTCTL,1,READ,LOKCTL)

	XCALL WATE (4,V)
	XCALL OUTPT (2,1,0,'DAILY P.O. ENTRY REPORT',1)
	XCALL OUTPT (2,45,0,'CREATING DPO:PORTMP - "POENTR.DBR"',1)
PROCES,
	TMPREC=1
	PORTMP=
	LOKCTL = 1
	XCALL IO (2,PORTMP,TMPREC,WRITE,LOKCTL)	;write a blank control record

;read p.o header file to get p.o.'s for the date range entered
REREAD,
	LOKCTL = NOABRT
	XCALL IOS (3,PORHDR,READ,LOKCTL)
	IF (LOKCTL.NE.0) GOTO ENDFIL
	IF (HPOSTS.EQ.'C'.OR.HPOSTS.EQ.'X') GOTO REREAD
	HDATE(1,2) = HPODTE(5,6)
	HDATE(3,6) = HPODTE(1,4)
	IF (HDATE.LT.STDATE.OR.HDATE.GT.ENDATE) GOTO REREAD

;find all line items for the valid p.o.
REPEAT,
	KEY1 = HPONUM
	KEY2 = HRLNUM
	LOKCTL = 1
	XCALL ISIO (4,POLINE,KEYPON,READ,LOKCTL)
CONTIN,
	IF (LOKCTL.NE.0) GOTO REREAD
	IF (LPONUM.LT.HPONUM) GO TO NXTREC
	IF (LPONUM.GT.HPONUM) GO TO REREAD
	IF (LRLNUM.LT.HRLNUM) GO TO NXTREC
	IF (LRLNUM.GT.HRLNUM) GO TO REREAD
	GOTO GETITM
NXTREC,
	LOKCTL = 1
	XCALL IOS (4,POLINE,READ,LOKCTL)
	GOTO CONTIN

;now find item in cost master to get standard cost
GETITM,
	CSTMAS =
	BSMID = 1
	SRCCTL = 1
	SRCOPT = 4
	XCALL SERCH (6,CSTIDX,LITMNO,1,15,ORG121,BSMID,SRCCTL,SRCOPT,18,22,0,0,0,0)
	IF (LITMNO.EQ.'A-151-1841') XCALL MESAG (CSTIDX,1)
	IF (SRCCTL.EQ.0) GO TO ITMFND
	CSTMAS=
	GOTO ADDTMP
ITMFND,
	XCALL IO (7,CSTMAS,IRC121,READ,LOKCTL)
	IF (LOKCTL.EQ.1) CSTMAS=

;now add a record for each valid line item on each p.o. to temporary file
ADDTMP,
	PORTMP=
	TACCT(1,2) = LPONCD(1)	
	TPODTE = HPODTE
	TPONUM = HPONUM
	TRLNUM = HRLNUM
	TPOTYP = HPOTYP
	TPOSTS = HPOSTS
	TVENDR = HVENDR
	TITMNO = LITMNO
	TITMDS = LITMDS
	TSTCST = CSVMCS
	TEXCST = LEXCST
	TACCST = LACCST
	TQTYOR = LQTYOR
	TQTYRC = LQTYRC
	TDTERQ = LDTERQ
	TDTERC = LDTERC
	TINSTS = LINSTS
	LOKCTL = 1
	INCR TMPREC
	XCALL IO (2,PORTMP,TMPREC,WRITE,LOKCTL)
	GOTO NXTREC

ENDFIL,
	PORTMP(1,30) = BRACKS
	PORTMP(31,60) = BRACKS
	PORTMP(61,90) = BRACKS
	PORTMP(91,120) = BRACKS
	PORTMP(121,124) = BRACKS
	LOKCTL = 1
	INCR TMPREC
	XCALL IO (2,PORTMP,TMPREC,WRITE,LOKCTL)

	CLOSE 2
	XCALL WATE (4,V)
	XCALL OUTPT(2,45,0,'SORTING DPO:PORTMP - "POENTR.DBR"',1)
;;Sort the p.o. line item file by item into a temporary file for this report
	SORT (IN=POITEM, OUT=POITEM, RECORD=PORTMP,
&		KEY=(TACCT,TITMNO) )

	OPEN (2,I,POITEM)			;open temporary file
PRTRPT,
	XRECNO=1
	SAVGL = ']]]]'
	XCALL WATE (2,V)
PRLOOP,
	INCR XRECNO
	LOKCTL = NOABRT
	XCALL IO (2,PORTMP,XRECNO,READ,LOKCTL)
	IF (PORTMP(1,9).EQ.BRACKS) GOTO DONE
	IF (TACCT.NE.SAVGL) CALL NEWGL

	KGLACT = TACCT(1,2)
	KIMNUM = TITMNO
	KIMDES = TITMDS
	KPONUM = TPONUM,'ZZZXXX'
	KDELDT = TDTERQ,'XX/XX/XX'
	KPOQTY = TQTYOR,'ZZZ,ZZX'
	KUCOST = TEXCST/100,MASK
	KUSTD = TSTCST/1000,MASK
	EXTCST = TQTYOR*(TEXCST/100)
	EXTSTD = TQTYOR*(TSTCST/1000)
	KPEXT = EXTCST,MASK
	KDIFF = EXTCST-EXTSTD,MASK2
	GLEXT = GLEXT + EXTCST
	GLSTD = GLSTD + EXTSTD
	ACCEXT = ACCEXT + EXTCST
	ACCSTD = ACCSTD + EXTSTD
	CALL PRINT
	INCR CNTR
	GOTO PRLOOP

NEWGL,
	IF (SAVGL.EQ.']]]]') GOTO ENDGL
	PLINE = DASHES
	CALL PRINT
	PLINE(1,21) = 'TOTALS FOR G/L ACCT #'
	PLINE(23,26) = SAVGL
	KPEXT = GLEXT,MASK
	KDIFF = GLEXT - GLSTD,MASK2
	CALL PRINT
	XCALL LINFD(1)
	LINCNT = LINCNT + 1
ENDGL,
	SAVGL = TACCT
	GLEXT = 0
	GLSTD = 0
	RETURN

DONE,
	CALL NEWGL
	XCALL LINFD(1)
	LINCNT = LINCNT+1
	PLINE = DASHES
	CALL PRINT
	PLINE(1,13) = 'GRAND TOTALS:'
	KPEXT = ACCEXT,MASK
	KDIFF = ACCEXT - ACCSTD,MASK2
	CALL PRINT
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	XCALL WATE (3,V)
	GO TO EXIT
;**********************************************************************
NONE,
	XCALL MESAG ('?ERROR IN TEMPORARY FILE - PORTMP.',1)
	GO TO EXIT
;**********************************************************************
PRINT,
	PRTCTL = 132
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,HDR3,
&		LEGEND,'NO LEGEND',' ',LINCTL,132,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;**********************************************************************
CLOSES,
	SWITCH = 4
	XCALL FILES (7,'I',121,SWITCH)
INUSE6,
	SWITCH = 4
	XCALL FILES (6,'I',122,SWITCH)
INUSE3,
	SWITCH = 4
	XCALL FILES (4,'SI',152,SWITCH)
INUSE2,
	SWITCH = 4
	XCALL FILES (3,'SI',151,SWITCH)
INUSE1,
ENDOFF,
	XCALL PGCHN ('PO:VAMENU',1)

;**********************************************************************
EXIT,
	CLOSE 2
;; 	XCALL DELET(2,POITEM)
	GOTO CLOSES
;**********************************************************************

END

