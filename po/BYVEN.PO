SUBROUTINE BYVEN
;  BYVEN / POR - D11
;
;		AUTHOR: 1-DEC-81  WRF
;		MODIFIED:  DVD  5-OCT-82  POR13-D11D-1
;
;		::CRELPOR.DEF::
;**********************************************************************
;		PURCHASE ORDER & RECEIVING -- Release 1.1
;		RELEASED: 1-MAY-82
;**********************************************************************
;:
;
;		::PCPYEMS.DEF::
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of Mini-Computer Business Applications, Inc. (MCBA) of
;		Glendale, California, USA and Effective Management Systems, Inc.
;		of Menomonee Falls, Wisconsin, USA embodying substantial
;		creative effort and confidential information, ideas and
;		expressions, no part of which may be reproduced or transmitted
;		in any form or by any means, electronic, mechanical or
;		otherwise, including photocopying and recording or in
;		connection with any information storage or retrieval system
;		without the permission in writing from MCBA.
;
;		COPYRIGHT NOTICES:  Copyright <C> 1980, 1981, 1982
;		by Mini-Computer Business Applications, Inc.
;		AN UNPUBLISHED WORK.  Copyright <C> 1980, 1981, 1982
;		by Effective Management Systems, Inc.  AN UNPUBLISHED WORK.
;
;
;		COMPILE & LINK PER DOCUMENTATION INSTALLATION NOTES.
;
;:
;		SUBROUTINE TO ACCEPT RANGES FOR SCHEDULED RECEIPTS REPORT
;			BY VENDOR.
;
RANGES		,A		;ALL RANGES
;**********************************************************************
RECORD RANGER
	STAMAJ	,A15		;STARTING VALUE OF MAJOR KEY
	ENDMAJ	,A15		;ENDING VALUE OF MAJOR KEY
	STADAT	,D6		;STARTING PROMISED DATE
	ENDDAT	,D6		;ENDING PROMISED DATE
	LTDAYS	,D3		;AT LEAST X DAYS LATE
	CLOSED	,D1		;1= INCLUDE CLOSED LINES, 2 = EXCLUDE.
	ALLRCV	,D1		;1 = INCLUDE 100% RECEIVED LINES, 2 = EXCLUDE.
	OBUYER	,A2		;ONLY INCLUDE THIS BUYER (BLANK=INCLUDE ALL)
	ONOTFY	,A3		;ONLY INCLUDE THIS NOTIFY (BLANK=INCLUDE ALL)
	OMOVTO	,A3		;ONLY INCLUDE THIS MOVE-TO (BLANK=INCLUDE ALL)
	OSHPTO	,A2		;ONLY INCLUDE THIS SHIP-TO (BLANK=INCLUDE ALL)
	OUSERC	,A2		;ONLY INCLUDE THIS USER CODE (BLANK=INCLUDE ALL)
	GETOUT	,D1		;1 = RETURN TO RANGE SELECTION
;**********************************************************************
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2

RECORD
	BLANKS	,A10
	BRACKS	,A30,	']]]]]]]]]]]]]]]]]]]]]]]]]]]]]]'
	BSMID	,D5
	CNGCTL	,D4
	DECMAL	,D4
	DSPSW	,D1
	ENTRY	,A30
	EOF	,D1
	EPO	,D1
	HDATE	,D6
	HRECNO	,D5
	INXCTL	,D1
	LINCNT	,D2,	60
	LINCTL	,D1
	LOKCTL	,D1
	LPSW	,D1
	LRECNO	,D5
	MUSTPT	,D1
	MSGCTL	,D1
	N	,D1
	PGCNT	,D3,	000
	PGNUM	,D2
	PRTCTL	,D3
	PRTTYP	,A1
	READ	,D1,	0
	RECNO	,D5
	RPTNUM	,D3
	SPLFIL	,A14
	SRCOPT	,D1
	SRCCTL	,D1
	SWITCH	,D1
	TITLE	,A2
	TODAY	,D6
	V	,D1
	VADR	,4A30
	WHATNO	,D2

PROC
	XCALL TERID (V)
	RANGER = RANGES
	GETOUT =
	CNGCTL =
BEGIN,
	XCALL OUTPT (2,1,2,'SCHEDULED RECEIPTS BY VENDOR',1)
	XCALL OUTPT (3,8,0,'1. STARTING VENDOR NUMBER',V)
	XCALL OUTPT (4,8,0,'2. ENDING VENDOR NUMBER',V)
	XCALL OUTPT (5,8,0,'3. STARTING PROMISED DATE ',V)
	XCALL OUTPT (6,8,0,'4. ENDING PROMISED DATE ',V)
	XCALL OUTPT (7,8,0,'5. AT LEAST X DAYS LATE ',V)
	XCALL OUTPT (8,8,0,'6. INCLUDE CLOSED LINES ? ',V)
	XCALL OUTPT (9,8,0,'7. INCLUDE 100% RECEIVED ? ',V)
	XCALL OUTPT (3,48,0,'8. BUYER ',V)
	XCALL OUTPT (4,48,0,'9. NOTIFY ',V)
	XCALL OUTPT (5,47,0,'10. MOVE TO ',V)
	XCALL OUTPT (6,47,0,'11. SHIP TO ',V)
	XCALL OUTPT (7,47,0,'12. USER CODE ',V)

STVEN,
	CTL = '03,36,04,00,AE'
	CALL INPUT
	GO TO (BEGIN,EXIT), INXCTL
	IF (ENTRY.EQ.BLANKS) GO TO ALLVEN
	XCALL FRMAT (ENTRY,4)
	STAMAJ = ENTRY
	XCALL OUTPT (ROW,COL,0,STAMAJ(1,4),V)
	IF (ENDMAJ(1,4).EQ.']]]]') GO TO ENDVE2
	IF (CNGCTL.NE.1) GO TO ENDVE2
	IF (ENDMAJ(1,4).LT.STAMAJ(1,4)) GO TO MSG1
	GO TO ANYCNG
ENDVEN,
	IF (ENDMAJ(1,4).EQ.']]]]') GO TO STVEN
ENDVE2,
	CTL = '04,36,04,00,A '
	CALL INPUT
	IF (INXCTL.EQ.1) GO TO BEGIN
	IF (ENTRY.EQ.BLANKS) ENTRY(1,4) = STAMAJ
	XCALL FRMAT (ENTRY,4)
	ENDMAJ = ENTRY(1,4)
	XCALL OUTPT (ROW,COL,0,ENDMAJ(1,4),V)
	IF (ENDMAJ(1,4).LT.STAMAJ(1,4)) GO TO MSG1
	GO TO (ANYCNG), CNGCTL

STDTE,
	CTL = '05,36,06,00,D '
	CALL INPUT
	IF (INXCTL.EQ.1) GO TO BEGIN
	IF (ENTRY.EQ.BLANKS) GO TO ALLDTE
	HDATE = ENTRY(1,6)
	STADAT(1,2) = HDATE(5,6)
	STADAT(3,6) = HDATE(1,4)
	IF (ENDDAT.EQ.999999) GO TO ENDTE2
	IF (CNGCTL.NE.1) GO TO ENDTE2
	IF (ENDDAT.LT.STADAT) GO TO MSG2
	GO TO ANYCNG
ENDTE,
	IF (ENDDAT.EQ.999999) GO TO STDTE
ENDTE2,
	CTL = '06,36,06,00,D '
	CALL INPUT
	IF (INXCTL.EQ.1) GO TO BEGIN
	IF (ENTRY.EQ.BLANKS) GO TO SAMEDA
	HDATE = ENTRY(1,6)
	ENDDAT(1,2) = HDATE(5,6)
	ENDDAT(3,6) = HDATE(1,4)
	IF (ENDDAT.LT.STADAT) GO TO MSG2
	GO TO (ANYCNG),CNGCTL

LATE,
	CTL = '07,36,03,00,# '
	CALL INPUT
	IF (INXCTL.EQ.1) GO TO BEGIN
	LTDAYS = ENTRY
	XCALL DSPLY (MAX,ROW,COL,LTDAYS,1,V)
	GO TO (ANYCNG), CNGCTL

CLOSLN,
	CTL = '08,36,01,00,YY'
	CALL INPUT
	IF (INXCTL.EQ.0.OR.INXCTL.GE.3) GO TO CLOSLN
	CLOSED = INXCTL
	GO TO (ANYCNG), CNGCTL

RCVDLN,
	CTL = '09,36,01,00,YY'
	CALL INPUT
	IF (INXCTL.EQ.0.OR.INXCTL.GE.3) GO TO RCVDLN
	ALLRCV = INXCTL
	GO TO (ANYCNG), CNGCTL

IBUYER,
	CTL = '03,62,02,00,A '
	CALL INPUT
	IF (INXCTL.EQ.1) GO TO BEGIN
	IF (ENTRY.EQ.BLANKS) GO TO ALLBYR
	OBUYER = ENTRY
	XCALL OUTPT (ROW,COL,0,OBUYER,V)
IBUY2,
	GO TO (ANYCNG), CNGCTL

INOTFY,
	CTL = '04,62,03,00,A '
	CALL INPUT
	IF (INXCTL.EQ.1) GO TO BEGIN
	IF (ENTRY.EQ.BLANKS) GO TO ALLNFY
	ONOTFY = ENTRY
	XCALL OUTPT (ROW,COL,0,ONOTFY,V)
INOT2,
	GO TO (ANYCNG), CNGCTL

IMOVTO,
	CTL = '05,62,03,00,A '
	CALL INPUT
	IF (INXCTL.EQ.1) GO TO BEGIN
	IF (ENTRY.EQ.BLANKS) GO TO ALLMOV
	OMOVTO = ENTRY
	XCALL OUTPT (ROW,COL,0,OMOVTO,V)
IMOV2,
	GO TO (ANYCNG), CNGCTL

ISHPTO,
	CTL = '06,62,02,00,A '
	CALL INPUT
	IF (INXCTL.EQ.1) GO TO BEGIN
	IF (ENTRY.EQ.BLANKS) GO TO ALLSHP
	OSHPTO = ENTRY
	XCALL OUTPT (ROW,COL,0,OSHPTO,V)
ISHP2,
	GO TO (ANYCNG), CNGCTL

IUSERC,
	CTL = '07,62,02,00,A '
	CALL INPUT
	IF (INXCTL.EQ.1) GO TO BEGIN
	IF (ENTRY.EQ.BLANKS) GO TO ALLUSR
	OUSERC = ENTRY
	XCALL OUTPT (ROW,COL,0,OUSERC,V)
IUSE2,
	CNGCTL =
ANYCNG,
	WHATNO =
	XCALL ANYCN (CNGCTL,WHATNO)
	GO TO (EXIT,LEAVE), CNGCTL+2
CNGBR,
	GO TO (STVEN,ENDVEN,STDTE,ENDTE,LATE,CLOSLN,RCVDLN,IBUYER,
&		INOTFY,IMOVTO,ISHPTO,IUSERC), WHATNO
BADCNG,
	CNGCTL = 3
	GO TO ANYCNG
;**********************************************************************
ALLVEN,
	XCALL OUTPT (3,36,0,'ALL ',V)
	XCALL OUTPT (4,36,0,'    ',V)
	STAMAJ =
	ENDMAJ = ']]]]'
	GO TO (ANYCNG), CNGCTL
	GO TO STDTE
;**********************************************************************
MSG1,
	XCALL MESAG ('STARTING/ENDING VENDOR NUMBERS ARE OUT OF ORDER',1)
	ENDMAJ = ']]]]'
	GO TO STVEN
;**********************************************************************
ALLDTE,
	XCALL OUTPT (5,36,0,'ALL   ',V)
	XCALL OUTPT (6,36,0,'      ',V)
	STADAT = 0
	ENDDAT = 999999
	GO TO (ANYCNG), CNGCTL
	GO TO LATE
;**********************************************************************
SAMEDA,
	IF (STADAT.EQ.0) GO TO STDTE
	ENDDAT = STADAT
	ENTRY(1,5) = STADAT(3,6), 'XX/XX'
	ENTRY(6,8) = STADAT(1,2), '/XX'
	XCALL OUTPT (ROW,COL,0,ENTRY(1,8),V)
	GO TO (ANYCNG), CNGCTL
	GO TO LATE
;**********************************************************************
MSG2,
	XCALL MESAG ('STARTING/ENDING PROMISED DATES ARE OUT OF ORDER',1)
	ENDDAT = 999999
	GO TO STDTE
;**********************************************************************
ALLBYR,
	XCALL OUTPT (ROW,COL,0,'ALL',V)
	OBUYER =
	GO TO IBUY2
;**********************************************************************
ALLNFY,
	XCALL OUTPT (ROW,COL,0,'ALL',V)
	ONOTFY =
	GO TO INOT2
;**********************************************************************
ALLMOV,
	XCALL OUTPT (ROW,COL,0,'ALL',V)
	OMOVTO =
	GO TO IMOV2
;**********************************************************************
ALLSHP,
	XCALL OUTPT (ROW,COL,0,'ALL',V)
	OSHPTO =
	GO TO ISHP2
;**********************************************************************
ALLUSR,
	XCALL OUTPT (ROW,COL,0,'ALL',V)
	OUSERC =
	GO TO IUSE2
;**********************************************************************
EXIT,
	GETOUT = 1
LEAVE,
	RANGES = RANGER
	RETURN
;**********************************************************************

INPUT,
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,V)
	RETURN
;**********************************************************************

END

