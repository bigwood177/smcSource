;  ITMAGE / POR - D11
;
;
;		ON ORDER DOLLARS AGED BY ITEM REPORT
;
;
;
RECORD POLINE
	.INCLUDE 'DEF:RD152A.DEF'

RECORD PLINE
 		,A132

RECORD LINE1,X
	KCOMM	,A4
		,A1
	KIMNUM	,A15
		,A1
	KIMDES	,A27
		,A1
	KBKT1	,A5
		,A1
	KBKT2	,A5
		,A1
	KBKT3	,A5
		,A1
	KBKT4	,A5
		,A1
	KBKT5	,A5
		,A1
	KBKT6	,A5
		,A1
	KBKT7	,A5
		,A1
	KBKT8	,A5
		,A1
	KBKT9	,A5
		,A1
	KBKT10	,A5
		,A1
	KBKT11	,A5
		,A1
	KBKT12	,A5
		,A1
	KBKT13	,A5
		,A1
	KTOTL	,A5

RECORD HDR1
		,A2
		,A11,	'PART-NUMBER'
		,A8
		,A11,	'DESCRIPTION'
		,A18
		,A4,	'PAST'
	MTHS	,11A6
		,A1
		,A6,	'FUTURE'
		,A1
		,A4,	'TOTL'
RECORD LEGEND
		,A1
		,A20,'COMMODITY CODES FROM'
		,A3
	COM1	,A4
		,A3
		,A2,'TO'
		,A3
	COM2	,A4

RECORD DASHES
		,A50,	'--------------------------------------------------'
		,A50,	'--------------------------------------------------'
		,A32,	'--------------------------------'

RECORD HLDATE
	YY	,D2
	MM2	,D2
RECORD
	CNGCTL	,D1
	WHATNO	,D1
	TNUM	,D3
	BLANKS	,A15
	SAVITM	,A15
	BRACKS	,A30,	']]]]]]]]]]]]]]]]]]]]]]]]]]]]]]'
	MASK	,A9,	'Z,ZZZ,ZZX'
	BSMID	,D5
	CNTR	,D3
	DSPSW	,D1
	EOF	,D1
	FRST	,D1
	IDFLAG	,D1
	LINCNT	,D2,	60
	LINCTL	,D1
	LOKCTL	,D1
	LPSW	,D1
	NOABRT	,D1,	1
	PGCNT	,D3,	000
	PRTCTL	,D3
	PRTTYP	,A1
	READ	,D1,	0
	RPTNUM	,D3
	SPLFIL	,A14
	SRCOPT	,D1
	SRCCTL	,D1
	SWITCH	,D1
	TITLE	,A29,	'ON ORDER DOLLARS AGED BY ITEM'
	TODAY	,D6
	V	,D1
	WTARG	,D1,	2
	XRECNO	,D5
	INXCTL	,D1
	ENTRY	,A15
	ALL	,A10
	ASOFDT	,A8
	SITEM	,A15
	EITEM	,A15	
	STCOM	,A4
	ENCOM	,A4
	AMTHS	,12A6,'   JAN','   FEB','   MAR','   APR','   MAY','   JUN',
&		      '   JUL','   AUG','   SEP','   OCT','   NOV','   DEC'

	MTHARR	,12D4
	DOLARR	,14D10
	TOTARR	,14D5
	GOTARR	,14D8
	ORDDTE	,D4
	STDATE	,D4
	TT	,D2
	LSTCOM	,A4
	LSTITM	,A15
	SAVDES	,A30	
	BKT	,D2
	

PROC
	XCALL TERID (V)
	XCALL OUTPT (2,1,2,'ON ORDER DOLLARS AGED BY ITEM',1)
GETCM1,
	XCALL OUTPT (6,20,2,'ENTER STARTING COMMODITY CODE',V)
	XCALL INPUT (6,51,04,0,'AE',ENTRY,INXCTL,V)
	GO TO (GETCM1,ENDOFF), INXCTL
	STCOM = ENTRY (1,4)
	IF (STCOM.EQ.'    ') GOTO ALLCOM
	COM1 = STCOM
GETCM2,
	XCALL OUTPT (7,20,2,'ENTER ENDING COMMODITY CODE',V)
	XCALL INPUT (7,51,04,0,'A ',ENTRY,INXCTL,V)
	GO TO (GETCM2,GETCM1), INXCTL
	ENCOM = ENTRY (1,4)
	IF (ENCOM.EQ.'    ') ENCOM = STCOM
	COM2 = ENCOM
	XCALL OUTPT (7,51,0,ENCOM,V)
	GOTO STITM
ALLCOM,
	ALL = 'ALL CODES'
	XCALL OUTPT(6,51,0,ALL,V)
	STCOM = 
	ENCOM = ']]]]'
	COM1 = ' ALL'
	COM2 = '    '
STITM,
	XCALL OUTPT (9,20,2,'PLEASE ENTER STARTING PART #',V)
	XCALL INPUT (9,50,15,0,'AE',ENTRY,INXCTL,V)
	GO TO (STITM,ENDOFF), INXCTL
	SITEM = ENTRY (1,15)
	IF (SITEM.EQ.BLANKS) GOTO ALLITM
ENITM,
	XCALL OUTPT (10,20,2,'PLEASE ENTER ENDING PART #',V)
	XCALL INPUT (10,50,15,0,'A ',ENTRY,INXCTL,V)
	GO TO (ENITM,STITM), INXCTL
	EITEM = ENTRY (1,15)
	IF (EITEM.EQ.BLANKS) EITEM = SITEM
	XCALL OUTPT(10,50,0,EITEM,V)
	GOTO START
ALLITM,
	ALL = 'ALL PARTS'
	SITEM=
	EITEM=']]]'
	XCALL OUTPT(9,50,0,ALL,V)
START,
	CNGCTL=2
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (GETCM1),CNGCTL
	XCALL WATE (4,V)
	XCALL RDATE (TODAY)
	SPLFIL(5,6) = 'NE'
	MM2 = TODAY(1,2)
	YY = TODAY(5,6)
	MTHS(1) = AMTHS(MM2)
	MTHARR(1) = HLDATE
	STDATE = HLDATE
	TT = 1
HLOOP,
	INCR TT
	IF (TT.GT.11) GOTO CONTIN
	INCR MM2
	IF (MM2.GT.12)
		BEGIN
		MM2 = 1
		INCR YY
		END
	MTHARR(TT) = HLDATE
	MTHS(TT) = AMTHS(MM2)
	GOTO HLOOP

;;Sort the p.o. line item file by code into a temporary file for this report
CONTIN,
	LPSW = 1
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO EXIT
	IF (LPSW.EQ.2) WTARG = 4
	XCALL WATE (4,V)

PROCES,
;;	FILNUM = 152
;;	XCALL FFILE (FILNUM,REDFIL,CLCTL)	;GET FILE SPEC
;;	SORT (IN=REDFIL, OUT=POITEM, RECORD=POLINE,
;;&		KEY=(LITMCC,LITMNO,LDTEPM) )
;;
;;	OPEN (2,I,POITEM)			;open temporary file
	SWITCH = 1
	XCALL FILES (2,'SI',152,SWITCH)
	XCALL WATE (2,V)
	FRST = 1
OPNLOOP,
	CALL READIX
	IF (EOF.EQ.1) GOTO DONE
	IF (LSTCOM.NE.LITMCC) CALL NEWCOM
	IF (LSTITM.NE.LITMNO) CALL NEWITM

	DOLARR(BKT+1) = DOLARR(BKT+1) + ((LQTYOR-LQTYRC)*LEXCST)#2
	DOLARR(14) = DOLARR(14) + ((LQTYOR-LQTYRC)*LEXCST)#2
	GOTO OPNLOOP

NEWITM,
	IF (LSTITM.EQ.BLANKS) GOTO ENDITM
	KIMNUM = LSTITM
	KCOMM = LSTCOM
	KIMDES = SAVDES
	KBKT1 = DOLARR(1)/100
	KBKT2 = DOLARR(2)/100
	KBKT3 = DOLARR(3)/100
	KBKT4 = DOLARR(4)/100
	KBKT5 = DOLARR(5)/100
	KBKT6 = DOLARR(6)/100
	KBKT7 = DOLARR(7)/100
	KBKT8 = DOLARR(8)/100
	KBKT9 = DOLARR(9)/100
	KBKT10 = DOLARR(10)/100
	KBKT11 = DOLARR(11)/100
	KBKT12 = DOLARR(12)/100
	KBKT13 = DOLARR(13)/100
	KTOTL = DOLARR(14)/100
	CALL PRINT
	FOR TT FROM 1 THRU 14
		BEGIN
		TOTARR(TT) = TOTARR(TT) + DOLARR(TT)/100
		GOTARR(TT) = GOTARR(TT) + DOLARR(TT)/100
		DOLARR(TT) =
		END
ENDITM,
	LSTITM = LITMNO
	SAVDES = LITMDS
	RETURN
NEWCOM,
	IF (LSTCOM.EQ.BLANKS) GOTO ENDCOM
	CALL NEWITM
	PLINE = DASHES
	CALL PRINT
	PLINE (6,9)=LSTCOM
	PLINE (11,18) = 'Subtotal'
	KBKT1 = TOTARR(1)
	KBKT2 = TOTARR(2)
	KBKT3 = TOTARR(3)
	KBKT4 = TOTARR(4)
	KBKT5 = TOTARR(5)
	KBKT6 = TOTARR(6)
	KBKT7 = TOTARR(7)
	KBKT8 = TOTARR(8)
	KBKT9 = TOTARR(9)
	KBKT10 = TOTARR(10)
	KBKT11 = TOTARR(11)
	KBKT12 = TOTARR(12)
	KBKT13 = TOTARR(13)
	KTOTL = TOTARR(14)
	CALL PRINT
	XCALL LINFD(1)
	LINCNT = LINCNT+1
ENDCOM,
	FOR TT FROM 1 THRU 14
		BEGIN
		TOTARR(TT) =
		END
	LSTCOM = LITMCC
	RETURN
	
DONE,
	CALL NEWCOM
	XCALL LINFD(1)
	PLINE = DASHES
	CALL PRINT
	PLINE(1,13) = 'GRAND TOTALS:'
	PLINE(46,54) = GOTARR(1),MASK
	PLINE(58,66) = GOTARR(3),MASK
	PLINE(70,78) = GOTARR(5),MASK
	PLINE(82,90) = GOTARR(7),MASK
	PLINE(94,102) = GOTARR(9),MASK
	PLINE(106,114) = GOTARR(11),MASK
	PLINE(118,126) = GOTARR(13),MASK
	CALL PRINT
	PLINE(52,60) = GOTARR(2),MASK
	PLINE(64,72) = GOTARR(4),MASK
	PLINE(76,84) = GOTARR(6),MASK
	PLINE(88,96) = GOTARR(8),MASK
	PLINE(100,108) = GOTARR(10),MASK
	PLINE(112,120) = GOTARR(12),MASK
	PLINE(124,132) = GOTARR(14),MASK
	CALL PRINT
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	XCALL WATE (3,V)
	GO TO EXIT
;**********************************************************************
NONE,
	XCALL MESAG ('?ERROR IN TEMPORARY FILE - TMPVAR.',1)
	GO TO EXIT
;**********************************************************************
;**********************************************************************
;	READ ONE PORLIN RECORD
;**********************************************************************
READIX,
	EOF =
	IF (FRST.EQ.1)
		BEGIN
		READ (2,POLINE,STCOM,KRF=1) [KEY=REREAD, EOF=ENDFIL]
		FRST = 0
		GOTO NXTREC
		END
REREAD,
	LOKCTL = NOABRT
	READS (2,POLINE,ENDFIL)
NXTREC,
	IF (LITMNO(1,6).EQ.']]]DEL') GO TO REREAD
	IF (LINSTS.NE.'N'.AND. LINSTS.NE.'P') GOTO REREAD
	IF (ENCOM.EQ.']]]') GOTO CHKITM
	IF (LITMCC.LT.STCOM.OR.LITMCC.GT.ENCOM) GOTO REREAD
CHKITM,
	IF (EITEM.EQ.']]]') GOTO GOODONE
	IF (LITMNO.LT.SITEM.OR.LITMNO.GT.EITEM) GOTO REREAD
GOODONE,
	ORDDTE(1,2) = LDTEPM(5,6)
	ORDDTE(3,4) = LDTEPM(1,2)
	IF (ORDDTE.LT.STDATE) GOTO PAST
	BKT = 0
TTLOOP,
	INCR BKT
	IF (BKT.GT.11) GOTO FUTURE
	IF (MTHARR(BKT).NE.ORDDTE) GOTO TTLOOP
	RETURN
FUTURE,
	BKT = 12		;THIS IS THE FUTURE BUCKET
	RETURN
PAST,
	BKT = 0
	RETURN
ENDFIL,
	EOF = 1
	RETURN
;**********************************************************************
PRINT,
	PRTCTL = 132
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,'NO HDR','NO HDR',
&		LEGEND,'NO LEGEND',' ',LINCTL,132,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;**********************************************************************
CLOSES,
	XCALL FILES (2,'SI',152,4)
	RETURN
;**********************************************************************
EXIT,
	CALL CLOSES
ENDOFF,
	XCALL PGCHN ('PO:VAMENU',1)

;**********************************************************************

END

