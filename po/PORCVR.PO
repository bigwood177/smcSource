;  PORCVR / POR - D11
;
;		COMPILE & LINK PER DOCUMENTATION INSTALLATION NOTES.
;
;
;
RECORD RCVHST
	.INCLUDE 'DEF:RD154A.DEF'

RECORD CSTMAS
	.INCLUDE 'DEF:RD121A.DEF'

RECORD DUMCST
	.INCLUDE 'DEF:RD121B.DEF'

RECORD CSTIDX
	.INCLUDE 'DEF:RD122A.DEF'

RECORD PLINE
 		,A132

RECORD LINE1,X
		,A1
	KIMNUM	,A15
		,A1
	KIMDES	,A30
		,A1
	KLOC	,A2
		,A3
	KQYRCV	,A7
		,A3
	KPMDAT	,A8
		,A3
	KRQDAT	,A8
		,A3
	KDAYS	,A4
		,A3
	KPONUM	,A6
	KDASH	,A1
	KRLNUM	,A2
	      	,A2
	KEXT  	,A13
		,A3
	KUNIT	,A13

RECORD HDR1
		,A1
		,A11,	'PART-NUMBER'
		,A5
		,A11,	'DESCRIPTION'
		,A19
		,A3,	'LOC'
		,A5
		,A3,	'QTY'
		,A7
		,A4,	'DATE'
		,A7
		,A4,	'DATE'
		,A6
		,A4,	'DAYS'
		,A4
		,A3,	'P/O'
		,A10
		,A8,	'EXTENDED'
		,A7
		,A8,	'EXTENDED'
		,A1

RECORD HDR2
		,A53
		,A8,	'RECEIVED'
		,A2
		,A8,	'PROMISED'
		,A3
		,A8,	'RECEIVED'
		,A4
		,A4,	'LATE'
		,A5
		,A3,	'NO.'
		,A9
		,A9,	'P.O. COST'
		,A3
		,A13,	'STANDARD COST'

RECORD LEGEND
		,A1
		,A18,'DATE RECEIVED FROM'
		,A3
	DTE1	,A8
		,A3
		,A2,'TO'
		,A3
	DTE2	,A8
		,A86

RECORD DASHES
		,A50,	'--------------------------------------------------'
		,A50,	'--------------------------------------------------'
		,A32,	'--------------------------------'

RECORD RKEY
	RITEM	,A15

RECORD
	CNGCTL	,D1
	WHATNO	,D1
	TNUM	,D3
	ACCEXT	,D10
	ACCSTD	,D10
	EXTCST	,D10
	EXTSTD	,D10
	BLANKS	,A15
	BRACKS	,A30,	']]]]]]]]]]]]]]]]]]]]]]]]]]]]]]'
	BSMID	,D5
	CNTR	,D3
	DSPSW	,D1
	EOF	,D1
	IDFLAG	,D1
	LINCNT	,D2,	60
	LINCTL	,D1
	LOKCTL	,D1
	LPSW	,D1
	NOABRT	,D1,	1
	PGCNT	,D3,	000
	PRTCTL	,D3
	PRTTYP	,A1
	READ	,D1,	0
	RPTNUM	,D3
	SPLFIL	,A14
	SRCOPT	,D1
	SRCCTL	,D1
	KEY	,A15
	BSEND	,D5
	SWITCH	,D1
	TITLE	,A24,	'RECEIVING REPORT BY ITEM'
	TODAY	,D6
	V	,D1
	WTARG	,D1,	2
	STRTLO	,A2		;(02)
	XRECNO	,D5
	INXCTL	,D1
	ENTRY	,A15
	ALL	,A10
	ASOFDT	,A8
	STDATE	,D6
	ENDATE	,D6
	HLDATE	,D6
	SITEM	,A15
	EITEM	,A15	
	LOCOPT	,A2
	ALPHA	,A3
	NODAYS	,D6
	

PROC
	XCALL TERID (V)
	XCALL OUTPT (2,1,2,'RECEIVING REPORT BY ITEM',1)
GETDAT,
	XCALL OUTPT (4,20,2,'PLEASE ENTER STARTING RECEIVE DATE',V)
	XCALL INPUT (4,56,6,0,'DE',ENTRY,INXCTL,V)
	GO TO (GETDAT,ENDOFF), INXCTL
	HLDATE = ENTRY (1,6)
	IF (HLDATE.EQ.0) XCALL RDATE (HLDATE)
	IF (HLDATE.EQ.0) GO TO GETDAT
	STDATE(1,2) = HLDATE(5,6)
	STDATE(3,6) = HLDATE(1,4)
	DTE1 = HLDATE, 'XX/XX/XX'
	ASOFDT = HLDATE, 'XX/XX/XX'
	XCALL OUTPT (4,56,0,ASOFDT,V)
GETDT2,
	XCALL OUTPT (5,20,2,'PLEASE ENTER ENDING RECEIVE DATE',V)
	XCALL INPUT (5,56,6,0,'D ',ENTRY,INXCTL,V)
	GO TO (GETDAT,GETDAT), INXCTL
	HLDATE = ENTRY (1,6)
	IF (HLDATE.EQ.0) XCALL RDATE (HLDATE)
	IF (HLDATE.EQ.0) GO TO GETDT2
	ENDATE(1,2) = HLDATE(5,6)
	ENDATE(3,6) = HLDATE(1,4)
	DTE2 = HLDATE, 'XX/XX/XX'
	ASOFDT = HLDATE, 'XX/XX/XX'
	XCALL OUTPT (5,56,0,ASOFDT,V)
STITM,
	XCALL OUTPT (7,20,2,'PLEASE ENTER STARTING PART #',V)
	XCALL INPUT (7,50,15,0,'A ',ENTRY,INXCTL,V)
	GO TO (STITM,GETDT2), INXCTL
	SITEM = ENTRY (1,15)
	IF (SITEM.EQ.BLANKS) GOTO ALLITM
ENITM,
	XCALL OUTPT (8,20,2,'PLEASE ENTER ENDING PART #',V)
	XCALL INPUT (8,50,15,0,'A ',ENTRY,INXCTL,V)
	GO TO (ENITM,STITM), INXCTL
	EITEM = ENTRY (1,15)
	IF (EITEM.EQ.BLANKS) EITEM = SITEM
	XCALL OUTPT (8,50,0,EITEM,V)
;>(02)
LOC,
	XCALL OUTPT (10,20,2,'LOCATION TO PRINT',V)
	XCALL INPUT (10,39,02,0,'A ',ENTRY,INXCTL,V)
	GOTO (LOC,ENITM), INXCTL
	IF (ENTRY.EQ.'  ') ENTRY = ']]'
	LOCOPT = ENTRY(1,2)
	STRTLO = LOCOPT
	ALPHA = LOCOPT
	IF (LOCOPT.EQ.']]') ALPHA = 'ALL'
	XCALL OUTPT (10,39,0,ALPHA,V)
	GOTO START
;<(02)
ALLITM,
	ALL = 'ALL PARTS'
	SITEM=
	EITEM='[[['
	XCALL OUTPT(7,50,0,ALL,V)
	GOTO LOC
START,
	CNGCTL=2
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (GETDAT),CNGCTL
	XCALL WATE (4,V)
	XCALL RDATE (TODAY)
	SPLFIL(5,6) = 'NA'

	LPSW = 1
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO EXIT

	IF (LPSW.EQ.2) WTARG = 4
	XCALL WATE (WTARG,V)
	
	SWITCH = 5		
	XCALL FILES (2,'SI',154,SWITCH)		;RCVHST FILE

	SWITCH = 5
	XCALL FILES (3,'I',122,SWITCH)		;CSTIDX FILE
	IF (SWITCH.EQ.9) GOTO EXIT
	
	SWITCH = 5
	XCALL FILES (1,'I',121,SWITCH)		;CSTMAS FILE
	IF (SWITCH.NE.9) GOTO PROCES
	
	XCALL FILES (3,'I',122,4)
	GOTO EXIT	

PROCES,
	LOKCTL = 1
	XCALL IO (1,DUMCST,1,READ,LOKCTL)

	RITEM = SITEM
	FIND (2,RCVHST,RKEY,KRF="ITEMDATE")[ERR=OPNLOOP]

OPNLOOP,
	CALL READIX
	IF (EOF.EQ.1) GOTO DONE

	SRCCTL = 0
	BSEND = ORG121
	KEY = RCVINO
	XCALL SERCH (3,CSTIDX,KEY,1,15,BSEND,BSMID,SRCCTL,4,18,22,0,0,0,0)
	GOTO (NOTFND),SRCCTL

	LOKCTL = 1
	XCALL IO (1,CSTMAS,IRC121,READ,LOKCTL)
	GOTO GOON
NOTFND,
	CSVMCS = 0
GOON,
	KIMNUM = RCVINO
	KIMDES = RCVDSC
	KLOC = RSHPTO			;(02)
	KPONUM = RPONUM,'ZZZXXX'
	KDASH = '-'
	KRLNUM = RRLNUM,'XX'
	HLDATE(1,4) = RDTERC(3,6)
	HLDATE(5,6) = RDTERC(1,2)
	KRQDAT = HLDATE,'XX/XX/XX'
	KPMDAT = RDTEPM,'XX/XX/XX'
	KQYRCV = RCVNOW,'ZZZ,ZZX'
	NODAYS = (RDTERC(1,2)-RDTEPM(5,6))*360 + (RDTERC(3,4)-RDTEPM(1,2))*30
&	+ (RDTERC(5,6)-RDTEPM(3,4))
	KDAYS = NODAYS
	KUNIT = 
	EXTSTD = 0
	IF (CSVMCS.NE.0) EXTSTD = (CSVMCS/1000)*RCVNOW
	KUNIT = EXTSTD,'Z,ZZZ,ZZX.XX-'
	EXTCST = RCVNOW*(RNWCST/100)
	KEXT = EXTCST,'Z,ZZZ,ZZX.XX-'
	ACCEXT = ACCEXT + EXTCST
	ACCSTD = ACCSTD + EXTSTD
	CALL PRINT
	INCR CNTR
	GO TO OPNLOOP

DONE,
	XCALL LINFD(1)
	LINCNT = LINCNT+1
	PLINE = DASHES
	CALL PRINT
	PLINE(1,12) = 'GRAND TOTAL:'
	KEXT = ACCEXT,'ZZ,ZZZ,ZZX.XX'
	KUNIT = ACCSTD,'ZZ,ZZZ,ZZX.XX'
	CALL PRINT
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	XCALL WATE (3,V)
	GO TO EXIT

;**********************************************************************
;	READ ONE RCVHST RECORD AND RETURN
;**********************************************************************
READIX,
	EOF =
REREAD,
	LOKCTL = NOABRT
	XCALL IOS (2,RCVHST,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GOTO ENDFIL
	IF (RDTERC.LT.STDATE.OR.RDTERC.GT.ENDATE) GOTO REREAD
	IF (RNOSTD.EQ.1) GOTO REREAD
	IF (EITEM.EQ.'[[[') GOTO CHKLOC			;(02)
	IF (RCVINO.LT.SITEM) GOTO REREAD
	IF (RCVINO.GT.EITEM) GOTO ENDFIL
CHKLOC,
	IF (STRTLO.EQ.']]') RETURN			;(02)
	IF (RSHPTO.NE.STRTLO) GOTO REREAD		;(02)
	RETURN
ENDFIL,
	EOF = 1
	RETURN
;**********************************************************************
PRINT,
	PRTCTL = 132
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,'NO HDR',
&		LEGEND,'NO LEGEND',' ',LINCTL,132,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;**********************************************************************
CLOSES,
	XCALL FILES (1,'I',121,4)
	XCALL FILES (3,'I',122,4)
	XCALL FILES (2,'SI',154,4)
	RETURN
;**********************************************************************
EXIT,
	CALL CLOSES
ENDOFF,
	XCALL PGCHN ('PO:VAMENU',1)
;**********************************************************************

END

