;  PRGPUR / POR - D11
;
;
;
;		MARKS PURCHASE ORDERS FOR DELETION
;
;
RECORD HEADER
	.INCLUDE 'DEF:RD151A.DEF'

RECORD POLINE
	.INCLUDE 'DEF:RD152A.DEF'

RECORD VENDOR
	.INCLUDE 'DEF:RD011A.DEF'

RECORD DUMVEN
	.INCLUDE 'DEF:RD011B.DEF'

RECORD VENIDX
	.INCLUDE 'DEF:RD012A.DEF'

RECORD KEYPON
	KEY1	,D6
	KEY2	,D2

RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2

RECORD TABLE
	TNO	,D2
	CDE	,A2
	SQ	,D1
	DSC	,A30

RECORD TBLKEY
	TKEYNO	,D2
	TKEYCD	,A2
	TKEYSQ	,D1

RECORD
	ALL	,D1
	APONUM	,A9
	BLANKS	,A10
	BRACKS	,A30,	']]]]]]]]]]]]]]]]]]]]]]]]]]]]]]'
	BSMID	,D5
	BUYER	,A2
	CDATE	,D6
	CNGCTL	,D1
	DUMMY	,A2
	ENTRY	,A30
	EOF	,D1
	EPO	,D1
	EVTFAC	,D5
	HDATE	,D6
	HRECNO	,D5
	INXCTL	,D1
	JDATE	,D6
	LOKCTL	,D1
	LRECNO	,D5
	N	,D1
	NOABRT	,D1,	1
	NONE	,D1
	PTDSLA	,D6
	PTLDTM	,D5
	PTPREJ	,D4
	PTPVAR	,D4
	READ	,D1,	0
	RECNO	,D5
	SRCOPT	,D1
	SRCCTL	,D1
	SUB	,D2
	SWITCH	,D1
	V	,D1
	WHATNO	,D2
	WRITE	,D1,	1
	DELETE	,D1,	4

PROC
	XCALL TERID (V)
	V = 1
	XCALL OUTPT (2,1,2,' ',1)
	XCALL WATE (4,V)

	SWITCH = 2
	XCALL FILES (1,'SU',151,SWITCH)		; FILE 151, PORHDR.
	IF (SWITCH.EQ.9) GO TO INUSE1

	SWITCH = 2
	XCALL FILES (2,'SU',152,SWITCH)		; FILE 152, PORLIN.
	IF (SWITCH.EQ.9) GO TO INUSE2

	SWITCH = 1
	XCALL FILES (4,'U',011,SWITCH)		; FILE 011, VENMAS.
	IF (SWITCH.EQ.9) GO TO INUSE3

	SWITCH = 1
	XCALL FILES (5,'I',012,SWITCH)		; FILE 012, VENIDX.
	IF (SWITCH.EQ.9) GO TO INUSE4

BEGIN,
	XCALL OUTPT (2,1,0,'DELETE CLOSED P.O.S',V)
;**********************************************************************
;	DELETE CONTROL ROUTINE
;**********************************************************************
PROCES,
	XCALL WATE (4,V)
	CALL READHD		;READ FIRST IN-RANGE HEADER
	IF (EOF) GO TO NONE

	LOKCTL = 0
	XCALL IO (4,DUMVEN,1,READ,LOKCTL)
	IF (LOKCTL.EQ.1) GO TO EXIT
	UNLOCK 4

	CALL DELRUN		;DELETE  PURCHASE ORDERS

	GO TO EXIT


;**********************************************************************

NONE,
	XCALL MESAG ('THERE ARE NO P/OS THAT QUALIFY FOR PURGING.',2)
	NONE = 1
	GO TO EXIT

;**********************************************************************
;	EACH PASS DISPLAYS ONE PURCHASE ORDER:
;**********************************************************************

DELRUN,
	VENDOR =
	SRCOPT = 4
	BSMID = 1
	SRCCTL = 1
	XCALL SERCH (5,VENIDX,HVENDR,1,4,ORG011,BSMID,SRCCTL,SRCOPT,6,10,
&			0,0,0,0)
	IF (SRCCTL.EQ.0) GO TO FOUND
	NAME = 'VENDOR NOT ON FILE'
	GO TO DELRU2
FOUND,
	LOKCTL = NOABRT
	XCALL IO (4,VENDOR,IRC011,READ,LOKCTL)
DELRU2,
	IF (HPOSTS.EQ.'C'.OR.HPOSTS.EQ.'D') INCR POSYTD
	LOKCTL = NOABRT
	XCALL ISIO (1,HEADER,PORKEY,DELETE,LOKCTL)

	CALL READLN		;FINDS THE FIRST CORRESPONDING LINE
	IF (EPO.OR.EOF) GO TO FINISH

	CALL LINES		;MARKS EACH LINE FOR DELETION AND TABULATES

FINISH,
	IF (SRCCTL) GO TO NEWHDR		;VENDOR NOT ON FILE
	LOKCTL = NOABRT
	XCALL IO (4,VENDOR,IRC011,WRITE,LOKCTL)
	LOKCTL = NOABRT
	XCALL IO (4,DUMVEN,MAX011,READ,LOKCTL)
	LOKCTL = NOABRT
	XCALL IO (4,DUMVEN,1,READ,LOKCTL)
	UNLOCK 4

NEWHDR,
	CALL READHD
	IF (EOF) RETURN
	GO TO DELRUN

;**********************************************************************
;	EACH PASS DELETES LINE AND TABULATES STATISTICS
;**********************************************************************

LINES,
	IF (LINSTS.NE.'X'.AND.LQTYRC.GE.LQTYOR.AND.LQTYIV.GE.LQTYOR)
&						LINSTS = 'C'
	IF (LINSTS.EQ.'C') CALL LNSTAT
	LOKCTL = NOABRT
	XCALL ISIO (2,POLINE,POLKEY,DELETE,LOKCTL)
	CALL REPEAT
	IF (EPO.OR.EOF) RETURN
	GO TO LINES

;**********************************************************************
;	TABULATES STATISTICAL INFORMATION FROM THE LINE
;**********************************************************************
LNSTAT,
	INCR LNSYTD
	IF (LDTEPM.EQ.0) LDTEPM = LDTERQ
	CDATE(1,2) = LDTERC(5,6)
	CDATE(3,6) = LDTERC(1,4)
	JDATE(1,2) = LDTEPM(5,6)
	JDATE(3,6) = LDTEPM(1,4)
	IF (CDATE.GT.JDATE) INCR LSLYTD
	DUMMY = AVCNTR
	AVCNTR = DUMMY
	IF (AVCNTR.LT.99) INCR AVCNTR

	PTPVAR =
	IF (LEXCST.NE.0) PTPVAR = (((LEXCST-LACCST)*10000)/LEXCST)#2

	PTPREJ =
	IF (LQTYRC.NE.0) PTPREJ = ((LQTYRJ*10000)/LQTYRC)#2

	XCALL BDATE (HPODTE,LDTERC,N,N,PTLDTM)

	IF (LDTEPM.NE.0) XCALL BDATE (LDTEPM,LDTERC,N,N,PTDSLA)
	IF (CDATE.LE.JDATE.OR.LDTEPM.EQ.0) PTDSLA = 0

	EVTFAC = (1000000/AVCNTR)#2
	IF (EVTFAC.LT.500) EVTFAC = 500

	AVPVAR = (EVTFAC*(PTPVAR) + (10000 - EVTFAC)*(AVPVAR))#4
	AVPREJ = (EVTFAC*(PTPREJ) + (10000 - EVTFAC)*(AVPREJ))#4
	AVLDTM = (EVTFAC*(PTLDTM) + (10000 - EVTFAC)*(AVLDTM))#4
	AVDSLA = (EVTFAC*(PTDSLA) + (10000 - EVTFAC)*(AVDSLA))#4
	RETURN

;**********************************************************************
;	READ ONE IN-RANGE HEADER RECORD
;**********************************************************************

READHD,
	EOF =
REREAD,
	LOKCTL = 1
	XCALL IOS (1,HEADER,READ,LOKCTL)
	IF (LOKCTL.NE.0) GOTO ENDFIL
	IF (HPOTYP.EQ.'B'.AND.HRLNUM.EQ.0) GO TO REREAD
	IF (HPOSTS.NE.'C'.AND.HPOSTS.NE.'X'.AND.HPOSTS.NE.'D') GO TO REREAD
	RETURN
ENDFIL,
	EOF = 1
	RETURN

;**********************************************************************
;	READ ONE IN-RANGE LINE RECORD
;**********************************************************************

READLN,
	EOF =
	EPO =
	KEY1 = HPONUM
	KEY2 = HRLNUM
	LOKCTL = 1
	XCALL ISIO (2,POLINE,KEYPON,READ,LOKCTL)
	GOTO CONTIN	
REPEAT,
	LOKCTL = 1
	XCALL IOS (2,POLINE,READ,LOKCTL)
CONTIN,
	IF (LOKCTL.EQ.2) GOTO TOOFAR
	IF (LOKCTL.EQ.1) GOTO EXIT2
	IF (LPONUM.LT.HPONUM) GO TO REPEAT
	IF (LPONUM.GT.HPONUM) GO TO TOOFAR
	IF (LRLNUM.LT.HRLNUM) GO TO REPEAT
	IF (LRLNUM.GT.HRLNUM) GO TO TOOFAR
	RETURN
TOOFAR,
	EPO = 1
	RETURN
EXIT2,
	EOF = 1
	RETURN
;**********************************************************************
;	REWRITE THE HEADER WITH DELETION MARK AND ADD 1 TO VENDOR'S YTD P/O'S
;**********************************************************************
HDSTAT,
	POSYTD = POSYTD + 1
	RETURN
EXIT,
	XCALL WATE (4,V)
	XCALL FILES (5,'I',012,4)
INUSE4,
	XCALL FILES (4,'U',011,4)
INUSE3,
	XCALL FILES (2,'SU',152,4)
INUSE2,
	XCALL FILES (1,'SU',151,4)
INUSE1,
	XCALL PGCHN ('PO:POSFMN',1)
;**********************************************************************

END

