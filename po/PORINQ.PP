;  PORINQ / POR - D11
;
;
;	01/30/95 DKS: CREATED FROM HSTINQ.CP
; (01)	10/05/95 DKS: MAKE P.O. FILES ISAM
;
;		INQUIRY ON OPEN PURCHASE ORDERS BY:
;		PN NUMBER OR
;		P/O NUMBER OR
;		ITEM NUMBER
;
;
;
	.DEFINE POOLSIZE	,25000	
	.DEFINE WNDCHNL		,15
	.DEFINE MAXWINS		,5
	.INCLUDE 'WND:WINDOWS.DEF'

RECORD PORHDR   	
	.INCLUDE 'DEF:RD151A.DEF'

RECORD PORLIN		
	.INCLUDE 'DEF:RD152A.DEF'

RECORD PORNXF
	.INCLUDE 'DEF:RD192A.DEF'

RECORD VENIDX
	.INCLUDE 'DEF:RD012A.DEF'

RECORD VENDOR
	.INCLUDE 'DEF:RD011A.DEF'

RECORD DUMVEN
	.INCLUDE 'DEF:RD011B.DEF'

RECORD CTL
	W_ID	,D3
		,A1
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2

RECORD KEY1
	PO1	,D6

RECORD KEY2
	PN2	,A6

RECORD KEY3
	PT3	,A15

RECORD
	ALPHA	,A11
	DEC	,D6
	L	,2D1
	TCHAR	,D3
	PONUM	,D8
	DKEY	,D11
	SRCOPT	,D1
	TOTQTY	,D5
	TOTORD	,D8
	EOFORD	,D1
	DSPCNT	,D2
	ORGORD	,D5
	ORGLIN	,D5
	OPTION	,D1
	ENTRY	,A36
	INXCTL  ,D1
	CNGCTL	,D1
	WHATNO	,D2
	DECMAL	,D18
	KEY   	,A6
	BSEND	,D5
	BSMID	,D5
	SRCCTL	,D1
	BLANKS	,A35
	BLANK1	,A80
	MASK	,A6,'XXXXXX'
	KPONO	,D6
	KRELNO	,D2
	RECNO	,D5
	PORDNO	,A9
	SWITCH	,D1
	SYSTEM	,D1
	V	,D1
	SAVROW	,D2
	DISDAT	,A8
	CHAN	,D1
	COL2	,D2
	ROW2	,D2
	READ	,D1	,0
	WRITE	,D1	,1
	XROW,	D2
	LOKCTL	,D1
	TNUM	,D3
	WNDW_1	,D3
	WNDW_2	,D3
	KTYPE	,D1



PROC
	XCALL TERID(V)
	XCALL ENVRN(SYSTEM)

	SWITCH = 5
	XCALL FILES(4,'SI',151,SWITCH)		;FILE # 151 -- PORHDR FILE
	IF (SWITCH.EQ.9) GO TO END
	SWITCH = 5
	XCALL FILES(5,'SI',152,SWITCH)		;FILE # 152 -- PORLIN FILE
	IF (SWITCH.EQ.9) GO TO END1
	SWITCH = 5
	XCALL FILES(2,'I',12,SWITCH)		;FILE # 12 -- VENIDX FILE
	IF (SWITCH.EQ.9) GOTO END2
	SWITCH = 5
	XCALL FILES(1,'I',11,SWITCH)		;FILE # 11 -- VENMAS FILE
	IF (SWITCH.EQ.9) GOTO END3
	SWITCH = 5
	XCALL FILES(6,'SI',192,SWITCH)		;FILE # 192 -- PORNXF FILE

	LOKCTL = 1
	XCALL IO (1,DUMVEN,1,READ,LOKCTL)

;>(03) initialize windowing system and create window
	XCALL W_INIT(POOLSIZE,WNDCHNL,MAXWINS)
	XCALL W_PROC(WP_CREATE,WNDW_1,'HEADER',21,78)
	XCALL W_PROC(WP_CREATE,WNDW_2,'NOTES',6,32)
	XCALL W_BRDR(WNDW_1,WB_TITLE,'INQUIRE ON OPEN PURCHASE ORDERS',
&			WB_TPOS,WBT_TOP,WBT_CENTER)
	XCALL W_BRDR(WNDW_2,WB_TITLE,'PO NOTES',WB_TPOS,WBT_TOP,WBT_CENTER)
DISPLA,
	KTYPE = 0
	W_ID = WNDW_1
	XCALL W_PROC (WP_PLACE,WNDW_1,1,1)
	CNGCTL =
	XCALL W_DISP (WNDW_1,WD_CLEAR)
	XCALL W_DISP (WNDW_1,WD_ATTR,ATTR_LOAD + ATTR_BOLD,WD_POS,1,3,'*P.O. #:')
	XCALL W_DISP (WNDW_1,WD_POS,2,3,'*ITEM #:')
	XCALL W_DISP (WNDW_1,WD_POS,3,3,'*P/N # :')
	XCALL W_DISP (WNDW_1,WD_POS,1,35,'P/O DATE:')
	XCALL W_DISP (WNDW_1,WD_POS,1,62,'P/O TYPE:')
	XCALL W_DISP (WNDW_1,WD_POS,3,35,'VENDOR:')
	XCALL W_DISP (WNDW_1,WD_POS,5,4,'LINE NO:')
	XCALL W_DISP (WNDW_1,WD_POS,5,35,'P/O U/M:')
	XCALL W_DISP (WNDW_1,WD_POS,6,4,'VEN-ITEM NO:')
	XCALL W_DISP (WNDW_1,WD_POS,6,40,'COMMODITY:')
	XCALL W_DISP (WNDW_1,WD_POS,6,60,'NOTIFY:')
	XCALL W_DISP (WNDW_1,WD_POS,7,4,'DATE REQUESTED:')
	XCALL W_DISP (WNDW_1,WD_POS,7,40,'QTY ORD:')
	XCALL W_DISP (WNDW_1,WD_POS,8,4,'DATE PROMISED :')
	XCALL W_DISP (WNDW_1,WD_POS,8,40,'QTY DUE:')
	XCALL W_DISP (WNDW_1,WD_POS,10,4,'DATE RECEIVED:')
	XCALL W_DISP (WNDW_1,WD_POS,10,40,'QTY RECEIVED:')
	XCALL W_DISP (WNDW_1,WD_POS,11,4,'COST:')
	XCALL W_DISP (WNDW_1,WD_POS,11,30,'RECEIVER:')
	XCALL W_DISP (WNDW_1,WD_POS,11,45,'QTY REJ:')
	XCALL W_DISP (WNDW_1,WD_POS,11,68,'CODE:')
	XCALL W_DISP (WNDW_1,WD_ATTR,ATTR_CLR + ATTR_ALL)
	XCALL W_DISP (WNDW_1,WD_HLINE,4,1,78)
	XCALL W_DISP (WNDW_1,WD_HLINE,9,1,78)
	XCALL W_DISP (WNDW_1,WD_HLINE,12,1,78)
	XCALL W_UPDT
	XCALL W_PROC (WP_PLACE,WNDW_2,16,24)
	XCALL W_DISP (WNDW_2,WD_CLEAR)
	
ENTPO,
	CTL(5,18) = '01,12,06,00,#E'
	CALL INPUT
	GOTO (DISPLA,ENDOFF), INXCTL
	PO1 = ENTRY(1,6)
	IF (PO1.EQ.0) GOTO ENTITM
	XCALL W_DISP (WNDW_1,WD_POS,1,18,'-00')
	KTYPE = 1
	FIND (6,PORNXF,KEY1,KRF="PO")[ERR=CHKPO]
	GOTO NXTITM
ENTITM,
	CTL(5,18) = '02,12,15,00,AE'
	CALL INPUT
	GOTO (DISPLA,ENDOFF), INXCTL
	PT3 = ENTRY(1,15)
	IF (PT3.EQ.BLANKS) GOTO ENTPN
	KTYPE = 2
	FIND (6,PORNXF,KEY3,KRF="PART")[ERR=CHKITM]
	GOTO NXTITM
ENTPN,
	CTL(5,18) = '03,12,06,00,AE'
	CALL INPUT
	GOTO (DISPLA,ENDOFF), INXCTL
	PN2 = ENTRY(1,6)
	IF (PN2.EQ.BLANKS) GOTO ENTPO
	KTYPE = 3
	FIND (6,PORNXF,KEY2,KRF="PN")[ERR=CHKPN]
NXTITM,
	XCALL IOS (6,PORNXF,READ,LOKCTL)
	GOTO (CHKPO,CHKITM,CHKPN), KTYPE
CHKPO,
	IF (PORPO.EQ.PO1) GOTO CONTIN
	XCALL MESAG ('NO MORE P/O # MATCHES FOUND',1)
	GOTO DISPLA
CHKITM,
	IF (POPART.EQ.PT3) GOTO CONTIN
	XCALL MESAG ('NO MORE ITEM NUMBER MATCHES FOUND',1)
	GOTO DISPLA
CHKPN,
	IF (PORPN.EQ.PN2) GOTO CONTIN
	XCALL MESAG ('NO MORE P/N # MATCHES FOUND',1)
	GOTO DISPLA
CONTIN,
	DKEY(1,6) = PORPO
	DKEY(7,8) = PORREL
	DKEY(9,11) = PORLNO
	LOKCTL = 1
	XCALL ISIO (5,PORLIN,DKEY,READ,LOKCTL)
	IF (LOKCTL.NE.0) PORLIN =
SRCHH,
	PONUM(1,6) = PORPO
	PONUM(7,8) = PORREL
	LOKCTL = 1
	XCALL ISIO (4,PORHDR,PONUM,READ,LOKCTL)
	IF (LOKCTL.NE.0) GOTO NOFIND
DISPO,
	W_ID = WNDW_1
	CTL(5,12) = '01,12,06'
	DECMAL = LPONUM
	CALL DSPNUM
	XCALL W_DISP (WNDW_1,WD_POS,1,18,'-00')
	XCALL W_DISP (WNDW_1,WD_POS,2,12,LITMNO)
	XCALL W_DISP (WNDW_1,WD_POS,3,12,LJOBNO)
	CTL(5,9) = '01,46'
	DECMAL(1,6) = HPODTE
	CALL DSPDTE
	XCALL W_DISP(WNDW_1,WD_POS,1,73,HPOTYP)
	XCALL W_DISP(WNDW_1,WD_POS,2,34,LITMDS)
	XCALL W_DISP(WNDW_1,WD_POS,3,43,HVENDR)
	BSMID = 1
	SRCOPT = 4
	XCALL SERCH (2,VENIDX,HVENDR,1,4,ORG011,BSMID,SRCCTL,
&			SRCOPT,5,10,0,0,0,0)
	IF (SRCCTL.EQ.0) GO TO VENFND
	XCALL W_DISP(WNDW_1,WD_POS,3,49,'VENDOR NOT ON FILE')
	GO TO GOON
VENFND,
	XCALL IO (1,VENDOR,IRC011,READ,LOKCTL)
	XCALL W_DISP(WNDW_1,WD_POS,3,49,NAME)
GOON,
	CTL(5,12) = '05,14,03'
	DECMAL = LLINNO
	CALL DSPNUM
	XCALL W_DISP(WNDW_1,WD_POS,5,45,LITMUM)
	XCALL W_DISP(WNDW_1,WD_POS,6,17,LVITMN)
	XCALL W_DISP(WNDW_1,WD_POS,6,52,LITMCC)
	XCALL W_DISP(WNDW_1,WD_POS,6,69,LNOTIF)
	CTL(5,9) = '07,20'
	DECMAL(1,6) = LDTERQ
	CALL DSPDTE
 	CTL(5,12) = '07,50,06'
	DECMAL = LQTYOR
	CALL DSPNUM
	CTL(5,9) = '08,20'
	DECMAL(1,6) = LDTEPM
	CALL DSPDTE
	CTL(5,12) = '08,50,06'
	DECMAL = (LQTYOR-LQTYRC)
	CALL DSPNUM
	IF (LQTYRC.NE.0)
		BEGIN
		CTL(5,9) = '10,21'
		DECMAL(1,6) = LDTERC
		CALL DSPDTE
		CTL(5,12) = '10,44,06'
		DECMAL = LQTYRC
		CALL DSPNUM
		XCALL W_DISP (WNDW_1,WD_POS,11,40,LRCVER)
		CTL(5,12) = '11,54,06'
		DECMAL = LQTYRJ
		CALL DSPNUM
		XCALL W_DISP (WNDW_1,WD_POS,11,74,LREJCD)
		END
	ALPHA = LEXCST, 'ZZ,ZZZ.XXXX'
	XCALL W_DISP (WNDW_1,WD_POS,11,12,ALPHA)
DSPNOT,
	W_ID = WNDW_2
	XCALL W_DISP(WNDW_2,WD_POS,1,2,LPONCD(1))
	XCALL W_DISP(WNDW_2,WD_POS,3,2,LPONTE(1))
	XCALL W_DISP(WNDW_2,WD_POS,4,2,LPONTE(2))
	XCALL W_DISP(WNDW_2,WD_POS,5,2,LPONTE(3))
	XCALL W_DISP(WNDW_2,WD_POS,6,2,LPONTE(4))
	XCALL W_UPDT

	XCALL OUTPT (24,1,0,'TYPE RETURN TO SEE NEXT ITEM',1)
	CALL ACCEPT
	IF (TCHAR.EQ.023) GO TO DISPLA		;CTRL-W
	IF (TCHAR.EQ.008) GO TO ENDOFF	
	IF (SYSTEM.NE.1) ACCEPT (15,TCHAR)
	GO TO NXTITM
ENDOFF,
	XCALL FILES(6,'SI',192,4)
	XCALL FILES(1,'I',11,4)
END3,
	XCALL FILES(2,'I',12,4)
END2,
	XCALL FILES(5,'SI',152,4)
END1,
	XCALL FILES(4,'SI',151,4)
END,
	XCALL W_PROC (WP_REMOVE,WNDW_1)
	XCALL W_PROC (WP_REMOVE,WNDW_2)
	XCALL W_EXIT
	XCALL PGCHN ('UT:INQMNU',1)

NOFIND,
	XCALL MESAG ('PURCHASE ORDER NOT FOUND',1)
	GOTO DISPLA
ACCEPT,
	XCALL FLAGS (10010000)
ACCEP1,
	ACCEPT (15,TCHAR)
	IF (TCHAR.NE.013 .AND. TCHAR.NE.23 .AND. TCHAR.NE.008) GOTO ACCEP1
	XCALL FLAGS (00000000)
	RETURN
DSPNUM,
	OPTION = 1
	GO TO CALDSP
DSPDTE,
	OPTION = 2
	GO TO CALDSP
DSPDLR,
	OPTION = 3
CALDSP,
	XCALL WDSPL(W_ID,MAX,ROW,COL,DECMAL,OPTION)
	RETURN
INPUT,
	XCALL WINPT(W_ID,ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL)
	RETURN
	END
