;  POPUPD / POR - D11
;
;
;
;		UPDATE FIELDS IN P/O AND INVENTORY FILES AS A RESULT
;			OF PRINTING PURCHASE ORDERS.
;
RECORD HEADER	
	.INCLUDE 'DEF:RD151A.DEF'

RECORD POLINE
	.INCLUDE 'DEF:RD152A.DEF'

RECORD ITEM
	.INCLUDE 'DEF:RD041A.DEF'

RECORD DUMITM	
	.INCLUDE 'DEF:RD041B.DEF'

RECORD ITMIDX	
	.INCLUDE 'DEF:RD042A.DEF'

RECORD KPONUM
	KEY1	,D6
	KEY2	,D2

RECORD RANGE1
	STPONO	,D6
	STRLNO	,D2
	ENPONO	,D6
	ENRLNO	,D2

RECORD RANGE2
	STPODA	,D6
	ENPODA	,D6
	ORDTYP	,A1
	ISSTYP	,A1

RECORD
	BLANKS	,A10
	BRACKS	,A30,	']]]]]]]]]]]]]]]]]]]]]]]]]]]]]]'
	BSMID	,D5
	CNGCTL	,D1
	CTR	,D2
	DSPSW	,D1
	ENTRY	,A30
	EOF	,D1
	EPO	,D1
	HDATE	,D6
	HRECNO	,D5
	INXCTL	,D1
	LOKCTL	,D1
	LRECNO	,D5
	MSGCTL	,D1
	N	,D1
	NOABRT	,D1,	1
	READ	,D1,	0
	RECNO	,D5
	SRCCTL	,D1
	SRCOPT	,D1
	SWITCH	,D1
	TODAY	,D6
	V	,D1
	WRITE	,D1,	1

PROC
	XCALL TERID (V)
	XCALL OUTPT (2,1,2,'UPDATE PO CONTROL FIELDS',1)
	XCALL WATE (4,V)
	XCALL RDATE (TODAY)
OPENIX,
	SWITCH = 1
	XCALL FILES (4,'I',042,SWITCH)		; FILE 042, ITMIDX.
	IF (SWITCH.EQ.9) GO TO INUSE1

	SWITCH = 5
	XCALL FILES (1,'SU',151,SWITCH)		; FILE 151, PORHDR.
	IF (SWITCH.EQ.9) GO TO ERROR1

	SWITCH = 5
	XCALL FILES (2,'SU',152,SWITCH)		; FILE 152, PORLIN.
	IF (SWITCH.EQ.9) GO TO ERROR2
OPNITM,
	SWITCH = 1
	XCALL FILES (3,'U',041,SWITCH)		; FILE 041, ITMMAS.
	IF (SWITCH.EQ.9) GO TO INUSE2

	LOKCTL = NOABRT
	XCALL IO (3,DUMITM,1,READ,LOKCTL)

	MSGCTL = 1				; READ A MESSAGE
	XCALL SNMSG (RANGE1,MSGCTL)
	IF (MSGCTL.EQ.9) GO TO NOMSG

	MSGCTL = 3				; CLEAR THE MESSAGE READ
	XCALL SNMSG (BLANKS,MSGCTL)
	IF (MSGCTL.EQ.9) GO TO ERROR3

	MSGCTL = 1				; READ A MESSAGE
	XCALL SNMSG (RANGE2,MSGCTL)
	IF (MSGCTL.EQ.9) GO TO NOMSG

	MSGCTL = 3				; CLEAR THE MESSAGE READ
	XCALL SNMSG (BLANKS,MSGCTL)
	IF (MSGCTL.EQ.9) GO TO ERROR3

	CALL READHD			; READ FIRST IN-RANGE HEADER & LINE
	IF (EOF) GO TO NONE

	CALL PORUN			; UPDATE ALL PURCHASE ORDERS

	XCALL FILES (4,'I',042,4)	; CLOSE ITMIDX.
	CLOSE 1				; CLOSE PORHDR BUT LEAVE PROTECTED.
	CLOSE 2				; CLOSE PORLIN BUT LEAVE PROTECTED.
	XCALL FILES (3,'U',041,4)	; CLOSE ITMMAS.

	XCALL PGCHN ('PO:POPRNT',1)

;**********************************************************************
;	EACH PASS UPDATES ONE PURCHASE ORDER:
;**********************************************************************
PORUN,
	IF (HCXCDE.EQ.'X') HPOSTS = 'X'
	IF (HPOSTS.EQ.'R') HPOSTS = 'P'
	HCXCDE = 'N'
UPDLIN,
	CALL LINES		; UPDATE ALL PORLIN RECORDS, THIS P/O
	CALL WRITHD		; WRITE HEADER RECORD
	CALL READHD		; READ NEXT IN-RANGE HEADER & LINE
	IF (EOF) RETURN
	GO TO PORUN
;**********************************************************************
;	EACH PASS UPDATES ONE LINE ITEM:
;**********************************************************************
LINES,
	IF (HPOTYP.EQ.'B') GO TO UPDSTS
	SRCOPT = 4
	BSMID = 1
	SRCCTL = 1
	XCALL SERCH (4,ITMIDX,LITMNO,1,15,ORG041,BSMID,SRCCTL,SRCOPT,
&			16,21,0,0,0,0)
	IF (SRCCTL.EQ.1) GO TO UPDSTS
	IF (IRC041.EQ.0) GO TO UPDSTS

	LOKCTL = NOABRT
	XCALL IO (3,ITEM,IRC041,READ,LOKCTL)

; only 1 loc for smc
FNDLOC,
MAKLOC,
UPDONO,
	IF (LCXCDE.EQ.'X'.AND.LINSTS.NE.'C') GO TO REVERS
	QTYONO = QTYONO + (LQTYOR - LQTYHD)
	GO TO WRTITM
REVERS,
	IF (LINSTS.NE.'N'.AND.LINSTS.NE.'X')
&			QTYONO = QTYONO - LQTYOR
WRTITM,
	LOKCTL = NOABRT
	XCALL IO (3,ITEM,IRC041,WRITE,LOKCTL)
UPDSTS,
	IF (LCXCDE.EQ.'X'.AND.LINSTS.NE.'C') LINSTS = 'X'
	IF (LINSTS.EQ.'N') LINSTS = 'P'
	LCXCDE = 'N'
	LQTYHD = LQTYOR
	LQTYCG =
	LDTECG =

	CALL WRITLN			; WRITE LINE RECORD
	CALL NXTREC			; READ NEXT IN-RANGE LINE RECORD
	IF (EOF.OR.EPO) RETURN
	GO TO LINES
;**********************************************************************
;	WRITE THE LAST HEADER RECORD BACK OUT
;**********************************************************************
WRITHD,
	LOKCTL = NOABRT
	XCALL ISIO (1,HEADER,PORKEY,WRITE,LOKCTL)
	RETURN
;**********************************************************************
;	WRITE THE LAST LINE RECORD BACK OUT
;**********************************************************************
WRITLN,
	LOKCTL = NOABRT
	XCALL ISIO (2,POLINE,POLKEY,WRITE,LOKCTL)
	RETURN
;**********************************************************************
;	READ ONE IN-RANGE HEADER RECORD AND ONE IN-RANGE LINE RECORD
;**********************************************************************
READHD,
	EOF =
REREAD,
	LOKCTL = 1
	XCALL IOS (1,HEADER,READ,LOKCTL)
	IF (LOKCTL.NE.0) GOTO ENDFIL
	IF (HPONUM.LT.STPONO) GO TO REREAD
	IF (HPONUM.GT.ENPONO) GO TO ENDFIL
	IF (HPONUM.EQ.STPONO.AND.HRLNUM.LT.STRLNO) GO TO REREAD
	IF (HPONUM.EQ.ENPONO.AND.HRLNUM.GT.ENRLNO) GO TO ENDFIL
	HDATE(1,2) = HPODTE(5,6)
	HDATE(3,6) = HPODTE(1,4)
	IF (HDATE.LT.STPODA.OR.HDATE.GT.ENPODA) GO TO REREAD
	IF (ORDTYP.NE.' '.AND.ORDTYP.NE.HPOTYP) GO TO REREAD
	IF (HCXCDE.EQ.' ') HCXCDE = 'N'
	IF (ISSTYP.NE.' '.AND.ISSTYP.NE.HCXCDE) GO TO REREAD
	IF (HCXCDE.NE.'N'.AND.HPOSTS.EQ.'P') GO TO TRYIT
	IF (HPOSTS.NE.'R') GO TO REREAD
TRYIT,
	CALL READLN
	IF (EOF) RETURN
	IF (EPO) GO TO REREAD
	RETURN
ENDFIL,
	EOF = 1
	RETURN
;**********************************************************************
;	READ ONE IN-RANGE LINE RECORD
;**********************************************************************
READLN,
	EOF =
	EPO =
REPEAT,
	KEY1 = HPONUM
	KEY2 = HRLNUM
	LOKCTL = 1
	XCALL ISIO (2,POLINE,KPONUM,READ,LOKCTL)
CONTIN,
	IF (LOKCTL.EQ.2) GOTO TOOFAR
	IF (LOKCTL.EQ.1) GOTO EXIT2
	IF (LPONUM.LT.HPONUM) GO TO NXTREC
	IF (LPONUM.GT.HPONUM) GO TO TOOFAR
	IF (LRLNUM.LT.HRLNUM) GO TO NXTREC
	IF (LRLNUM.GT.HRLNUM) GO TO TOOFAR
	IF (HPOSTS.EQ.'P'.AND.HCXCDE.EQ.'C') GO TO CNGORD
	RETURN
NXTREC,
	LOKCTL = 1
	XCALL IOS (2,POLINE,READ,LOKCTL)
	GOTO CONTIN
CNGORD,
	IF (LINSTS.EQ.'N') RETURN
	IF (LCXCDE.EQ.'C'.OR.LCXCDE.EQ.'X') RETURN
	GO TO NXTREC
TOOFAR,
	EPO = 1
	RETURN
EXIT2,
	EOF = 1
	RETURN
;**********************************************************************
;	IN-USE, ERROR, AND EXIT ROUTINES
;**********************************************************************
INUSE2,
	XCALL MESAG ('INVENTORY FILES NOT YET AVAILABLE.',2)
	GO TO OPNITM
INUSE1,
	XCALL MESAG ('INVENTORY FILES NOT YET AVAILABLE',2)
	GO TO OPENIX
;**********************************************************************
ERROR1,
	XCALL MESAG ('?PORHDR FILE ERROR.',1)
	GO TO ERROUT
;**********************************************************************
ERROR2,
	XCALL MESAG ('? PORLIN FILE ERROR.',1)
	GO TO ERROUT
;**********************************************************************
ERROR3,
	XCALL MESAG ('? MESSAGE ERROR IN POPUPD.',1)
	GO TO ERROUT
;**********************************************************************
NONE,
	XCALL MESAG ('THERE ARE NO PURCHASE ORDERS IN SELECTED RANGE.',1)
	GO TO ERROUT
;**********************************************************************
NOMSG,
	XCALL MESAG ('? NO MESSAGE AVAILABLE IN POPUPD.',1)
ERROUT,
	XCALL FILES (1,'SU',151,4)		; CLOSE PORHDR
	XCALL FILES (2,'SU',152,4)		; CLOSE PORLIN
	XCALL FILES (3,'U',041,4)		; CLOSE ITMMAS
	XCALL FILES (4,'I',042,4)		; CLOSE ITMIDX
	XCALL PGCHN ('PO:POMENU',1)
;**********************************************************************

END

