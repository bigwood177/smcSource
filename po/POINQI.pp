;POINQI / POR  DIBOL - 11
;
;
;		AUTHOR: 1-DEC-81  WRF
;
;
;
RECORD VENDR	
	.INCLUDE 'DEF:RD011A.DEF'

RECORD VENCTL	
	.INCLUDE 'DEF:RD011B.DEF'

RECORD VENIDX	
	.INCLUDE 'DEF:RD012A.DEF'

RECORD ITMMAS		
	.INCLUDE 'DEF:RD041A.DEF'

RECORD ITMCTL	
	.INCLUDE 'DEF:RD041B.DEF'

RECORD ITMIDX	
	.INCLUDE 'DEF:RD042A.DEF'

RECORD DSPLIN
		,A1
	LINNUM	,A2
		,A2,'. '
	VNNUM	,A4
		,A2
	VNNAM	,A25
		,A4
	ACTIV	,A1
		,A5
	MNORD	,A5
		,A3
	LDTIM	,A3
		,A3
	PCTLA	,A3
		,A3
	PCREJ	,A3
		,A3
	PCVAR	,A3
		,A4

RECORD HDR1
		,A40,'    VENDOR          VENDOR           ACT'
		,A40,'IVE? MINIMUM  LEAD  DAYS  PCT  PERCENT  '
RECORD HDR2
		,A40,'    NUMBER           NAME               '
		,A40,'      ORDER   TIME  LATE  REJ  VARIANCE '
RECORD ARRAYS
	VNNMAR	,10A25		;VENDOR NAME ARRAY
	ACTVAR	,10A1		;ACTIVE/NOT ACTIVE CODE ARRAY
	MNORAR	,10D5		;MINIMUM ORDER ARRAY
	LDTMAR	,10D3		;LEAD TIME ARRAY
	PCLAAR	,10D3		;PERCENT LATE ARRAY
	PCRJAR	,10D3		;PERCENT REJECTED ARRAY
	PVARAR	,10D3		;PERCENT VARIANCE ARRAY
	VEN	,D2		;SUBSCRIPT FIELD FOR VENDORS

RECORD CTL
	ROW 	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2

RECORD
	BSEND	,D5
	BSMID	,D5
	CNGCTL	,D1
	ENTRY	,A15
	INXCTL	,D1
	LIN	,D2
	LOKCTL	,D1
	READ	,D1,	0
	RESTRT	,D1
	SRCCTL	,D1
	SRCOPT	,D1
	SWITCH	,D1
	V	,D1
	BLANKS	,A15
	RJUST	,D1
	KEY	,A4
	DECMAL	,D4
	WCHVEN	,A4

PROC(2)
	XCALL TERID (V)
	V = 1
	XCALL WATE (1,V)

	SWITCH = 1
	XCALL FILES (1,'I',012,SWITCH)			;VENIDX FILE
	IF (SWITCH.EQ.9) GO TO INUSE1

	SWITCH = 1
	XCALL FILES (2,'I',011,SWITCH)			;VENMAS FILE
	IF (SWITCH.EQ.9) GO TO INUSE2

	SWITCH = 1
	XCALL FILES (3,'I',042,SWITCH)			;ITMIDX FILE
	IF (SWITCH.EQ.9) GO TO INUSE3

	SWITCH = 1
	XCALL FILES (4,'I',041,SWITCH)			;ITMMAS FILE
	IF (SWITCH.EQ.9) GO TO INUSE4
	LOKCTL = 0
	XCALL IO (4,ITMCTL,1,READ,LOKCTL)		;READS ITMMAS CONTROL RECORD INTO ITMCTL
	RJUST = JSTIFY
	UNLOCK 4
	LOKCTL = 0
	XCALL IO (2,VENCTL,1,READ,LOKCTL)		;READS VENMAS CONTROL RECORD
	UNLOCK 2


;**********************************************************************
;SECTION ASKS FOR ITEM NUMBER AND DISPLAYS DESCRIPTION IF ITEM IS FOUND
;**********************************************************************


BEGIN,
	CNGCTL =
	ARRAYS =
	XCALL OUTPT (1,1,3,'ITEM VENDOR MAINTENANCE',V)
	XCALL OUTPT (2,1,0,'ITEM VENDOR INQUIRY',V)
	XCALL OUTPT (5,10,0,'ITEM NO',V)

	CTL = '05,19,15,01,AE'
	CALL INPUT
	XCALL OUTPT (5,19,0,ENTRY,V)
	GO TO (BEGIN,END), INXCTL
	IF (RJUST) XCALL FRMAT (ENTRY(1,15),15)

	SRCOPT = 4
	BSMID = 1
	SRCCTL = 1
	XCALL SERCH (3,ITMIDX,ENTRY,1,15,ORG041,BSMID,SRCCTL,
&                     SRCOPT,30,35,0,0,0,0)
	IF (SRCCTL) GO TO NOTFND
	IF (IRC041 .EQ. 0) GOTO NOTFND
	LOKCTL = 1
	XCALL IO (4,ITMMAS,IRC041,READ,LOKCTL)
	UNLOCK 4
	GO TO CONTIN
NOTFND,
	XCALL MESAG ('ITEM NUMBER NOT ON FILE',1)
	GO TO BEGIN

CONTIN,
	XCALL OUTPT (5,36,0,DESCR,V)
	CALL FNDVEN			;PUTS THE VENDOR INFORMATION IN THE ARRAYS
	XCALL OUTPT (08,1,0,HDR1,V)
	XCALL OUTPT (09,1,0,HDR2,V)

SCRN1,	LIN = 9
	VEN =
VENLIN,
	CALL DISPLA
	IF (VEN.EQ.5.AND.NUMVEN.GT.5) GO TO SCRN2
	IF (VEN.EQ.5.AND.NUMVEN.LE.5) GO TO ONLY5
	IF (VEN.EQ.NUMVEN) GO TO ONLY5
	GO TO VENLIN

ONLY5,
	XCALL MESAG ('PRESS RETURN TO SELECT NEW ITEM',2)
	GO TO BEGIN

SCRN2,
	XCALL MESAG ('PRESS RETURN TO VIEW OTHER VENDORS',2)

	LIN = 9
	VEN = 5
	CALL DISPLA

CHOICE,
	XCALL OUTPT (24,1,0,'RETURN TO FIRST SCREEN ?',V)
	CTL = '24,27,01,00,A '
	CALL INPUT
	IF (ENTRY.NE.'Y'.AND.ENTRY.NE.'N'.AND.ENTRY.NE.BLANKS) GO TO CHOICE
	IF (ENTRY.EQ.'Y') GO TO SCRN1
	GO TO BEGIN


;**********************************************************************
;	DISPLAYS A SCREEN OF VENDORS
;**********************************************************************

DISPLA,
	LIN = LIN +2
	INCR VEN
	LINNUM = VEN,'ZX'
	VNNUM = VENDOR (VEN)
	VNNAM = VNNMAR(VEN)
	ACTIV = ACTVAR(VEN)
	MNORD = MNORAR(VEN),'ZZZZX'
	LDTIM = LDTMAR(VEN),'ZZX'
	PCTLA = PCLAAR(VEN),'ZZX'
	PCREJ = PCRJAR(VEN),'ZZX'
	PCVAR = PVARAR(VEN),'ZZX'
	IF (VEN.GT.NUMVEN) DSPLIN =
	IF (VNNUM(VEN).EQ.'0'.OR.VNNUM(VEN).EQ.BLANKS) DSPLIN(4,80) =
	XCALL OUTPT (LIN,1,0,DSPLIN,V)
	DSPLIN(4,4) = '.'
	DSPLIN(56,56) = '.'
	IF (VEN.EQ.5.OR.VEN.EQ.10) RETURN
	GO TO DISPLA



;**********************************************************************

FNDVEN,
	VEN =
STRSCH,
	INCR VEN
	IF (VEN.GT.NUMVEN) RETURN
	IF (VENDOR(VEN).EQ.'    ') VENDOR(VEN) ='0'	;ENSURE NO BLANK FIELDS
	WCHVEN = VENDOR (VEN)				;USED IN SEARCH
	BSMID = 1
	SRCCTL = 1
	SRCOPT = 4
	XCALL SERCH (1,VENIDX,WCHVEN,1,4,ORG011,BSMID,SRCCTL,
&			SRCOPT,6,10,0,0,0,0)
	IF (SRCCTL.NE.1 .AND. IRC011.NE.0) GO TO ACCUM

	VENDR =
	VNNMAR(VEN) = 'VENDOR NOT ON FILE'
	GO TO STRSCH
ACCUM,
	LOKCTL = 1
	XCALL IO (2,VENDR,IRC011,READ,LOKCTL)
	UNLOCK 2
	VNNMAR(VEN) = NAME
	IF (ACTIVE.EQ.'1') ACTVAR(VEN) = 'Y'		;1 = ACTIVE
	IF (ACTIVE.EQ.'2') ACTVAR(VEN) = 'N'		;2 = INACTIVE
	LDTMAR(VEN) = AVLDTM
	PCLAAR(VEN) = AVDSLA
	PCRJAR(VEN) = AVPREJ
	PVARAR(VEN) = AVPVAR
	MNORAR(VEN) = MINORD(VEN)
	GO TO STRSCH

;**********************************************************************

INPUT,
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,V)
	RETURN

;**********************************************************************
;	EXITS PROGRAM IF FILE(S) IN USE
;**********************************************************************
END,
	XCALL WATE (1,V)
	SWITCH = 4
	XCALL FILES (4,'I',041,SWITCH)
INUSE4,
	SWITCH = 4
	XCALL FILES (3,'I',042,SWITCH)
INUSE3,
	SWITCH = 4
	XCALL FILES (2,'I',011,SWITCH)
INUSE2,
	SWITCH = 4
	XCALL FILES (1,'I',012,SWITCH)
INUSE1,
	RESTRT = 1
	XCALL PGCHN ('PO:POIMNU',RESTRT)

;**********************************************************************

END

