;  VCRRPT.PO / POR - D11
;
;		CREATED: 08-JUN-94
;		AUTHOR:  DIANA K. SCHEPPA
;
;		VENDOR INVENTORY REPORT BY VENDOR NUMBER
;
; REVISIONS:
;
;**********************************************************************
;		PURCHASE ORDER PROCESSING
;**********************************************************************
;:
;		COMPILE & LINK PER DOCUMENTATION INSTALLATION NOTES.
;
;
;
RECORD VIPART
	.INCLUDE 'DEF:RD061A.DEF'

RECORD	FFILE
	FILNUM	,D3
	CLCTL	,D1
	REDFIL	,A14

RECORD VITEMP
		,A11,'DPO:VITEMP.'
	TEXT	,A3

RECORD PLINE
 		,A132

RECORD LINE1,X
	KVNO	,A6
		,A5
	KVPART	,A20
		,A5
	KPART	,A15

RECORD HDR1
		,A8,	'VENDOR #'
		,A3
		,A13,	'VENDOR PART #'
		,A12
		,A13,	'NUAIRE PART #'

RECORD LEGEND
		,A1
		,A19,'VENDOR NUMBERS FROM'
		,A1
	SNO	,A4
		,A3
		,A2,'TO'
		,A3
	ENO	,A4

RECORD
	CNGCTL	,D1
	WHATNO	,D1
	TNUM	,D3
	TMPREC	,D5
	ALL	,A12
	BLANKS	,A15
	BRACKS	,A34,']]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]'
	BSMID	,D5
	CNTR	,D3
	DSPSW	,D1
	EOF	,D1
	IDFLAG	,D1
	LINCNT	,D2,	60
	LINCTL	,D1
	LOKCTL	,D1
	LPSW	,D1
	NOABRT	,D1,	1
	PGCNT	,D3,	000
	PRTCTL	,D3
	PRTTYP	,A1
	READ	,D1,	0
	WRITE	,D1,1
	RPTNUM	,D3
	SPLFIL	,A14
	SRCOPT	,D1
	SRCCTL	,D1
	SWITCH	,D1
	TITLE	,A33,	'VENDOR/INVENTORY REPORT BY VENDOR'
	TODAY	,D6
	V	,D1
	WTARG	,D1,	2
	XRECNO	,D5
	LRECNO	,D5
	NORECS	,D5
	INXCTL	,D1
	ENTRY	,A15
	SVEND	,A4
	EVEND	,A4
	YN	,D1


PROC
	XCALL TERID (V)
	XCALL TNMBR(TNUM)
	TEXT=TNUM,'XXX'
	XCALL OUTPT (2,1,2,'PRINT VENDOR/INVENTORY REPORT BY VENDOR',1)

	XCALL OUTPT (6,10,2,'DO YOU WISH TO RUN THIS PROGRAM NOW ?',V)
	XCALL INPUT (6,49,1,0,'YN',ENTRY,INXCTL,V)
	GO TO (GOON,ENDOFF,ENDOFF), INXCTL
GOON,
	XCALL OUTPT (2,1,2,'PRINT VENDOR/INVENTORY REPORT BY VENDOR',1)
	LINCNT = 60
	PGCNT = 0

GETSNO,
	XCALL OUTPT (4,20,2,'PLEASE ENTER STARTING VENDOR NUMBER',V)
	XCALL INPUT (4,60,4,0,'#E',ENTRY,INXCTL,V)
	GO TO (GETSNO,ENDOFF), INXCTL
	XCALL FRMAT (ENTRY(1,4),4)
	SVEND = ENTRY (1,4)
	SNO = ENTRY (1,4)
GETENO,
	XCALL OUTPT (5,20,2,'PLEASE ENTER ENDING VENDOR NUMBER',V)
	XCALL INPUT (5,60,4,0,'# ',ENTRY,INXCTL,V)
	GO TO (GETSNO,GETSNO), INXCTL
	XCALL FRMAT (ENTRY(1,4),4)
	EVEND = ENTRY (1,4)
	IF (EVEND.EQ.'    ') EVEND = SVEND
	ENO = ENTRY (1,4)
	XCALL OUTPT(5,60,0,EVEND,V)
	IF (EVEND.EQ.'    ') CALL ALLVND
	GOTO START
ALLVND,
	ALL = 'ALL VENDORS'
	SVEND=
	EVEND='ZZZZ'
	SNO = 'ALL '
	ENO = 
	XCALL OUTPT(5,60,0,ALL,V)
	RETURN 
START,
	YN = INXCTL
	CNGCTL=2
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (GETSNO),CNGCTL
	XCALL OUTPT (1,1,2,'PRINT VENDOR/INVENTORY REPORT BY VENDOR',1)
	XCALL OUTPT (2,50,0,'BUILDING TEMPORARY FILE',1)
	XCALL WAIT (4,V)

	SWITCH = 1
	XCALL FILES (2,'SI',61,SWITCH)		;FILE #61 -- VIPART FILE
	IF (SWITCH.EQ.9) GOTO ENDOFF

	OPEN (1,O,VITEMP)			;FILE 'DPO:VITEMP.XXX'

READIT,
	LOKCTL = NOABRT
	XCALL IOS (2,VIPART,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GOTO ENDBLD
	IF (VPART.EQ.']]]DEL') GOTO READIT
	IF (IVEN.LT.SVEND) GOTO READIT
	IF (IVEN.GT.EVEND) GOTO READIT
	WRITES (1,VIPART)
	GOTO READIT
ENDBLD,
	VIPART = BRACKS
	WRITES (1,VIPART)
	XCALL FILES (2,'SI',61,4)
	CLOSE 1
	XCALL OUTPT (2,1,0,'PRINT VENDOR/INVENTORY REPORT BY VENDOR',1)
	XCALL OUTPT (2,45,0,'SORTING DPO:VITEMP - "VCRRPT.DBR"',1)
	XCALL WAIT (4,V)
PROCES,
	FILNUM = 61 
	XCALL FFILE (FILNUM,REDFIL,CLCTL)	;GET FILE SPEC

	SORT (IN=VITEMP, OUT=VITEMP, RECORD=VIPART,
&		KEY=(IVEN,VPART) )

	XCALL RDATE (TODAY)
	SPLFIL(5,6) = 'N2'

	LPSW = 1
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO EXIT
	IF (LPSW.EQ.2) WTARG = 4
	XCALL WAIT (WTARG,V)
OPNFIL,
	OPEN (2,I,VITEMP)			;open temporary file
PRLOOP,
	LOKCTL = NOABRT
	XCALL IOS (2,VIPART,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GOTO DONE
	IF (VIPART(1,9).EQ.BRACKS) GOTO DONE
	IF (VPART.EQ.']]]DEL') GOTO PRLOOP
CONTIN,
	KVNO = IVEN
	KVPART = VPART
	KPART = IPART
	CALL PRINT
	GOTO PRLOOP
DONE,
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	XCALL WAIT (3,V)
	GO TO EXIT

NONE,
	XCALL MESAG ('?ERROR IN TEMPORARY FILE - VITEMP.',1)
	GO TO EXIT

PRINT,
	PRTCTL = 132
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,'NO HDR',' ',
&		LEGEND,'NO LEGEND',' ',LINCTL,132,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN

CLOSES,
	XCALL PGCHN ('PO:VIMENU',1)
EXIT,
	CLOSE 2
	XCALL DELET (2,VITEMP)
ENDOFF,
	XCALL PGCHN ('PO:VIMENU',1)
;**********************************************************************

END

