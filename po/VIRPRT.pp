;VIRPRT / POR  D-11
;
;
;
RECORD POSIDX
	.INCLUDE 'DEF:RD155C.DEF'

RECORD ITMIDX
	.INCLUDE 'DEF:RD042A.DEF'

RECORD ITMMAS	
	.INCLUDE 'DEF:RD041A.DEF'

RECORD ITMCTL	
	.INCLUDE 'DEF:RD041B.DEF'

RECORD VENDR
	.INCLUDE 'DEF:RD011A.DEF'

RECORD VENIDX		
	.INCLUDE 'DEF:RD012A.DEF'

RECORD VENCTL
	.INCLUDE 'DEF:RD011B.DEF'

RECORD PLINE
		,A132
RECORD LINE1 ,X
		,A4
	VENDNO	,A4
		,A4
	VENNAM	,A25
		,A2
	ITMNO	,A15
		,A2
	ITMDSC	,A30
		,A46
RECORD HDR1
		,A45,	'   VEND-NO  VENDOR NAME                ITEM N'
		,A45,	'UMBER      ITEM DESCRIPTION                  '
		,A42

RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,D2

RECORD TITLE
	,A18,	'ITEM VENDOR REPORT'
RECORD
	V	,D1
	SWITCH	,D1
	LOKCTL	,D1
	LPSW	,D1
	SPLFIL	,A14
	PGCNT	,A2
	OLDVEN	,A4
	NOITMS	,D3
	BSMID	,D5
	SRCOPT	,D1
	SRCCTL	,D1
	READ	,D1,	0
	CHANTO	,A9
	RESTRT	,D1
	LINCNT	,D3
	XRECNO	,D5
	WVEN	,A4
	RPTNUM	,D3
	PRTTYP	,A1
	WTARG	,D1,	2
	MSGSW	,D1
	EOF	,D1
	NOABRT	,D1,	1
	BRACKS	,A15,	']]]]]]]]]]]]]]]'
	RJUST	,D1
PROC
	XCALL TERID (V)
	V = 1
	XCALL OUTPT (1,1,2,'VENDOR ITEM REPORT',V)
	XCALL WATE (4,V)

	SWITCH = 5
	XCALL FILES (1,'I',155,SWITCH)		;OPENS POSIDX

	SWITCH = 1
	XCALL FILES (2,'I',041,SWITCH)		;OPENS ITMMAS
	IF (SWITCH.EQ.9) GO TO INUSE2

	SWITCH = 1
	XCALL FILES (3,'I',011,SWITCH)		;OPENS VENMAS
	IF (SWITCH.EQ.9) GO TO INUSE3
	LOKCTL = 0
	XCALL IO (3,VENCTL,1,READ,LOKCTL)		;READS THE CONTROL RECORD OF THE VENDOR FILE
	UNLOCK 3

	SWITCH = 1
	XCALL FILES (4,'I',012,SWITCH)		;OPENS VENIDX
	IF (SWITCH.EQ.9) GO TO INUSE4

	LOKCTL = 1
	XCALL IO (2,ITMCTL,1,READ,LOKCTL)
	RJUST = JSTIFY
	XRECNO = 1
	CALL READIX
	IF (EOF) GO TO NONE

	LPSW = 1
	SPLFIL(5,6) = 'N4'
	PGCNT =
	LINCNT = 60
	XCALL LPON (LPSW,SPLFIL)
	GO TO (EXIT), LPSW + 1
	IF (LPSW.EQ.2) WTARG = 4
	IF (LPSW.NE.4) XCALL WATE (WTARG,V)

	CALL VENDRS		;PRINT ALL VENDORS (ENTIRE REPORT)

	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	XCALL OUTPT (1,1,2,'VENDOR ITEM REPORT',V)
	XCALL WATE (4,V)
	GO TO EXIT
;**********************************************************************
NONE,
	XCALL MESAG ('?REPORT INDEX ERROR IN VIRPRT.',1)
	GO TO EXIT
;**********************************************************************
;**********************************************************************
;	EACH PASS PRINTS THE ITEMS CARRIED BY ONE VENDOR
;**********************************************************************
VENDRS,
	NOITMS =
	OLDVEN = VENNUM
	VENDR =
	SRCOPT = 4
	BSMID = 1
	SRCCTL = 1
	XCALL SERCH (4,VENIDX,VENNUM,1,4,ORG011,BSMID,SRCCTL,SRCOPT,6,10,
&			0,0,0,0)
	IF (SRCCTL.EQ.0) GO TO FOUND
	NAME = 'VENDOR NOT ON FILE'
	GO TO VENDR2
FOUND,
	LOKCTL = NOABRT
	XCALL IO (3,VENDR,IRC011,READ,LOKCTL)
VENDR2,
	VENDNO = VENNUM
	XCALL LEFTJ (VENDNO,4)
	VENNAM = NAME

	CALL ITEMS		; PRINT ALL ITEMS FOR THIS VENDOR
	XCALL LINFD (1)
	INCR LINCNT

	VENNAM(1,3) = NOITMS,'ZZX'
	VENNAM(5,25) = 'ITEMS FOR THIS VENDOR'
	CALL PRINT
	XCALL LINFD(1)
	INCR LINCNT

	IF (EOF) RETURN
	GO TO VENDRS

;**********************************************************************
;	EACH PASS PRINTS ONE ITEM UNDER ONE VENDOR
;**********************************************************************
ITEMS,

	LOKCTL = 1
	XCALL IO (2,ITMMAS,ITRCNO,READ,LOKCTL)
	UNLOCK 2
	ITMNO = ITMNUM
	IF (RJUST) XCALL LEFTJ (ITMNO(1,15),15)
	ITMDSC = DESCR
	INCR NOITMS
	CALL PRINT
	CALL READIX

	IF (EOF.EQ.1.OR.VENNUM.NE.OLDVEN) RETURN
	GO TO ITEMS
;**********************************************************************
READIX,
	EOF =
REREAD,
	INCR XRECNO
	LOKCTL = NOABRT
	XCALL IO (1,POSIDX,XRECNO,READ,LOKCTL)
	IF (ITMNUM.EQ.BRACKS) GO TO ENDFIL
	IF (ITRCNO.EQ.0) GO TO REREAD
	RETURN
ENDFIL,
	EOF = 1
	RETURN

;**********************************************************************

EXIT,
	SWITCH = 4
	XCALL FILES (4,'I',012,SWITCH)
INUSE4,
	SWITCH = 4
	XCALL FILES (3,'I',011,SWITCH)
INUSE3,
	SWITCH = 4
	XCALL FILES (2,'I',041,SWITCH)
INUSE2,
	SWITCH = 7
	XCALL FILES (1,'I',155,SWITCH)
INUSE1,
	RESTRT = 1
	XCALL PGCHN ('PO:POITMR',RESTRT)

;**********************************************************************

PRINT,
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,'NO HDR','NO HDR',
&		'NO LEGEND','NO LEGEND','NO LEGEND',0,132,0,0,LPSW,
&		RPTNUM,PRTTYP)
	RETURN
;**********************************************************************

END

