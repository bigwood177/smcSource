;  POSCRG / POR - D11
;
;
;
;		CREATE INDEX FILE FOR SCHEDULED RECEIPTS REPORTS
;
RECORD HEADER	
	.INCLUDE 'DEF:RD151A.DEF'

RECORD POLINE	
	.INCLUDE 'DEF:RD152A.DEF'

RECORD INDEX	
	.INCLUDE 'DEF:RD155A.DEF'

RECORD KEYPON
	KEY1	,D6
	KEY2	,D2

RECORD RANGES
	STAMAJ	,A15		;STARTING VALUE OF MAJOR KEY
	ENDMAJ	,A15		;ENDING VALUE OF MAJOR KEY
	STADAT	,D6		;STARTING PROMISED DATE
	ENDDAT	,D6		;ENDING PROMISED DATE
	LTDAYS	,D3		;AT LEAST X DAYS LATE
	CLOSED	,D1		;1= INCLUDE CLOSED LINES, 2 = EXCLUDE.
	ALLRCV	,D1		;1 = INCLUDE 100% RECEIVED LINES, 2 = EXCLUDE.
	OBUYER	,A2		;ONLY INCLUDE THIS BUYER (BLANK=INCLUDE ALL)
	ONOTFY	,A3		;ONLY INCLUDE THIS NOTIFY (BLANK=INCLUDE ALL)
	OMOVTO	,A3		;ONLY INCLUDE THIS MOVE-TO (BLANK=INCLUDE ALL)
	OSHPTO	,A2		;ONLY INCLUDE THIS SHIP-TO (BLANK=INCLUDE ALL)
	OUSERC	,A2		;ONLY INCLUDE THIS USER CODE (BLANK=INCLUDE ALL)
	GETOUT	,D1		;1 = RETURN TO RANGE SELECTION

RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2

RECORD SNDMSG
	PRGNAM	,A9
	RCNT	,D5
	OCNT	,D5

RECORD BRACKS
		,A30,	']]]]]]]]]]]]]]]]]]]]]]]]]]]]]]'
		,A21,	']]]]]]]]]]]]]]]]]]]]]'
RECORD
	BLANKS	,A30
	BSMID	,D5
	CNGCTL	,D1
	COUNTR	,D5
	ENTRY	,A30
	EOF	,D1
	EPO	,D1
	HDATE	,D6
	HRECNO	,D5
	INXCTL	,D1
	LOKCTL	,D1
	LRECNO	,D5
	MSGCTL	,D1
	N	,D1
	NOABRT	,D1,	1
	NUMDAY	,D5
	READ	,D1,	0
	RECNO	,D5
	SEL	,D1
	SIZE	,D5
	SRCOPT	,D1
	SRCCTL	,D1
	SWITCH	,D1
	TODAY	,D6
	V	,D1
	WHATNO	,D2
	WKDY1	,D1
	WKDY2	,D2
	WRITE	,D1,	1
	YTODAY	,D6

PROC
	XCALL TERID (V)
	XCALL WATE (4,V)
	XCALL RDATE (TODAY)

	SWITCH = 1
	XCALL FILES (1,'SI',151,SWITCH)		; FILE 151, PORHDR.
	IF (SWITCH.EQ.9) GO TO INUSE1

	SWITCH = 1
	XCALL FILES (2,'SI',152,SWITCH)		; FILE 152, PORLIN.
	IF (SWITCH.EQ.9) GO TO INUSE2

RPTSEL,
	XCALL OUTPT (1,1,2,'SCHEDULED RECEIPTS REPORTS',1)
	XCALL OUTPT (5,20,0,'PLEASE SELECT REPORT',V)
	XCALL OUTPT (6,25,0,'1. SCHEDULED RECEIPTS BY VENDOR',V)
	XCALL OUTPT (7,25,0,'2. SCHEDULED RECEIPTS BY ITEM',V)
;;;	XCALL OUTPT (8,25,0,'3. SCHEDULED RECEIPTS BY JOB',V)

ACCEPT,
	CTL = '05,42,01,00,NE'
	CALL INPUT
	GO TO (RPTSEL,EXIT), INXCTL
	SEL = ENTRY(1,1)
	IF (SEL.LT.1.OR.SEL.GT.2) GO TO ACCEPT
	GETOUT =
GETRGE,
	IF (SEL.EQ.1) XCALL BYVEN (RANGES)
	IF (SEL.EQ.2) XCALL BYITM (RANGES)
	IF (SEL.EQ.3) XCALL BYJOB (RANGES)
	IF (GETOUT.EQ.1) GO TO RPTSEL
	KEY1 =
	KEY2 = 
	LOKCTL = 1
	XCALL ISIO (1,HEADER,KEYPON,READ,LOKCTL)
PROCES,
	CALL READHD		;READ FIRST IN-RANGE HEADER & LINE
	IF (EOF) GO TO NONE
	XCALL WATE (4,V)

	SWITCH = 2
	XCALL FILES (3,'O',155,SWITCH)		; FILE 155, POSIDX
	IF (SWITCH.EQ.9) GO TO EXIT

	INDEX =
	LOKCTL = NOABRT
	XCALL IOS (3,INDEX,WRITE,LOKCTL)
	COUNTR = 1

	CALL PORUN		;SCAN ALL PURCHASE ORDERS

	INDEX = BRACKS
	LOKCTL = NOABRT
	XCALL IOS (3,INDEX,WRITE,LOKCTL)

	XCALL FILES (1,'SI',151,4)	; UNPROTECT AND CLOSE PORHDR
	XCALL FILES (2,'SI',152,4)	; UNPROTECT AND CLOSE PORLIN
	CLOSE 3				; CLOSE POSIDX BUT LEAVE PROTECTED

	IF (SEL.EQ.1) PRGNAM = 'PO:POSCHV'
	IF (SEL.EQ.2) PRGNAM = 'PO:POSCHI'
	IF (SEL.EQ.3) PRGNAM = 'PO:POSCHJ'
	RCNT = COUNTR
	OCNT = 1

	MSGCTL = 5
	XCALL SNMSG (SNDMSG,MSGCTL)
	XCALL PGCHN ('PO:SRTPOI',1)
;**********************************************************************
NOROOM,
	XCALL MESAG ('NO ROOM ON DISK TO CREATE POSIDX FILE',2)
	GO TO ENDOFF
;**********************************************************************
NONE,
	XCALL MESAG ('THERE ARE NO RECORDS IN SELECTED RANGE',2)
	GO TO GETRGE
;**********************************************************************
;	EACH PASS WRITES INDEX FOR ONE PURCHASE ORDER:
;**********************************************************************
PORUN,
	CALL LINES		; SCAN ALL LINE RECORDS, THIS P/O

	CALL READHD		; READ NEXT IN-RANGE HEADER & LINE
	IF (EOF) RETURN
	GO TO PORUN
;**********************************************************************
;	EACH PASS WRITES INDEX FOR ONE LINE ITEM:
;**********************************************************************
LINES,
	IF (SEL.EQ.1) MAJKEY = HVENDR
	IF (SEL.EQ.2) MAJKEY = LITMNO
	IF (SEL.EQ.3) MAJKEY = LJOBNO
	ITMKEY = LITMNO
	VENKEY = HVENDR
	DATKEY = HDATE
	IPODAT = HPODTE
	POSNUM = LPONUM
	POSREL = LRLNUM
	POSLIN = LLINNO
	LOKCTL = NOABRT
	XCALL IOS (3,INDEX,WRITE,LOKCTL)
	INCR COUNTR

	CALL REPEAT
	IF (EOF.OR.EPO) RETURN
	GO TO LINES
;**********************************************************************
;	READ ONE IN-RANGE HEADER RECORD AND ONE IN-RANGE LINE RECORD
;**********************************************************************
READHD,
	EOF =
REREAD,
	LOKCTL = 1
	XCALL IOS (1,HEADER,READ,LOKCTL)
	IF (LOKCTL.NE.0) GOTO ENDFIL
	IF (HPOSTS.NE.'P') GO TO REREAD
	IF (HPOTYP.EQ.'B') GO TO REREAD
	IF (SEL.NE.1) GO TO NOTVEN
	IF (HVENDR.LT.STAMAJ(1,4)) GO TO REREAD
	IF (HVENDR.GT.ENDMAJ(1,4)) GO TO REREAD
NOTVEN,
	IF (OBUYER.NE.BLANKS.AND.OBUYER.NE.HBUYER) GO TO REREAD
	IF (OSHPTO.NE.BLANKS.AND.OSHPTO.NE.HSHPTO) GO TO REREAD
	IF (OUSERC.NE.BLANKS.AND.OUSERC.NE.HUSERC) GO TO REREAD

	CALL READLN
	IF (EOF) RETURN
	IF (EPO) GO TO REREAD
	RETURN
ENDFIL,
	EOF = 1
	RETURN
;**********************************************************************
;	READ ONE IN-RANGE LINE RECORD
;**********************************************************************
READLN,
	EOF =
	EPO =
	KEY1 = HPONUM
	KEY2 = HRLNUM
	XCALL ISIO (2,POLINE,KEYPON,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GOTO TOOFAR
	IF (LOKCTL.EQ.1) GOTO EXIT2
	GOTO REPEA2
REPEAT,
	LOKCTL = 1
	XCALL IOS (2,POLINE,READ,LOKCTL)
REPEA2,
	IF (LOKCTL.EQ.2) GOTO TOOFAR
	IF (LOKCTL.EQ.1) GOTO EXIT2
	IF (LPONUM.LT.HPONUM) GO TO REPEAT
	IF (LPONUM.GT.HPONUM) GO TO TOOFAR
	IF (LRLNUM.LT.HRLNUM) GO TO REPEAT
	IF (LRLNUM.GT.HRLNUM) GO TO TOOFAR
TRYITM,
	IF (SEL.NE.2) GO TO TRYJOB
	IF (LITMNO.LT.STAMAJ) GO TO REPEAT
	IF (LITMNO.GT.ENDMAJ) GO TO REPEAT
TRYJOB,
	IF (SEL.NE.3) GO TO RLIN2
	IF (LJOBNO.LT.STAMAJ(1,6)) GO TO REPEAT
	IF (LJOBNO.GT.ENDMAJ(1,6)) GO TO REPEAT
RLIN2,
	HDATE(1,2) = LDTEPM(5,6)
	HDATE(3,6) = LDTEPM(1,4)
	IF (LDTEPM.NE.0) GO TO RLIN3
	HDATE(1,2) = LDTERQ(5,6)
	HDATE(3,6) = LDTERQ(1,4)
RLIN3,
	IF (HDATE.LT.STADAT.OR.HDATE.GT.ENDDAT) GO TO REPEAT
	IF (LTDAYS.EQ.0) GO TO RLIN4
	YTODAY(1,2) = TODAY(5,6)
	YTODAY(3,6) = TODAY(1,4)
	IF (YTODAY.LE.HDATE) GO TO RLIN4
	IF (LDTEPM.NE.0) XCALL BDATE (TODAY,LDTEPM,WKDY1,WKDY2,NUMDAY)
	IF (LDTEPM.EQ.0) XCALL BDATE (TODAY,LDTERQ,WKDY1,WKDY2,NUMDAY)
	IF (NUMDAY.LT.LTDAYS) GO TO REPEAT
RLIN4,
	IF (LINSTS.EQ.'X') GO TO REPEAT
	IF (CLOSED.EQ.2.AND.LINSTS.EQ.'C') GO TO REPEAT
	IF (ALLRCV.EQ.2.AND.LQTYRC.GE.LQTYOR) GO TO REPEAT
	IF (ONOTFY.NE.BLANKS.AND.ONOTFY.NE.LNOTIF) GO TO REPEAT
	IF (OMOVTO.NE.BLANKS.AND.OMOVTO.NE.LMOVET) GO TO REPEAT
	RETURN
TOOFAR,
	EPO = 1
	RETURN
EXIT2,
	EOF = 1
	RETURN
;**********************************************************************
;	INPUT, IN-USE, AND EXIT ROUTINES
;**********************************************************************
INPUT,
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,V)
	RETURN
;**********************************************************************
ENDOFF,
	XCALL FILES (3,'O',155,4)
EXIT,
	XCALL FILES (2,'SI',152,4)
INUSE2,
	XCALL FILES (1,'SI',151,4)
INUSE1,
	XCALL PGCHN ('PO:POMENU',1)
;**********************************************************************

END

