;POLDEL / POR  D-11
;		THIS APPLICATION DELETES NEW PURCHASE ORDER LINES
;
RECORD HEADER
	.INCLUDE 'DEF:RD151A.DEF'

RECORD POLINE	
	.INCLUDE 'DEF:RD152A.DEF'

RECORD ITMIDX
	.INCLUDE 'DEF:RD042A.DEF'

RECORD ITMMAS	
	.INCLUDE 'DEF:RD041A.DEF'

RECORD ITMCTL	
	.INCLUDE 'DEF:RD041B.DEF'

RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2

RECORD	OLDLIN
	.INCLUDE 'DEF:RD152C.DEF'

RECORD
	EXITSW	,D1
	INXCTL	,D1
	V	,D1
	PONUM	,D8
	LINEIN	,D3
	LASNUM	,D3
	CNGCTL	,D1
	REORLV	,D8
	QONORD	,D8
	SRCOPT	,D1
	HRCNUM	,D5
	BSMID	,D5
	ENTRY	,A12
	SRCCTL	,D1
	RESTRT	,D1
	SWITCH	,D1
	TBLCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	DELETE	,D1,3
	LOKCTL	,D1
	BLANKS	,A10
	SAVORD	,D6
	DKEY	,D11
	QONHND	,D8
	UPDFLG	,D1
	WCHLIN	,D5
	LINWGT	,D8
;;;	OLDLIN	,A319
	RJUST	,D1
	AITMNO	,A15
PROC
	XCALL TERID (V)
	V = 1

	SWITCH = 1
	XCALL FILES (1,'SU',151,SWITCH)
	IF (SWITCH.EQ.9) GO TO INUSE1

	SWITCH = 1
	XCALL FILES (2,'SU',152,SWITCH)
	IF (SWITCH.EQ.9) GO TO INUSE2

	SWITCH = 1
	XCALL FILES (7,'I',041,SWITCH)
	IF (SWITCH.EQ.9) GO TO INUSE6

	SWITCH = 1
	XCALL FILES (6,'I',042,SWITCH)
	IF (SWITCH.EQ.9) GO TO INUSE7

	LOKCTL = 1
	XCALL IO (7,ITMCTL,1,READ,LOKCTL)
	RJUST = JSTIFY
BEGIN,
	LASNUM =
	XCALL OUTPT (1,1,3,'PURCHASE ORDER ENTRY',V)
	XCALL OUTPT (2,1,0,'DELETE P/O LINE',V)
	XCALL OUTPT (08,20,0,'PLEASE ENTER P/O NUMBER:',V)
	XCALL IO (7,ITMCTL,1,READ,LOKCTL)
	IF (LOKCTL.EQ.1) GO TO END

	CTL = '10,25,06,00,#E'
	CALL INPUT
	GO TO (BEGIN,END), INXCTL
	IF (ENTRY.EQ.BLANKS) GO TO BEGIN
	PONUM(1,6) = ENTRY(1,6)
	ENTRY(1,6) = PONUM(1,6),'ZZZZXX'
	XCALL OUTPT (10,25,0,ENTRY(1,6),V)
	XCALL OUTPT (10,31,0,'-',V)

	CTL = '10,32,02,00,# '
	CALL INPUT
	GO TO (BEGIN), INXCTL
	PONUM(7,8) = ENTRY(1,2)
	XCALL OUTPT (10,32,0,PONUM(7,8),V)

	SRCOPT = 4
	SRCCTL = 2
	BSMID = 1
	LOKCTL = 1
	XCALL ISIO (1,HEADER,PONUM,READ,LOKCTL)
	IF (LOKCTL.EQ.0) GO TO INPT1
	XCALL MESAG ('PURCHASE ORDER NOT ON FILE',1)
	HEADER =
	PONUM =
	GO TO BEGIN

;**********************************************************************

INPT1,
	CALL DISPLA
	CTL = '04,20,03,00,#E'
	CALL INPUT
	GO TO (INPT1,BEGIN), INXCTL
	LINEIN = ENTRY(1,3)
	IF (ENTRY.EQ.BLANKS) LINEIN = LASNUM + 1
	XCALL OUTPT (4,20,0,LINEIN,V)

	LASNUM = LINEIN
	DKEY(1,6) = HPONUM
	DKEY(7,8) = HRLNUM
	DKEY(9,11) = LINEIN
	CALL LSERCH
	IF (LOKCTL.EQ.0) GO TO LNAVBL
	XCALL MESAG ('LINE NOT ON FILE',1)
	POLINE =
	LINEIN =
	GO TO INPT1
LNAVBL,
	CALL LINE
	OLDLIN = POLINE
	IF (LINSTS.NE.'N') GO TO NOTNEW
	GO TO DELETE

NOTNEW,
	IF (LCXCDE.EQ.'X') GO TO CANCLD
	XCALL OUTPT (24,1,1,'ONLY NEW LINES MAY BE DELETED.',V)
	XCALL OUTPT (24,33,0,'DO YOU WISH TO CANCEL THIS LINE ?',V)
	CTL = '24,67,01,00,YY'
	CALL INPUT
	IF (INXCTL.EQ.2) XCALL MESAG ('LINE NOT CANCELLED AND NOT DELETED',2)
	IF (INXCTL.EQ.2) GO TO INPT1
	XCALL MESAG ('LINE CANCELLED AND NOT DELETED',2)
	LCXCDE = 'X'
	GO TO CHANGE

CANCLD,
	XCALL MESAG ('ONLY NEW LINES MAY BE DELETED',2)
	GO TO INPT1
;**********************************************************************
LSERCH,
	LOKCTL = 1
	XCALL ISIO (2,POLINE,DKEY,READ,LOKCTL)
	RETURN
;**********************************************************************
;	DISPLAYS THE SCREEN
;**********************************************************************

DISPLA,
	POLINE =
	XCALL OUTPT (1,1,3,'PURCHASE ORDER ENTRY',V)
	XCALL OUTPT (2,1,0,'DELETE P/O LINE',V)
	XCALL OUTPT (2,41,0,'P/O NO',V)
	ENTRY(1,6) = HPONUM,'ZZZZXX'
	XCALL OUTPT (2,52,0,ENTRY(1,6),V)
	XCALL OUTPT (2,58,0,'-',V)
	XCALL OUTPT (2,59,0,HRLNUM,V)
	XCALL OUTPT (4,2,0,'1. LINE NO',V)
	XCALL OUTPT (6,2,0,'2. STATUS',V)
	XCALL OUTPT (8,2,0,'3. CHANGE/CANCEL',V)
	XCALL OUTPT (10,2,0,'4. ITEM NO',V)
	XCALL OUTPT (12,2,0,'5. DESCRIPTION',V)
	XCALL OUTPT (14,2,0,'6. U OF M',V)
	XCALL OUTPT (16,2,0,'7. COMMODITY',V)
	XCALL OUTPT (18,2,0,'8. COST/UNIT',V)
	XCALL OUTPT (20,2,0,'9. VENDOR-ITM NO',V)
	XCALL OUTPT (22,1,0,'10. BLANKET TYPE',V)
	XCALL OUTPT (4,37,0,'11. QTY ORD',V)
	XCALL OUTPT (6,37,0,'12. QTY RLS',V)
	XCALL OUTPT (8,37,0,'13. QTY RCV',V)
	XCALL OUTPT (10,37,0,'14. NOTIFY',V)
	XCALL OUTPT (14,37,0,'15. DATE REQ',V)
	XCALL OUTPT (16,37,0,'16. DATE PROM',V)
	XCALL OUTPT (18,37,0,'17. DATE RCVD',V)
	XCALL OUTPT (20,37,0,'18. JOB NO',V)
	XCALL OUTPT (22,37,0,'19. MOVE TO',V)
	XCALL OUTPT (8,63,0,'P/O WGT',V)
	XCALL OUTPT (10,63,0,'LIN WGT',V)
	XCALL OUTPT (12,63,0,'AVAILBL',V)
	XCALL OUTPT (14,63,0,'ON-ORDER',V)
	XCALL OUTPT (16,63,0,'RE-ORDER',V)
	RETURN

;**********************************************************************
;	DISPLAYS THE CONTENTS OF A LINE RECORD
;**********************************************************************

LINE,
	QONHND =
	REORLV =
	QONORD =
	XCALL OUTPT (04,20,0,LLINNO,V)
	XCALL OUTPT (06,20,0,LINSTS,V)
	XCALL OUTPT (08,20,0,LCXCDE,V)
	AITMNO = LITMNO
	IF (RJUST) XCALL LEFTJ (AITMNO(1,15),15)
	XCALL OUTPT (10,20,0,AITMNO,V)
	XCALL OUTPT (12,20,0,LITMDS,V)
	XCALL OUTPT (14,20,0,LITMUM,V)
	XCALL OUTPT (16,20,0,LITMCC,V)
	ENTRY(1,12) = LEXCST,'ZZZ,ZXX.XXXX'
	XCALL OUTPT (18,20,0,ENTRY(1,12),V)
	XCALL OUTPT (20,20,0,LVITMN,V)
	XCALL OUTPT (22,20,0,LBLKTP,V)
	ENTRY(1,7) = LQTYOR,'ZZZ,ZXX'
	XCALL OUTPT (04,52,0, ENTRY(1,7),V)
	ENTRY(1,7) = LQTYRC,'ZZZ,ZXX'
	XCALL OUTPT (06,52,0,ENTRY(1,7),V)
	ENTRY(1,7) = LQTYRJ,'ZZZ,ZXX'
	XCALL OUTPT (08,52,0,ENTRY(1,7),V)
	XCALL DSPLY (06,14,52,LDTERQ,2,V)
	XCALL DSPLY (06,16,52,LDTEPM,2,V)
	XCALL DSPLY (06,18,52,LDTERC,2,V)
	XCALL OUTPT (20,52,0,LJOBNO,V)
	XCALL OUTPT (22,52,0,LMOVET,V)
	ENTRY(1,8) = HPOWGT,'ZZ,ZXX.X'
	XCALL OUTPT (08,72,0,ENTRY(1,8),V)
	LINWGT = (LQTYOR*WEIGHT)
	ENTRY(1,9) = LINWGT,'ZZ,ZXX.XX'
	XCALL OUTPT (10,72,0,ENTRY(1,9),V)
	BSMID = 1
	SRCCTL = 1
	SRCOPT = 4
	XCALL SERCH (6,ITMIDX,LITMNO,1,15,ORG041,BSMID,SRCCTL,SRCOPT,16,20,0,0,0,0)
	IF (SRCCTL.EQ.0) GO TO FOUND
	ITMMAS =
	ITMIDX =
	GO TO NOFND
FOUND,
	XCALL IO (7,ITMMAS,IRC041,READ,LOKCTL)
NOFND,
; only 1 loc for smc...
	QONHND = QTYONH
	QONORD = QTYONO
	REORLV = REOLVL
	ENTRY(1,10) = QTYONO,'ZZ,ZZZ,ZXX'
	XCALL OUTPT (12,71,0,ENTRY(1,10),V)
	ENTRY(1,9) = QONORD,'ZZ,ZZZ,ZXX'
	XCALL OUTPT (14,71,0,ENTRY(1,10),V)
	ENTRY(1,7) = REORLV,'ZZ,ZZZ,ZXX'
	XCALL OUTPT (16,71,0,ENTRY(1,10),V)
	RETURN

;**********************************************************************

DELETE,
	XCALL OUTPT (24,1,0,'RIGHT LINE ?',V)
	CTL = '24,14,01,00,YN'
	CALL INPUT
	IF (INXCTL.EQ.2) XCALL MESAG ('LINE NOT DELETED',2)
	IF (INXCTL.EQ.2) GO TO INPT1
	UPDFLG = 0
	IF (HPOTYP.EQ.'R') UPDFLG = 1
CHANGE,
	LOKCTL = 1
	XCALL ISIO (2,POLINE,POLKEY,DELETE,LOKCTL)
	IF (LOKCTL.NE.0) XCALL MESAG ('ERROR DELETING LINE',2)
	IF (LOKCTL.EQ.0) XCALL MESAG ('LINE DELETED',2)
	IF (POLINE.NE.OLDLIN) HCXCDE = 'C'
FREBUF,
	UNLOCK 2
	IF (UPDFLG.EQ.0) GO TO INPT1
	SAVORD = LQTYOR
	DKEY(1,6) = HPONUM
	DKEY(7,8) = 00
	DKEY(9,11) = LLINNO
	CALL LSERCH
	IF (LOKCTL.NE.0) GO TO INPT1
	LQTYRC = LQTYRC - SAVORD
	LOKCTL = 1
	XCALL ISIO (2,POLINE,POLKEY,WRITE,LOKCTL)
	GO TO INPT1
;**********************************************************************
END,
	XCALL WATE (1,V)
	IF (INXCTL.EQ.2) GO TO INUSE7
	CALL REWRIT

	SWITCH = 4
	XCALL FILES (7,'I',041,SWITCH)
INUSE7,
	SWITCH = 4
	XCALL FILES (6,'I',042,SWITCH)
INUSE6,
	SWITCH = 4
	XCALL FILES (2,'SU',152,SWITCH)
INUSE2,
	SWITCH = 4
	XCALL FILES (1,'SU',151,SWITCH)

INUSE1,
	RESTRT = 1
	XCALL PGCHN ('PO:POENMN',RESTRT)

;**********************************************************************
;	 REWRITES THE P/O HEADER WITH 'CHANGE' MARKED
;**********************************************************************

REWRIT,
	LOKCTL = 1
	XCALL ISIO (1,HEADER,PORKEY,WRITE,LOKCTL)
	RETURN

;**********************************************************************

INPUT,
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,V)
	RETURN


END

