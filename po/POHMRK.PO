;  POHMRK.POR
;
;
;	MANUALLY MARK PO'S COMPLETE (HPOSTS = 'M')
;
;
;
;
;
;		MARKS CLOSED OR CANCELLED PURCHASE ORDERS FOR DELETION
;
RECORD HEADER	
	.INCLUDE 'DEF:RD151A.DEF'

RECORD POLINE
	.INCLUDE 'DEF:RD152A.DEF'

RECORD VENDOR	
	.INCLUDE 'DEF:RD011A.DEF'

RECORD DUMVEN	
	.INCLUDE 'DEF:RD011B.DEF'

RECORD VENIDX	
	.INCLUDE 'DEF:RD012A.DEF'

RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2

RECORD TABLE
	TNO	,D2
	CDE	,A2
	SQ	,D1
	DSC	,A30

RECORD TBLKEY
	TKEYNO	,D2
	TKEYCD	,A2
	TKEYSQ	,D1

RECORD RANGE1
	STPONO	,D6
	STRLNO	,D2
	ENPONO	,D6
	ENRLNO	,D2

RECORD RANGE2
	STPODA	,D6
	ENPODA	,D6

RECORD
	ALL	,D1
	PONUM	,A8
	DKEY	,A8
	APONUM	,A9
	BLANKS	,A10
	BRACKS	,A30,	']]]]]]]]]]]]]]]]]]]]]]]]]]]]]]'
	BSMID	,D5
	BUYER	,A2
	CDATE	,D6
	CNGCTL	,D1
	DUMMY	,A2
	ENTRY	,A30
	EOF	,D1
	EPO	,D1
	EVTFAC	,D5
	TDATE	,D6	;TODAY'S DATE
	MDATE	,D6
	HDATE	,D6
	HRECNO	,D5
	INXCTL	,D1
	JDATE	,D6
	LOKCTL	,D1
	LRECNO	,D5
	N	,D1
	NOABRT	,D1,	1
	PTDSLA	,D6
	PTLDTM	,D5
	PTPREJ	,D4
	PTPVAR	,D4
	READ	,D1,	0
	RECNO	,D5
	SRCOPT	,D1
	SRCCTL	,D1
	SUB	,D2
	SWITCH	,D1
	V	,D1
	WHATNO	,D2
	WRITE	,D1,	1

RECORD	LINOR
		,A4,	'LINE'
		,A1
	LNNO	,A3
		,A38,	'NOT FULLY RECEIVED, MARK AS COMPLETE ?'


PROC
	XCALL TERID (V)
	XCALL RDATE(TDATE)
	V = 1
	XCALL OUTPT (2,1,2,' ',1)
	XCALL WATE (4,V)

	SWITCH = 5
	XCALL FILES (1,'SU',151,SWITCH)		; FILE 151, PORHDR.
	IF (SWITCH.EQ.9) GO TO INUSE1

	SWITCH = 5
	XCALL FILES (2,'SU',152,SWITCH)		; FILE 152, PORLIN.
	IF (SWITCH.EQ.9) GO TO INUSE2

	SWITCH = 1
	XCALL FILES (4,'I',011,SWITCH)		; FILE 011, VENMAS.
	IF (SWITCH.EQ.9) GO TO INUSE3

	SWITCH = 1
	XCALL FILES (5,'I',012,SWITCH)		; FILE 012, VENIDX.
	IF (SWITCH.EQ.9) GO TO INUSE4

BEGIN,
	ALL = 1
	XCALL OUTPT (1,1,3,'PURCHASE ORDER ENTRY',V)
	XCALL OUTPT (2,1,0,'MANUALLY CLOSE PURCHASE ORDERS',V)
	XCALL OUTPT (08,20,0,'PLEASE ENTER:',V)
	XCALL OUTPT (10,24,0,'1. STARTING P/O NUMBER',V)
	XCALL OUTPT (12,24,0,'2. ENDING P/O NUMBER',V)
	XCALL OUTPT (14,24,0,'3. STARTING P/O DATE',V)
	XCALL OUTPT (16,24,0,'4. ENDING P/O DATE',V)

STARPO,
	CTL = '10,48,06,00,#E'
	CALL INPUT
	GO TO (BEGIN,EXIT), INXCTL
	IF (ENTRY.EQ.BLANKS) GO TO ALLPO
	ALL =
	STPONO = ENTRY(1,6)
	ENTRY(1,6) = STPONO,'ZZZZXX'
	XCALL OUTPT (10,48,0,ENTRY(1,6),V)
	XCALL OUTPT (10,54,0,'-',V)

	CTL = '10,55,02,00,# '
	CALL INPUT
	IF (INXCTL.EQ.1) GO TO BEGIN
	STRLNO = ENTRY(1,2)
	ENTRY(1,2) = STRLNO,'XX'
	XCALL OUTPT (10,55,0,ENTRY(1,2),V)
	IF (ENPONO.EQ.999999.AND.ENRLNO.EQ.99) GO TO ENDNO2
	IF (CNGCTL.NE.1) GO TO ENDNO2
	IF (ENPONO.LT.STPONO) GO TO MSG1
	IF (ENPONO.EQ.STPONO.AND.ENRLNO.LT.STRLNO) GO TO MSG1
	GO TO ANYCNG
ENDNO,
	IF (ENPONO.EQ.999999.AND.ENRLNO.EQ.99) GO TO STARPO
ENDNO2,
	CTL = '12,48,06,00,# '
	CALL INPUT
	IF (INXCTL.EQ.1) GO TO BEGIN
	IF (ENTRY.EQ.BLANKS) ENTRY(1,6) = STPONO
	ENPONO = ENTRY(1,6)
	ENTRY(1,6) = ENPONO,'ZZZZXX'
	XCALL OUTPT (12,48,0,ENTRY(1,6),V)
	XCALL OUTPT (12,54,0,'-',V)

	CTL = '12,55,02,00,# '
	CALL INPUT
	IF (INXCTL.EQ.1) GO TO BEGIN
	IF (ENTRY.EQ.BLANKS) ENTRY(1,2) = STRLNO,'XX'
	ENRLNO = ENTRY(1,2)
	ENTRY(1,2) = ENRLNO,'XX'
	XCALL OUTPT (12,55,0,ENTRY(1,2),V)
	IF (ENPONO.LT.STPONO) GO TO MSG1
	IF (ENPONO.EQ.STPONO.AND.ENRLNO.LT.STRLNO) GO TO MSG1
ENDNO3,
	GO TO (ANYCNG), CNGCTL
STDTE,
	CTL = '14,48,06,00,D '
	CALL INPUT
	IF (INXCTL.EQ.1) GO TO BEGIN
	IF (ENTRY.EQ.BLANKS) GO TO ALLDTE
	ALL =
	HDATE = ENTRY(1,6)
	STPODA(1,2) = HDATE(5,6)
	STPODA(3,6) = HDATE(1,4)
	IF (ENPODA.EQ.999999) GO TO ENDTE2
	IF (CNGCTL.NE.1) GO TO ENDTE2
	IF (ENPODA.LT.STPODA) GO TO MSG2
	GO TO ANYCNG
ENDTE,
	IF (ENPODA.EQ.999999) GO TO STDTE
ENDTE2,
	CTL = '16,48,06,00,D '
	CALL INPUT
	IF (INXCTL.EQ.1) GO TO BEGIN
	IF (ENTRY.EQ.BLANKS) GO TO SAMEDA
	HDATE = ENTRY(1,6)
	ENPODA(1,2) = HDATE(5,6)
	ENPODA(3,6) = HDATE(1,4)
	IF (ENPODA.LT.STPODA) GO TO MSG2
ANYCNG,
	WHATNO =
	XCALL ANYCN (CNGCTL,WHATNO)
	IF (CNGCTL.EQ.0) GO TO PROCES
CNGBR,
	GO TO (STARPO,ENDNO,STDTE,ENDTE), WHATNO
BADCNG,
	CNGCTL = 3
	GO TO ANYCNG
;**********************************************************************
ALLPO,
	XCALL OUTPT (10,48,0,'ALL      ',V)
	XCALL OUTPT (12,48,0,'         ',V)
	STPONO =
	ENPONO = 999999
	STRLNO =
	ENRLNO = 99
	GO TO (ANYCNG), CNGCTL
	GO TO STDTE
;**********************************************************************
MSG1,
	XCALL MESAG ('STARTING/ENDING PO NUMBERS ARE OUT OF ORDER',1)
	ENPONO = 999999
	ENRLNO = 99
	GO TO STARPO
;**********************************************************************
ALLDTE,
	XCALL OUTPT (14,48,0,'ALL     ',V)
	XCALL OUTPT (16,48,0,'        ',V)
	STPODA =
	ENPODA = 999999
	GO TO ANYCNG
;**********************************************************************
SAMEDA,
	IF (STPODA.EQ.0) GO TO STDTE
	ENPODA = STPODA
	ENTRY(1,5) = STPODA(3,6),'XX/XX'
	ENTRY(6,8) = STPODA(1,2),'/XX'
	XCALL OUTPT (ROW,COL,0,ENTRY(1,8),V)
	GO TO ANYCNG
;**********************************************************************
MSG2,
	XCALL MESAG ('STARTING/ENDING PO DATES ARE OUT OF ORDER',1)
	ENPODA = 999999
	GO TO STDTE
;**********************************************************************
;	DELETE CONTROL ROUTINE
;**********************************************************************
PROCES,
	XCALL WATE (3,V)
	EOF =
	PONUM(1,6) = STPONO,'XXXXXX'
	PONUM(7,8) = STRLNO,'XX'
	LOKCTL = 1
	XCALL ISIO (1,HEADER,PONUM,READ,LOKCTL)
	IF(LOKCTL .NE. 0) EOF = 1
	IF (EOF) GO TO NONE

	LOKCTL = 0
	XCALL IO (4,DUMVEN,1,READ,LOKCTL)
	IF (LOKCTL.EQ.1) GO TO EXIT
	UNLOCK 4

	CALL DELRUN		;DELETE  PURCHASE ORDERS

	IF (ALL) GO TO EXIT
	GO TO BEGIN
;**********************************************************************

NONE,
	XCALL MESAG ('THERE ARE NO CLOSED/CANCELLED P/OS IN SELECTED RANGE.',2)
	GO TO BEGIN

;**********************************************************************
;	EACH PASS DISPLAYS ONE PURCHASE ORDER:
;**********************************************************************

DELRUN,
	VENDOR =
	SRCOPT = 4
	BSMID = 1
	SRCCTL = 1
	XCALL SERCH (5,VENIDX,HVENDR,1,4,ORG011,BSMID,SRCCTL,SRCOPT,6,10,
&			0,0,0,0)
	IF (SRCCTL.EQ.0) GO TO FOUND
	NAME = 'VENDOR NOT ON FILE'
	GO TO DELRU2
FOUND,
	LOKCTL = NOABRT
	XCALL IO (4,VENDOR,IRC011,READ,LOKCTL)
DELRU2,
	CALL TAGS		;DISPLAYS THE FIELD NAMES ON THE SCREEN

	CALL DISPLA		;DISPLAYS THE VALUES IN THE HEADER RECORD
ASK,
	XCALL OUTPT (24,1,0,'OK TO CLOSE THIS PURCHASE ORDER ?',V)
	CTL = '24,37,01,00,AT'
	CALL INPUT
	IF (INXCTL.EQ.3) GO TO BEGIN
	IF (ENTRY.EQ.'N') GO TO NOTDEL
	IF (ENTRY.EQ.' ') GO TO NOTDEL
	IF (ENTRY.NE.'Y'.AND.ENTRY.NE.' ') GO TO ASK

	XCALL WATE (3,V)
	IF (HPOSTS.EQ.'C') INCR POSYTD

	HPOSTS = 'M'

	LOKCTL = NOABRT
	XCALL ISIO (1,HEADER,PORKEY,WRITE,LOKCTL)

	CALL READLN		;FINDS THE FIRST CORRESPONDING LINE
	IF (EPO.OR.EOF) GO TO FINISH

	CALL LINES		;MARKS EACH LINE FOR DELETION AND TABULATES

FINISH,

DELETD,
	XCALL MESAG ('PURCHASE ORDER CLOSED',2)
	GO TO NEWHDR
NOTDEL,
	XCALL MESAG ('PURCHASE ORDER NOT CLOSED',2)
NEWHDR,
	CALL READHD
	IF (EOF) RETURN
	GO TO DELRUN

;**********************************************************************
;	EACH PASS MARKS LINE FOR DELETION AND TABULATES STATISTICS
;**********************************************************************

LINES,
	IF(LINSTS.NE.'C' .AND. LQTYRC.LT.LQTYOR)
		BEGIN
		LNNO = LLINNO,'ZZX'
		XCALL OUTPT(24,1,1,LINOR,1)
		XCALL INPUT(24,70,01,01,'YN',ENTRY,INXCTL,1)
		IF(ENTRY(1,1) .NE. 'Y') GOTO NLIN
		END

	LINSTS = 'C'		;MARK LINE ITEM CLOSED
	MDATE = TDATE		;SET COMPLETE DATE TO TODAY'S DATE

	LOKCTL = NOABRT
	XCALL ISIO (2,POLINE,POLKEY,WRITE,LOKCTL)
NLIN,
	CALL RDLN
	IF (EPO.OR.EOF) RETURN
	GO TO LINES

;**********************************************************************
;	READ ONE IN-RANGE HEADER RECORD
;**********************************************************************

READHD,
	EOF =
REREAD,
	LOKCTL = 1
	XCALL IOS (1,HEADER,READ,LOKCTL)
	IF (LOKCTL.NE.0) GOTO ENDFIL

	IF (HPONUM.LT.STPONO) GO TO REREAD
	IF (HPONUM.GT.ENPONO) GO TO ENDFIL
	IF (HRLNUM.LT.STRLNO) GO TO REREAD
	IF (HRLNUM.GT.ENRLNO) GO TO ENDFIL
	HDATE(1,2) = HPODTE(5,6)
	HDATE(3,6) = HPODTE(1,4)
	IF (HDATE.LT.STPODA.OR.HDATE.GT.ENPODA) GO TO REREAD
	RETURN
ENDFIL,
	EOF = 1
	RETURN

;**********************************************************************
;	READ ONE IN-RANGE LINE RECORD
;**********************************************************************

READLN,
	EOF = 
	EPO =
	DKEY = PONUM
	LOKCTL = 1
	XCALL ISIO (2,POLINE,DKEY,READ,LOKCTL)	
	IF (LOKCTL.EQ.2) GOTO TOOFAR
	IF (LOKCTL.EQ.1) GOTO EXIT2
	GOTO CONTIN

RDLN,	EOF =
	EPO =
REPEAT,
	LOKCTL = 1
	XCALL IOS (2,POLINE,READ,LOKCTL)
CONTIN,
	IF (LOKCTL.EQ.2) GOTO TOOFAR
	IF (LOKCTL.EQ.1) GOTO EXIT2
	IF (LPONUM.LT.HPONUM) GO TO REPEAT
	IF (LPONUM.GT.HPONUM) GO TO TOOFAR
	IF (LRLNUM.LT.HRLNUM) GO TO REPEAT
	IF (LRLNUM.GT.HRLNUM) GO TO TOOFAR
	RETURN
TOOFAR,
	EPO = 1
	RETURN
EXIT2,
	EOF = 1
	RETURN

;**********************************************************************
;	DISPLAY FIELD NAMES
;**********************************************************************
TAGS,
	XCALL OUTPT (4,2,2,'1. P/O NO',V)
	XCALL OUTPT (6,2,0,'2. P/O TYPE',V)
	XCALL OUTPT (8,2,0,'3. P/O STATUS',V)
	XCALL OUTPT (10,2,0,'4. CHANGE/CANCEL',V)
	XCALL OUTPT (12,2,0,'5. VENDOR NO',V)
	XCALL OUTPT (14,2,0,'6. P/O DATE',V)
	XCALL OUTPT (16,2,0,'7. SHIP TO',V)
	XCALL OUTPT (18,2,0,'8. BUYER',V)
	XCALL OUTPT (20,2,0,'9. SHIP-VIA',V)
	XCALL OUTPT (22,1,0,'10. ACKNOWLEDGE',V)
	XCALL OUTPT (4,33,0,'11. COL/PPD',V)
	XCALL OUTPT (6,33,0,'12. TERMS',V)
	XCALL OUTPT (8,33,0,'13. FOB',V)
	XCALL OUTPT (10,33,0,'14. CONFIRM',V)
	XCALL OUTPT (04,51,0,'15. CONFIRMED WITH',V)
	XCALL OUTPT (06,51,0,'16. PRINT PRICE',V)
	XCALL OUTPT (08,51,0,'17. USER CODE',V)
	XCALL OUTPT (10,51,0,'18. P/O WEIGHT',V)
	RETURN
;**********************************************************************
;	DISPLAY PO HEADER DATA
;**********************************************************************
DISPLA,
	APONUM(1,6) = HPONUM,'ZZZZXX'
	APONUM(7,7) = '-'
	APONUM(8,9) = HRLNUM,'XX'
	XCALL OUTPT (04,20,0,APONUM,V)
	XCALL OUTPT (06,20,0,HPOTYP,V)
	XCALL OUTPT (08,20,0,HPOSTS,V)
	XCALL OUTPT (10,20,0,HCXCDE,V)
	XCALL OUTPT (12,20,0,HVENDR,V)
	XCALL OUTPT (12,26,0,NAME,V)
	XCALL DSPLY (6,14,20,HPODTE,2,V)
	XCALL OUTPT (16,20,0,HSHPTO,V)
	XCALL OUTPT (18,20,0,HBUYER,V)
	XCALL OUTPT (20,20,0,HSHPVI,V)
	XCALL OUTPT (22,20,0,HACKNW,V)
	XCALL OUTPT (04,46,0,HCOLPP,V)
	XCALL OUTPT (06,46,0,HTERMS,V)
	XCALL OUTPT (08,46,0,HFOB,V)
	XCALL OUTPT (10,46,0,HCONFR,V)
	XCALL OUTPT (04,71,0,HCNFRW,V)
	XCALL OUTPT (06,71,0,HPRTPR,V)
	XCALL OUTPT (08,71,0,HUSERC,V)
	XCALL DSPLY (6,10,71,HPOWGT,5,V)
	RETURN

;**********************************************************************
INPUT,
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,V)
	RETURN
;**********************************************************************
EXIT,
	XCALL WATE (4,V)
	XCALL FILES (5,'I',012,4)
INUSE4,
	XCALL FILES (4,'I',011,4)
INUSE3,
	XCALL FILES (2,'SU',152,4)
INUSE2,
	XCALL FILES (1,'SU',151,4)
INUSE1,
	XCALL PGCHN ('PO:POENMN',1)
;**********************************************************************

END

