;  CODRPT / POR - D11
;
;		AUTHOR: 13-MAY-94  DKS
;
;		SHOW PURCHASING HISTORY FOR COMMODITY CODES PIN,TERM, SCKT
;
;
;
RECORD RCVHST
	.INCLUDE 'DEF:RD154A.DEF'

RECORD	FFILE
	FILNUM	,D3
	CLCTL	,D1
	REDFIL	,A14

RECORD POITEM
		,A11,'DPO:CODITM.'
	TEXT	,A3

RECORD PLINE
 		,A132

RECORD LINE1,X
		,A1
	KIMNUM	,A15
		,A5
	KIMDES	,A30
		,A8
	KQYRCV	,A7

RECORD HDR1
		,A1
		,A11,	'PART-NUMBER'
		,A9
		,A11,	'DESCRIPTION'
		,A29
		,A3,	'QTY'

RECORD HDR2
		,A59
		,A8,	'RECEIVED'

RECORD LEGEND
		,A1
		,A18,'DATE RECEIVED FROM'
		,A3
	DTE1	,A8
		,A3
		,A2,'TO'
		,A3
	DTE2	,A8
		,A86

RECORD
	CNGCTL	,D1
	WHATNO	,D1
	TNUM	,D3
	TOTQTY	,D8
	LSTITM	,A15
	LSTDSC	,A30
	BLANKS	,A15
	BRACKS	,A30,	']]]]]]]]]]]]]]]]]]]]]]]]]]]]]]'
	DSPSW	,D1
	EOF	,D1
	IDFLAG	,D1
	LINCNT	,D2,	60
	LINCTL	,D1
	LOKCTL	,D1
	LPSW	,D1
	NOABRT	,D1,	1
	PGCNT	,D3,	000
	PRTCTL	,D3
	PRTTYP	,A1
	READ	,D1,	0
	RPTNUM	,D3
	SPLFIL	,A14
	SRCOPT	,D1
	SWITCH	,D1
	TITLE	,A30,	'ML2 - RECEIVING HISTORY REPORT'
	TODAY	,D6
	V	,D1
	WTARG	,D1,	2
	XRECNO	,D5
	INXCTL	,D1
	ENTRY	,A15
	ALL	,A10
	ASOFDT	,A8
	STDATE	,D6
	ENDATE	,D6
	HLDATE	,D6
	

PROC
	XCALL TERID (V)
	XCALL TNMBR(TNUM)
	TEXT=TNUM,'XXX'
	XCALL OUTPT (2,1,2,'ML2 - RECEIVING HISTORY REPORT',1)
GETDAT,
	XCALL OUTPT (6,20,2,'PLEASE ENTER STARTING RECEIVE DATE',V)
	XCALL INPUT (6,56,6,0,'DE',ENTRY,INXCTL,V)
	GO TO (GETDAT,ENDOFF), INXCTL
	HLDATE = ENTRY (1,6)
	IF (HLDATE.EQ.0) XCALL RDATE (HLDATE)
	IF (HLDATE.EQ.0) GO TO GETDAT
	STDATE(1,2) = HLDATE(5,6)
	STDATE(3,6) = HLDATE(1,4)
	DTE1 = HLDATE, 'XX/XX/XX'
	ASOFDT = HLDATE, 'XX/XX/XX'
	XCALL OUTPT (6,56,0,ASOFDT,V)
GETDT2,
	XCALL OUTPT (7,20,2,'PLEASE ENTER ENDING RECEIVE DATE',V)
	XCALL INPUT (7,56,6,0,'D ',ENTRY,INXCTL,V)
	GO TO (GETDAT,GETDAT), INXCTL
	HLDATE = ENTRY (1,6)
	IF (HLDATE.EQ.0) XCALL RDATE (HLDATE)
	IF (HLDATE.EQ.0) GO TO GETDT2
	ENDATE(1,2) = HLDATE(5,6)
	ENDATE(3,6) = HLDATE(1,4)
	DTE2 = HLDATE, 'XX/XX/XX'
	ASOFDT = HLDATE, 'XX/XX/XX'
	XCALL OUTPT (7,56,0,ASOFDT,V)
START,
	CNGCTL=2
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (GETDAT),CNGCTL
	XCALL WATE (4,V)
	XCALL RDATE (TODAY)
	SPLFIL(5,6) = 'NA'

	LPSW = 1
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO EXIT

;;Sort the receiving history file by comm code into a temporary file for this report
	XCALL WATE(4,V)
	XCALL OUTPT(2,1,0,'RECEIVING HISTORY REPORT',1)
	XCALL OUTPT(2,45,0,'SORTING DPO:CODITM - "CODRPT.DBR"',1)
	FILNUM = 154
	XCALL FFILE (FILNUM,REDFIL,CLCTL)	;GET FILE SPEC
	SORT (IN=REDFIL, OUT=POITEM, RECORD=RCVHST,
&		KEY=(RITMCC,RCVINO) )
		
	OPEN (2,I,POITEM)			;open temporary file

	LSTITM = 
	XRECNO = 1
	IF (LPSW.EQ.2) WTARG = 4
	XCALL WATE (WTARG,V)

OPNLOOP,
	CALL READIX
	IF (EOF.EQ.1) GOTO DONE
	IF (RCVINO.NE.LSTITM) CALL NEWITM
	TOTQTY = TOTQTY + RCVNOW
	GOTO OPNLOOP
NEWITM,
	IF (LSTITM.EQ.BLANKS) GOTO ENDITM
	KIMNUM = LSTITM
	KIMDES = LSTDSC
	KQYRCV = TOTQTY,'ZZZ,ZZX'
	CALL PRINT
ENDITM,
	LSTITM = RCVINO
	LSTDSC = RCVDSC
	TOTQTY =
	RETURN

DONE,
	CALL NEWITM
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	XCALL WATE (3,V)
	GO TO EXIT
;**********************************************************************
;	READ ONE RCVHST RECORD AND RETURN
;**********************************************************************
READIX,
	EOF =
REREAD,
	INCR XRECNO
	LOKCTL = NOABRT
	XCALL IO (2,RCVHST,XRECNO,READ,LOKCTL)
	IF (RCVINO.EQ.BRACKS) GO TO ENDFIL
	IF (RDTERC.LT.STDATE.OR.RDTERC.GT.ENDATE) GOTO REREAD
	IF (RITMCC.NE.'PIN ' .AND. RITMCC.NE.'TERM' .AND.
&	    RITMCC.NE.'SCKT') GOTO REREAD
	RETURN
ENDFIL,
	EOF = 1
	RETURN
;**********************************************************************
PRINT,
	PRTCTL = 80
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,'NO HDR',
&		LEGEND,'NO LEGEND',' ',LINCTL,80,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;**********************************************************************
CLOSES,
	XCALL FILES (1,'I',121,4)
	XCALL FILES (3,'I',122,4)
	CLOSE 2
	XCALL DELET(2,POITEM)
	RETURN
;**********************************************************************
EXIT,
	CALL CLOSES
ENDOFF,
	XCALL PGCHN ('PO:VAMENU',1)
;**********************************************************************

END

