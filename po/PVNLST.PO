;   PVNLST / POR - D11
;
;
;;;RECORD RD012A
;;;	.INCLUDE 'DEF:RD012A.DEF'

RECORD RD011A
	.INCLUDE 'DEF:RD011A.DEF'

RECORD RD011B	
	.INCLUDE 'DEF:RD011B.DEF'

RECORD RD153A
	.INCLUDE 'DEF:RD153A.DEF'

RECORD RD153B
	.INCLUDE 'DEF:RD153B.DEF'

RECORD HDR1
	,A4,	'NAME'
	,A29
	,A7,	'ADDRESS'
	,A23
	,A5,	'CODES'
	,A27
	,A11,	'COMMODITIES'
	,A2
	,A12,	'YEAR TO DATE'
	,A12

;**********************************************************************
RECORD CLEAR
	KFOB	,A9
	KPERM	,A3
	KTERMS	,A10
	KACK	,A3
	KACTIV	,A3
	KSPVIA	,A15
	KCNFRM	,A3

;**********************************************************************
RECORD CLEAR2
	STNAME	,A15
	ENNAME	,A15
	STVNNO	,A4
	ENVNNO	,A4
	RUSERC	,A2
	RVTYPE	,D1
	SUBA	,A1
	SUBD	,D1
	SUBP	,D1
	USERSW	,D1
	ACTVSW	,D1
	PERMSW	,D1
;**********************************************************************
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2

;**********************************************************************
RECORD TBLKEY
	KTBLNO	,D2
	KCODNO	,A2

;**********************************************************************
RECORD WORK
	ALL	,D1
	BLANKS	,A15
	BRACKS	,A15,	']]]]]]]]]]]]]]]'
	BSMID	,D5
	CNGCTL	,D1
	DECMAL	,D4
	DSPSW	,D1
	ENTRY	,A30
	EOF	,D1
	INXCTL	,D1
	LINCNT	,D2,	60
	LINCTL	,D1
	LOKCTL	,D1
	LPSW	,D1
	NOABRT	,D1,	1
	NUM	,D5
	NUMFMT	,A5
	NXTROW	,D2
	PERMNT	,2A9,	'PERMANENT','TEMPORARY'
	PGCNT	,D3,	000
	PLINE	,A132
	PRTTYP	,A1
	READ	,D1,	0
	RPTNUM	,D3
	SAVLPS	,D1
	SPLFIL	,A14
	SRCOPT	,D1
	SRCCTL	,D1
	SWITCH	,D1
	TITLE	,A18,	'P/O VENDOR LISTING'
	TODAY	,D6
	V	,D1
	VRECNO	,D5
	WACTIV	,2A10,	'ACTIVE    ','NOT-ACTIVE'
	WHATNO	,D2
	WTARG	,D1,	2

;**********************************************************************
PROC
;**********************************************************************
	XCALL TERID (V)
	XCALL OUTPT (2,1,2,'PRINT VENDOR LISTING',1)
	XCALL WATE (4,V)
	SPLFIL(5,6) = 'N8'

	SWITCH = 1
	XCALL FILES (1,'I',153,SWITCH)
	IF (SWITCH.EQ.9) GO TO EXIT

	LOKCTL = NOABRT
	XCALL IO (1,RD153B,1,READ,LOKCTL)

	SWITCH = 5
	XCALL FILES (2,'SI',11,SWITCH)
	IF (SWITCH.EQ.9) GO TO CLOSE1

;;;	SWITCH = 5
;;;	XCALL FILES (3,'I',12,SWITCH)
;;;	IF (SWITCH.EQ.9) GO TO CLOSE2
;;;
;;;	LOKCTL = NOABRT
;;;	XCALL IO (2,RD011B,1,READ,LOKCTL)


;**********************************************************************
BEGIN,
	LPSW = 1
	PGCNT =
	LINCNT = 60
	NUM =
	CLEAR2 =
	CNGCTL =
	XCALL OUTPT (1,1,2,'VENDOR MAINTENANCE',V)
	XCALL OUTPT (2,1,0,'PRINT VENDOR LISTING',1)
	XCALL OUTPT (3,20,0,'PLEASE ENTER:',V)
	XCALL OUTPT (4,25,0,'1. STARTING VENDOR NUMBER',V)
	XCALL OUTPT (5,25,0,'2. ENDING VENDOR NUMBER',V)
	XCALL OUTPT (6,25,0,'3. STARTING VENDOR NAME',V)
	XCALL OUTPT (7,25,0,'4. ENDING VENDOR NAME',V)
	XCALL OUTPT (8,25,0,'5. USER CODE',V)
	XCALL OUTPT (9,25,0,'6. ACTIVE/NOT-ACTIVE ?',V)
	XCALL OUTPT (10,25,0,'7. PERMANENT/TEMPORARY ?',V)

ONE,
	CTL = '04,52,04,00,AE'
	CALL INPUT
	GO TO (BEGIN,CLOSE3), INXCTL
	IF (ENTRY.EQ.BLANKS) GO TO ALLVEN
	XCALL FRMAT (ENTRY(1,4),4)
	STVNNO = ENTRY
	XCALL OUTPT (ROW,COL,0,STVNNO,V)
	IF (ENVNNO.EQ.BRACKS) GO TO TWO2
	IF (CNGCTL.NE.1) GO TO TWO2
	IF (ENVNNO.LT.STVNNO) GO TO MSG1
	GO TO ANYCN
TWO,
	IF (ENVNNO.EQ.BRACKS) GO TO ONE
TWO2,
	CTL = '05,52,04,00,A '
	CALL INPUT
	GO TO (BEGIN) ,INXCTL
	IF (ENTRY.EQ.BLANKS) ENTRY = STVNNO
	XCALL FRMAT (ENTRY(1,4),4)
	ENVNNO = ENTRY
	XCALL OUTPT (ROW,COL,0,ENVNNO,V)
	IF (ENVNNO.LT.STVNNO) GO TO MSG1
	GO TO (ANYCN), CNGCTL
THREE,
	CTL = '06,52,15,00,A '
	CALL INPUT
	IF (INXCTL) GO TO BEGIN
	IF (ENTRY.EQ.BLANKS) GO TO ALLNAM
	STNAME = ENTRY
	XCALL OUTPT (ROW,COL,0,STNAME,V)
	IF (ENNAME.EQ.BRACKS) GO TO FOUR2
	IF (CNGCTL.NE.1) GO TO FOUR2
	IF (ENNAME.LT.STNAME) GO TO MSG2
	GO TO ANYCN
FOUR,
	IF (ENNAME.EQ.BRACKS) GO TO THREE
FOUR2,
	CTL = '07,52,15,00,A '
	CALL INPUT
	IF (INXCTL) GO TO BEGIN
	IF (ENTRY.EQ.BLANKS) ENTRY = STNAME
	ENNAME = ENTRY
	XCALL OUTPT (ROW,COL,0,ENNAME,V)
	IF (ENNAME.LT.STNAME) GO TO MSG2
	IF (CNGCTL) GO TO ANYCN

FIVE,
	CTL = '08,52,02,00,A '
	CALL INPUT
	IF (INXCTL) GO TO BEGIN
	IF (ENTRY.EQ.BLANKS) GO TO ALLUSE
	RUSERC = ENTRY
	XCALL OUTPT (ROW,COL,0,RUSERC,V)
	IF (CNGCTL) GO TO ANYCN

SIX,
	IF (CNGCTL) XCALL OUTPT (9,52,1,'\',V)
	XCALL OUTPT (9,58,0,'1 = ACTIVE',V)
	XCALL OUTPT (10,58,0,'2 = NOT-ACTIVE',V)
	CTL = '09,52,01,00,A '
	CALL INPUT
	IF (INXCTL) GO TO BEGIN
	IF (ENTRY.EQ.BLANKS) GO TO ALLACT
	SUBA = ENTRY
	SUBD = SUBA
	IF (ENTRY.GT.'2'.OR.ENTRY.LT.'1') GO TO SIX
	XCALL OUTPT (9,58,1,'\',V)
	XCALL OUTPT (10,58,1,'\',V)
	ENTRY = WACTIV(SUBD)
	XCALL OUTPT (9,COL,0,ENTRY,V)
	IF (CNGCTL) GO TO ANYCN


SEVEN,
	IF (CNGCTL) XCALL OUTPT (10,52,1,'\',V)
	XCALL OUTPT (10,58,0,'1 = PERMANENT',V)
	XCALL OUTPT (11,58,0,'2 = TEMPORARY',V)
	CTL = '10,52,01,00, A'
	CALL INPUT
	IF (INXCTL) GO TO BEGIN
	IF (ENTRY.EQ.BLANKS) GO TO ALLPRM
	IF (ENTRY.LT.'1'.OR.ENTRY.GT.'2') GO TO SEVEN
	SUBP = ENTRY
	RVTYPE = SUBP - 1
	XCALL OUTPT (10,58,1,'\',V)
	XCALL OUTPT ( 11,58,1,'\',V)
	XCALL OUTPT (ROW,COL,0,PERMNT(SUBP),V)
ANYCN,
	XCALL ANYCN (CNGCTL, WHATNO)
	IF (CNGCTL) GO TO (ONE,TWO,THREE,FOUR,FIVE,SIX,SEVEN), WHATNO
	IF (WHATNO.GT.7) GO TO ANYCN
	GO TO PROCES

;**********************************************************************
MSG1,
	XCALL MESAG ('STARTING/ENDING VENDOR NUMBERS ARE OUT OF ORDER',1)
	ENVNNO = BRACKS
	GO TO ONE
MSG2,
	XCALL MESAG ('STARTING/ENDING VENDOR NAMES ARE OUT OF ORDER',1)
	ENNAME = BRACKS
	GO TO THREE
;**********************************************************************
ALLVEN,
	XCALL OUTPT (ROW,COL,1,'ALL',V)
	NXTROW = ROW + 1
	XCALL OUTPT (NXTROW,COL,1,'\',V)
	STVNNO =
	ENVNNO = BRACKS
	GO TO (ANYCN), CNGCTL
	GO TO THREE

ALLNAM,
	XCALL OUTPT (ROW,COL,1,'ALL',V)
	NXTROW = ROW + 1
	XCALL OUTPT (NXTROW,COL,1,'\',V)
	STNAME =
	ENNAME = BRACKS
	IF (CNGCTL) GO TO ANYCN
	GO TO FIVE

ALLUSE,
	XCALL OUTPT (ROW,COL,1,'ALL',V)
	USERSW = 1
	IF (CNGCTL) GO TO ANYCN
	GO TO SIX

ALLACT,
	XCALL OUTPT (ROW,COL,1,'ALL',V)
	ACTVSW = 1
	XCALL OUTPT (9,58,1,'\',V)
	XCALL OUTPT (10,58,1,'\',V)
	IF (CNGCTL) GO TO ANYCN
	GO TO SEVEN

ALLPRM,
	XCALL OUTPT (ROW,COL,1,'ALL',V)
	PERMSW = 1
	XCALL OUTPT (10,58,1,'\',V)
	XCALL OUTPT ( 11,58,1,'\',V)
	IF (CNGCTL) GO TO ANYCN
	GO TO ANYCN

;**********************************************************************
PROCES,
	ALL =
	IF (STVNNO.EQ.BLANKS .AND. ENVNNO.EQ.BRACKS .AND.
&	STNAME.EQ.BLANKS .AND. ENNAME.EQ.BRACKS .AND.
&	USERSW .AND. ACTVSW .AND. PERMSW) ALL = 1
	VRECNO = 1
	read(2, rd011a, ^first) [err=reread]

	CALL READVN
	IF (EOF) GO TO NONE
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO CLOSE3
	IF (LPSW.EQ.2) WTARG = 1
	XCALL WATE (WTARG,V)

	CALL VENLST	;PRINT OUT THE ENTIRE VENDOR LIST

	NUMFMT = NUM,'ZZZZX'
	PLINE (11,15)	= NUMFMT
	PLINE (17,30) 	= 'VENDORS LISTED'
	XCALL LINFD (2)
	INCR LINCNT
	INCR LINCNT
	CALL PRINT
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	IF (ALL) GO TO CLOSE3
	GO TO BEGIN
;**********************************************************************
NONE,
	XCALL MESAG ('THERE ARE NO VENDORS IN SELECTED RANGE',2)
	GO TO BEGIN
;**********************************************************************
READVN,
	EOF =
REREAD,
;;;	INCR VRECNO
;;;	IF (VRECNO.GT.REC011) GO TO ENDFIL
;;;	LOKCTL = NOABRT
;;;	XCALL IO (3,RD012A,VRECNO,READ,LOKCTL)
;;;	IF (RD012A.EQ.BRACKS) GO TO ENDFIL
;;;	IF (IRC011.EQ.0) GO TO REREAD

	LOKCTL = 1
	XCALL IOS (2, RD011A, READ, LOKCTL)
	if (lokctl .ne. 0) goto endfil

	IF (VENNO.LT.STVNNO) GO TO REREAD
	IF (VENNO.GT.ENVNNO) GO TO REREAD

RDNEXT,
;;;	LOKCTL = NOABRT
;;;	XCALL IO (2,RD011A,IRC011,READ,LOKCTL)
	IF (NAME(1,6).EQ.']]]DEL') GO TO REREAD
	IF (NAME(1,15).LT.STNAME) GO TO REREAD
	IF (NAME(1,15).GT.ENNAME) GO TO REREAD
	IF (USERSW.NE.1.AND.RUSERC.NE.USERCD) GO TO REREAD
	IF (ACTVSW.NE.1.AND.SUBA.NE.ACTIVE) GO TO REREAD
	IF (PERMSW.NE.1.AND.RVTYPE.NE.VTYPE) GO TO REREAD
	RETURN

ENDFIL,
	EOF = 1
	RETURN

;**********************************************************************
VENLST,
	CLEAR =

	KTBLNO 	= '05'
	KCODNO	= FOB
	CALL TBLFND
	KFOB = TDESC
	TBLKEY =

	KTBLNO 	= '04'
	KCODNO	= TERMS
	CALL TBLFND
	KTERMS = TDESC
	TBLKEY =

	KTBLNO 	= '03'
	KCODNO	= SHPVIA
	CALL TBLFND
	KSPVIA = TDESC
	TBLKEY =

	IF (VTYPE.EQ.0) KPERM = 'YES'
	IF (VTYPE.EQ.1) KPERM = ' NO'

	IF (ACKNWL.EQ.'Y') KACK = 'YES'
	IF (ACKNWL.EQ.'N') KACK = ' NO'

	IF (ACTIVE.EQ.'1') KACTIV = 'YES'
	IF (ACTIVE.EQ.'2') KACTIV = ' NO'

	IF (CONFRM.EQ.'Y') KCNFRM = 'YES'
	IF (CONFRM.EQ.'N') KCNFRM = ' NO'

FIRST,
	INCR NUM

	PLINE (1,30)	= NAME
	PLINE (33,58)	= ADD1
	PLINE (65,68)	= 'FOB:'
	PLINE (70,78)	= KFOB
	PLINE (86,90)	= 'USER:'
	PLINE (94,95)	= USERCD
;;;	PLINE (98,101)	= COMMCD(1)
;;;	PLINE (103,106)	= COMMCD(2)
	PLINE (109,113)	= 'P/OS:'
	PLINE (117,119)	= POSYTD,'ZZX'
	PLINE (123,127) = 'PERM:'
	PLINE (130,132)	= KPERM

	LINCTL =
	CALL PRINT

SECOND,
	PLINE (1,3)	= 'NO:'
	PLINE (5,8)	= VENNO
	PLINE (11,16)	= 'PHONE:'
	PLINE (19,30)	= TELNO
	PLINE (33,58)	= ADD2
	PLINE (63,69)	='TERMS:'
	PLINE (70,79)	=KTERMS
	PLINE (86,89)	= 'ACK:'
	PLINE (93,95)	= KACK
;;;	PLINE (98,101)	= COMMCD(3)
;;;	PLINE (103,106)	= COMMCD(4)
	PLINE (109,114)	= 'LINES:'
	PLINE (116,119)	= LNSYTD,'ZZZX'
	PLINE (123,128)	= 'ACTVE:'
	PLINE (130,132)	= KACTIV


	CALL PRINT

THIRD,
	PLINE (1,8)	= 'CONTACT:'
	PLINE (11,30)	= CONTCT
	PLINE (33,47)	= CITY
	PLINE (49,50)	= STATE
	PLINE (52,61)	= ZIP
	PLINE (63,68)	= 'S/VIA:'
	PLINE (70,84)	= KSPVIA
	PLINE (86,91)	= 'CNFRM:'
	PLINE (93,95)	= KCNFRM
;;;	PLINE (100,104)	= COMMCD(5)
	PLINE (109,112)	= 'INV:'
	PLINE (114,126)	= BILYTD,'Z,ZZZ,ZZX.XX-'

	LINCTL = 1
	CALL PRINT

	CALL READVN
	IF (EOF) RETURN
	GO TO VENLST

;**********************************************************************
CLOSE3,
	XCALL WATE (4,1)
;;;	XCALL FILES (3,'I',12,4)
CLOSE2,
	XCALL FILES (2,'SI',11,4)
CLOSE1,
	XCALL FILES (1,'I',153,4)
EXIT,
	XCALL PGCHN ('PO:PVNMNT',1)

;**********************************************************************
TBLFND,
	SRCCTL = 2
	XCALL SERCH (1,RD153A,TBLKEY,1,4,ORG153,BSMID,SRCCTL,4,6,11,0,0,0,0)
	IF (SRCCTL) RD153A =
	RETURN

;**********************************************************************
PRINT,
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,'NO HDR',' ',
&	'NO LEGEND',' ',' ',LINCTL,0,0,0,LPSW,RPTNUM,PRTTYP)
	RETURN

;**********************************************************************
INPUT,
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,V)
	RETURN

;**********************************************************************

END

