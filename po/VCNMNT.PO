;VCNMNT.PO
;	COP TABLE MAINTENANCE
;	VENDOR CONTACTS
;
RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'

RECORD	VENMAS
	.INCLUDE 'DEF:RD011A.DEF'
RECORD,X
	.INCLUDE 'DEF:RD011B.DEF'

RECORD	VENIDX
	.INCLUDE 'DEF:RD012A.DEF'

RECORD	CHANNEL
	CHN182	,D2
	CHN011	,D2
	CHN012	,D2


RECORD	PRINT
	TITLE	,A*,	'VENDOR CONTACTS TABLE'
	HD	,A6,	'NO HDR'
	LG	,A9,	'NO LEGEND'
	PLINE	,A80
	PRNTON	,D1
	LINCNT	,D2,60
	PGCNT	,D6
	LPSW	,D2
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	PRTCTL	,D3,080
	LPARG	,D1
	PRNTSW	,D1
	PRTCTR	,D1


RECORD	HD1
		,A*,	'VENDOR  SEQ  CONTACT'

RECORD	VARS
	JROW	,D2
	XROW	,D2
	ALPHA	,A12
	OPNOK	,D1
	BLANKS	,A15
	CONTACT	,A20
	EMAIL	,A50
	VENDOR	,A4
	SEQ	,D3
	ST_VEN	,A4
	EN_VEN	,A4
	PGM	,D1
	ROW	,D2
	BSEND	,D5
	BSMID	,D5
	SRCCTL	,D1
;;;	ENTRY	,A30
	ENTRY	,A50
	INXCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	STORE	,D1,2
	DELETE	,D1,3
	LOKCTL	,D1
	WHATNO	,D2
	SELECT	,D1
	CNGCTL	,D1
	I	,D3
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,TITLE,1)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO CLOSE

MENU,
	XCALL OUTPT (1,1,2,TITLE,1)
	XCALL OUTPT (3,9,0,'PLEASE SELECT APPLICATION',1)
	XCALL OUTPT (5,15,0,'1. TABLE MAINTENANCE',1)
	XCALL OUTPT (6,15,0,'2. PRINT TABLE',1)
MINPUT,
	XCALL INPUT (3,36,1,1,'#E',ENTRY,INXCTL,1)
	GOTO (MINPUT,ENDOFF), INXCTL
	PGM = ENTRY(1,1)
	GOTO (DISPLA,PRINT_TABLE),PGM
	GOTO MINPUT

DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,TITLE,1)
	XCALL OUTPT ( 4,4,0,'1. VENDOR #',1)
	XCALL OUTPT ( 6,4,0,'2. SEQ #',1)
	XCALL OUTPT ( 8,4,0,'3. CONTACT',1)
	XCALL OUTPT (10,4,0,'4. EMAIL',1)

VENDOR,
	XCALL INPUT (4,24,04,00,'AE',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	VENDOR = ENTRY(1,4)
	XCALL SERCH (CHN012,VENIDX,VENDOR,1,4,BSEND,BSMID,SRCCTL,
&			4,5,10,0,0,0,0)
	IF (SRCCTL.NE.0 .OR. IRC011.LE.0)
		BEGIN
		XCALL MESAG ('VENDOR NOT ON FILE',1)
		GOTO VENDOR
		END
	XCALL IO (CHN011,VENMAS,IRC011,READ,LOKCTL)
	XCALL OUTPT (4,30,1,NAME,1)
	CALL GET_CONT
	GOTO (CHKTBL),CNGCTL
SEQ,
	XCALL INPUT (6,24,03,00,'# ',ENTRY,INXCTL,1)
	GOTO(DISPLA),INXCTL
	SEQ = ENTRY(1,3)
	IF (JROW.GT.4)
  	  BEGIN
	  CLEAR PLINE
	  FOR XROW FROM 5 THRU JROW XCALL OUTPT (XROW,30,0,PLINE(1,49),1)
	  END

	GOTO (CHKTBL),CNGCTL
CHKTBL,
	CLEAR TBL_KEY
	TBL_KEY(1,2) = 'VC'
	VC_VEN = VENDOR
	VC_SEQ = SEQ
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .EQ. 0)
		BEGIN
		SELECT = 2
		CALL DSPREC
		GOTO ANYCNG
		END

	SELECT = 1			;ADD MODE
	CLEAR COPTBL
CONTACT,
	XCALL INPUT (8,24,20,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	CONTACT = ENTRY(1,20)
	GOTO (ANYCNG), CNGCTL
EMAIL,
	XCALL INPUT (10,24,50,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	EMAIL = ENTRY(1,50)

ANYCNG,
	XCALL OUTPT (24,1,1,'FIELD # TO CHANGE            <TAB> = DELETE',1)
	XCALL INPUT (24,20,02,00,'#T',ENTRY,INXCTL,1)
	IF (INXCTL .EQ. 3)
		BEGIN
		XCALL OUTPT (24,1,1,'DELETE, ARE YOU SURE ?',1)
		XCALL INPUT (24,24,01,01,'YN',ENTRY,INXCTL,1)
		GOTO (ANYCNG),INXCTL-1
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,DELETE,LOKCTL)
		GOTO DISPLA
		END

	WHATNO = ENTRY(1,2)
	IF (WHATNO .EQ. 0)
	THEN 	CLEAR CNGCTL
	ELSE 	CNGCTL = 1

	GOTO (PROCES,CNGBR),CNGCTL+1
CNGBR,
	GOTO (VENDOR, SEQ, CONTACT, EMAIL),WHATNO
	GOTO ANYCNG

PROCES,
	CASE SELECT OF
	BEGINCASE
	1:	BEGIN
		TBLCOD = 'VC'
		VC_VEN = VENDOR
		VC_SEQ = SEQ
		VC_CONTCT = CONTACT
		VC_EMAIL = EMAIL
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,STORE,LOKCTL)
		END
	2:	BEGIN
		VC_CONTCT = CONTACT
		VC_EMAIL = EMAIL
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,WRITE,LOKCTL)
		END
	ENDCASE
	GOTO DISPLA

ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('PO:TBLMNU',1)
	STOP


GET_CONT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		;;; DISPLAY EXISTING CONTACTS
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	JROW = 5
	CLEAR TBL_KEY
	TBLCOD = 'VC'
	VC_VEN = VENDOR
	FIND (CHN182,COPTBL,TBL_KEY)[ERR=GC_LOOP]
GC_LOOP,
	XCALL IOS (CHN182, COPTBL, READ, LOKCTL)
	IF (LOKCTL .NE. 0) GOTO GC_EOF
	IF (TBLCOD .NE. 'VC') GOTO GC_EOF
	IF (VC_VEN .NE. VENDOR) GOTO GC_EOF
	PLINE(1,3) = VC_SEQ,	'ZZX'
	PLINE(5,25) = VC_CONTCT
	PLINE(27,57) = VC_EMAIL
	INCR JROW
	IF (JROW .GT. 22) JROW = 4
	XCALL OUTPT (JROW,30,0,PLINE(1,49),1)
	GOTO GC_LOOP
GC_EOF,
	RETURN
;-----------------------------------------------

DSPREC,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; DISPLAY RECORD
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;	XCALL OUTPT (4,24,1,VC_VEN,1)
	ENTRY(1,3) = VC_SEQ,	'ZZX' [LEFT]
	XCALL OUTPT (6,24,0,ENTRY(1,3),1)
	XCALL OUTPT (8,24,0,VC_CONTCT,1)
	XCALL OUTPT (10,24,0,VC_EMAIL,1)

	CONTACT = VC_CONTCT
	EMAIL = VC_EMAIL

	RETURN
;-----------------------------------


;===================================================================
PRINT_TABLE,
;===================================================================
	LINCNT = 66
	PGCNT  = 0
	
PDISP,
	CNGCTL = 
	XCALL OUTPT (1,1,2,TITLE,1)
	XCALL OUTPT (4,4,0,'1. STARTING VENDOR',1)
	XCALL OUTPT (6,4,0,'2. ENDING   VENDOR',1)
ST_VEN,
	XCALL INPUT (4,27,04,00,'AE',ENTRY,INXCTL,1)
	GOTO (PDISP,PDONE),INXCTL
	ST_VEN = ENTRY(1,4)
	IF (ST_VEN .EQ. BLANKS)
		BEGIN
		EN_VEN = 'ZZZZ'
		XCALL OUTPT (4,25,1,'ALL',1)
		XCALL OUTPT (6,25,1,' ',1)
		GOTO P_ANY
		END
	GOTO (P_ANY),CNGCTL
EN_VEN,
	XCALL INPUT (6,25,04,00,'A ',ENTRY,INXCTL,1)
	GOTO (PDISP),INXCTL
	EN_VEN = ENTRY(1,4)
	IF (EN_VEN .EQ. BLANKS)
		BEGIN
		EN_VEN = ST_VEN
		XCALL OUTPT (6,25,0,EN_VEN,1)
		END
P_ANY,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (P_PRINT,P_CNGBR),CNGCTL + 1
P_CNGBR,
	GOTO (ST_VEN,EN_VEN),WHATNO
	GOTO P_ANY

P_PRINT,
	CLEAR TBL_KEY
	TBLCOD = 'VC'
	VC_VEN = ST_VEN
	FIND (CHN182,COPTBL,TBL_KEY)[ERR=PLOOP]

PLOOP,
	XCALL IOS (CHN182,COPTBL,READ,LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF
	IF (TBLCOD .NE. 'VC') GOTO EOF
	IF (VC_VEN .LT. ST_VEN) GOTO PLOOP
	IF (VC_VEN .GT. EN_VEN) GOTO EOF

	PLINE(3,6) = VC_VEN
	PLINE(9,11) = VC_SEQ,	'ZZX'
	PLINE (14,33) = VC_CONTCT
	PLINE (35,80) = VC_EMAIL
;VENDOR  SEQ  CONTACT
;  AAAA  XXX  AAAAAAAAAAAAAAAAAAAA
;12345678901234567890123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5         6         7         8
	CALL PRINT
	GOTO PLOOP

EOF,
	IF (PRNTON.EQ.1)  XCALL LPOFF(LPSW,SPLFIL,PGCNT)
	PRNTON  = 0

PDONE,
	GOTO MENU

PRINT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (PRNTON .EQ. 0) CALL PRNTON
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HD1,HD,HD
&		,LG,LG,LG,0,080,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;-------------------------------------------------------------
PRNTON,
	SPLFIL (5,6) = 'EF'
	LPSW = 1		;PRINT,SPOOL, OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTON = 1
	RETURN
;-------------------------------------------------------------
;===================================================================


OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (1,'SU',182,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN182 = 1

	SWITCH = 5
	XCALL FILES (2,'I',011,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN011 = 2

	SWITCH = 5
	XCALL FILES (3,'I',012,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN012 = 3

	LOKCTL = 1
	XCALL IO (CHN011, VENMAS, 1, READ, LOKCTL)
	BSEND = ORG011

	OPNOK = 1
	RETURN
;----------------------------------------------------

CLOSE,
	CLOSE CHN182
	RETURN
;----------------------------------------------------
