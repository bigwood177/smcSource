;RECEDT.PO	isam
;
;

RECORD	INVTRX
	.INCLUDE 'DEF:RD143A.DEF'

RECORD	ITMMAS
	.INCLUDE 'DEF:RD041A.def'
RECORD ITMCTL,x
	.INCLUDE 'DEF:RD041B.def'

RECORD TITLE1
		,A30,	'INVENTORY RECEIVINGS EDIT LIST'
RECORD HDR1
		,A46
	BLOC	,A3,	'LOC'
		,A1
	ALOC	,A3
		,A2
		,A3,	'OLD'
		,A4
		,A3,	'QTY'
		,A4
		,A3,	'NEW'
		,A2
	AODR	,A3,	'ORD'
		,A2
	OLD1	,A3,	'OLD'
		,A4
	NEW1	,A3,	'NEW'
		,A5
		,A7,	'OLD AVG'
		,A2
		,A8,	'NEW UNIT'
		,A4
		,A7,	'NEW AVG'
		,A3
		,A5,	'ORDER'
		,A2
RECORD HDR2
		,A7,	'ITEM NO'
		,A9
		,A16,	'ITEM DESCRIPTION'
		,A15
	BTO	,A2,	'TO'
		,A1
	AFRM	,A3
		,A1
	AVAIL	,A5,	'ONHND'
		,A2
	ARCVD	,A5,	'RCVED'
		,A2
	AVAIL2	,A5,	'ONHND'
		,A1
	ACPL	,A3,	'CPL'
		,A1
	AVAIL3	,A5,	'ONORD'
		,A2
	AVAIL4	,A5,	'ONORD'
		,A5
		,A4,	'COST'
		,A6
		,A4,	'COST'
		,A7
		,A4,	'COST'
		,A4
		,A6,	'NUMBER'
		,A2
RECORD LEGNDA
		,A38,	'NOTE: LOCATIONS BRACKETED BY ASTERISKS'
		,A32,	' ARE NEW LOCATIONS FOR THAT ITEM'
RECORD
	ITMCNT	,D5
	TOTQTY	,D7
	LEGND1	,A70
	LEGND2	,A8
	LEGND3	,A70
	MASK1	,A11,	'ZZZZZZ.XXX-'
	MASK2	,A10,	'ZZZZZZ.XX-'
	MASK3	,A10,	'Z,ZZZ,ZZX-'
	MASK4	,A7,	'ZZZZZX-'
	STARS	,A4,	'****'
	LINCNT	,D2,	60
	LINCTL	,D2
	PGCNT	,D3
	TITLE	,A30
	PLINE	,A132
	PRTCTL	,D3,	132
	NEWONO	,D5
	SPLFIL	,A14
	SWITCH	,D1
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	V	,D1
	LPSW	,D2
	LPARG	,D1
	RPTNUM	,D3
	PRTTYP	,A1
PROC 
	XCALL TERID (V)
	XCALL OUTPT (2,1,1,'EDIT LIST',1)
	XCALL WATE (4,V)

	SWITCH = 1
	XCALL FILES (1,'SI',41,SWITCH)		;FILE # 41 -- ITMMAS FILE
	IF (SWITCH.EQ.9) GO TO END

	ITEMNO = '!!!!!!!!!!!!!!!'
	LOKCTL = 1
	XCALL ISIO (1,ITMCTL,ITEMNO,READ,LOKCTL)
	XCALL FILES (1,'SI',41,4)

	SWITCH = 1
	XCALL FILES (3,'I',143,SWITCH)		;FILE # 143 -- PORTRX FILE
	IF (SWITCH.EQ.9) GO TO END
	LOKCTL = 1
	XCALL IOS (3,INVTRX,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO ENDOFF
	SPLFIL(5,6) = 'CF'
	LPSW = 1
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
READ,
	LOKCTL = 1
	XCALL IOS (3,INVTRX,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOF
	IF (RITMNO.EQ.']]]]]]') GO TO EOF
	IF (RDESCR(1,6).EQ.']]]DEL') GO TO READ
	IF (TRXTYP.EQ.1) GO TO TRECVS			;TOTAL RECEIVINGS
	IF (TRXTYP.EQ.2) GO TO TTRNSF			;TOTAL TRANSFERS
BUFF,
	PLINE (1,15) = RITMNO
	IF (JSTIFY) XCALL LEFTJ (PLINE(1,15),15)
	PLINE (17,46) = RDESCR
	IF (NEWLOC.EQ.1) PLINE (47,50) = STARS
	PLINE (48,49) = LOCTO
	PLINE (54,60) = TOOONH, MASK4
	PLINE (62,67) = QTYRCD, MASK4
	PLINE (68,74) = (TOOONH + QTYRCD), MASK4
	PLINE (76,76) = ORDCMP
	PLINE (78,84) = TOOONO, MASK4
	NEWONO = TOOONO - QTYRCD
	IF (NEWONO.GE.0) GO TO PRINEW
	NEWONO = 0
PRINEW,
	PLINE (85,91) = NEWONO, MASK4
	PLINE (92,102) = OLDAVG, MASK1
	PLINE (103,112) = NEWCST, MASK2
	PLINE (113,123) = NEWAVG, MASK1
	PLINE (124,132) = PONUM
	LINCTL = 1
	CALL PRINT
	INCR ITMCNT
	TOTQTY = TOTQTY + QTYRCD
	GO TO READ
PRINT,
	IF (LINCNT.GE.60) CALL SETLIN
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,
&	'NO HDR',LEGND1,LEGND2,LEGND3,LINCTL,132,PRTCTL,1,LPSW,RPTNUM,PRTTYP)
	RETURN
SETLIN,
	TITLE = TITLE1
	LEGND1 = LEGNDA
	LEGND2 = 'NO LEGEND'
	IF (LPSW.NE.4 .AND. LPSW.NE.-4) RETURN
	TITLE = 'NO TITLE'
	LEGND1 = TITLE1
	LEGND2 =
	LEGND3 = LEGNDA
	RETURN
TRECVS,
	IF (ITMCNT.EQ.0) GO TO TRANSF
	CALL TOTLIN
TRANSF,
	IF (LPSW.EQ.-4) SLEEP 2
	IF (LPSW.EQ.-4) LPSW = -1 * LPSW
	TITLE1 (11,30) = 'TRANSFERS EDIT LIST '
	LINCNT = 60
	ITMCNT =
	ALOC = 'LOC'
	AFRM = 'FRM'
	ARCVD = 'TRFRD'
	HDR1(74,132) =
	HDR2(74,132) =
	AVAIL3 = 'AVAIL'
	AVAIL4 = 'AVAIL'
	OLD1 = 'OLD'
	NEW1 = 'NEW'
	GO TO BUFF1
READ1,
	LOKCTL = 1
	XCALL IOS (3,INVTRX,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOF
	IF (RITMNO.EQ.']]]]]]') GO TO EOF
	IF (RDESCR(1,6).EQ.']]]DEL') GO TO READ1
	IF (TRXTYP.EQ.2) GO TO TTRNSF			;TOTAL TRANSFERS
BUFF1,
	PLINE (1,15) = RITMNO
	IF (JSTIFY) XCALL LEFTJ (PLINE(1,15),15)
	PLINE (17,46) = RDESCR
	IF (NEWLOC.EQ.1.OR.NEWLOC.EQ.3) PLINE (47,50) = STARS
	PLINE (48,49) = LOCTO
	PLINE (54,60) = TOOONH, MASK4
	PLINE (62,67) = QTYRCD, MASK4
	PLINE (68,74) = (TOOONH + QTYRCD), MASK4
	LINCTL = 0
	CALL PRINT
	IF (NEWLOC.EQ.2.OR.NEWLOC.EQ.3) PLINE (50,53) = STARS
	PLINE (51,52) = LOCFRM
	PLINE (78,84) = FROONH, MASK4
	PLINE (85,91) = (FROONH - QTYRCD), MASK4
	LINCNT = LINCNT - 1
	LINCTL = 1
	CALL PRINT
	INCR LINCNT
	INCR ITMCNT
	GO TO READ1
TTRNSF,
	IF (ITMCNT.EQ.0) GO TO ISSUES
	CALL TOTLIN
ISSUES,
	IF (LPSW.EQ.-4) SLEEP 2
	IF (LPSW.EQ.-4) LPSW = -1 * LPSW
	TITLE1 (11,30) = 'ISSUES EDIT LIST    '
	LINCNT = 60
	ITMCNT =
	BLOC =
	BTO =
	HDR1 (74,132) =
	HDR2 (74,132) =
	ALOC = 'LOC'
	AFRM = 'FRM'
	AVAIL = 'AVAIL'
	AVAIL2 = 'AVAIL'
	ARCVD = 'ISSUE'
	HDR1 (79,83) = 'ORDER'
	HDR2 (78,83) = 'NUMBER'
	GO TO BUFF2
READ2,
	LOKCTL = 1
	XCALL IOS (3,INVTRX,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOF
	IF (RITMNO.EQ.']]]]]]') GO TO EOF
	IF (RDESCR(1,6).EQ.']]]DEL') GO TO READ2
BUFF2,
	PLINE (1,15) = RITMNO
	IF (JSTIFY) XCALL LEFTJ (PLINE(1,15),15)
	PLINE (17,46) = RDESCR
	IF (NEWLOC.EQ.2) PLINE (50,53) = STARS
	PLINE (51,52) = LOCFRM
	PLINE (54,60) = FROONH, MASK4
	PLINE (62,67) = QTYRCD, MASK4
	PLINE (68,74) = (FROONH - QTYRCD), MASK4
	PLINE (76,85) = PONUM				;WILL BE BLANK UNLESS
							;YOU HAVE SHOP FLOOR
							;INTERFACE
							;OR PO&R INSTALLED
	LINCTL = 1
	CALL PRINT
	INCR ITMCNT
	GO TO READ2
EOF,
	CALL TOTLIN
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
ENDOFF,
	XCALL FILES (3,'I',143,4)
END,
	XCALL PGCHN ('PO:RECENT',1)
TOTLIN,
	XCALL LINFD (2)
	PLINE (1,7) = ITMCNT, MASK3
	PLINE (9,15) = 'ENTRIES'
	LINCNT = LINCNT - 1
	CALL PRINT
	LINCNT = 61
	RETURN

END
