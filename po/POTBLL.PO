;  POTBLL / POR - D11
;
;
;		TABLE FILE PRINT
;
;
RECORD TABLE
	.INCLUDE 'DEF:RD153A.DEF'

RECORD DUMTBL
	.INCLUDE 'DEF:RD153B.DEF'

RECORD HDR1
		,A6
	      	,A5,	'TABLE'
		,A5
		,A25,	'CODE SEQUENCE DESCRIPTION'
		,A91

RECORD TITLE
		,A18,	'P/O TABLES LISTING'

RECORD PLINE
		,A1
	TBLTTL	,A11
		,A5
	CODPRT	,A2
		,A5
	SEQPRT	,A1
		,A5
	DESPRT	,A30
		,A72

RECORD
	BRACKS	,A30,	']]]]]]]]]]]]]]]]]]]]]]]]]]]]]]'
	KEY	,A5
	LINCNT	,D2,	60
	LOKCTL	,D1
	LPSW	,D1
	OLDCDE	,A2
	OLDTBL	,D2
	PGCNT	,D3
	PRTCTL	,D3
	PRTTYP	,A1
	READ	,D1,	0
	REASON	,D1
	RECNO	,D5
	RPTNUM	,D3
	SPLFIL	,A14
	SWITCH	,D1
	TBLNAM	,7A11,	'1. SHIP TO ','2. BUYER   ',
&			'3. SHIP VIA','4. TERMS   ',
&			'5. FOB     ','6. NOTES   ',
&			'7. REJECT  '
	TBLNUM	,D2
	WTARG	,D1,	2
	V	,D1

PROC
	XCALL TERID (V)
	XCALL OUTPT (2,1,2,'PRINT P/O TABLES LISTING',1)
	SPLFIL(5,6) = 'N7'

	SWITCH = 1
	XCALL FILES(1,'I',153,SWITCH)		; PORTBL FILE
	IF (SWITCH.EQ.9) GO TO EXIT
	LOKCTL = 1
	XCALL IO (1,DUMTBL,1,READ,LOKCTL)
	RECNO = 1
READ1,
	INCR RECNO
	IF (RECNO.GT.REC153) GO TO EMPTY
	LOKCTL = 1
	XCALL IO (1,TABLE,RECNO,READ,LOKCTL)
	IF (TDESC.EQ.BRACKS) GO TO EMPTY
	IF (TDESC.EQ.']]]DEL') GO TO READ1

	LPSW = 1
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO EXIT2
	IF (LPSW.EQ.2) WTARG = 4
	XCALL WATE (WTARG,V)

	CALL TABLES			;PRINT ALL TABLES
END,
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
EXIT2,
	XCALL WATE (3,1)
	XCALL FILES (1,'I',153,4)
EXIT,
	XCALL PGCHN('PO:POTBLM',1)
;**********************************************************************
EMPTY,
	XCALL MESAG ('NO RECORDS IN PO TABLE FILE',1)
	GO TO EXIT2
;**********************************************************************
TABLES,
	TBLTTL = TBLNAM(TTBLNO)
	OLDTBL = TTBLNO
	IF (TTBLNO.EQ.7) REASON = 1
	CALL CODES			; PRINT ALL CODES IN ONE TABLE
	XCALL LINFD (1)
	IF (TDESC.EQ.BRACKS) RETURN
	IF (RECNO.GT.REC153) RETURN
	GO TO TABLES
;**********************************************************************
CODES,
	CODPRT = TCODE
	OLDCDE = TCODE
	CALL LINEZ			; PRINT ALL LINES FOR ONE CODE
	IF (REASON.EQ.1) TBLTTL = '   REASON  '
	REASON = 0
	IF (TTBLNO.EQ.OLDTBL .AND. RECNO.LE.REC153) GO TO CODES
	IF (TBLTTL.EQ.'   REASON  ') CALL PRINT
	RETURN
;**********************************************************************
LINEZ,
	IF (TTBLNO.EQ.1) SEQPRT = TSEQNO
	DESPRT = TDESC
	CALL PRINT
READ2,
	TABLE =
	INCR RECNO
	IF (RECNO.GT.REC153) RETURN
	LOKCTL = 1
	XCALL IO (1,TABLE,RECNO,READ,LOKCTL)
	IF (TDESC.EQ.']]]DEL') GO TO READ2
	IF (TCODE.EQ.OLDCDE) GO TO LINEZ
	RETURN
;**********************************************************************
PRINT,
	PRTCTL = 80
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,'NO HDR',' ',
&		'NO LEGEND',' ',' ',0,80,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;**********************************************************************

END

