;  POTBLM / POR - D11
;
;
;
;
RECORD TABLE
	.INCLUDE 'DEF:RD153A.DEF'

RECORD DUMTBL
	.INCLUDE 'DEF:RD153B.DEF'

RECORD SEARCH
		,A5
	FNDDES	,A30

RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD
        TBLDES  ,7A19,	'SHIP-TO TABLE      ','BUYER TABLE        ',
&			'SHIP-VIA TABLE     ','TERMS TABLE        ',
&			'FOB TABLE          ','NOTE TABLE         ',
&			'REJECT-REASON TABLE'

RECORD SNDMSG
	PRGNAM	,A9,	'PO:POTCNT'
	RCNT	,D5
	OCNT	,D5
RECORD
	ASEQNO	,A1
	BSMID	,D5
	CNGCTL	,D1
	CODSIZ	,7D2,	02,02,01,01,01,02,02
	DECMAL	,D18
	ENTRY	,A30
	FULL	,D1
	INXCTL  ,D1
	KEY   	,A6
	LOKCTL 	,D1
	MAXENT	,7D2,	30,16,15,10,09,30,20
	MODEID	,3A14,	'ADD           ','CHANGE/INQUIRE','DELETE        '
	MSGCTL	,D1
	NOTADD	,D1
	NXTPGM	,A9,	'PO:POMENU'	;NAME OF NEXT PROGRAM AFTER 'ORGTBL'
	OLDCDE	,A2
	OLDCNT	,D5
	OLDNUM	,D1
	OLDSEQ	,D1
	READ	,D1,	0
	SELECT	,D1
	SRCCTL	,D1
	SRCOPT	,D1		;LINEAR/BINARY SEARCH METHOD
	SWITCH	,D1
	V	,D1
	WHATNO	,D2
	WRITE	,D1,	1
	WRKTBL	,A35
PROC
	XCALL TERID (V)
OPENER,
	SWITCH = 1				;OPEN
	XCALL FILES (1,'U',153,SWITCH)		;FILE # 153 -- POR TABLE FILE
	IF (SWITCH.EQ.9) GO TO INUSE1
	LOKCTL = 0
	XCALL IO (1,DUMTBL,1,READ,LOKCTL)
	IF (LOKCTL.EQ.1) GO TO EXIT
	OLDCNT = REC153
	UNLOCK 1
BEGIN,
	XCALL OUTPT (1,1,2,'TABLES MAINTENANCE',1)
	XCALL OUTPT (5,20,0,'PLEASE SELECT APPLICATION',V)
	XCALL OUTPT (6,25,0,'1. ADD NEW ENTRIES ',V)
	XCALL OUTPT (7,25,0,'2. CHANGE/INQUIRE ENTRIES',V)
	XCALL OUTPT (8,25,0,'3. DELETE ENTRIES ',V)
	XCALL OUTPT (9,25,0,'4. PRINT P/O TABLES LISTING ',V)
MNUSEL,
	NXTPGM = 'PO:POMENU'
	XCALL INPUT (5,47,1,0,'#E',ENTRY,INXCTL,V)	; INPUT SELECTED OPTION
	GO TO (MNUSEL,ENDMNU), INXCTL
	SELECT = ENTRY
	IF (SELECT.LE.0.OR.SELECT.GT.4) GO TO MNUSEL
	IF (SELECT.EQ.1.AND.REC153.GE.MAX153) GO TO FULL
	IF (SELECT.EQ.3.AND.DEL153.GT.10) GO TO PURGE
	IF (SELECT.LT.4) GO TO DISPLA
	NXTPGM = 'PO:POTBLL'
ENDMNU,
	XCALL WATE (4,V)
	LOKCTL = 1
	XCALL IO (1,DUMTBL,1,READ,LOKCTL)
	XCALL FILES (1,'U',153,4)
	RCNT = REC153
	OCNT = ORG153
	IF (RCNT.NE.OCNT.OR.DEL153.GT.10) GO TO SORDER
	XCALL PGCHN (NXTPGM,1)
;*************************************************************************
SORDER,
	SWITCH = 3				;PROTECT
	IF (INXCTL.EQ.2) SWITCH = 2
	XCALL FILES (1,'U',153,SWITCH)		;FILE # 153 -- POR TABLE FILE
	IF (SWITCH.EQ.9) GO TO INUSE3
	IF (INXCTL.EQ.2) CLOSE 1

	IF (RCNT.EQ.OCNT) GO TO ORGONL
	MSGCTL = 5
	XCALL SNMSG (SNDMSG,MSGCTL)
	MSGCTL = 2
	XCALL SNMSG (NXTPGM,MSGCTL)
	XCALL PGCHN ('PO:SRTPOT',1)
;*************************************************************************
ORGONL,
	MSGCTL = 5
	XCALL SNMSG (NXTPGM,MSGCTL)
	XCALL PGCHN ('PO:ORGPOT',1)
;*************************************************************************
PURGE,
	XCALL MESAG ('OLD DELETED RECORDS MUST NOW BE PURGED',1)
	NXTPGM = 'PO:POTBLM'
	GO TO ENDMNU
;*************************************************************************
DISPLA,
	FULL =
	CNGCTL =
	TABLE =
	XCALL OUTPT (2,1,2,MODEID(SELECT),1)
	IF (SELECT.EQ.1.AND.REC153.GE.MAX153) GO TO FULL
	XCALL OUTPT (4,15,0,'1. TABLE NUMBER',V)
	XCALL OUTPT (5,15,0,'2. CODE',V)
	XCALL OUTPT (6,15,0,'3. SEQUENCE',V)
	XCALL OUTPT (7,15,0,'4. DESCRIPTION',V)
	IF (SELECT.NE.2) GO TO TBLNUM
	XCALL OUTPT (4,14,0,'*',V)
	XCALL OUTPT (5,14,0,'*',V)
TBLNUM,
	XCALL OUTPT (4,35,1,' ',V)
	XCALL OUTPT (4,69,0,'TABLES:',V)
	XCALL OUTPT (5,66,0,'1 = SHIP-TO',V)
	XCALL OUTPT (6,66,0,'2 = BUYER',V)
	XCALL OUTPT (7,66,0,'3 = SHIP-VIA',V)
	XCALL OUTPT (8,66,0,'4 = TERMS',V)
	XCALL OUTPT (9,66,0,'5 = FOB',V)
	XCALL OUTPT (10,66,0,'6 = NOTES',V)
	XCALL OUTPT (11,66,0,'7 = REJ REASON',V)
	CTL = '04,32,01,00,#E'
	CALL INPUT
	GO TO (DISPLA,BEGIN), INXCTL
	IF (ENTRY.EQ.' '.AND.OLDNUM.LE.0) GO TO TBLNUM
	IF (ENTRY.EQ.' ') ENTRY(1,1) = OLDNUM
	TTBLNO = ENTRY
	OLDNUM = ENTRY
	IF (TTBLNO.LT.1.OR.TTBLNO.GT.7) GO TO TBLNUM
	XCALL OUTPT (ROW,COL,0,ENTRY(1,1),V)
	XCALL OUTPT (4,35,0,TBLDES(TTBLNO),V)
	XCALL OUTPT (4,65,1,' ',V)
	XCALL OUTPT (5,65,1,' ',V)
	XCALL OUTPT (6,65,1,' ',V)
	XCALL OUTPT (7,65,1,' ',V)
	XCALL OUTPT (8,65,1,' ',V)
	XCALL OUTPT (9,65,1,' ',V)
	XCALL OUTPT (10,65,1,' ',V)
	XCALL OUTPT (11,65,1,' ',V)
	GO TO (ANYCNG), CNGCTL
CODE,
	CTL = '05,32,02,00,A '
	MAX = CODSIZ(TTBLNO)
	CALL INPUT
	GO TO (DISPLA,DISPLA),INXCTL
	IF (ENTRY.NE.'  ') GO TO CODE2
	IF (TTBLNO.NE.1) GO TO CODE
	IF (OLDCDE.EQ.'  ') GO TO CODE
	ENTRY = OLDCDE
CODE2,
	TCODE = ENTRY
	OLDCDE = ENTRY
	XCALL OUTPT (ROW,COL,0,TCODE,V)
	IF (TTBLNO.NE.1) GO TO LOOKUP
	GO TO (ANYCNG), CNGCTL
SEQUEN,
	IF (SELECT.EQ.2) XCALL OUTPT (6,14,0,'*',V)
	CTL = '06,32,01,00,N '
	CALL INPUT
	GO TO (DISPLA),INXCTL
	IF (ENTRY.EQ.' ') ENTRY(1,1) = (OLDSEQ+1)
	TSEQNO = ENTRY
	IF (TSEQNO.LT.1.OR.TSEQNO.GT.4) GO TO SEQUEN
	XCALL OUTPT (ROW,COL,0,ENTRY(1,1),V)
	OLDSEQ = TSEQNO
	IF (OLDSEQ.EQ.4) OLDSEQ = 0
LOOKUP,
	KEY = TABLE(1,5)
	SRCOPT = 4
	BSMID = 1
	SRCCTL = 2
	XCALL SERCH (1,SEARCH,KEY,1,5,ORG153,BSMID,SRCCTL,SRCOPT,6,11,0,0,0,0)
	GO TO (FNDREC,FNDREC), SELECT-1
	IF (SRCCTL.EQ.0.AND.TTBLNO.NE.1) GO TO BADTBL
	IF (SRCCTL.EQ.1) GO TO LOOK2
	INCR TSEQNO
	IF (TSEQNO.GT.4) GO TO BADTBL
	ASEQNO = TSEQNO
	XCALL OUTPT (ROW,COL,0,ASEQNO,V)
	GO TO LOOKUP
LOOK2,
	GO TO (ANYCNG), CNGCTL
DESCRP,
	CTL = '07,32,30,01,A '
	MAX = MAXENT(TTBLNO)
	CALL INPUT
	GO TO (DISPLA),INXCTL
	TDESC = ENTRY
	XCALL OUTPT (ROW,COL,0,TDESC,V)
	CNGCTL =
ANYCNG,
	WHATNO =
	XCALL ANYCN (CNGCTL,WHATNO)
	IF (TTBLNO.NE.1.AND.WHATNO.EQ.3) GO TO BADCNG
	IF (SELECT.EQ.2.AND.CNGCTL.EQ.1.AND.WHATNO.LT.4) GO TO NOCNG
	IF (CNGCTL.EQ.0) GO TO PROCES
CNGBR,
	GO TO (TBLNUM,CODE,SEQUEN,DESCRP), WHATNO
BADCNG,
	CNGCTL = 3
	GO TO ANYCNG
;*************************************************************************
NOCNG,
	XCALL MESAG ('SORRY, KEY FIELDS MAY NOT BE CHANGED.',2)
	GO TO ANYCNG

;*************************************************************************
FULL,
	XCALL OUTPT (2,1,2,'\',V)
	IF (FULL) XCALL OUTPT (5,15,0,'**WARNING - RECORD NOT ADDED**',V)
	XCALL OUTPT (6,15,0,
&	'The PORTBL file is now full.  Use the EXPAND option in',V)
	XCALL OUTPT (7,15,0,
&	'the SYSTEM FUNCTIONS menu to increase the size of the',V)
	XCALL OUTPT (8,15,0,
&	'PORTBL file before reentering TABLES MAINTENANCE.',V)
	XCALL MESAG (' ',2)
	GO TO ENDMNU
;*************************************************************************
BADTBL,
	XCALL MESAG ('DUPLICATE CODE. This table entry is already on file.',1)
	GO TO DISPLA
;*************************************************************************
FNDREC,
	GO TO (NOFIND), SRCCTL
	TABLE = SEARCH
	XCALL OUTPT (12,33,1,' ',V)
	XCALL OUTPT (7,32,0,TDESC,V)
	IF (SELECT.NE.3) GO TO ANYCNG
DELETE,
	XCALL OUTPT (12,1,0,'RIGHT RECORD ?',V)
	CTL = '12,17,01,00,YN'
	CALL INPUT
	IF (ENTRY(1,1).EQ.'N') XCALL MESAG ('RECORD NOT DELETED',2)
	XCALL WATE (3,V)
	IF (INXCTL.EQ.2) GO TO DISPLA
	LOKCTL = 1
	XCALL IO (1,DUMTBL,1,READ,LOKCTL)
	INCR DEL153
	LOKCTL = 1
	XCALL IO (1,DUMTBL,1,WRITE,LOKCTL)
	TDESC = ']]]DEL'
CHANGE,
	LOKCTL = 1
	XCALL IO (1,TABLE,BSMID,WRITE,LOKCTL)
	IF (SELECT.EQ.3) XCALL MESAG ('RECORD DELETED.',2)
FREBUF,
	LOKCTL = 1
	XCALL IO (1,DUMTBL,MAX153,READ,LOKCTL)
	LOKCTL = 1
	XCALL IO (1,DUMTBL,1,READ,LOKCTL)
	OLDCNT = REC153
	UNLOCK 1
	GO TO DISPLA
NOFIND,
	XCALL MESAG ('Table record not on file.',2)
	GO TO DISPLA
;*************************************************************************
PROCES,
	XCALL WATE (3,V)
	IF (SELECT.EQ.2) GO TO CHANGE
ADD,
	LOKCTL = 1
	XCALL IO (1,DUMTBL,1,READ,LOKCTL)
	IF (REC153.GE.MAX153) FULL = 1
	IF (FULL) GO TO FULL
	IF (OLDCNT.NE.REC153) CALL CHKREC
	IF (NOTADD) GO TO FREBUF
	INCR REC153
	LOKCTL = 1
	XCALL IO (1,DUMTBL,1,WRITE,LOKCTL)
	LOKCTL = 1
	XCALL IO (1,TABLE,REC153,WRITE,LOKCTL)
	GO TO FREBUF
CHKREC,
	NOTADD =
	WRKTBL = TABLE
CHKLUP,
	INCR OLDCNT
	IF (OLDCNT.GT.REC153) GO TO CHKOK
	LOKCTL = 1
	XCALL IO (1,TABLE,OLDCNT,READ,LOKCTL)
	LOKCTL = 1
	XCALL IO (1,DUMTBL,1,READ,LOKCTL)
	IF (WRKTBL(1,5).EQ.TABLE(1,5)) GO TO NOTADD
	GO TO CHKLUP
NOTADD,
	NOTADD = 1
	XCALL MESAG ('RECORD ALREADY ADDED BY ANOTHER USER',1)
	RETURN
CHKOK,
	TABLE = WRKTBL
	RETURN
;**********************************************************************
INPUT,
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,V)
	RETURN
;*************************************************************************
INUSE3,
	IF (NXTPGM.EQ.'PO:POTBLM') GO TO OPENER
	XCALL PGCHN (NXTPGM,1)
;*************************************************************************
EXIT,
	XCALL WATE (3,V)
	XCALL FILES (1,'U',153,4)
INUSE1,
	XCALL PGCHN ('PO:POMENU',1)
;*************************************************************************

END

