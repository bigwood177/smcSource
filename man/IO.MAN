SUBROUTINE IO ; (CHANEL,BUFFER,RECNO,IOCTL,LOKCTL)
;
;  IO / MAN 
;
;
;		::PCPYMAN.DEF::
;*****************************************************************************
;		PACKAGE SUBROUTINES - DIBOL FOR RT-11
;		
;		RELEASED: JANUARY 1, 1985
;*****************************************************************************
;
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984,
;		1985, MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		SUBROUTINE FOR PERFORMING RELATIVE READ/WRITE OPERATIONS
;		ON A FILE OPENED IN UPDATE MODE. LOCKED RECORD
;		CONDITIONS ARE TRAPPED, AND THE USER IS PROMPTED (OPTIONAL)
;		WITH A QUESTION FOR CONTINUATION.
;

CHANEL	,D	; CHANNEL TO PERFORM READ/WRITE OPERATION ON
BUFFER	,A	; BUFFER AREA TO READ/WRITE RECORDS FROM
RECNO	,D	; RELATIVE RECORD NUMBER FOR READ/WRITE OPEATION
IOCTL	,D	; I-O CONTROL VARIABLE
		;	0 = READ
		;	1 = WRITE
LOKCTL	,D	; READ/WRITE TYPE
		;  ON ENTRY:
		;	0 = DISPLAY LOCK MESSAGE, AND ALLOW ABORT
		;	1 = DISPLAY LOCK MESSAGE, DO NOT ALLOW ABORT
		;  ON EXIT:
		;	0 = SUCCESSFUL OPERATION
		;	1 = ABORT SELECTED

RECORD
	ALPHA	,A3
	ALPH2	,A2
	ARECNO	,A5
	DELETE	,D1
	ERL	,D5
	ERR	,D3
	LOCKED	,D1
	MASK	,A5,	'ZZZZX'
	MESSAG	,A43
	MESAGE	,A43,	'RECORD TEMPORARILY IN USE - WILL YOU WAIT ?'
	MESAG2	,A14,	'PLEASE WAIT...'
	SYSTEM	,D1
	TCHAR	,D3
PROC
;;;	XCALL ENVRN (SYSTEM)
	system = 0	;envrn.man conflicts w/ synergy envrn
	IF (SYSTEM.GT.1) SYSTEM = 1	;MAINTAIN TSX+ = 1
	MESSAG = MESAGE
	IF (LOKCTL.EQ.1) MESSAG (29,43) = MESAG2
START,
	GO TO (WRITE), IOCTL		;DETERMINE IF READ OR WRITE OPERATION
READ,
	ON ERROR FILERR
	READ (CHANEL,BUFFER,RECNO)
	OFF ERROR

	LOKCTL =
	GO TO EXIT
WRITE,
	ON ERROR FILERR
	WRITE (CHANEL,BUFFER,RECNO)
	OFF ERROR
	LOKCTL =
	GO TO EXIT
FILERR,
	OFF ERROR
	XCALL ERROR (ERR,ERL)
	IF (ERR.EQ.40) GO TO LOCKED		;LOCKED RECORD ERROR NUMBER
ERRMSG,
	ALPHA = ERR, 'ZZX'
	ALPH2 = CHANEL, 'ZX'
	ARECNO = RECNO, MASK
	XCALL OUTPT(24,1,1,'\',1)
	IF (IOCTL.EQ.0) XCALL OUTPT(0,0,0,'READ ',1)
	IF (IOCTL.EQ.1) XCALL OUTPT(0,0,0,'WRITE ',1)
	DISPLAY (15,'ERROR ',ALPHA,'--CHANNEL ',ALPH2,' ON RECORD ',ARECNO)
	XCALL OUTPT (3,1,1,'\',1)
	STOP
LOCKED,						;BLOCK IS LOCKED
	OFFERROR
	XCALL OUTPT(24,1,1,MESSAG,1)
	LOCKED = 1
	GO TO (WAIT1), LOKCTL
	XCALL FLAGS (00010000,1)			;TURN OFF CHAR ECHO
	IF (SYSTEM.EQ.1) DISPLAY (15,29,'F',29,'S')	;F=TURN OFF CHAR ECHO
					;S=TURN ON SINGLE CHAR ACTIVATION
	ACCEPT (15,TCHAR)
	IF (TCHAR.EQ.89) DISPLAY (15,'Y')
	IF (TCHAR.EQ.78) DISPLAY (15,'N')
	IF (TCHAR.EQ.089) GO TO WAIT	;'Y' = 089 IN ASCII
	IF (TCHAR.NE.078) GO TO LOCKED	;'N' = 078 IN ASCII
	CALL ACPTCR
	IF (DELETE) GO TO DELETE
	LOKCTL = 1
EXIT,
	IF (LOCKED) XCALL OUTPT (24,1,1,'\',1)
	LOCKED =
	RETURN
WAIT,
	CALL ACPTCR
	IF (DELETE) GO TO DELETE
	XCALL OUTPT(24,1,1,MESAG2,1)
WAIT1,
	SLEEP 5
	GO TO START
ACPTCR,
	DELETE =
	ACCEPT (15,TCHAR)
	IF (TCHAR.EQ.127) DELETE = 1
	IF (TCHAR.EQ.127) RETURN
	IF (TCHAR.NE.13) GO TO ACPTCR
	IF (SYSTEM.NE.1) ACCEPT (15,TCHAR)
	IF (SYSTEM.EQ.1) DISPLAY (15,29,'T',29,'E')
					;T=TURN OFF SINGLE CHAR ACTIVATION,
					;E=TURN ON CHAR ECHO
	XCALL FLAGS (00010000,0)	;TURN ON CHAR ECHO
	RETURN
DELETE,
	IF (SYSTEM.EQ.1) DISPLAY (15,29,'T',29,'E')
	XCALL FLAGS (00010000,0)
	GO TO LOCKED
END
