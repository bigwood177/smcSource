SUBROUTINE LPOUT; (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,HDR3,LGD1,LGD2,LGD3,
;			LINCTL,PAGSIZ,PRCTL2,LGNCTL,LPSW,RPTNUM,PRNTYP)
;
;	SMC VERSION
;	1. Local Printer
;	2. Local Printer - Compressed
;	3. Local Printer - Landscape
;	4. Edit
;	5. Okidata ML 395
;
;		SUBROUTINE TO HANDLE OUTPUT TO PRINTER/TERMINAL
;
	LINCNT	,D	; LINE COUNT
	PGCNT	,D	; PAGE COUNT
	PLINE   ,A	; LINE TO BE PRINTED
	TITLE	,A	; TITLE AT TOP OF PAGE
	HDR1	,A	; HEADER LINE 1
	HDR2	,A	; HEADER LINE 2
	HDR3	,A	; HEADER LINE 3
	LEGND1	,A	; LEGEND LINE 1
	LEGND2	,A	; LEGEND LINE 2
	LEGND3	,A	; LEGEND LINE 3
	LINCTL	,D	; CONTROLS # OF LINES TO BE SKIPPED
	PAGSIZ	,D	; IF SET TO 80 PRINTS THE TOP LINE IN 80 COLS
	PRCTL2	,D	; # OF CHARACTERS IN PRINT LINE
	LGNCTL	,D	; IF SET TO 1, WILL PRINT LEGEND ON EVERY PAGE
	LPSW	,D
	RPTNUM	,D	; REPORT SEQUENCE NUMBER
	PRNTYP	,A	; PRINTER TYPE:     L = LINE PRINTER,
			;			OTHERWISE CHARACTER PRINTER
			;
			; VALUE OF "LPSW"
			; ===============
			;    1 = OUTPUT IS TO BE PRINTED USING SYSTEM PRINTER
			;    2 = OUTPUT IS TO BE SPOOLED
			; (-)3 = OUTPUT IS TO BE PRINTED LOCALLY
			; (-)4 = OUTPUT IS TO BE DISPLAYED
			;    5 = OUTPUT TO NON-QUEUED PRINTER/TERMINAL
			; VALUE OF "TERMID" INDICATES TERMINAL TYPE
			; ========================================
			; 1 = VT52 WITH NO OPTIONS
			; 2 = VT52 WITH PRINTER PORT OPTION
			; 3 = VT100 WITH NO OPTIONS - VT52 MODE
			; 4 = VT100 WITH ADVANCED VIDEO OPTION - VT52 MODE
			; 5 = VT100 WITH PRINTER PORT OPTION - VT52 MODE
			; 6 = VT100 WITH NO OPTIONS - ANSI MODE
			; 7 = VT100 WITH ADVANCED VIDEO OPTION - ANSI MODE
			; 8 = VT100 WITH PRINTER PORT OPTION - ANSI MODE

RECORD	EJECT
	E_CHAR	,A1		;<ESC>
		,A4,	"&l0H"	;pitch mode = 2 = 16.5-16.7 (compressed)


RECORD TOP80		; FOR TOP LINE WHEN PRINTED IN 80 CHARACTERS
	TOP802	,A100
RECORD CONAM2
			.INCLUDE 'DEF:RD099B.DEF'
RECORD MESARA
			.INCLUDE 'DEF:MES001.DEF'
RECORD TOPLIN		; PRINTS AT TOP OF PAGE
		,A10,	'RUN DATE: '
	RUNDTE	,A9
		,A3
		,A6,	'TIME: '
	RUNTIM	,A8
		,A5
	CONAME	,A61
		,A6,	'TERM #'
	TERMNO	,D2
		,A3
		,A5,	'SEQ #'
	SEQNUM	,D3
		,A3
		,A5,	'PAGE '
	PAGE	,D3
RECORD
	ASCII	,A1
	CONFIL	,A14
	DCHAR	,D3
	ERR	,D3
	ERL	,D4
	ERRRET	,D2
	HDRCNT	,D1
	LNFEED	,D2
	NOHDR	,A6,'NO HDR'
	NOLGND	,A9,'NO LEGEND'
	PRTCTL	,D3	; PRCTL2 ASSIGNED TO THIS VARIABLE IN CASE IT
			; IS PASSED AS A LITERAL
	SWITCH	,D1
	SYSTEM	,D1
	TIME	,D6
	TRMTYP	,D2
	SYTTNO	,D3
	MCTTNO	,D2
	TTNOSW	,D1
PROC
;;;	GO TO (LOCAL,DIS100,LOCAL), LPSW - 2
BEGIN,
	ON ERROR OFFLIN
;;;	IF (LINCNT.GE.60.AND.LPSW.NE.-4) CALL NEWPAG	; LINCNT = 60 ON 1ST CALL
	IF (LINCNT.GE.59.AND.LPSW.NE.-4) CALL NEWPAG	; LINCNT = 60 ON 1ST CALL

	IF (LPSW.EQ.-4) LINCNT =
	PRTCTL = PRCTL2
	IF (PRTCTL.EQ.0) PRTCTL = 132	; 0 = STANDARD 132 CHARACTER PRINT LINE
	IF (LPSW.EQ.2 .OR. LPSW.EQ.5) GO TO PRINT2
;;;	IF (PRNTYP.EQ.'L'.OR.LPSW.EQ.-4) GO TO PRINT3
PRINT2,				; SCAN PLINE BACKWARDS TO FIND 1ST CHARACTER
	IF (PLINE (PRTCTL-1,PRTCTL).NE.'  '.OR.PRTCTL.LE.3) GO TO PRINT3
	PRTCTL = PRTCTL-2
	GO TO PRINT2
PRINT3,				; PRINT AS MUCH OF PLINE AS NECESARY
	ERRRET = 1
	WRITES (14,PLINE (1,PRTCTL))
	INCR LINCNT
	PLINE =
	IF (LINCTL.GE.1) CALL LNSKIP
	OFF ERROR
	RETURN
NEWPAG,				; NEW PAGE ROUTINE
	IF (PGCNT.EQ.0.AND.LINCNT.NE.99) CALL GETNAM
	LNFEED =
;;;	IF (PGCNT.NE.0) CALL LINFD
	IF (PGCNT.EQ.0) GOTO FIRST_PG

	USING LPSW SELECT
	(1,2,3),	BEGIN			;LASER PRINTERS
			XCALL ASCII(27,E_CHAR)
			WRITES (14,EJECT)	;PAGE EJECT			
			END
	(),		call linfd	;5-20-98
	ENDUSING

FIRST_PG,
	XCALL DATE (RUNDTE)
	XCALL TIME (TIME)
	RUNTIM(7,8) = 'AM'
	IF (TIME(1,2).GE.12) RUNTIM(7,8) = 'PM'
	IF (TIME(1,2).GT.12) TIME(1,2) = TIME(1,2) - 12
	RUNTIM(1,5) = TIME(1,4),'XX:XX'
	SEQNUM = RPTNUM
	XCALL TTNO (SYTTNO,MCTTNO,TTNOSW)
	TERMNO = MCTTNO
	INCR TERMNO
	INCR PGCNT
	PAGE = PGCNT
	IF (PGCNT.GT.1) CALL CENTTL
	IF (PAGSIZ.EQ.80) GO TO PGSZ80
ERR2,
	ERRRET = 2
	WRITES (14,TOPLIN)
NEWPG2,
	LNFEED = 1
	CALL LINFD
	LINCNT = 2
	IF (PAGE.GT.1.OR.TITLE.EQ.'NO TITLE') GO TO LEGEND	; TITLE PRINTS
								; ON PAGE 1 ONLY
	IF (TITLE.EQ.'BLANKS') TITLE =
	TOP80 = TITLE
	LINCNT = 50
EXPAND,
	TOP802 (LINCNT*2-1,LINCNT*2-1) = TOP802 (LINCNT,LINCNT)
	TOP802 (LINCNT,LINCNT) =
	LINCNT = LINCNT - 1
	IF (LINCNT.GT.1) GO TO EXPAND
ERR3,
	ERRRET = 3
	WRITES (14,TOP80)
	LNFEED = 1
	CALL LINFD
	LINCNT = 4
LEGEND,						; PRINTS LEGENDS AND HEADERS
	CALL HEADLG
	RETURN
HEADLG,
	HDRCNT =
	IF (LEGND1.EQ.NOLGND) GO TO HEADER
ERR4,
	ERRRET = 4
	WRITES (14,LEGND1)
	INCR LINCNT
	INCR HDRCNT
	IF (LEGND2.EQ.NOLGND) GO TO ENDLGN
ERR5,
	ERRRET = 5
	WRITES (14,LEGND2)
	INCR LINCNT
	INCR HDRCNT
	IF (LEGND3.EQ.NOLGND) GO TO ENDLGN
ERR6,
	ERRRET = 6
	WRITES (14,LEGND3)
	INCR LINCNT
	INCR HDRCNT
ENDLGN,
	IF (LGNCTL.EQ.0) LEGND1 = NOLGND
;;;	IF (LPSW.EQ.4) GO TO HEADER
	LNFEED = 1
	CALL LINFD
HEADER,				; PRINTS HEADERS
	IF (HDR1.EQ.NOHDR) RETURN
ERR7,
	ERRRET = 7
	WRITES (14,HDR1)
	INCR LINCNT
	INCR HDRCNT
	IF (HDR2.EQ.NOHDR) GO TO ENDHDR
ERR8,
	ERRRET = 8
	WRITES (14,HDR2)
	INCR LINCNT
	INCR HDRCNT
	IF (HDR3.EQ.NOHDR) GO TO ENDHDR
ERR9,
	ERRRET = 9
	WRITES (14,HDR3)
	INCR LINCNT
	INCR HDRCNT
ENDHDR,
;;;	IF (LPSW.EQ.4) RETURN
	LNFEED = 1
;new	LNFEED = 2
	CALL LINFD
	RETURN
PGSZ80,				; PRINTS TOP LINE IN 80 CHARS
	TOP802 =
	TOP802(1,15) = TOPLIN(5,19)
	TOP802(16,65) = CONAME
	TOP802 (66,74) = TOPLIN (114,122)
	TOP802 (75,77) = 'PG '
	TOP802 (78,80) = PAGE
ERR10,
	ERRRET = 10
	WRITES (14,TOP80(1,80))
	GO TO NEWPG2
CENTTL,
	CONAME = TITLE
	LINCNT = 52
CENTER,
	LINCNT = LINCNT-2
	IF (LINCNT.EQ.0) GO TO SETTOP
	IF (CONAME(LINCNT-1,LINCNT).EQ.'  ') GO TO CENTER
SETTOP,
	CONAME =
	CONAME((52-LINCNT)/2,50) = TITLE
	RETURN
LNSKIP,				; SKIPS LINES IF LINCTL GE 1
	LNFEED = LINCTL
	CALL LINFD
	RETURN
GETNAM,
	OFFERROR
;;;	IF (LPSW.GT.0) CALL GETMES
	XCALL FFILE (99,CONFIL,SWITCH)
	OPEN (11,U,CONFIL)
	ON ERROR READCO
READCO,
	READ (11,CONAME,1)
	READ (11,TOP80(1,50),2)
	OFF ERROR
	CONAM2 = TOP80
	IF (RPTNUM.NE.0) GO TO CLOS11
	RPTNUM = NXTRPT
	IF (NXTRPT.EQ.999) NXTRPT = 0
	INCR NXTRPT
	TOP80 (1,10) = CONAM2
	IF (CURPTR.GT.0) PRNTYP = PRTTYP (CURPTR)
	WRITE (11,TOP80(1,50),2)
CLOS11,
	CLOSE 11
	ON ERROR OFFLIN
	RETURN

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;GETMES,
;;;	XCALL TTNO (SYTTNO,MCTTNO,TTNOSW)
;;;	TERMNO = MCTTNO
;;;	INCR TERMNO
;;;	OPEN (11,I,'UT:MESARA.DDF')
;;;	ON ERROR READMS
;;;READMS,
;;;	READ (11,MESARA,TERMNO+1)
;;;	OFF ERROR
;;;	CLOSE 11
;;;	TRMTYP = TERMID
;;;	RETURN
;;;LOCAL,
;;;	CALL GETMES
;;;	IF (TRMTYP.NE.2) GO TO LOC100
;;;	LPSW = -LPSW
;;;	GOTO BEGIN
;;;LOC100,
;;;	LPSW = -LPSW
;;;	GOTO BEGIN				;LEAVE IN ANSI MODE UNTIL LPOFF
;;;DIS100,
;;;	XCALL SC132		;CHANGE TO 132 COLUMNS
;;;	XCALL OUTPT (1,1,2,'\',1)
;;;	XCALL FLAGS (00010000,1)
;;;	CALL HEADLG
;;;	LPSW = -LPSW
;;;	XCALL FLAGS (00010000,0)
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	GO TO BEGIN
LINFD,
	ERRRET = 11
	FORMS (14,LNFEED)
	LINCNT = LINCNT + LNFEED
	RETURN
OFFLIN,
	XCALL ERROR (ERR,ERL)
	IF (ERR.EQ.25) GO TO FULL
	XCALL OUTPT (24,1,1,'?LPOUT-PRINTER OFF LINE',1)
	SLEEP 5
	XCALL OUTPT (24,1,1,'\',1)
	GO TO (PRINT3,ERR2,ERR3,ERR4,ERR5,ERR6,ERR7,ERR8,ERR9,ERR10), ERRRET
	GO TO LINFD
FULL,
	XCALL OUTPT (15,5,2,
& '?LPOUT-SPOOL FILE FULL. Please resolve the problem before continuing.',1)
	XCALL OUTPT (16,5,0,
& 'Once the problem is resolved, restart this program using the',1)
	XCALL OUTPT (17,5,0,'PROGRAM RESTART utility in System Functions.',1)
	XCALL OUTPT (0,0,-1,'BELL',1)
	ON ERROR NVRMND
	CLOSE 14
NVRMND,
	STOP
END
