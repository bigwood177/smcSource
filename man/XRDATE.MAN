SUBROUTINE XRDATE; (TODAY)
;
;		GETS THE CALENDER DATE STORED IN THE 
;		OPERATING SYSTEM AND CONVERTS IT FROM
;		        ALPHA - DD-MMM-YY, TO
;		        DECIMAL  - MMDDYY
;		AND RETURNS IT IN "TODAY"
; 
	TODAY	,D
RECORD ADATE
	ADAY	,A2
		,A1				; (HYPHEN)
	AMON	,A3
		,A1				; (HYPHEN)
	ACC	,A2
	AYY	,A2
RECORD
	TABLEA	,12A3,	'JAN','FEB','MAR','APR','MAY','JUN','JUL','AUG',
&			'SEP','OCT','NOV','DEC'
	POINT	,D2,	01
PROC
	XCALL DATE (ADATE)			; GET THE SYSTEM DATE
CHECK,
	IF (POINT.GT.12) GO TO MOVMON
	IF (AMON.EQ.TABLEA(POINT)) GO TO MOVMON	; GOT A MATCH ON THE MONTH
	INCR POINT				; POINT TO NEXT MONTH IN TABLEA
	GO TO CHECK				; CHECK AGAIN
MOVMON,
	IF (POINT.LE.12) TODAY(1,2) = POINT	; MOVE THE MONTH AND GO HOME

	TODAY(3,4) = ADAY			; MOVE IN DAY
	USING %SIZE(TODAY) SELECT
	(8),	BEGIN
		TODAY(5,6) = ACC	
		TODAY(7,8) = AYY
		END
	(),	BEGIN
		TODAY(5,6) = AYY		; MOVE IN YEAR
		END
	ENDUSING

	IF (TODAY(1,2).EQ.0.OR.TODAY(3,4).EQ.0.OR.TODAY(5,6).EQ.0) TODAY =

	RETURN
END
