SUBROUTINE WDSPL ; (W_ID,MAX,ROW,COL,DECMAL,OPTION)
;
;
; REVISIONS:
;	(01)	07-03-96 DKS - ADD OPTION SWITCH FOR A 4-DIGIT YEAR IN DATE
;:
;
;		ROUTINE TO FORMAT & DISPLAY A NUMBER,
;		DATE OR DOLLAR FIELD ON THE SCREEN IN A WINDOW 
;
; 
	W_ID	,D	; WINDOW ID - WINDOW NUMBER TO DISPLAY IN
	MAX	,D	; MAX # OF DIGITS IN NUMBER OR DOLLAR FIELD
	ROW     ,D	; SCREEN ROW
	COL	,D	; SCREEN COLUMN
	DECMAL	,D	; INCOMING DECIMAL FIELD
	OPTION	,D	; OPTION SWITCH
			;	1 = NUMBER
			;	2 = DATE
			;	3 = DOLLARS
			;	4 = DISPLAY 3 DIGITS TO RIGHT OF DEC PT
			;	5 = DISPLAY 1 DIGIT TO RIGHT OF DEC PT
			;	6 = NUMBER, BLANK ONLY ONE FOLLOWING CHARACTER
			;	7 = DATE WITH 4 DIGIT YEAR

	.DEFINE POOLSIZE	,25000
	.DEFINE WNDCHNL		,15
	.DEFINE MAXWINS		,5
	.INCLUDE 'DEF:WINDOWS.DEF'

RECORD
	ALPHA	,A18	; WORK
	COL2	,D2
	CHAR	,D2	; FIELDS
	ROW2	,D2
	TSTLEN	,D2,16	; DETERMINES NUMBER OF FOLLOWING BLANKS TO BE ADDED
PROC
	CHAR = 0
	IF (OPTION.EQ.6) TSTLEN = 18
	GO TO (DSPNUM,DSPDTE,DSPDLR,DSPDG3,DSPDG1,DSPNUM,DSPDT2), OPTION
	RETURN
DSPNUM,				; CONVERT TO ALPHA
	ALPHA = DECMAL
	GO TO JUSTFY
DSPDTE,				; FORMAT & DISPLAY DATE
	ALPHA(1,8) = DECMAL(1,6),'XX/XX/XX'
	XCALL W_DISP (W_ID,WD_POS,ROW,COL,ALPHA(1,8))
	RETURN
DSPDT2,								;(01)
	ALPHA(1,10) = DECMAL(1,8),'XX/XX/XXXX'
	XCALL OUTPT (ROW,COL,0,ALPHA(1,10),1)
	RETURN
DSPDLR,				; FORMAT DOLLAR FIELD
	ALPHA = DECMAL,'ZZ,ZZZ,ZZZ,ZZZ.XX-'
	GO TO JUSTFY
DSPDG3,				; FORMAT FOR 3 PLACES TO RIGHT OF DECIMAL POINT
	ALPHA = DECMAL,'Z,ZZZ,ZZZ,ZZZ.XXX-'
	GO TO JUSTFY
DSPDG1,				; FORMAT FOR 1 PLACE TO RIGHT OF DECIMAL POINT
	ALPHA = DECMAL,'ZZ,ZZZ,ZZZ,ZZZ.X-'
JUSTFY,				; LEFT JUSTIFY ROUTINE
	INCR CHAR
	IF (ALPHA(CHAR,CHAR).EQ.' ') GO TO JUSTFY
	XCALL W_DISP (W_ID,WD_POS,ROW,COL,ALPHA(CHAR,18))
	IF (MAX-(TSTLEN-CHAR).LE.0) RETURN
	ALPHA =
	COL2 = COL+50-CHAR-31
	XCALL W_DISP (W_ID,WD_POS,ROW,COL2,ALPHA(1,MAX-(TSTLEN-CHAR)))
				; DISPLAY NUMBER OR DOLLAR FIELD
	RETURN
;
