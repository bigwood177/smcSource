SUBROUTINE INPT3 ; (ROW,COL,MAX,MIN,INTYPE,ENTRY,INXCTL,V)
;
;		AN ABBREVIATED VERSION OF THE
;		STANDARD "INPUT" SUBROUTINE
; 
;		SPECIAL INPUT ROUTINE TO TURN OFF INPUT CHARACTER ECHO
;		TO THE TERMINAL AND DISPLAY AN '*' FOR EACH CHARACTER
;		ENTERED.
;
	ROW	,D	; SCREEN ROW
	COL	,D	; SCREEN COLUMN
	MAX	,D	; MAXIMUM # OF CHARACTERS ALLOWED
	MIN	,D	; MINIMUM # OF CHARACTERS ALLOWED
	INTYPE	,A	; TYPE OF DATA INPUT (SEE BELOW)
	ENTRY	,A	; FIELD DATA IS PASSED BACK IN
	INXCTL	,D	; CONTROLS POST EXECUTION BRANCH
	V	,D	; VT50/52 CONTROL
RECORD
	ALPHA	,A8
	CHAR	,D2			; WORK
	DOTS	,A15,'...............'
	DECMAL	,D15			; FIELDS
	SYSTEM	,D1
	TCHAR	,D3
	DCHAR	,D3
	TYPE	,A2
 
			; ***** TYPE CODES *****
			;	"A " = ALPHANUMERIC
			;	"D " = DATE, EDITED FOR VALID MONTH & DAY
			;	       & REDISPLAYED IN FORMAT MM/DD/YY
			;	"# " = NUMERIC, POSITIVE ONLY
			;	"?E" = WHEN "E" IS 2ND CHARACTER OF ANY TYPE,
			;	       THAT FIELD WILL TAKE "END"
			;	"YN" = YES/NO ANSWERS, ACCEPTS ONLY "Y" OR "N"
			;	"YE" = YES/NO/END ANSWERS, ALLOWS END IN Y/N
			;	"YY" = YES/NO DEFAULT TO "Y" ON CR
			; ***** INXCTL RETURN CODES *****
			;	0 = INPUT OK, NO MESSAGE
			;	1 = "Y" ANSWER TO YES/NO TYPE FIELD
			;	2 = "N" ANSWER TO YES/NO TYPE FIELD, OR
			;	    "END" HAS BEEN TYPED IF "?E" TYPE FIELD
			;
PROC
;;;	XCALL ENVRN (SYSTEM)
	system = 0	;envrn.man conflicts w/ synergy envrn
	TYPE = INTYPE	; REASSIGN TO VARIABLE SO A LITERAL CAN BE PASSED IN
CLRLIN,
	XCALL OUTPT (ROW,COL,0,DOTS(1,MAX),V)
	XCALL OUTPT (0,0,0,'   ',V)
	XCALL OUTPT (ROW,COL,0,'\',V)
	ENTRY =
	INXCTL =
	CHAR = 
	IF (SYSTEM.NE.1) XCALL FLAGS (00010000,1)
;;;	IF (SYSTEM.EQ.1) DISPLAY (15,29,'F')
INPUT1,
	INCR CHAR
INPUT2,
;;;	IF (SYSTEM.EQ.1) DISPLAY (15,29,'Q',1)
	ACCEPT (15,TCHAR)		; INPUT 1 DECIMAL CHARACTER AT A TIME
	IF (TCHAR.EQ.127) GO TO DELETE	; DELETE KEY
	IF (TCHAR.EQ.21) GO TO CLRLIN	; CTRL/U - ERASE LINE
	IF (TCHAR.EQ.13) GO TO ENDFLD	; CARRIAGE RETURN
	IF (TCHAR.EQ.8.AND.CHAR.EQ.1) GO TO ENDNOW	; BACKSPACE = 'END'
	IF (TCHAR.EQ.27) GOTO ESCAPE
	IF (TCHAR.LT.32.OR.TCHAR.GT.122) GO TO INPUT2	; IGNORES INVALID CHARS.
	XCALL ASCII (TCHAR,ENTRY(CHAR,CHAR))	;CONVERTS CHARACTER TO ASCII
	IF (CHAR.GT.MAX) GO TO TOOLNG
	DISPLAY (15,'*')
	GO TO INPUT1
ESCAPE,
	XCALL FLAGS (10000,1)
	XCALL TTSTS (TCHAR)
	IF (TCHAR.EQ.0) 
	BEGIN
	  TCHAR = 8
	  DCHAR = 53
	  GOTO E
	END
	IF (TCHAR) ACCEPT (15,TCHAR)
	IF (TCHAR.EQ.27) GOTO ESCAPE
	IF (TCHAR.NE.91) GOTO PURBUF
	XCALL TTSTS (TCHAR)
	IF (TCHAR) ACCEPT (15,DCHAR)
	IF (DCHAR.GT.54.OR.DCHAR.LT.52) GOTO PURBUF
	XCALL TTSTS (TCHAR)
	IF (TCHAR) ACCEPT (15,TCHAR)
	IF (TCHAR.EQ.126) 
	BEGIN
	  IF (DCHAR.EQ.54) TCHAR = 9
	  IF (DCHAR.EQ.53) TCHAR = 8
	  IF (DCHAR.EQ.52) TCHAR = 127
E,
	  XCALL FLAGS (10000,0)
	  GOTO (DELETE,ENDFLD,PURBUF), DCHAR-51
	END
PURBUF,
	XCALL TTSTS (TCHAR)
	IF (TCHAR) ACCEPT (15,TCHAR)
	IF (TCHAR) GOTO PURBUF
	XCALL FLAGS (10000,0)
	GOTO INPUT1
DELETE,					; ERASES 1 CHARACTER ON SCREEN & IN CORE
	IF (CHAR.EQ.1) GO TO INPUT2
	CHAR = CHAR-1
	ENTRY(CHAR,CHAR) =
	XCALL OUTPT (0,0,-1,'DELETE',V)
	GO TO INPUT2
ENDNOW,					; SETS INXCTL FOR ENDING
	IF (TYPE(2,2).NE.'E') GO TO CLRLIN
	XCALL OUTPT (ROW,COL,0,'END',V)
	INXCTL = 2
	GO TO RETRN
ENDFLD,					; BRANCH FOR EDITING AFTER CR
	IF (ENTRY.EQ.'END   '.OR.ENTRY.EQ.'end   ') GO TO ENDNOW
	IF (CHAR-1.LT.MIN) GO TO INVALD	; TEST FOR TOO SHORT
	IF (TYPE(1,1).EQ.'Y') GO TO TSTANS
	IF (TYPE.EQ.'#') GO TO TSTNUM
	IF (TYPE.EQ.'D') GO TO TSTDTE
	GO TO RETRN
TOOLNG,					; ENTRY TOO LONG
	IF (TYPE(2,2).NE.'E') GO TO INVALD
	IF (ENTRY(1,CHAR).EQ.'END   '.OR.ENTRY(1,CHAR).EQ.'end   ') GO TO INPUT1
INVALD,					; DISPLAYS "INVALID ENTRY" MESSAGE
	IF (V.EQ.2.AND.ROW.NE.12) XCALL MESAG (' ',3)
	IF (V.EQ.1.AND.ROW.NE.24) XCALL MESAG (' ',3)
	GO TO CLRLIN
TSTNUM,					; NUMERIC EDITING
	ON ERROR INVALD
	DECMAL = ENTRY(1,MAX)
	IF (DECMAL.LT.0) GO TO INVALD
	GO TO RETRN
TSTDTE,					; EDITING FOR VALID DATE
	IF (ENTRY(1,3).EQ.'   ') GO TO RETRN
	ON ERROR INVALD
	DECMAL(1,6) = ENTRY(1,6)
	IF (DECMAL(1,2).LT.1.OR.DECMAL(1,2).GT.12) GO TO INVALD
	IF (DECMAL(3,4).LT.1.OR.DECMAL(3,4).GT.31) GO TO INVALD
	IF (DECMAL(1,6).LT.0) GO TO INVALD
	ALPHA = DECMAL(1,6),'XX/XX/XX'
	XCALL OUTPT (ROW,COL,0,ALPHA(1,8),V)
	GO TO RETRN
TSTANS,					; EDITING FOR "Y" OR "N" ANSWER
	IF (ENTRY.EQ.' '.AND.TYPE.EQ.'YY') ENTRY = 'Y'
	IF (ENTRY.EQ.' ') ENTRY = 'N'
	IF (ENTRY.EQ.'Y'.OR.ENTRY.EQ.'y') INXCTL = 1
	IF (ENTRY.EQ.'N'.OR.ENTRY.EQ.'n') INXCTL = 2
	IF (INXCTL.EQ.0) GO TO INVALD
	XCALL OUTPT (ROW,COL,0,ENTRY(1,1),V)
RETRN,
	XCALL FLAGS (00010000,0)
;;;	IF (SYSTEM.EQ.1) DISPLAY (15,29,'E')
	RETURN
END
