;  PRTACS.MAN (USE INSTEAD OF .UTL)
;
;
;
;		PRINT ACCESS CODES FOR FILE NAMES BY PASSWORD
;
RECORD LEGEND
		,A27
		,A24,	'FILE ACCESS CODES AS OF '
	DATE	,A8
		,A14,	' FOR PASSWORD '
	PSWD	,A9
		,A16,	' / COMPANY CODE '
	CMCODE	,A3
COMMON
			.INCLUDE 'DEF:COM001.DEF'
RECORD DEVICE
			.INCLUDE 'DEF:DEV001.DEF'
RECORD SECURE
			.INCLUDE 'DEF:SEC001.DEF'
RECORD SECCTL
			.INCLUDE 'DEF:SEC003.DEF'
RECORD,X
			.INCLUDE 'DEF:SEC004.DEF'
RECORD LEGND2
		,A27
		,A44,	'"U"=UNLIMITED ACCESS "I"=INQUIRY-ONLY ACCESS'
		,A27,	' " "=NO ACCESS TO EACH FILE'
RECORD HDR1
		,A2
		,A26,	'FILE  FILE  PACKAGE ACCESS'
		,A4
		,A26,	'FILE  FILE  PACKAGE ACCESS'
		,A4
		,A26,	'FILE  FILE  PACKAGE ACCESS'
		,A4
		,A26,	'FILE  FILE  PACKAGE ACCESS'
RECORD HDR2
		,A1
		,A26,	'NUMBER NAME    CODE   CODE'
		,A4
		,A26,	'NUMBER NAME    CODE   CODE'
		,A4
		,A26,	'NUMBER NAME    CODE   CODE'
		,A4
		,A26,	'NUMBER NAME    CODE   CODE'
RECORD
	PKCOD	,20A1,	'E','C','D','I','H','O','L','K','M','A','F','G','B',
&			'J','N','P','Q','R','S','T'
					;PACKAGE CODES IN REC #2 OF SECURE
					;SORTED BY ORDER OF DISPLAY (PACKAGE
					;CODES COME FROM SPOOL CODES.
	PCKGS	,20A3,	'COP','IM ','BMP','SFC','JC ','LP ','SPC','SPR','MRP',
&			'AR ','AP ','PR ','GL ','AD ','POR','UTL','   ','   ',
&			'   ','   '
					;PACKAGES CORRESPONDING TO THE ABOVE
					;PACKAGE CODES
	MAXPK	,D2,	18		;MAXIMUM NUMBER OF ALLOWED PACKAGES
RECORD SPWORD
	WPWORD	,9A1
RECORD
		,A30,	' !"#$%&''()*+,-./0123456789:;<='
		,A35,	'>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[\]^_`'
RECORD,X
	DECODE	,65A1
RECORD
		,A30,	'VHA. WIB/!XJC0"YKD1#ZLE2$[MF3%'
		,A35,	'\NG4&]O5''^P6(_Q7)`R8*S9+T:,U;-<=>?@'
RECORD,X
	CODE	,65A1
RECORD
	BLANKS	,A9
	BSMID	,D5
	CNGCTL	,D1
	CNT	,D3
	COUNT	,D3
	TYPE	,D1
	ENTRY	,A9
	HPWORD	,4A1
	INXCTL	,D1
	KEY	,A12
	TODAY	,D6
	LPSW	,D1
	LINCNT	,D3,	000
	MASK	,A3,	'ZZX'
	MSG	,A6
	MSGCTL	,D1
	ORGCNT	,D5
	PGCNT	,D3,	000
	PRTCTL	,D3
	PRTTYP	,A1
	RPTNUM	,D3
	PLINE	,A132
	READ	,D1,	0
	RECCNT	,D5
	SUB	,D2
	SPLFIL	,A14
	SRCCTL	,D1
	LOKCTL	,D1
	REDCNT	,D3
	WHATNO	,D1
	V	,D1
PROC
	CNPOS = -1
	XCALL TERID (V)
	MSGCTL = 1
	XCALL SNMSG (MSG,MSGCTL)
	IF (MSGCTL.EQ.9) GO TO NOTRUN
	IF (MSG.NE.'PRTSEC') GO TO NOTRUN
	MSGCTL = 4
	XCALL SNMSG (MSG,MSGCTL)
	OPEN (1,I,'UT:DEVICE.DDF')
	OPEN (2,I,'UT:SECURE.DDF')
	LOKCTL = 1
	XCALL IO (2,SECCTL,1,READ,LOKCTL)
	ORGCNT = ORGSEC
	RECCNT = RECSEC
	LOKCTL = 1
	XCALL IO (2,SECCTL,2,READ,LOKCTL)
	XCALL RDATE (TODAY)
BEGIN,
	XCALL OUTPT (1,1,2,'SECURITY SYSTEM MAINTENANCE',1)
	XCALL OUTPT (2,1,0,'PRINT FILE ACCESS CODES BY PASSWORD',1)
	XCALL OUTPT (10,20,0,'PLEASE ENTER',1)
	XCALL OUTPT (12,25,0,'PASSWORD',1)
	XCALL OUTPT (14,25,0,'COMPANY CODE',1)
PASWRD,
	ENTRY =
;;;	XCALL INPUT (12,39,9,9,'A   ','E',ENTRY,INXCTL)
	XCALL INPUT (12,39,9,9,'AE',ENTRY,INXCTL,1)
	GO TO (BEGIN,END), INXCTL
	IF (ENTRY.EQ.BLANKS) GO TO PASWRD
	PSWD = ENTRY
CMPCOD,
	ENTRY = CMCODE
;;;	XCALL INPUT (14,39,03,03,'A  ','U',ENTRY,INXCTL)
	XCALL INPUT (14,39,03,03,'AE',ENTRY,INXCTL,1)
	GO TO (BEGIN,END,END,PASWRD), INXCTL
	IF (ENTRY.EQ.BLANKS) GO TO CMPCOD
	CMCODE = ENTRY
	SPWORD = PSWD
	CALL SCRMBL
	CALL CODE
	SRCCTL = 2
	XCALL SERCH (2,SECURE,KEY,1,12,ORGCNT,BSMID,SRCCTL,1,4,9,0,0,0,0)
	GO TO (NOTFND), SRCCTL
ANYCNG,
	CNGCTL = 6
	XCALL ANYCN (CNGCTL,WHATNO,0,0,0,0)
	GO TO (END,LPON,BEGIN,BEGIN), CNGCTL + 2
	GO TO LPON
NOTFND,
	XCALL MESAG ('?UT-INVALID PASSWORD/COMPANY CODE COMBINATION',1)
	GO TO BEGIN
LPON,
	SPLFIL (5,6) = 'UT'		;FOR SYSTEM QUEUE ONLY - NO SPOOLING
	LPSW = 1			;PRINTED ONLY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO END
	XCALL WATE (2)
	DATE = TODAY,'XX/XX/XX'
	PLINE = LEGEND
	CALL PRINT
	XCALL LINFD (1)
	PLINE = LEGND2
	CALL PRINT
	XCALL LINFD (1)
	PLINE = HDR1
	CALL PRINT
	PLINE = HDR2
	CALL PRINT
	XCALL LINFD (1)
	COUNT =
PROCES,
	IF (COUNT.GE.50) GO TO EOF
	INCR COUNT
	REDCNT = COUNT
	LOKCTL = 1
	XCALL IO (1,DEVICE,REDCNT,READ,LOKCTL)
	PLINE (3,5) = REDCNT, MASK
	PLINE (7,7) = '-'
	PLINE (9,14) = FILNAM
	CALL GETPKG
	PLINE (17,19) = PCKGS (SUB)
	PLINE (25,25) = ACCESS (REDCNT)
	REDCNT = COUNT + 50
	LOKCTL = 1
	XCALL IO (1,DEVICE,REDCNT,READ,LOKCTL)
	PLINE (33,35) = REDCNT, MASK
	PLINE (37,37) = '-'
	PLINE (39,44) = FILNAM
	CALL GETPKG
	PLINE (47,49) = PCKGS (SUB)
	PLINE (55,55) = ACCESS (REDCNT)
	REDCNT = COUNT + 100
	LOKCTL = 1
	XCALL IO (1,DEVICE,REDCNT,READ,LOKCTL)
	PLINE (63,65) = REDCNT, MASK
	PLINE (67,67) = '-'
	PLINE (69,74) = FILNAM
	CALL GETPKG
	PLINE (77,79) = PCKGS (SUB)
	PLINE (85,85) = ACCESS (REDCNT)
	REDCNT = COUNT + 150
	LOKCTL = 1
	XCALL IO (1,DEVICE,REDCNT,READ,LOKCTL)
	PLINE (93,95) = REDCNT, MASK
	PLINE (97,97) = '-'
	PLINE (99,104) = FILNAM
	CALL GETPKG
	PLINE (107,109) = PCKGS (SUB)
	PLINE (115,115) = ACCESS (REDCNT)
	CALL PRINT
	GO TO PROCES
GETPKG,
	SUB =
	IF (FILPAK(REDCNT).EQ.BLANKS) SUB = MAXPK
PAKLUP,
	IF (SUB.GE.MAXPK) RETURN
	INCR SUB
	IF (FILPAK(REDCNT).NE.PKCOD(SUB)) GO TO PAKLUP
	RETURN
EOF,
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	GO TO BEGIN
END,
	CLOSE 1
	CLOSE 2
	XCALL PGCHN ('UT:SECMNT',2)
NOTRUN,
	XCALL MESAG ('?UT-MUST RUN THIS PROGRAM FROM SECURITY SYSTEM',1)
	XCALL PGCHN ('UT:SECMNT',2)
PRINT,
	LINCNT =
	XCALL LPOUT (LINCNT,PGCNT,PLINE,'NO TITLE','NO HDR',' ',' ',
&		'NO LEGEND',' ',' ',0,132,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
SCRMBL,
	HPWORD(1) = WPWORD(1)
	HPWORD(2) = WPWORD(3)
	HPWORD(3) = WPWORD(5)
	HPWORD(4) = WPWORD(7)
	WPWORD(1) = WPWORD(2)
	WPWORD(3) = WPWORD(4)
	WPWORD(5) = WPWORD(6)
	WPWORD(7) = WPWORD(8)
	WPWORD(2) = HPWORD(1)
	WPWORD(4) = HPWORD(2)
	WPWORD(6) = HPWORD(3)
	WPWORD(8) = HPWORD(4)
	HPWORD(1) = WPWORD(1)
	HPWORD(2) = WPWORD(2)
	WPWORD(1) = WPWORD(8)
	WPWORD(2) = WPWORD(9)
	WPWORD(8) = HPWORD(1)
	WPWORD(9) = HPWORD(2)
	RETURN
CODE,
	SUB =
CODELP,
	IF (SUB.GE.9) GO TO MAKKEY
	INCR SUB
	XCALL DECML (WPWORD(SUB),CNT)
	CNT = CNT - 31
	WPWORD (SUB) = CODE (CNT)
	GO TO CODELP
MAKKEY,
	KEY = SPWORD
	KEY (10,12) = CMCODE
	RETURN
END
