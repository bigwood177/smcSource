SUBROUTINE WANCN ; (W_ID,LROW,CNGCTL,WHATNO)
;
;  WANCN / MAN - D11
;
;
;
;	ROUTINE THAT ASKS IF THERE IS ANY CHANGE IN THE DATA INPUT
;	AND RETURNS A CODED ANSWER WITH THE # OF THE FIELD TO BE CHANGED
;	IN A WINDOWING ENVIRONMENT
;
	W_ID	,D	; WINDOW ID
	LROW	,D	; LAST ROW OF THE WINDOW TO DISPLAY ON
	CNGCTL	,D	; BRANCH & EXECUTION CONTROL VARIABLE (SEE BELOW)
	WHATNO	,D	; # OF FIELD TO BE CHANGED

	.DEFINE POOLSIZE	,25000
	.DEFINE WNDCHNL		,15
	.DEFINE MAXWINS		,5
	.INCLUDE 'DEF:WINDOWS.DEF'

RECORD
	DIGITS	,D2
	SAVCTL	,D1
	INXCTL	,D1	; PARAMETERS
	ALPHA   ,A4	; USED IN INPUT
	TCHAR	,D3
	TYPE	,A2	; USED IN INPUT
	SYSTEM	,D1
	V	,D1,1	;DUMMY VARIABLE FOR VT50/52 CONTROL
 
			; ***** CNGCTL CODES *****
			;     OUTGOING:
			;      -1 = "END" FROM INCOMING VALUE OF 4 OR 6
			;	0 = NO CHANGE
			;	1 = CHANGE MODE
			;     INCOMING:
			;	0 = NORMAL CHANGE MODE
			;	1 = NORMAL CHANGE MODE
			;	2 = DON'T GET WHAT #
			;	3 = DISPLAY ERROR MESSAGE
			;	4 = ALLOW "END" INPUT AT "ANY CHANGE ?"
			;	5 = ALLOW "0" WHATNO
			;	6 = ALLOW "END" INPUT AT "FIELD # TO CHANGE"
PROC
;;;	XCALL ENVRN (SYSTEM)
	system = 0	;envrn.man conflicts w/ synergy envrn
	SAVCTL = CNGCTL
ANYCNG,
	IF (CNGCTL.EQ.3) GO TO BADCNG	; INCOMING CODE TO DISPLAY ERROR MESSAGE
	IF (CNGCTL.NE.2.AND.CNGCTL.NE.4) GO TO WHTNUM
	XCALL W_DISP (W_ID,WD_POS,LROW,1,wd_clr,wdc_lin,'ANY CHANGE ?')
	TYPE = 'YN'
	IF (CNGCTL.EQ.4) TYPE = 'YE'
	XCALL WINPT (W_ID,LROW,15,1,0,TYPE,ALPHA,INXCTL) ; ASKS FOR YES/NO ANSWER
	GO TO (NONUM),INXCTL		; BRANCH IF INXCTL = 1 ("Y")
NOCNG,
	CNGCTL =			; RETURN CODE FOR NO CHANGE
	IF (ALPHA.EQ.'  '.AND.INXCTL.EQ.2) CNGCTL = -1
					; BACKSPACE WAS ENTERED TO END
	RETURN
WHTNUM,
	TYPE = 'A '
	IF (CNGCTL.EQ.6) TYPE = 'AE'
	CNGCTL = 1			; SET FOR CHANGE MODE
	XCALL W_DISP (W_ID,WD_POS,LROW,1,wd_clr,wdc_lin,'FIELD # TO CHANGE')
	XCALL WINPT (W_ID,LROW,20,2,0,TYPE,ALPHA,INXCTL)    ; GET # OF FIELD
	GO TO (WHTNUM), INXCTL
	IF (ALPHA.EQ.'  '.OR.INXCTL.EQ.2) GO TO NOCNG
	ON ERROR BADCNG
	DIGITS = ALPHA
	OFF ERROR
	WHATNO = ALPHA					    ; TO BE CHANGED
	IF (WHATNO.EQ.0.AND.SAVCTL.NE.5) GO TO ANYCNG
	RETURN
BADCNG,
	OFF ERROR
	XCALL W_DISP (W_ID,WD_POS,LROW,1,'INVALID SELECTION')
	XCALL W_DISP (W_ID,WD_POS,LROW,67,'CR TO RECOVER')
	xcall beep		;ssq 2-28-01
	CALL ACPTCR
	CNGCTL =
	GO TO ANYCNG
ACPTCR,
	XCALL FLAGS (10001000)
	XCALL W_DISP(W_ID,WD_ACCEPT,TCHAR)	
	IF (TCHAR.NE.13) GO TO ACPTCR
	IF (SYSTEM.NE.1) XCALL W_DISP(W_ID,WD_ACCEPT,TCHAR)
	XCALL FLAGS (10000000)
	RETURN
NONUM,
	CNGCTL = 1			; SET FOR CHANGE MODE
	RETURN
;
