SUBROUTINE PRSPL	;(SPLFIL,SELECT,TERMNO,DEFPRN,TRMID,SPLCTL)
;
;   PRSPL / MAN 
;
;
;		::PCPYMAN.DEF::
;*****************************************************************************
;		PACKAGE SUBROUTINES - DIBOL FOR RT-11
;		
;		RELEASED: JANUARY 1, 1985
;*****************************************************************************
;
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984,
;		1985, MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;	PATCHED AS PER MCBA PATCH UT5-RT-7		FEB 21, 1986
;
;
;		SUBROUTINE TO HANDLE OUTPUT OF SPOOLED REPORTS
;
	SPLFIL	,A	;SPOOL FILE SPECIFICATION AS FOUND IN SPLDIR
	SELECT	,D	;SELECTION PASSED BACK TO CALLING PROGRAM
			; 1 = DISPLAY
			; 2 = PRINT
			; 3 = DELETE
			; 4 = PRINT THEN DELETE
	TERMNO	,D	;TERMINAL NUMBER
	DEFPRN	,D	;DEFAULT PRINTER NUMBER
	TRMID	,D	;TERMINAL ID
	SPLCTL	,D	;SPOOL CONTROL FLAG
			;(0 = SELECTION OK, 1 = ABORT, 2 = END)
			;
RECORD CONAME
			.INCLUDE 'DEF:RD099B.DEF'
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD MESSG1
		,A32,	'SENDING REPORT TO PRINTER QUEUE '
	QUENUM	,A3
RECORD
;	BLANKS	,A25					;UT5-RT
	BLANKS	,A4					;UT5-RT
	CNT	,D3
	CNT1	,D3
	CNT2	,D3
	COPIES	,D1
	COUNT	,D2
	DCHAR	,D3
	ENTRY	,A1
	ERROR	,D3
	NODSP	,D1
	NUMCPY	,D1
	PLINE	,A135
	PRNTR	,D1
	PRTARR	,4A3,	'LP:','LQ:','LR:','LS:'
	READ	,D1,	0
	SAVFIL	,A14
	SWITCH	,D1
;------ UT5-RT --------------------------------------------------
	RECLEN	,D3
	TERM	,A1
	DECML	,D3
	SYSTEM	,D1
;----------------------------------------------------------------
PROC
;;;	XCALL ENVRN (SYSTEM)			;UT5-RT
	system = 0	;envrn.man conflicts w/ synergy envrn
	IF (SYSTEM.GT.1) SYSTEM = 1	;MAINTAIN TSX+ = 1
	IF (CONAME.NE.BLANKS) GO TO BEGIN
	CLOSE 14
	SWITCH = 5
	XCALL FILES (14,'I',99,SWITCH)
	IF (SWITCH.EQ.9) GO TO RETURN
	READ (14,CONAME,2)
	CLOSE 14
	NODSP =
	IF (TRMID.LE.3) NODSP = 1
BEGIN,
	XCALL OUTPT (24,1,1,
&'(1)=DISPLAY (2)=PRINT (3)=DELETE (4)=PRINT THEN DELETE',1)
	CALL SQUEZ
	CTL = '24,57,01,01,NE'
	CALL INPUT
	GO TO (RETURN,RETURN), SPLCTL
	SELECT = ENTRY
	IF (SELECT.LT.1 .OR. SELECT.GT.4) GO TO BEGIN
	GO TO (DISPLA,PRINT,RETURN,PRINT), SELECT
DISPLA,
	IF (NODSP) GO TO INVALD
	ON ERROR RETURN
	OPEN (2,I,SPLFIL)
	OFF ERROR
	IF (TRMID.LT.6) DISPLAY (15,27,'<')	;ENTER ANSI MODE
	DISPLAY (15,27,'[?3h',27,'[?4h')	;132 col and smooth scroll
	COUNT =
DSPLUP,
	READS (2,PLINE,DSPDUN)
	INCR COUNT
	CALL SQZLIN
	WRITES (15,PLINE(1,CNT))
	IF (COUNT.GE.24) GO TO CHKABT
	GO TO DSPLUP
SQZLIN,
	CNT = 133
LINLUP,
	IF (CNT.LE.6) RETURN
	CNT = CNT - 6
	IF (PLINE(CNT,CNT+5).EQ.BLANKS) GO TO LINLUP
	CNT = CNT + 6
	IF (CNT.GE.133) CNT = 132
	RETURN
DSPDUN,
	DISPLAY (15,13,10,'END OF REPORT - PRESS RETURN')
ACCEPT,
	ACCEPT (15,DCHAR)
	IF (DCHAR.NE.13) GO TO ACCEPT
	XCALL TTSTS (SPLCTL,15)
	IF (SPLCTL.EQ.0) GO TO DSPOFF
	ACCEPT (15,DCHAR)
	GO TO DSPOFF
CHKABT,
	COUNT =
	XCALL TTSTS (SPLCTL,15)
	IF (SPLCTL.EQ.0) GO TO DSPLUP
	ACCEPT (15,DCHAR)
	IF (DCHAR.EQ.23) GO TO DSPOFF
	GO TO CHKABT
DSPOFF,
	DISPLAY (15,27,'[?4l')
	DISPLAY (15,27,'[?3l')	;TURN OFF SMOOTH SCROLL AND 132
	IF (TRMID.LT.6) DISPLAY (15,27,'[?2l')
	CLOSE 2
	SPLCTL =
	GO TO RETURN
INVALD,
	XCALL MESAG ('INVALID SELECTION FOR THIS TERMINAL TYPE',1)
	GO TO BEGIN
;***********************SQUEEZE SPACES OUT OF FILE SPECIFICATION***************
SQUEZ,
	CNT1 =
	CNT2 =
	SAVFIL =
SQZLUP,
	IF (CNT1.GE.14) GO TO SQZDUN
	INCR CNT1
	IF (SPLFIL(CNT1,CNT1).EQ.BLANKS) GO TO SQZLUP
	INCR CNT2
	SAVFIL (CNT2,CNT2) = SPLFIL (CNT1,CNT1)
	GO TO SQZLUP
SQZDUN,
	SPLFIL = SAVFIL
	RETURN
;*****************************************************************************
PRINT,
	PRNTR = DEFPRN
	IF (DEFPRN.GT.0 .AND. DEFPRN.LE.4) GO TO PRNTIT
GETPRT,
	XCALL OUTPT (24,1,1,'ENTER NUMBER OF PRINTER TO USE',1)
	CTL = '24,33,01,01,NE'
	CALL INPUT
	GO TO (RETURN,RETURN), SPLCTL
	PRNTR = ENTRY
	IF (PRNTR.LT.1 .OR. PRNTR.GT.4) GO TO GETPRT
PRNTIT,
;	IF (QUEPTR(PRNTR).NE.'Y') GO TO NOQUE			;UT5-RT
;;;	IF (QUEPTR(PRNTR).NE.'Y'.OR.SYSTEM.EQ.1) GO TO NOQUE	;UT5-RT
NUMCPY,
	XCALL OUTPT (24,1,1,'ENTER NUMBER OF COPIES',1)
	CTL = '24,25,01,00,#E'
	CALL INPUT
	GO TO (RETURN,RETURN), SPLCTL
	NUMCPY = ENTRY
	IF (NUMCPY.LE.0) GO TO NUMCPY
	QUENUM = PRTARR(PRNTR)
	XCALL OUTPT (24,1,1,MESSG1,1)
	IF (SELECT.NE.4) LPQUE (SPLFIL,LPNUM:PRNTR,COPIES:NUMCPY)
	IF (SELECT.EQ.4) LPQUE (SPLFIL,LPNUM:PRNTR,COPIES:NUMCPY,DELETE)
	XCALL OUTPT (24,1,1,'\',1)
	GO TO RETURN
NOQUE,
	ON ERROR BADDEV
	CLOSE 13
	OPEN (13,O,PRTARR(PRNTR))
	XCALL OUTPT (24,1,1,'PRINTING OCCURRING ... PLEASE WAIT',1)
	ON ERROR RETURN
	OPEN (2,I,SPLFIL)
	ON ERROR OFFLIN
PRTLP,
	READS (2,PLINE,PRTDUN)
;------ UT5-RT --------------------------------------------------------------
	XCALL RSTAT (RECLEN,TERM)
	XCALL DECML (TERM,DECML)
	IF (DECML.EQ.12)
		BEGIN
		FORMS (13,0)
		GOTO PRTLP
		END
;----------------------------------------------------------------------------
	CALL SQZLIN
WRTLP,
	WRITES (13,PLINE(1,CNT))
	GO TO PRTLP
PRTDUN,
	FORMS (13,0)
	CLOSE 2
	CLOSE 13
	IF (SELECT.EQ.4) XCALL DELET (2,SAVFIL)		;UT5-RT
RETURN,
	RETURN
BADDEV,
	XCALL MESAG ('?PRSPL-INVALID PRINTER NUMBER',1)
	GO TO GETPRT
OFFLIN,
	XCALL MESAG ('?PRSPL-PRINTER OFF LINE',1)
	GO TO WRTLP
INPUT,
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,SPLCTL,1)
	RETURN
END
