SUBROUTINE MMENU; (MENUID,RECNAM,SELECT,V)
;
;  MMENU / MAN 
;
;
;		::PCPYMAN.DEF::
;*****************************************************************************
;		PACKAGE SUBROUTINES - DIBOL FOR RT-11
;		
;		RELEASED: JANUARY 1, 1985
;*****************************************************************************
;
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984,
;		1985, MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		ROUTINE TO DISPLAY STANDARD MASTER FILE
;		MAINTENANCE MENU & PASS BACK OPTION SELECTED
;
	MENUID	,A	; TITLE TO DISPLAY AT TOP OF SCREEN
	RECNAM	,A	; NAME OF KEY FIELD OF MASTER RECORD
	SELECT	,D	; VALUE OF OPTION PASSED BACK (SELECT = 0-5)
			; *INCOMING*
			; -1 = DO NOT DISPLAY 'S' AFTER EACH RECNAM
	V	,D	; VT50/52 CONTROL
RECORD
	INXCTL	,D1	; PARAMETERS
	ENTRY	,A3	; FOR INPUT
	MODEID	,3A14,'ADD           ','CHANGE/INQUIRE','DELETE        '
	SAVSEL	,D1	; SAVE VALUE OF SELECT
PROC
	SAVSEL = SELECT
	XCALL OUTPT (1,1,2,MENUID,1)
	XCALL OUTPT (10,20,0,'PLEASE SELECT APPLICATION',1)
	XCALL OUTPT (12,25,0,'1. ADD NEW ',1)
	XCALL OUTPT (0,0,0,RECNAM,1)
	IF (SAVSEL.NE.-1) XCALL OUTPT (0,0,0,'S',1)
	XCALL OUTPT (14,25,0,'2. CHANGE/INQUIRE ',1)
	XCALL OUTPT (0,0,0,RECNAM,1)
	IF (SAVSEL.NE.-1) XCALL OUTPT (0,0,0,'S',1)
	XCALL OUTPT (16,25,0,'3. DELETE ',1)
	XCALL OUTPT (0,0,0,RECNAM,1)
	IF (SAVSEL.NE.-1) XCALL OUTPT (0,0,0,'S',1)
	XCALL OUTPT (18,25,0,'4. PRINT OUT ',1)
	XCALL OUTPT (0,0,0,RECNAM,1)
	IF (SAVSEL.NE.-1) XCALL OUTPT (0,0,0,'S',1)
MNUSEL,
	XCALL INPUT (10,47,1,0,'#E',ENTRY,INXCTL,1)	; INPUT SELECTED OPTION
	GO TO (MNUSEL,SELEND), INXCTL
	SELECT = ENTRY
	GO TO (DSPMOD,DSPMOD,DSPMOD,SELLST), SELECT
	GO TO MNUSEL
DSPMOD,							; DISPLAY MODE
	XCALL OUTPT (2,1,2,MODEID(SELECT),1)
	RETURN
SELEND,
	SELECT =			; PASS BACK SELECT = 0 FOR END OF MENU
	RETURN
SELLST,
	XCALL OUTPT (2,1,2,'\',1)
	XCALL OUTPT (12,20,0,'SORT BEFORE PRINTING ?',1)
	XCALL INPUT (12,44,1,0,'YN',ENTRY,INXCTL,1)
	IF (INXCTL.EQ.1) SELECT = 5
	IF (SELECT.EQ.5) XCALL WATE (4,2)
	RETURN
END
