;common files: cusmas,cusidx,aropen	ssq 8-12-04
SUBROUTINE FILES ; (CHANEL,MODE,FILNUM,SWITCH)
;
;
;		SUBROUTINE TO OPEN AND CLOSE FILES AND
;		PROVIDE FILE PROTECTION (FOR ALL SYSTEMS)
;
;
	CHANEL	,D		;CHANNEL ON WHICH TO OPEN OR CLOSE FILE
	MODE	,A		;'I' = INPUT, 'O' = OUTPUT, 'U' = UPDATE
				; 'SI'=ISAM INPUT, AND  'SU' = ISAM UPDATE
	FILNUM	,D		;RECORD NUMBER ON DEVICE FILE
	SWITCH	,D		;INCOMING CODES:
				;***************SIGNED CODES***********
				;	+ VALUES INDICATE CHECK FOR USER ACCESS
				;		BEFORE PERFORMING OPERATION
				;		(EXCEPT CLOSE/DELETE OPERATIONS)
				;	- VALUES INDICATE DO NOT CHECK FOR
				;		ACCESS PRIVILEGE BEFORE
				;		PERFORMING OPERATION (EXCEPT
				;		WHEN THERE IS NO ACCESS
				;****************************************
				;	(-)1 - OPEN AND INCREMENT USER COUNT
				;	(-)2 - OPEN AND PROTECT - NO MESSAGE IF
				;		IN USE
				;	(-)3 - PROTECT W/O OPENING
				;RECORD SECURE
                                ;ND UNPROTECT/DECREMENT
				;	(-)5 - OPEN W/O CHANGING STATUS
				;	(-)6 - INCREMENT USER COUNT W/O OPENING
				;		FILE
				;	   7 - CLOSE, DECREMENT/UNPROTECT, AND
				;		DELETE
				;	(-)8 - OPEN AND PROTECT - DISPLAY
				;		MESSAGE IF IN USE
				;OUTGOING CODES:
				;	0 - ACTION SUCCESSFUL
				;	9 - ACTION UNSUCCESSFUL -
				;	    FILE IN USE, UNAVAILABLE
				;	    OR ERROR OCCURRED

record	fil8
	modnam	,a14
		,a1
	dat8	,a11
		,a1
	tim8	,a8

RECORD SECURE
			.INCLUDE 'DEF:SEC001.DEF'
RECORD,X
			.INCLUDE 'DEF:SEC002.DEF'
RECORD MESARA
			.INCLUDE 'DEF:MES001.DEF'
RECORD DEVICE
			.INCLUDE 'DEF:DEV001.DEF'
RECORD REDFIL
	REDDEV	,A3
		,A1	,':'
	RDFILE	,A6
		,A1	,'.'
	REDEXT	,A3
RECORD UNAV
		,A12,	' UNAVAILABLE'
		,A40
		,A12,	'PRESS RETURN'
RECORD	VARS
	WRKCMP	,A3
	WRKPOS	,D3
	ANSWER	,D3
	BLANKS	,A200
	CNT1	,D2
	CNT2	,D2
	ERR	,D3
	ERRA	,A3
	INUS	,A29,	' IN USE - WILL YOU WAIT ?  . '
	LINE	,D5
	LOCKED	,D1
	MESGE	,A78
	PROT	,A32,	' PROTECTED - WILL YOU WAIT ?  . '
	SAVFIL	,A14
	SAVSWT	,D1
	SYSTEM	,D1
	TERMNO	,D2
	SYTTNO	,D3
	MCTTNO	,D2
	TTNOSW	,D1
PROC
	ON ERROR FILERR
	CLOSE 11
	SAVSWT = SWITCH
;;;	XCALL ENVRN (SYSTEM)
	system = 0	;envrn.man conflicts w/ synergy envrn
	IF (SYSTEM.GT.1) SYSTEM = 1	;MAINTAIN TSX+ = 1
	IF (WACCES.NE.BLANKS .AND. CMPPOS.NE.0) GO TO BEGIN
					; NO NEED TO RECHECK SECURITY
					; UNLESS ACCESS CODES AND COMPANY
					; CODES POSITION ARE BLANK
	OPEN (11,I,'UT:MESARA.DDF')
	XCALL TTNO (SYTTNO,MCTTNO,TTNOSW)
	TERMNO = MCTTNO
	TERMNO = TERMNO + 2
	READ (11,MESARA,TERMNO)
	CLOSE 11

	IF (SECREC.LE.0) GO TO BADSEC
	OPEN (11,I,'UT:SECURE.DDF')
	READ (11,SECURE,SECREC)
BEGIN,
	CLOSE 11

;1-cusmas, 2-cusidx, 3-aropen are common to smc and roc...
; so are: 166-cusalp, 183-rolo

	wrkpos = cmppos
	wrkcmp = cmpcod
	using filnum select
;;;	(1,2,3),	if(cmppos.eq.7 .or. cmppos.eq.8) 
	(1,2,3,166,183),	if(cmppos.eq.7 .or. cmppos.eq.8) 
				begin
				wrkpos=1		;smc
				wrkcmp = 'SMC'
				end
	endusing

	OPEN (11,U,'UT:DEVICE.DDF')
	ON ERROR REDERR
REDDEV,
	READ (11,DEVICE,FILNUM)
	OFF ERROR
	IF (LOCKED) XCALL OUTPT (24,1,1,'\',1)
	LOCKED =
FINDCO,
	IF (DEVNAM(wrkpos).EQ.BLANKS) GO TO UNAVBL
	IF (ACCESS(FILNUM).EQ.' ') GO TO UNAVBL	;NO ACCESS PRIVILEGE FOR FILE
	IF (SAVSWT.LT.0) GO TO ACESOK		;NEGATIVE VALUES-DO NOT CHECK
						;FOR ACCESS PRIVILEGE
	IF (SAVSWT.EQ.4 .OR. SAVSWT.EQ.7) GO TO ACESOK
	IF (ACCESS(FILNUM).EQ.'I' .AND.
&		(MODE.EQ.'U'.OR.MODE.EQ.'O'.OR.MODE.EQ.'SU')) GO TO UNAVBL
ACESOK,
	IF (SAVSWT.LT.0) SAVSWT = -SAVSWT
	GO TO (OPENIT,PROTEC,PROTEC,CLOSE,EXIT,OPENIT,CLOSE,PROTEC), SAVSWT
OPENIT,
	IF (STATUS(wrkpos).GE.98) GO TO INUSE
	INCR STATUS (wrkpos)
	GO TO EXIT
PROTEC,
	IF (STATUS(wrkpos).GT.0) GO TO INUSE
	STATUS(wrkpos) = 99
	GO TO EXIT
CLOSE,
	IF (STATUS(wrkpos).LT.99 .AND. STATUS(wrkpos).GT.0)
&		STATUS(wrkpos) = STATUS(wrkpos) - 1
	IF (STATUS(wrkpos).EQ.99) STATUS(wrkpos) = 0
	CLOSE CHANEL
EXIT,
	IF (SAVSWT.EQ.3 .OR. SAVSWT.EQ.4 .OR. SAVSWT.EQ.6) GO TO WRTDEV
	REDDEV = DEVNAM(wrkpos)
	RDFILE = FILNAM
	REDEXT = wrkcmp
;	IF (SYSTEM.NE.1) GO TO OPNCTN
;	IF (FILNUM.EQ.44.OR.FILNUM.EQ.45) REDEXT(3,3) = 'M'
        IF (MODE(1,1).EQ.'S') REDEXT(3,3) = 'M'
OPNCTN,
	CALL SQUEZ		;SQUEEZE OUT SPACES FROM FILE SPEC. (DEVICE)
	ON ERROR NOTFND
	IF (SAVSWT.EQ.7) GO TO DELETE
	IF (MODE.EQ.'O') OPEN (CHANEL,O,SAVFIL)
	IF (MODE.EQ.'U') OPEN (CHANEL,U,SAVFIL)
	IF (MODE.EQ.'I') OPEN (CHANEL,I,SAVFIL)
	IF (MODE.EQ.'SU') OPEN (CHANEL,SU,SAVFIL)
	IF (MODE.EQ.'SI') OPEN (CHANEL,SI,SAVFIL)
WRTDEV,
	WRITE (11,DEVICE,FILNUM)
	OFF ERROR
RETURN,
	CLOSE 11
	RETURN
DELETE,
	XCALL DELET (CHANEL,SAVFIL)
	GO TO WRTDEV
SQUEZ,
	CNT1 =
	CNT2 =
	SAVFIL =
SQZLUP,
	IF (CNT1.GE.14) RETURN
	INCR CNT1
	IF (REDFIL(CNT1,CNT1).EQ.BLANKS) GO TO SQZLUP
	INCR CNT2
	SAVFIL (CNT2,CNT2) = REDFIL (CNT1,CNT1)
	GO TO SQZLUP
INUSE,
	UNLOCK 11
	IF (SAVSWT.EQ.2) GO TO EREXIT		;DON'T DISPLAY PROTECT MESSAGE
						;IN THIS CASE BUT ALLOW IT TO
						;BE HANDLED BY MAIN PROGRAM
	XCALL OUTPT (24,1,2,'\',1)
	CALL ACPTON
	IF (STATUS(wrkpos).EQ.99) GO TO PROTMS
        XCALL OUTPT (24,1,4,'?UT-FILE ',1)
	XCALL OUTPT (0,0,4,FILNAM,1)
	XCALL OUTPT (0,0,4,INUS,1)
;;;	DISPLAY (15,8,8,7)
	GO TO YESNO
PROTMS,
	XCALL OUTPT (24,1,4,'?UT-FILE ',1)
        XCALL OUTPT (0,0,4,FILNAM,1)
	XCALL OUTPT (0,0,4,PROT,1)
;;;	DISPLAY (15,8,8,7)
YESNO,
	ACCEPT (15,ANSWER)
	IF (ANSWER.EQ.89) GO TO TRYAGN		;'Y' = 089  IN ASCII
	IF (ANSWER.EQ.78) XCALL OUTPT (0,0,4,'N',1)
	IF (ANSWER.EQ.78) GO TO ACCEPT		;'N' = 078  IN ASCII
	GO TO INUSE
TRYAGN,
	XCALL OUTPT (0,0,4,'Y',1)
	CALL ACPTCR
	XCALL OUTPT (24,1,1,'PLEASE WAIT ...',1)
	CALL ACPOFF
	SLEEP 15
	GO TO BEGIN
ACPTON,				;SUPPRESS CHARACTER ECHO AND ENABLE SINGLE
				;CHARACTER ACTIVATION
	XCALL FLAGS (00010000,1)
;;;	IF (SYSTEM.EQ.1) DISPLAY (15,29,'F',29,'S')
	RETURN
ACPOFF,
	XCALL OUTPT (24,1,0,'\',1)
;;;	IF (SYSTEM.EQ.1) DISPLAY (15,29,'T',29,'E')
	XCALL FLAGS (00010000,0)
	RETURN
ACCEPT,
	CALL ACPTCR
	CALL ACPOFF
	XCALL OUTPT (24,1,1,'\',1)
EREXIT,
	SWITCH = 9
	CLOSE 11
	GO TO RETURN
ACPTCR,
	ACCEPT (15,ANSWER)
;;;	IF (ANSWER.NE.13) GO TO ACPTCR
;;;	IF (SYSTEM.NE.1) ACCEPT (15,ANSWER)
	RETURN
BADSEC,
	CALL ACPTON
	XCALL OUTPT (23,1,2,'\',1)
	XCALL OUTPT (23,1,4,
&'?UT-ERROR TERMINAL NOT LOGGED ONTO SECURITY SYSTEM',1)
	XCALL OUTPT (24,1,4,
&'RUN UT:MSMENU AND ENTER PASSWORD AND COMPANY CODE                 PRESS RETURN',1)
	GO TO ACCEPT
UNAVBL,
	CALL ACPTON
	UNLOCK 11
	XCALL OUTPT (24,1,4,'?UT-FILE ',1)
        XCALL OUTPT (0,0,4,FILNAM,1)
	XCALL OUTPT (0,0,4,UNAV,1)
	DISPLAY (15,7)
	XCALL BEEP		;9-16-98 SSQ
	GO TO ACCEPT
REDERR,
	XCALL ERROR (ERR,LINE)
	IF (ERR.EQ.40) GO TO LOCKED
	CALL ACPTON
	ERRA = ERR
	MESGE = '?UT-FATAL
& READ ERROR #     ON UT:DEVICE.DDF RECORD #              PRESS RETURN'
	MESGE (24,26) = ERRA
	MESGE (54,56) = FILNUM
	XCALL OUTPT (24,1,4,MESGE,1)
	DISPLAY (15,7)
	XCALL BEEP		;9-16-98 SSQ
	GO TO ACCEPT
FILERR,
	XCALL ERROR (ERR,LINE)
	CALL ACPTON
	MESGE = '?UT-FATAL ERROR #     ON OPENING UTILITY FILES'
	MESGE (67,78) = 'PRESS RETURN'
	MESGE (19,21) = ERR
	XCALL OUTPT (24,1,4,MESGE,1)
	DISPLAY (15,7)
	XCALL BEEP		;9-16-98 SSQ
	GO TO ACCEPT
LOCKED,
	XCALL OUTPT (24,1,4,'?UT-DEVICE.DDF RECORD LOCKED - PLEASE WAIT',1)
	SLEEP 2
	GO TO REDDEV
NOTFND,
	offerror 	;ssq 3/23/98
	UNLOCK 11
	XCALL ERROR (ERR,LINE)
	CALL ACPTON
	ERRA = ERR
	MESGE = '?UT-FATAL
& ERROR #     ON FILE                (FILE NOT FOUND)     PRESS RETURN'
	MESGE (19,21) = ERRA
	MESGE (31,44) = SAVFIL
	XCALL OUTPT (24,1,4,MESGE,1)
	DISPLAY (15,7)
	XCALL BEEP		;9-16-98 SSQ
	GO TO ACCEPT
END
