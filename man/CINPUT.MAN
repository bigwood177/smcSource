SUBROUTINE CINPUT

	ROW	,D
	COL	,D
	MAX	,D
	MIN	,D
	TYPE	,A
	ENTRY	,A
	INXCTL	,D
	POS	,D
RECORD
	DCHAR	,D3
	ACHAR	,A10
	BLANKS	,A30
	STAT	,D1
	DECMAL	,D10
	J	,D2
PROC
	IF (POS.EQ.0) 
	BEGIN
	  ENTRY =
	  XCALL OUTPT (ROW,COL,0,BLANKS(1,MAX+3-POS),1)
	  XCALL OUTPT (ROW,COL,0,'\',1)
	END
	IF (POS.NE.0) XCALL OUTPT (ROW,COL,0,ENTRY(1,POS),1)

ACCPT,
	ACCEPT (15,DCHAR)
	IF (DCHAR.GE.0.AND.DCHAR.LE.31)
	BEGIN
	  IF (DCHAR.EQ.8.AND.(TYPE(2,2).EQ.'E'.OR.TYPE(2,2).EQ.'X')) GOTO RETERN
	  IF (DCHAR.EQ.9.AND.(TYPE(2,2).EQ.'T'.OR.TYPE(2,2).EQ.'X')) GOTO RETERN
	  IF (DCHAR.EQ.23) GOTO RETERN
	  IF (DCHAR.EQ.27.OR.DCHAR.EQ.13) 
	  BEGIN
	    J =
	    ACHAR =
	    DO BEGIN
	      XCALL TTSTS (STAT,15)
	      IF (STAT) 
		BEGIN
		  INCR J
		  ACCEPT (15,ACHAR(J,J))
		END
	    END UNTIL (STAT.EQ.0)
	    GOTO RETERN
	  END
	  GOTO ACCPT
	END

	IF (DCHAR.EQ.127)
	BEGIN
	  POS = POS - 1
	  IF (POS.LT.1)
	  BEGIN
	    ENTRY =
	    POS =
	    GOTO RETERN
	  END
	  ENTRY = ENTRY(1,POS)
	  GOTO RETERN
	END
	
	IF (TYPE(1,1).EQ.'#'.AND.(DCHAR.LT.48.OR.DCHAR.GT.57)) GOTO NUMONL

	INCR POS
	IF (POS.GT.MAX) GOTO TOOLONG
	XCALL ASCII (DCHAR,ENTRY(POS,POS))
	XCALL OUTPT (ROW,COL,0,ENTRY(1,MAX),1)
RETERN,
	INXCTL =
	IF (DCHAR.EQ.23) INXCTL = 1
	IF (DCHAR.EQ. 8) INXCTL = 2
	IF (DCHAR.EQ. 9) INXCTL = 3
	IF (DCHAR.EQ.13) INXCTL = 4
	RETURN
TOOLONG,
	XCALL MESAG ('TOO LONG',2)
	POS = POS - 1
	RETURN	
NUMONL,
	XCALL MESAG ('MUST BE NUMERIC',2)
	RETURN
