SUBROUTINE TERID; (V)
;
;   TERID / MAN 
;
;
	V	,D

RECORD MESARA
			.INCLUDE 'DEF:MES001.DEF'
RECORD,X
			.INCLUDE 'DEF:MES002.DEF'
RECORD
	SYTTNO	,D3
	TERMNO	,D3
	ERR	,D3
	ERRA	,A3
	LINE	,D3
	DSPTRM	,A2
	SYSTEM	,D2
	MCTTNO	,D2
	TTNOSW	,D1
RECORD	,X
	ASYTTNO	,A3
	ATERMNO	,A3
PROC
	CLOSE 15
	OPEN (15,I,'TT:')
	XCALL TTNO (SYTTNO,MCTTNO,TTNOSW)
	TERMNO = MCTTNO
	TERMNO = TERMNO + 2
	ON ERROR BADOPN
	OPEN (11,I,'UT:MESARA.DDF')
	READ (11,MESARA,1)
	IF (MAXTER.LT.TERMNO) GO TO BADMFL
	READ (11,MESARA,TERMNO)
	CLOSE 11
;-
	XCALL CID(CMPCOD)	;SSQ DISPLAY COMPANY NAME & ICON
;-
	IF (V.LT.0) GO TO NOSEC
	IF (SECREC.LE.0) GO TO LOGERR
NOSEC,
	V = 2

	XCALL FLAGS (00000000)
;;;	IF (CNTRLC) XCALL FLAGS (10000000,1)	;TRAP CONTROL/C
	xcall flags (80000000,1)	;always !  ssq 3-37-98
	RETURN
BADOPN,
	XCALL ERROR (ERR,LINE)
	IF (ERR.NE.18) GO TO BADERR
	DISPLAY (15,13,10,
&	'?TERID-FATAL ERROR - FILE UT:MESARA.DDF NOT FOUND',7)
	STOP
BADERR,
	ERRA = ERR
	DISPLAY (15,13,10,
&	'?TERID-FATAL DIBOL ERROR # ',ERRA,' ON FILE UT:MESARA.DDF',7)
	STOP
BADMFL,
	DSPTRM = TERMNO - 1
	DISPLAY (15,13,10,
& 	'?TERID-FATAL ERROR - FILE UT:MESARA.DDF NOT GENERATED LARGE ENOUGH',13,10,
&	'TO SUPPORT THIS TERMINAL NUMBER (',DSPTRM,').',7,7)
	STOP
LOGERR,
	DISPLAY (15,13,10,
&	'?TERID-TERMINAL NOT LOGGED INTO SECURITY SYSTEM - RUN UT:MSMENU',7)
	STOP
END
