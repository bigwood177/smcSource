SUBROUTINE LPON; (LPSW,SPLFIL)
;
;
;		SUBROUTINE TO OPEN OUTPUT DEVICE FOR REPORT PRINT/DISPLAY
;
			; VALUE OF "LPSW" INCOMING
			; ========================
			; 1 = OUTPUT MAY BE PRINTED, SPOOLED OR DISPLAYED
			; 2 = OUTPUT MAY BE PRINTED OR SPOOLED, BUT NOT 
			;	DISPLAYED
			; 3 = OUTPUT MAY BE PRINTED ONLY
			; 4 = OUTPUT MAY BE AUTO-SPOOLED
			; 5 = OUTPUT IS SPECIAL FORMS-SEND TO NON-QUEUED
			;	DEVICE (COULD BE LOCAL PRINTER)
			;
			; VALUE OF "LPSW" OUTGOING
			; ========================
			; 0 = THE DEVICE IS NOT READY OR USER WANTS TO END
			; 1 = OUTPUT IS TO BE PRINTED USING SYSTEM 
			;	 PRINTER (1 - 4)
			; 2 = OUTPUT IS TO BE SPOOLED
			; 3 = OUTPUT IS TO BE PRINTED LOCALLY
			; 4 = OUTPUT IS TO BE DISPLAYED
			; 5 = OUTPUT IS PRINTED LOCALLY WITH NO FORM FEED 
			;	AT END
			; 6 = OUTPUT IS GOING TO NON-QUEUED DEVICE
			;
			; VALUE OF "TERMID" INDICATES TERMINAL TYPE
			; ========================================
			; 0 = NO TERMINAL
			; 1 = VT52 WITH NO OPTIONS
			; 2 = VT52 WITH PRINTER PORT OPTION
			; 3 = VT100 WITH NO OPTIONS - VT52 MODE
			; 4 = VT100 WITH ADVANCED VIDEO OPTION - VT52 MODE
			; 5 = VT100 WITH PRINTER PORT OPTION - VT52 MODE
			; 6 = VT100 WITH NO OPTIONS - ANSI MODE
			; 7 = VT100 WITH ADVANCED VIDEO OPTION - ANSI MODE
			; 8 = VT100 WITH PRINTER PORT OPTION - ANSI MODE
			;
	LPSW	,D
	SPLFIL	,A
RECORD MESARA
			.INCLUDE 'DEF:MES001.DEF'
RECORD MESCTL 
			.INCLUDE 'DEF:MES002.DEF'
RECORD CONAM2
			.INCLUDE 'DEF:RD099B.DEF'
RECORD DEVICE
			.INCLUDE 'DEF:DEV001.DEF'
record  s_filnam
		,a4,    'spl:'
		,a1,    's'
	fter    ,d3             ;terminal #
	ftime   ,a4             ;hhmm of current time
		,a4,    '.spl'
record,x
		,a4
	s_name          ,a12

record  time
	hh      ,a2
	mm      ,a2
	ss      ,a2

RECORD FILBL
	FILDEV	,A3
		,A1,	':'
	FILCOD	,A2
	FILTRM	,A2
	FILSEQ	,A2
		,A1,	'.'
	FILCMP	,A3
		,A1,	'['
	SIZE	,D5
		,A1,	']'
RECORD FILB1,X
		,A14
RECORD,X
		,A4
	NAME	,A6
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD AERR
	ERR	,D3
RECORD
	BLANK	,A3
	CNGCTL	,D1
	CNT1	,D2
	CNT2	,D2
	LOCAL	,D1
	RPDEST	,D2
	ENTRY	,A4
	ERL	,D5
	FILSPC	,A21
	FILSP1	,A14
	INXCTL	,D1
	J	,D2
	MASK	,A3,'XXX'
	MESAGE	,A62
	PRNTRS	,4A3,	'LP:','LQ:','LR:','LS:'
	PRTNUM	,D1
	SUB	,D1
	SVLPSW	,D1
	MSXCTL ,D1
	TERMNO	,D2
	WHATNO	,D1
	SYTTNO	,D3
	MCTTNO	,D2
	TTNOSW	,D1
PROC
	ERR =
	SVLPSW = LPSW
	FILCOD = SPLFIL (5,6)
	XCALL TTNO (SYTTNO,MCTTNO,TTNOSW)
	TERMNO = MCTTNO
	TERMNO = TERMNO + 2
	OPEN (11,U,'UT:MESARA.DDF')
	ON ERROR RDERR
RDERR,
	READ (11,MESARA,TERMNO)
	OFF ERROR
	PRTNUM = DEFPTR
	CURPTR = DEFPTR
	SIZE = SPLSZE
	DELRPT = 'Y'
	NUMCPY = 1
	QUEDPT = 0
	LPSW = 1				;DEFAULT VALUE OF LPSW
	IF (SVLPSW.EQ.4 .AND. SPOOLS.EQ.2) GO TO SPOOL
	IF (CMPCOD.NE.BLANK) CALL GETPTT	;GET DEFAULT PRINTER INFO

	LOCAL = 1
BEGIN,
	XCALL OUTPT (3,1,2,'\',1)
	XCALL OUTPT (9,20,0,'REPORT DESTINATION',1)
	XCALL OUTPT (10,25,0,'1. LP (Q7)',1)
	XCALL OUTPT (11,25,0,'2. LQ (Q6)',1)
	XCALL OUTPT (12,25,0,'3. LR (Q8)',1)
	XCALL OUTPT (13,25,0,'4. LS (Q5)',1)
	XCALL OUTPT (14,25,0,'5. SPOOL FILE',1)
	XCALL OUTPT (15,25,0,'6. VIDEO TERMINAL',1)
	XCALL OUTPT (16,25,0,'7. LOCAL PRINTER',1)
	XCALL OUTPT (17,25,0,'8. PC EDIT',1)
REPDES,
	CTL = '09,44,01,01,NE'
	CALL INPUT
	GO TO (BEGIN,END), INXCTL
	RPDEST = ENTRY
	IF (RPDEST.LE.0 .OR. RPDEST.GT.8) GO TO REPDES
	CALL TRMTST
	IF (RPDEST.EQ.0) GO TO REPDES		;RPDEST = 0 IF BAD TERM TYPE
	IF (RPDEST.GT.4) GO TO (SPOOL,DISPLA), RPDEST - 4
	LPSW = 1
	IF (SVLPSW.EQ.5) LPSW=6
	PRTNUM = RPDEST
	CURPTR = RPDEST

ANYCN1,
	CNGCTL = 4
	XCALL ANYCN (CNGCTL,WHATNO)
	GO TO (END,PROCES,REPDES), CNGCTL + 2
TRMTST,
	IF (RPDEST.EQ.5 .AND. SVLPSW.EQ.3) GO TO BADPRT
	IF (RPDEST.EQ.6 .AND. SVLPSW.GT.1) GO TO BADPRT
	IF (RPDEST.EQ.5 .AND. SPOOLS.LE.0) RPDEST = 0
	IF (RPDEST.EQ.0) XCALL MESAG
&		('?LPON-REPORT DESTINATION INVALID FOR THIS TERMINAL',1)
	RETURN
GETPTT,			; RETRIEVES DEFAULT PRINTER INFO FROM CONAME FILE
	CLOSE 14
	OPEN (14,I,'UT:DEVICE.DDF')
	READ (14,DEVICE,99)
	CLOSE 14
	FILDEV = DEVNAM (CMPPOS)
	NAME = 'CONAME'
	FILCMP = CMPCOD
	CALL SQUEZ
	ON ERROR NOCMP
	OPEN (14,I,FILSP1)			;CONAME MANUALLY OPENED TO
						;AVOID USE OF FILES SUBROUTINE
	READ (14,CONAM2,2)
	CLOSE 14
NOCMP,
	NAME =
	FILCOD = SPLFIL (5,6)
	OFF ERROR
	RETURN
BADPRT,
	RPDEST = 0
	XCALL MESAG ('?LPON-REPORT DESTINATION INVALID FOR THIS REPORT',1)
	RETURN
INVPTR,
	RPDEST = 0
	XCALL MESAG 
&		('?LPON-INVALID PRINTER SELECTION - PRINTER NOT INSTALLED',1)
	GO TO BEGIN


PROCES,
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; ssq
;-----------------------
;;; open print file for output

	xcall tnmbr (fter)
	xcall time (time)
	ftime(1,2) = hh
	ftime(3,4) = mm
	open (14,o,s_filnam)            ;print output file
	s_name(1,1) = curptr,'X'           ;save the printer # selected
	splfil = s_name                 
;-----------------------
	GO TO RETURN
SPOOL,
	XCALL OUTPT (24,1,1,'PLEASE WAIT ...',1)
	FILDEV = SPOLDV
	FILTRM = TERMNO - 1,MASK
	FILCMP = CMPCOD
	ON ERROR GOOD
	CLOSE 14
	ERR = 1
	J =
OPENSP,			;DETERMINE VALID SPOOL FILE NAME
	INCR J
	IF (J.GT.20 .AND. RPDEST.EQ.5) GO TO SPLERR	;REPORT NUMBER CAN'T 
							;GO OVER 20 IF SPOOLED
	IF (J.GT.20 .AND. DELRPT.NE.'Y') GO TO SPLERR	;IF REPORT IS NOT TO 
							;DELETED, IT WILL BE
							;SPOOLED
	FILSEQ = J, MASK
	CALL SQUEZ
	OPEN (14,I,FILSP1)
	CLOSE 14
	GO TO OPENSP
GOOD,
	OFF ERROR
	CLOSE 14
	XCALL ERROR (ERR,ERL)
	IF (ERR.NE.18) GO TO SPLERR	;FILE NOT FOUND
	ON ERROR SPLERR
	ERR = 2
	CALL SQUEZ
	OPEN (14,O,FILSPC)
	IF (RPDEST.LE.4 .AND. RPDEST.GE.1) LPSW = 1
	IF ((SVLPSW.EQ.4.AND.SPOOLS.EQ.2) .OR. RPDEST.EQ.5) LPSW = 2
					;SPOOLED AND NOT PRINTED
	IF (LPSW.EQ.2) CURPTR = 0	;SCAN OFF SPACES FOR SPOOLING
	SPLFIL = FILB1			;SEND UNSQUEEZED FILESPEC TO LPOFF
	GO TO RETURN
DISPLA,
	CLOSE 14
	OPEN (14,I,'TT:')
;;; ssq
	xcall sc132

	LPSW = 4
	GO TO RETURN
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;LOCAL,
;;;	CLOSE 14
;;;	OPEN (14,I,'TT:')
;;;	CURPTR = 0
;;;	LPSW = 3
;;;	IF (SVLPSW.EQ.5) LPSW = 5	;NO FORM FEED AFTER PRINTING
;;;	GO TO RETURN
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;***********************SQUEEZE SPACES OUT OF FILE SPECIFICATION**************
SQUEZ,
	FILSPC =
	FILSP1 =
	CNT1 =
	CNT2 =
SQZLUP,
	IF (CNT1.GE.21) RETURN
	INCR CNT1
	IF (FILBL(CNT1,CNT1).EQ.BLANK) GO TO SQZLUP
	INCR CNT2
	FILSPC (CNT2,CNT2) = FILBL (CNT1,CNT1)
	IF (CNT1.LE.14) FILSP1 (CNT2,CNT2) = FILBL (CNT1,CNT1)
	GO TO SQZLUP
;*****************************************************************************
RETURN,
	OFF ERROR
	WRITE (11,MESARA,TERMNO)
	CLOSE 11
	XCALL OUTPT (3,1,2,'\',1)
	RETURN
END,
	LPSW = 0
	GO TO RETURN
REDCNT,
	ON ERROR REDCN1
REDCN1,
	READ (11,MESCTL,1)
	OFF ERROR
	RETURN
INPUT,
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,1)
	RETURN
SPCERR,
	UNLOCK 11
	XCALL MESAG 
&	  ('PRINTER CURRENTLY BEING USED FOR SPECIAL FORMS BY ANOTHER USER',1)
	GO TO BEGIN
SPLERR,
	OFF ERROR
	MESAGE = '?LPON-SPOOLER ERROR '
	IF (ERR.EQ.1)
&		MESAGE (21,59) = '- TOO MANY SPOOLED REPORTS OF THIS TYPE'
	IF (ERR.EQ.2)
&		MESAGE (21,60) = '- CANNOT OPEN SPOOL FILE ON SPOOL DEVICE'
	IF (ERR.NE.2 .AND. ERR.NE.1) 
&		MESAGE (21,43) = '- #     ON OPENING FILE'
	IF (ERR.NE.2 .AND. ERR.NE.1) 
&		MESAGE (25,27) = ERR,'ZZX'
	XCALL MESAG (MESAGE,1)
	GO TO BEGIN
END
