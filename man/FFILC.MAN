;FFILC.MAN - COMPANY CODE
SUBROUTINE FFILC; (FILNUM,REDFIL,CLCTL)
;
;		GIVEN A DEVICE NUMBER (IE. NUMBER USED IN AN XCALL TO
;		FILES), RETURN THE FULLY QUALIFIED FILE NAME.
;

	CMPNY	,A		;COMPANY CODE
	FILNUM	,D		;RECORD NUMBER ON DEVICE TABLE
	REDFIL	,A		;COMPLETE FILE NAME
	CLCTL	,D		;OUTGOING
				; 0 = OK
				; 1 = NO FILE AVAILABLE
				;INCOMING
				; -1 = DO NOT SQUEEZE FILE SPECIFICATION

RECORD DEVFIL
			.INCLUDE 'DEF:DEV001.DEF'
RECORD MESARA
			.INCLUDE 'DEF:MES001.DEF'
RECORD	VARS
	WRKCMP	,A3
	WRKPOS	,D3
	BLANKS	,A3
	CNT1	,D2
	CNT2	,D2
	ERR	,D3
	LINE	,D3
	SAVFIL	,A14
	TERMNO	,D2
	SYTTNO	,D3
	MCTTNO	,D2
	TTNOSW	,D1
PROC
	ON ERROR ERROR
	IF (CMPPOS.NE.0 .AND. CMPCOD.NE.BLANKS) GO TO BEGIN
				;RECORDS READ IN PREVIOUS XCALL
	OPEN (11,I,'UT:MESARA.DDF')
	XCALL TTNO (SYTTNO,MCTTNO,TTNOSW)
	TERMNO = MCTTNO
	TERMNO = TERMNO + 2
	READ (11,MESARA,TERMNO)
	CLOSE 11
BEGIN,
;1-cusmas, 2-cusidx, 3-aropen are common to smc and roc...
	wrkpos = cmppos
	wrkcmp = cmpcod
	using filnum select
;;;	(1,2,3),	if(cmppos.eq.8) 
	(1,2,3),	if(cmppos.eq.7 .or. cmppos.eq.8) 
				begin
				wrkpos=1		;smc
				wrkcmp = 'SMC'
				end
	(),			wrkcmp = cmpny		;passed code
	endusing

	using wrkcmp select
	('SMC'),	wrkpos = 1
	('ROC'),	wrkpos = 8
	('CAT'),	wrkpos = 7
	('TST'),	wrkpos = 6
	endusing

	OPEN (11,I,'UT:DEVICE.DDF')
READ2,
	READ (11,DEVFIL,FILNUM)
	CLOSE 11
	OFF ERROR
	IF (FILNAM.EQ.'     ') GO TO UNAVBL
	SAVFIL(1,3) = DEVNAM (WRKPOS)
	SAVFIL(4,4) = ':'
	SAVFIL(5,10) = FILNAM
	SAVFIL(11,11) = '.'
	SAVFIL(12,14) = WRKCMP
	CALL SQUEZ
	CLCTL =
	RETURN
SQUEZ,
	IF (CLCTL.EQ.-1) GO TO NOSQZ
	CNT1 =
	CNT2 =
	REDFIL =
SQZLUP,
	IF (CNT1.GE.14) RETURN
	INCR CNT1
	IF (SAVFIL(CNT1,CNT1).EQ.' ') GO TO SQZLUP
	INCR CNT2
	REDFIL (CNT2,CNT2) = SAVFIL (CNT1,CNT1)
	GO TO SQZLUP
NOSQZ,
	REDFIL = SAVFIL
	RETURN
UNAVBL,
	CLCTL = 1
	OFF ERROR
	CLOSE 11
	RETURN
ERROR,
	XCALL ERROR (ERR,LINE)
	IF (ERR.NE.40) GO TO UNAVBL
	ON ERROR READ2
	GO TO READ2
END

