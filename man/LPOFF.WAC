SUBROUTINE LPOFF ;(LPSW,SLPFIL,PGCNT)
;
;   LPOFF / MAN 
;
;		MODIFIED 3-23-87 SSQ: ADDED CODE AT "LPDONE," TO
;		TURN OFF SPECIAL FORMS IF LPSW = 5
;
;
;
;
	LPSW	,D
	SPLFIL	,A
	PGCNT	,D
;
;		SUBROUTINE TO CLOSE OUTPUT DEVICE AND RESET FOR REPORT/DISPLAY
;
			; VALUE OF "LPSW" INCOMING
			; ========================
			;  1 = OUTPUT HAS BEEN PRINTED USING SYSTEM PRINTER
			;  2 = OUTPUT HAS BEEN SPOOLED
			; -3 = OUTPUT HAS BEEN PRINTED LOCALLY
			; -4 = OUTPUT HAS BEEN DISPLAYED
			; -5 = OUTPUT HAS BEEN PRINTED LOCALLY (NO FF AT END)
			;  6 = OUTPUT HAS BEEN PRINTED ON NON-QUEUED DEVICE
			;
			; VALUE OF "LPSW" OUTGOING
			; ========================
			; 0 = CHANNEL 14 CLOSED
			; VALUE OF "TERMID" INDICATES TERMINAL TYPE
			; ========================================
			; 0 = NO TERMINAL
			; 1 = VT52 WITH NO OPTIONS
			; 2 = VT52 WITH PRINTER PORT OPTION
			; 3 = VT100 WITH NO OPTIONS - VT52 MODE
			; 4 = VT100 WITH ADVANCED VIDEO OPTION - VT52 MODE
			; 5 = VT100 WITH PRINTER PORT OPTION - VT52 MODE
			; 6 = VT100 WITH NO OPTIONS - ANSI MODE
			; 7 = VT100 WITH ADVANCED VIDEO OPTION - ANSI MODE
			; 8 = VT100 WITH PRINTER PORT OPTION - ANSI MODE
record  s_filnam
		,a4,    'spl:'
		,a1,    's'
	fter    ,d3             ;terminal #
	ftime   ,a4             ;hhmm of current time
		,a4,    '.spl'
record,x
		,a4
	s_name          ,a12

RECORD MESARA
			.INCLUDE 'DEF:MES001.DEF'
RECORD	MESCTL
			.INCLUDE 'DEF:MES002.DEF'
RECORD SPLDIR
			.INCLUDE 'DEF:SPL001.DEF'
RECORD	,X
			.INCLUDE 'DEF:SPL002.DEF'
RECORD	vars
	pnum	,d1
	BLANK	,A1
	CNT1	,D2
	CNT2	,D2
	DATE	,A9
	ENDRPT	,A30	,'END OF REPORT     PRESS RETURN'
	ENTRY	,A2
	ERRCNT  ,D1     ; COUNT USE FOR SPOOLER ERRORS
	INXCTL	,D1
	LPQMSG  ,A5,	'?NNNN'
	PRNTRS	,4A2,	'LP','LQ','LR','LS'
	SAVFIL	,A14
	SPLCNT	,D3
	SYSTEM	,D1
	TCHAR	,D3
	TERMNO	,D2
	TIME	,D6
	SYTTNO	,D3
	MCTTNO	,D2
	TTNOSW	,D1
PROC
	XCALL ENVR2 (SYSTEM)
	IF (LPSW.EQ.0) RETURN
	IF (LPSW.LT.0) LPSW = -LPSW
	XCALL TTNO (SYTTNO,MCTTNO,TTNOSW)
	TERMNO = MCTTNO
	TERMNO = TERMNO + 2
	OPEN (11,I,'UT:MESARA.DDF')
	ON ERROR READ1
READ1,
	READ (11,MESARA,TERMNO)
	OFF ERROR
	CLOSE 11
	GO TO (SPOOL,SPOOL,LOCAL,DIS100,LOCAL,LPDONE), LPSW
SPOOL,
	IF (QUEDPT.NE.1) CALL FORMFD	;NOT NEEDED FOR QUEUED PTR(AUTOMATIC FF)
	IF (LPSW.EQ.2 .OR. DELRPT.EQ.'N') CALL SPLDIR
	IF (SPLCNT.GT.200) GO TO SPLERR
	IF (LPSW.EQ.2) GO TO LPDONE	;NO PRINTING, JUST SPOOLING
	IF (QUEDPT.NE.1) GO TO LPDONE	;NOT QUEUED, JUST CLOSE PRINTER
	CALL SQUEZ			;SQUEEZE FILESPEC AFTER WRITING SPLDIR
					;AND BEFORE USING BY LPQUE
	CLOSE 14
	XCALL OUTPT (24,1,1,'Submitting report ',1)
	DISPLAY (15,SPLFIL,'  to printer queue ',PRNTRS(CURPTR))
	LPQMSG(CURPTR+1,CURPTR+1)='Y'
	SEND(LPQMSG,'LPTSPL',-2)
	IF (DELRPT.NE.'N') LPQUE (SPLFIL,LPNUM:CURPTR,COPIES:NUMCPY,DELETE)
	IF (DELRPT.EQ.'N') LPQUE (SPLFIL,LPNUM:CURPTR,COPIES:NUMCPY)
	GO TO LPDONE
;***********************SQUEEZE SPACES OUT OF FILE SPECIFICATION***************
SQUEZ,
	CNT1 =
	CNT2 =
	SAVFIL =
SQZLUP,
	IF (CNT1.GE.14) GO TO SQZDUN
	INCR CNT1
	IF (SPLFIL(CNT1,CNT1).EQ.BLANK) GO TO SQZLUP
	INCR CNT2
	SAVFIL (CNT2,CNT2) = SPLFIL (CNT1,CNT1)
	GO TO SQZLUP
SQZDUN,
	SPLFIL = SAVFIL
	RETURN
;*************************************************************************
SPLDIR,
	XCALL DATE(DATE)
	XCALL TIME(TIME)
	OPEN (11,U,'UT:SPLDIR.DDF')
	ON ERROR LOCKED
LOCKED,
	READ (11,SPLDIR,1)
	OFF ERROR
	SPLCNT = SPLREC
	INCR SPLCNT
	IF (SPLCNT.GT.200) RETURN
	SPLREC = SPLCNT
	WRITE (11,SPLDIR,1)
	SPLNAM = SPLFIL
	SPDATE = DATE
	SPTIME = TIME (1,4), 'XX:XX'
	SPPAGS = PGCNT
	ON ERROR WRTERR
WRTERR,
	WRITE (11,SPLDIR,SPLCNT)
	OFF ERROR
	CLOSE 11
	RETURN
LOCAL,
	IF (LPSW.EQ.3) CALL FORMFD
	IF (TERMID.NE.2) GO TO LOC100		;2 = VT52 W/LOCAL PRINTER
	DISPLAY (14,27,88)	;(ESC X) TURN VT52 PRINTER CONTROLLER OFF
	GO TO LPDONE
LOC100,
	DISPLAY (14,27,91,52,105)	;(ESC [ 4 i) TURN VT100 PRINTER
					;CONTROLLER OFF
	IF (TERMID.EQ.5)
&		DISPLAY (14,27,91,63,50,108)	;(ESC [ ? 2 l) ENTER VT52 MODE
	GO TO LPDONE
DIS100,
	DISPLAY (14,ENDRPT)
	XCALL FLAGS (00010000,1)		;TURN OFF CHARACTER ECHO
GETCHR,
	ACCEPT (14,TCHAR)
	IF (TCHAR.NE.13) GO TO GETCHR

	XCALL SC080

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;	IF (SYSTEM.EQ.1.OR.SYSTEM.EQ.4) DISPLAY (14,29,'Q',1)	;UNDER TSX, ACTIVATE ON FIRST
;;;						;CHARACTER
;;;
;;;	IF (SYSTEM.NE.1.AND.SYSTEM.NE.4) ACCEPT (14,TCHAR)
;;;	DISPLAY (14,27,91,63,51,108)		;(ESC [ ? 3 l) SET SCREEN TO
;;;						;80 COLUMNS
;;;	DISPLAY (14,27,91,49,59,50,52,114)	;(ESC [ 1 ; 2 4 r) SET FULL
;;;						;SCREEN SCROLL
;;;	DISPLAY (14,27,91,63,52,108)		;JUMP SCROLL MODE
;;;	DISPLAY (14,27,91,48,109)		;(ESC [ 0 m) SET CHARACTER
;;;						;ATTRIBUTES OFF
;;;	IF (TERMID.EQ.3.OR.TERMID.EQ.4.OR.TERMID.EQ.5)
;;;&		DISPLAY (14,27,91,63,50,108)	;(ESC [ ? 2 l) ENTER VT52 MODE
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL FLAGS (00010000,0)		;TURN ON CHARACTER ECHO
LPDONE,
	CLOSE 14
	if (lpsw.eq.1 .or. lpsw.eq.3 .or. lpsw.eq.6)
		begin
		s_name = splfil         ;restore printer output file name
		pnum = s_name(1,1)      ;printer number
		s_name(1,1) = 's'       ;always first char of printer output file name
		if (pnum .eq. 8)
		then	begin
			xcall spedit (s_filnam)
			xcall delet (s_filnam)
			end
		else	lpque (s_filnam,lpnum:pnum,copies:1,delete)
		end
	LPSW =

	IF(LPSW .EQ. 6) CALL SPCFRM	;HANDLE SPECIAL FORMS FLAG

	RETURN
SPCFRM,
	CLOSE 11
	OPEN (11,U,'UT:MESARA.DDF')
	IF(CURPTR.LT.1 .OR. CURPTR.GT.4) RETURN	
	ONERROR RDM
RDM,	READ (11,MESCTL,1)
	SPCFRM(CURPTR,CURPTR) =
	FRMTRM(CURPTR) =
	WRITE (11,MESCTL,1)
	CLOSE 11
	RETURN
SPLERR,
	XCALL OUTPT (23,1,2,'\',1)
	XCALL OUTPT (23,1,4,'?LPOFF-SPOOLER DIRECTORY FULL - FILE ',1)
	XCALL OUTPT (0,0,4,SPLFIL,1)
        XCALL OUTPT (0,0,4,' MUST BE PRINTED MANUALLY.',1)
	XCALL MESAG ('PLEASE MAKE A NOTE OF THE SPOOL FILE',1)
	GO TO LPDONE
FORMFD,
	ON ERROR OFFLIN
	FORMS (14,0)
	OFF ERROR
	RETURN
OFFLIN,
	XCALL OUTPT (24,1,1,'?LPOFF-PRINTER OFF LINE',1)
	SLEEP 5
	XCALL OUTPT(24,1,1,'\',1)
	GO TO FORMFD
END
