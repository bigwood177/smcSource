SUBROUTINE DSPLY ; (MAX,ROW,COL,DECMAL,OPTION,V)
;
;		ROUTINE TO FORMAT & DISPLAY A NUMBER,
;		DATE OR DOLLAR FIELD ON THE SCREEN
; 
	MAX	,D	; MAX # OF DIGITS IN NUMBER OR DOLLAR FIELD
	ROW     ,D	; SCREEN ROW
	COL	,D	; SCREEN COLUMN
	DECMAL	,D	; INCOMING DECIMAL FIELD
	OPTION	,D	; OPTION SWITCH
			;	1 = NUMBER
			;	2 = DATE
			;	3 = DOLLARS
			;	4 = DISPLAY 3 DIGITS TO RIGHT OF DEC PT
			;	5 = DISPLAY 1 DIGIT TO RIGHT OF DEC PT
			;	6 = NUMBER, BLANK ONLY ONE FOLLOWING CHARACTER
	V	,D	;VT50/52 CONTROL 
RECORD
	ALPHA	,A18	; WORK
	COL2	,D2
	CHAR	,D2	; FIELDS
	ROW2	,D2
	TSTLEN	,D2,16	; DETERMINES NUMBER OF FOLLOWING BLANKS TO BE ADDED
PROC
	CHAR = 0
	IF (OPTION.EQ.6) TSTLEN = 18
	GO TO (DSPNUM,DSPDTE,DSPDLR,DSPDG3,DSPDG1,DSPNUM), OPTION
	RETURN
DSPNUM,				; CONVERT TO ALPHA
	ALPHA = DECMAL
	GO TO JUSTFY
DSPDTE,				; FORMAT & DISPLAY DATE
	ALPHA(1,8) = DECMAL(1,6),'XX/XX/XX'
	XCALL OUTPT (ROW,COL,0,ALPHA(1,8),V)
	RETURN
DSPDLR,				; FORMAT DOLLAR FIELD
	ALPHA = DECMAL,'ZZ,ZZZ,ZZZ,ZZZ.XX-'
	GO TO JUSTFY
DSPDG3,				; FORMAT FOR 3 PLACES TO RIGHT OF DECIMAL POINT
	ALPHA = DECMAL,'Z,ZZZ,ZZZ,ZZZ.XXX-'
	GO TO JUSTFY
DSPDG1,				; FORMAT FOR 1 PLACE TO RIGHT OF DECIMAL POINT
	ALPHA = DECMAL,'ZZ,ZZZ,ZZZ,ZZZ.X-'
JUSTFY,				; LEFT JUSTIFY ROUTINE
	INCR CHAR
	IF (ALPHA(CHAR,CHAR).EQ.' ') GO TO JUSTFY
	IF (CHAR.LE.18) XCALL OUTPT (ROW,COL,0,ALPHA(CHAR,18),V)
	IF (MAX-(TSTLEN-CHAR).LE.0) RETURN
	ALPHA =
	COL2 = COL+50-CHAR-31
	XCALL OUTPT (ROW,COL2,0,ALPHA(1,MAX-(TSTLEN-CHAR)),V)
				; DISPLAY NUMBER OR DOLLAR FIELD
	RETURN
END
