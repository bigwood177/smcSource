;  CCTMNT / COP 
;
;
;
;		MAINTAINS COPCTL FILE
;

;	01-16-14: make aracct isam
RECORD COPCTL
		.INCLUDE 'DEF:RD060A.DEF'
RECORD ARACCT		
		.INCLUDE 'DEF:RD007A.DEF'
;RECORD	,X		
;		.INCLUDE 'DEF:RD007B.DEF'
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD
	ADD	,D1
	BLANKS	,A7
	BSEND	,D5
	BSMID	,D5
	DECMAL	,D7
	KEY	,A7
	SRCCTL	,D1
	SRCHFL	,D1
	DEFACT	,D7
	ALPHA	,A8
	MASK	,A7,'XXXXXXX'
	MASK2	,A8,'XXXX-XXX'
	ENTRY	,A30
	INXCTL	,D1
	CNGCTL	,D1
	WHATNO	,D2
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	SWITCH	,D1
	V	,D1
PROC
	XCALL TERID (V)
	V = 1
	SWITCH = 1
	XCALL FILES (1,'U',60,SWITCH)		;FILE # 60 - COPCTL
	IF (SWITCH.EQ.9) GO TO ENDOFF
	SWITCH = 1
	XCALL FILES (5,'SI',7,SWITCH)		;FILE # 7 - ARACCT
	IF (SWITCH.EQ.9) GO TO END1
BEGIN,
	ADD = 
	LOKCTL = 0
	XCALL IO (1,COPCTL,1,READ,LOKCTL)
	IF (LOKCTL) GO TO END
	IF (COPCTL.EQ.']]]]]]]]]]') ADD = 1	;ADD MODE
;	LOKCTL = 1
;	XCALL IO (5,ARACCT,1,READ,LOKCTL)
;	BSEND = ORG007
DISPL1,
	CNGCTL =
	XCALL OUTPT (1,1,2,'CONTROL FILE MAINTENANCE - COP',1)
	XCALL OUTPT (4,5,0,'1. LAST ORDER NUMBER USED',1)
	XCALL OUTPT (5,5,0,'2. LAST INVOICE NUMBER USED',1)
	XCALL OUTPT (6,5,0,'3. LAST CREDIT MEMO NUMBER USED',1)
	XCALL OUTPT (7,5,0,'4. LAST FAB-TICKET NUMBER USED',1)
	XCALL OUTPT (8,5,0,'5. G/L DISTRIBUTIONS ?',1)
	XCALL OUTPT (10,5,0,'6. MULTIPLE A/R ACCOUNTS ?',1)
	XCALL OUTPT (11,5,0,'7. DEFAULT A/R ACCOUNT NO.',1)
	XCALL OUTPT (12,5,0,'8. MULTIPLE MISC ACCOUNTS ?',1)
	XCALL OUTPT (13,5,0,'9. DEFAULT MISC ACCOUNT NO.',1)
	XCALL OUTPT (14,4,0,'10. MULTIPLE SALES TAX ACCOUNTS ?',1)
	XCALL OUTPT (15,4,0,'11. DEFAULT SALES TAX ACCOUNT NO.',1)
	XCALL OUTPT (16,4,0,'12. MULTIPLE FREIGHT ACCOUNTS ?',1)
	XCALL OUTPT (17,4,0,'13. DEFAULT FREIGHT ACCOUNT NO.',1)
	XCALL OUTPT (19,4,0,'14. DEFAULT COMMENTS (1)',1)
	XCALL OUTPT (20,4,0,'15. DEFAULT COMMENTS (2)',1)
	IF (ADD) COPCTL =
	IF (ADD) GO TO LSTORD
DSPDEF,
	SRCHFL = 1
	CTL (1,8) = '04,39,06'
	DECMAL = LSTORD
	CALL DSPNUM
	ROW = 5
	DECMAL = LSTINV
	CALL DSPNUM
	ROW = 6
	DECMAL = LSTCRM
	CALL DSPNUM

	ROW = 7
	DECMAL = LSTJOB
	CALL DSPNUM

	XCALL OUTPT (8,COL,0,DSTFLG,1)
	XCALL OUTPT (10,COL,0,MLARFG,1)
	ROW = 11
	DEFACT = DEFARA
	CALL GETACT
	XCALL OUTPT (ROW,50,0,ARACDS,1)
	XCALL OUTPT (12,COL,0,MLMSFG,1)
	ROW = 13
	DEFACT = DEFMSA
	CALL GETACT
	XCALL OUTPT (ROW,50,0,ARACDS,1)
	XCALL OUTPT (14,COL,0,MLSTFG,1)
	ROW = 15
	DEFACT = DEFSTA
	CALL GETACT
	XCALL OUTPT (ROW,50,0,ARACDS,1)
	XCALL OUTPT (16,COL,0,MLFRFG,1)
	ROW = 17
	DEFACT = DEFRET
	CALL GETACT
	XCALL OUTPT (ROW,50,0,ARACDS,1)
	XCALL OUTPT (19,COL,0,ORDCOM(1),1)
	XCALL OUTPT (20,COL,0,ORDCOM(2),1)
	SRCHFL =
	GO TO ANYCNG
LSTORD,
	CTL = '04,39,06,00,#E'
	CALL INPUT
	GO TO (BEGIN,END), INXCTL
	LSTORD = ENTRY
	GO TO (ANYCNG), CNGCTL
LSTINV,
	CTL = '05,39,06,00,# '
	CALL INPUT
	GO TO (BEGIN,END), INXCTL
	LSTINV = ENTRY
	GO TO (ANYCNG), CNGCTL
LSTCRM,
	CTL = '06,39,06,00,# '
	CALL INPUT
	GO TO (BEGIN,END), INXCTL
	LSTCRM = ENTRY
	GO TO (ANYCNG), CNGCTL
LSTJOB,
	CTL = '07,39,06,00,# '
	CALL INPUT
	GO TO (BEGIN,END), INXCTL
	LSTJOB = ENTRY
	GO TO (ANYCNG), CNGCTL
GLDIST,
	CTL = '08,39,01,01,YN'
	CALL INPUT
	DSTFLG = ENTRY
	IF (DSTFLG.EQ.'N') GO TO (ORDCM1,ANYCNG), CNGCTL + 1
	GO TO (ANYCNG), CNGCTL
MLTAR,
	CTL = '10,39,01,00,YN'
	CALL INPUT
	MLARFG = ENTRY (1,1)
	GO TO (ANYCNG), CNGCTL
DEFAR,
	XCALL OUTPT (11,39,1,'\',V)
	CTL = '11,39,04,03,# '
	CALL INPACT
;;	GO TO (DISPL1), INXCTL
	GO TO (BEGIN), INXCTL
	DEFARA = DECMAL
	DEFACT = DEFARA
	CALL GETACT
	IF (SRCCTL.EQ.1) GO TO DEFAR
	GO TO (ANYCNG), CNGCTL
MLTMSC,
	CTL = '12,39,01,00,YN'
	CALL INPUT
	MLMSFG = ENTRY (1,1)
	GO TO (ANYCNG), CNGCTL
DEFMSC,
	XCALL OUTPT (13,39,1,'\',V)
	CTL = '13,39,04,03,# '
	CALL INPACT
;;;	GO TO (DISPL1), INXCTL
	GO TO (BEGIN), INXCTL
	DEFMSA = DECMAL
	DEFACT = DEFMSA
	CALL GETACT
	IF (SRCCTL.EQ.1) GO TO DEFMSC
	GO TO (ANYCNG), CNGCTL
MLTTAX,
	CTL = '14,39,01,00,YN'
	CALL INPUT
	MLSTFG = ENTRY (1,1)
	GO TO (ANYCNG), CNGCTL
DEFTAX,
	XCALL OUTPT (15,39,1,'\',V)
	CTL = '15,39,04,03,# '
	CALL INPACT
;;;	GO TO (DISPL1), INXCTL
	GO TO (BEGIN), INXCTL
	DEFSTA = DECMAL
	DEFACT = DEFSTA
	CALL GETACT
	IF (SRCCTL.EQ.1) GO TO DEFTAX
	GO TO (ANYCNG), CNGCTL
MLTFRT,
	CTL = '16,39,01,00,YN'
	CALL INPUT
	MLFRFG = ENTRY (1,1)
	GO TO (ANYCNG), CNGCTL
DEFFRT,
	XCALL OUTPT (17,39,1,'\',V)
	CTL = '17,39,04,03,# '
	CALL INPACT
;;;	GO TO (DISPL1), INXCTL
	GO TO (BEGIN), INXCTL
	DEFRET = DECMAL
	DEFACT = DEFRET
	CALL GETACT
	IF (SRCCTL.EQ.1) GO TO DEFFRT
	GO TO (ANYCNG), CNGCTL
ORDCM1,
	CTL = '19,39,30,00,A '
	CALL INPUT
	GO TO (BEGIN,END), INXCTL
	ORDCOM (1) = ENTRY
	GO TO (ANYCNG), CNGCTL
ORDCM2,
	CTL = '20,39,30,00,A '
	CALL INPUT
	GO TO (BEGIN), INXCTL
	ORDCOM (2) = ENTRY
	GO TO ANYCNG
GETACT,
;	KEY = DEFACT, MASK
	ALPHA = DEFACT, MASK2
;	IF (KEY.EQ.BLANKS) GO TO BADACT
;	XCALL SERCH (5,ARACCT,KEY,1,7,BSEND,BSMID,SRCCTL,2,32,37,0,0,0,0)
	LOKCTL = 1
	XCALL ISIO (5, ARACCT, DEFACT, READ, LOKCTL)
	IF (LOKCTL .EQ. 0)
	  THEN SRCCTL = 0
	  ELSE SRCCTL = 1
	    
	IF (SRCCTL.EQ.1) CALL BADACT
	IF (SRCCTL.EQ.0) CALL DSPACT
	RETURN
BADACT,
	IF (SRCHFL.EQ.1) ARACDS = 'ACCOUNT NOT ON FILE'
	IF (SRCHFL.EQ.0) XCALL MESAG ('ACCOUNT NOT ON FILE',1)
	IF (SRCHFL.EQ.1) SRCCTL = 0
	RETURN
DSPACT,
	XCALL OUTPT (ROW,39,0,ALPHA,V)
	XCALL OUTPT (ROW,50,0,ARACDS,V)
	RETURN
INPACT,
	XCALL GETAC (ROW,COL,DECMAL,INXCTL,V)
	RETURN
DSPNUM,
	XCALL DSPLY (MAX,ROW,COL,DECMAL,1,V)
	RETURN
CNGBR,
	GO TO (LSTORD,LSTINV,LSTCRM,LSTJOB,GLDIST,MLTAR,DEFAR,MLTMSC,DEFMSC,MLTTAX,
&	DEFTAX,MLTFRT,DEFFRT,ORDCM1,ORDCM2), WHATNO
	GO TO ANYCNG
PROCES,
	XCALL WATE (3,V)
	LOKCTL = 1
	XCALL IO (1,COPCTL,1,WRITE,LOKCTL)
	GO TO END
INPUT,
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,V)
	RETURN
ANYCNG,
	CNGCTL = 6
	XCALL ANYCN (CNGCTL,WHATNO)
	GO TO (END,PROCES,CNGBR,ANYCNG), CNGCTL+2
	GO TO ANYCNG
END,
	XCALL FILES (1,'U',60,4)
END1,
	XCALL FILES (5,'I',7,4)
ENDOFF,
	XCALL PGCHN ('CP:SPCFUN',1)
END
