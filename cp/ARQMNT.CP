;ARQMNT.CP
;
;
;		AUTHOR: SHERWOOD S. QUIRING
;		DATE  : 3-DEC-1999
;
;	CUSTOMER ORDER MAINTENANCE
;
	.INCLUDE 'WND:TOOLS.DEF'
	.INCLUDE 'WND:WINDOWS.DEF'


RECORD	ORDCM2
	.INCLUDE 'DEF:RD135A.DEF'

RECORD ORDHDR   	
	.INCLUDE 'DEF:RD044A.DEF'

RECORD ORDLIN		
	.INCLUDE 'DEF:RD045A.DEF'
RECORD	,X
	.INCLUDE 'DEF:RD045D.DEF'


RECORD	FUNKEY
	.INCLUDE 'DEF:FUNKEY.DEF'

RECORD	CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2

RECORD	WINS
	W_ID	,D4
	WND_1	,D4
	C_WND	,D4

RECORD	WN_NAME
		,A5,	'CRDMM'
	WN_TNMBR,D4

RECORD	CHANNEL
	CHN044	,D2
	CHN045	,D2
	CHN182	,D2
	CHN135	,D3

RECORD	VARS
	USET	,D1
	LNAM	,A25
	SNAM	,A12
	SINT	,A3
	SEXT	,D10
	OPT	,D1
	XCUST	,D6
	CCORD	,A1	;C = CREDIT CARD ORDER
	CORDN	,A6
	ABORT	,D1
	F_KEY	,D3
	I	,D4
	OPNOK	,D1
	DECMAL	,D18
	EXEPRC	,D10
	TAXTOT	,D8
	RUNTOT	,D8
	TAXAMT	,D8
	TODAY	,D6
	DSTFLG	,A1
	BLANKS	,A30
	ENTRY	,A30
	INXCTL	,D1
	LOKCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	STORE	,D1,2
	TMPORD	,D6
	FIRST	,D1
	SWITCH	,D1
	CLCTL	,D1
	V	,D1

PROC
	XCALL TERID (V)
	XCALL RDATE(TODAY)

	CLEAR ABORT

BEGIN_ORDER,
	CALL INIT_WINDOW
	CALL OPENS
	IF (.NOT. OPNOK) GOTO ENDOFF

DISPLA,
	INXCTL =
	DSTFLG = 'N'
	XCALL OE1W (DSTFLG,W_ID)
	XCALL W_DISP(W_ID,WD_POS,1,1,'ARCHIVE QUOTES')
	XCALL W_UPDT
ORDNO,
	OFFERROR
	CTL = '04,16,06,00,#E'
	CALL INPUT
	GO TO (DISPLA,ENDOFF), INXCTL
	USING F_KEY SELECT
	(END_KEY,F_11), GOTO ENDOFF
	ENDUSING

	TMPORD = ENTRY(1,6)
	IF (TMPORD .EQ. 0) GOTO DISPLA


	OORDNO = TMPORD

	LOKCTL = 1
	XCALL ISIO (CHN044,ORDHDR,OORDNO,READ,LOKCTL)
	IF (LOKCTL .NE. 0)
		BEGIN
		XCALL WNMSG(W_ID,24,"CAN'T FIND ORDER",1)
		GOTO DISPLA
		END

	IF (OLOC .NE. 'E') 
		BEGIN
		XCALL WNMSG (W_ID, 24, 'NOT A QUOTE',1)
		GOTO DISPLA
		END

	clear ordcm2
	c2_ord = oordno
	c2_seq = 0

	lokctl = 0
	xcall isio (chn135, ordcm2, c2_key, read, lokctl)
	if (lokctl .ne. 0) clear ordcm2
	CALL DISP_ORD


	USING OQARC SELECT
	(1),	XCALL W_DISP(W_ID, WD_POS, 24, 1, 'RESTORE QUOTE?')
	(),	XCALL W_DISP(W_ID, WD_POS, 24, 1, 'ARCHIVE QUOTE?')
	ENDUSING

	CTL = '24,16,01,00,YN'
	CALL INPUT
	GOTO (DISPLA),INXCTL-1

	IF (OQARC .EQ. 1)
	THEN	USET = 0
	ELSE	USET = 1

	XCALL ARQ (OORDNO, USET, 8, 2, CHN182)

	GOTO DISPLA

;======================================================

ENDOFF,
	XCALL W_PROC(WP_DELETE, WND_1)
	XCALL U_FINISH
	CALL CLOSE


	XCALL PGCHN ('CP:ARQMNU',1)
;=====================================================

INPUT,
	XCALL WINPT (W_ID,ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,F_KEY)
	RETURN
;-------------------------------------------------

DISP_ORD,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	ENTRY(1,6) = OORDNO,	'ZZZZZX' [LEFT]
	XCALL W_DISP(W_ID, WD_POS, 4, 16, ENTRY(1,6))

	ENTRY(1,6) = OORDDT(5,8),	'XX/XX/'
	ENTRY(7,10) = OORDDT(1,4),	'XXXX'
	XCALL W_DISP(W_ID, WD_POS, 4, 38, ENTRY(1,10))

	ENTRY(1,6) = OPROMD(5,8),	'XX/XX/'
	ENTRY(7,10) = OPROMD(1,4),	'XXXX'
	XCALL W_DISP(W_ID, WD_POS, 4, 68, ENTRY(1,10))

	ENTRY(1,6) = OCUSNO,	'ZZZZZX' [LEFT]
	XCALL W_DISP(W_ID, WD_POS, 6, 16, ENTRY(1,6))

	XCALL W_DISP(W_ID, WD_POS, 6, 24, OCUSNM)


	ENTRY(1,2) = OSLMAN,	'ZX' [LEFT]
	XCALL W_DISP(W_ID, WD_POS, 8, 16, ENTRY(1,2))

	XCALL SREP(OSLMAN,LNAM,SNAM,SINT)
	XCALL W_DISP(W_ID, WD_POS, 9, 9, SNAM)

	XCALL W_DISP(W_ID, WD_POS, 8, 35, OLOC)

	XCALL W_DISP(W_ID, WD_POS, 8, 53, OSCAC)


	IF (OMETRO .NE. BLANKS)
		BEGIN
		XCALL W_DISP(W_ID, WD_POS, 7,58,'12DPO:')
		XCALL W_DISP(W_ID, WD_POS, 7,66,OMETRO)
		END

	XCALL W_DISP(W_ID, WD_POS, 8, 69, OPONO)
	XCALL W_DISP(W_ID, WD_POS, 10, 53, OCLPPD)
	XCALL W_DISP(W_ID, WD_POS, 10, 69, OJOBNO)

	ENTRY(1,4) = OSHPTO,'ZZZZ'
	XCALL W_DISP(W_ID, WD_POS, 12,16,ENTRY(1,4))
	XCALL W_DISP(W_ID, WD_POS, 13,13,OSHPNM)
	XCALL W_DISP(W_ID, WD_POS, 14,13,OSHAD1)
	XCALL W_DISP(W_ID, WD_POS, 15,13,OSHAD2)
	XCALL W_DISP(W_ID, WD_POS, 16,13,OSHAD3)
	
	XCALL W_DISP(W_ID, WD_POS, 19,19,OCOMNT(1))
	XCALL W_DISP(W_ID, WD_POS, 20,19,OCOMNT(2))
	XCALL W_DISP(W_ID, WD_POS, 21,19,C2_COM)

	RETURN
;--------------------------------------------------



OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK
	SWITCH = 5
	XCALL FILES (4, 'SI', 44, SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN044 = 4

	XCALL FILES(5, 'SI', 45, SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN045 = 5

	SWITCH = 5
	XCALL FILES (17,'SI',182,SWITCH)
	CHN182 = 17

	SWITCH = 5
	XCALL FILES (35, 'SI', 135, SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN135 = 35

	OPNOK = 1
	RETURN
;------------------------------------------------
CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (CHN044) CLOSE CHN044
	IF (CHN045) CLOSE CHN045
	IF (CHN182) CLOSE CHN182
	IF (CHN135) CLOSE CHN135

	RETURN
;------------------------------------------------


INIT_WINDOW,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		;;; SET UP SCREEN 1 WINDOW
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL U_START("LIB:SMC_WND",,,,,,,100)
	XCALL E_SECT("Order Maintenance", D_CAPTION)
;;;	XCALL W_INIT(POOLSIZE,WNDCHNL,MAXWINS)
;;;	xcall u_start("lib:smc_wnd",,,,,,,35)
	XCALL TNMBR (WN_TNMBR)
	XCALL W_PROC(WP_FIND,WND_1,WN_NAME)
	IF (.NOT. WND_1)
		BEGIN
		XCALL W_PROC(WP_CREATE,WND_1,WN_NAME,24,78)
		END
	XCALL W_BRDR(WND_1,WB_TITLE,'ARCHIVE QUOTES',
&			WB_TPOS,WBT_TOP,WBT_CENTER)
	XCALL W_PROC(WP_PLACE,WND_1,1,1)	
	XCALL W_DISP(WND_1,WD_CLEAR)
	XCALL W_UPDT
	W_ID = WND_1
	xcall u_logwnd(wnd_1)
	RETURN




