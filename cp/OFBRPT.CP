;OFBRPT.CP
;
;		AUTHOR: SHERWOOD S. QUIRING
;		DATE  : 16-OCT-1998
;
;		OUTGOING FREIGHT AND B/L SUMMARY REPORT - REPRINT
;	20-jul-2004: sort by carrier
;	16-SEP-2004: use historic files
;	
RECORD	D_STUFF
	D_IN	,D8		;DATE-IN, ANY FORMAT
	D_OUT	,D6		;RETURN VALUE MMDDYY
	D_OUTR	,D8		;RETURN VALUE CCYYMMDD
	D_FMT	,A10		;RETURN VALUE MM/DD/CCYY
	D_SW	,A2		;"99" = DATE CONVERSION ERROR



RECORD ORDHDR
		.INCLUDE 'DEF:RD044A.DEF'

RECORD	COPTBL
		.INCLUDE 'DEF:RD182A.DEF'

RECORD BLHEAD
		.INCLUDE 'DEF:RD178A.DEF'
RECORD HDR1
		,A50,'INVOICE      ------ CUSTOMER ---------------------'
		,A21,'---------------------'
RECORD HDR2
		,A50,'NUMBER       NO.    NAME                        SH'
		,A50,'IP TO NAME              ---- FREIGHT -----   SHP D'
		,A14,'ATE   SHIP VIA'
RECORD HDR3
		,A50,'      B/L #                                       '
		,A50,'                         BIL/LAD   INVOICE   B/L D'
		,A32,'ATE   B/L CARRIER     PRO NUMBER'
RECORD LEG1
		,A33,'FOR INVOICE POSTING BATCH DATED: '
	LBATDT	,A10

RECORD ASHIPNO
	AORDNO	,A6
		,A1,'.'
	AHDRN	,A1
		,A1,'.'
	ASHPN	,A2

RECORD	CHANNEL
	CHN182	,D2
	CHN184	,D2
	CHN188	,D2

RECORD	VARS
	OPNOK	,D1
	INVDAT	,D8
	REDFIL	,A14
	OUTFIL	,A14
	ENTRY	,A30
	INXCTL	,D1
	CLCTL	,D1
	SCAC	,A4
	CNGCTL	,D1
	WHATNO	,D2
	LOKCTL	,D1
	V	,D1
	SWITCH	,D1
	SRCCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	BSMID	,D5
	NUMMSK	,A6,'XXXXXX'
	DATMSK	,A8,'XX/XX/XX'
	DLRMSK	,A10,'ZZ,ZZZ.XX-'
	BSEND	,D5
	PLINE	,A132
	LINCNT	,D2,60
	PGCNT	,D3
	LPSW	,D1
	RPTNUM	,D3
	PRTTYP	,A1
	TITLE	,A39,'OUTGOING FREIGHT AND B/L SUMMARY REPORT'
	LPARG	,D1
	SPLFIL	,A14
	TOTFRT	,D7,0000000
	TOTBLF	,D7,0000000
	LEG2	,A6,'NO LEG'
	LEG3	,A40,'NO LEG'
RECORD	,X
	ALOKCTL	,A1

PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,TITLE,V)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO ENDOFF

DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,TITLE,V)
	XCALL OUTPT (4,4,0,'INVOICE DATE: ',1)
	XCALL INPUT (4,21,08,00,'DE',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	INVDAT = ENTRY(1,8)
	CNGCTL = 2
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (DISPLA),CNGCTL
LOOP,
	XCALL IOS (CHN184, ORDHDR, READ, LOKCTL)
	IF (LOKCTL .NE. 0) GOTO END_HST
	IF (OINVDT .NE. INVDAT) GOTO LOOP
	WRITES (8, ORDHDR)
	GOTO LOOP

END_HST,
	CLOSE 8
	OUTFIL = REDFIL
	OUTFIL(12,14) = 'SRT'
	SORT (INPUT=REDFIL,OUTPUT=OUTFIL,RECORD=ORDHDR,KEY=(OSCAC,OCUSNO,OORDNO))
	OPEN(8,I,OUTFIL)

OPENLP,
	LPSW = 4	; MAY BE AUTO-SPOOLED
	SPLFIL (5,6) = 'E2'
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)

BEGIN1,
	XCALL OUTPT (1,1,1,TITLE,V)
BEGIN,
	LOKCTL = 1
	XCALL IOS (8,ORDHDR,READ,LOKCTL)
	IF (LOKCTL.NE.0) GOTO EOF
	IF (OFRGHT.NE.0.OR.OCLPPD.EQ.'P') CALL PRTIT
	GOTO BEGIN

PRTIT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	SCAC = OSCAC
	CALL GET_SCAC

	XCALL DATE8(OINVDT, D_OUT, D_OUTR, LBATDT, D_SW)
;;;	LBATDT = OINVDT,DATMSK
	PLINE (1,6) = OINVNO,NUMMSK
	PLINE (13,18) = OCUSNO,NUMMSK
	PLINE (21,45) = OCUSNM
	PLINE (49,78) = OSHPNM
	PLINE (84,93) = OFRGHT,DLRMSK	;XX,XXX.XX-

	XCALL DATE8(OSHDAT, D_OUT, D_OUTR, D_FMT, D_SW)
	PLINE (96,105) = D_FMT
	PLINE (107,121) = SC_NAME
	IF (LINCNT+3.GE.60) LINCNT = 62
	CALL PRINT
	TOTFRT = TOTFRT + OFRGHT
	CALL GETBL
	XCALL LINFD (1)
	INCR LINCNT
	RETURN

GETBL,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; GET B/L FOR SUMMARY INFORMATION
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	BHORDN = OORDNO
	LOKCTL = 1
	XCALL ISIO (CHN188,BLHEAD,BHORDN,READ,LOKCTL)
	IF (LOKCTL.OR.BHORDN.NE.OORDNO) RETURN
	DO BEGIN
	    SCAC = BHSCAC
	    CALL GET_SCAC
	    AORDNO = BHORDN
	    AHDRN = BHHDRN
	    ASHPN = BHSHPN
	    PLINE (7,11) = ASHIPNO(7,11)
	    PLINE (74,83) = BHFRTAM,DLRMSK

	    XCALL DATE8(BHBILD, D_OUT, D_OUTR, D_FMT, D_SW)
	    PLINE (96,105) = D_FMT
	    PLINE (107,121) = SC_NAME
	    PLINE (123,132) = BHPRON
	    CALL PRINT
	    TOTBLF = TOTBLF + BHFRTAM
	  LOKCTL = 1
	  XCALL IOS (CHN188,BLHEAD,READ,LOKCTL)
	END UNTIL (LOKCTL.OR.BHORDN.NE.OORDNO)
	RETURN

PRINT,
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,HDR3,
&		LEG1,LEG2,LEG3,0,132,0,1,LPSW,RPTNUM,PRTTYP)
	RETURN

;-----------------------------------------------------------
GET_SCAC,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		;;; COP TABLE SCAC LOOK-UP
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR TBL_KEY
	TBLCOD = 'SC'
	TBLKEY = SCAC
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .NE. 0)
		BEGIN
		CLEAR COPTBL
		SC_NAME = "* NOT ON FILE *"
		END
	RETURN
;-----------------------------------------------------

;-----------------------------------------------------------
EOF,
	PLINE (84,92) = '---------'
	CALL PRINT
	PLINE (59,73) = 'TOTAL FREIGHT: '
	PLINE (74,83) = TOTBLF,DLRMSK
	PLINE (84,93) = TOTFRT,DLRMSK
	CALL PRINT

ENDOFF,
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	CALL CLOSE
;;;	CLOSE 8
;;;	CLOSE 9
;;;	CLOSE 5
;;;	CLOSE 4
	XCALL PGCHN ('CP:COPRPT',1)

NO_INVOICE_FILE,
	OFFERROR
	XCALL MESAG ("No work file from last invoice run",1)
	GOTO ENDOFF


OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (4,'SI',184,SWITCH)		;SLHHDR - ORDER HEADER FILE
	IF (SWITCH .EQ. 9) RETURN
	CHN184 = 4

	SWITCH = 5
	XCALL FILES (28,'SI',188,5)		;SLBHDR - B/L HEADER FILE
	CHN188 = 28

	XCALL FILES (18,'SI',182,5)		;COPTBL - COP TABLES FILE
	CHN182 = 18

	XCALL FFILE (44, REDFIL, CLCTL)
	REDFIL(5,14) = 'OFBRPT.WRK'
	OPEN(8,O,REDFIL)

	OPNOK = 1
	RETURN
;-----------------------------------------------------

CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	IF (CHN184) CLOSE CHN184
	IF (CHN188) CLOSE CHN188
	CLOSE 8
	RETURN
;-----------------------------------------------------
END 	
;INVOICE      ------ CUSTOMER --------------------------------------------
;NUMBER       NO.    NAME                        SHIP TO NAME                         FREIGHT   SHP DATE   SHIP VIA
;      B/L #                                                                          CHARGED   B/L DATE   B/L CARRIER     PRO NUMBER
;
;XXXXXX       XXXXX  XXXXXXXXXXXXXXXXXXXXXXXXX   XXXXXXXXXXXXXXXXXXXXXXXXX   XX,XXX.XX   XX/XX/XX   XXXXXXXXXXXXXXX
;      .X.XX                                                                                         XX/XX/XX   XXXXXXXXXXXXXXX
;      .X.XX                                                                                         XX/XX/XX   XXXXXXXXXXXXXXX


