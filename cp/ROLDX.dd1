.subroutine	roldx
	cus_rec	,a		;cusmas record
	allow_update	,d	;0 = no update allowed, 1 = updates
	x	,d		;row
	y	,d		;col
	r_type	,d		;0=both, 1=sales, 2=shipping
	chan	,d		;channel (optional)
;

	.include 'wnd:tools.def'

record	cusmas
	.include 'def:rd001a.def'

record	rolo		;(534 bytes)
	.include 'def:rolo.def'

record	vars
	r_chan	,d2
	inp_id	,i4
	col_id	,i4
	select	,d1
	status	,d1	;0 = ok, 1 = abort
	srcflg	,d1,	1
	sts	,d4
	done	,d1

.proc
	cusmas = cus_rec

	xcall i_ldinp(inp_id, g_utlib, "roldx_input",D_NOPLC)

	xcall i_display(inp_id,"cus_info",cusmas,"cusno,name,add1,add2,city,
&		state,zip,phone,efax")

	if (allow_update)
	then	xcall b_enable(inp_id,"Save")
	else	xcall b_disable(inp_id, "Save")

	if (%passed(chan))
	then	r_chan = chan
	else	begin
		xcall u_open(r_chan,"u:i","SMC:ROLO.SMM",,srcflg,sts)
		if (sts)
			begin
			xcall u_message("Error opening Rolo")
			xreturn
			end
		end
	select = 2			;change
	read (r_chan,rolo,cusno) [err=eof]

	using r_type select
	(0),	xcall i_display(inp_id,"rolo_info",rolo,"r_cont1,r_cont2,
&		r_cont3,r_cont4,r_sales,r_ship")
	(1),	xcall i_display(inp_id,"rolo_sales",rolo,"r_cont1,r_cont2,
&		r_cont3,r_cont4,r_sales")
	(2),	xcall i_display(inp_id,"rolo_shipping",rolo,"r_cont1,r_cont2,
&		r_cont3,r_cont4,r_ship")
	endusing
eof,
	xcall U_WINDOW (D_PLACE, inp_id, x, y)

	if (r_cust .eq. cusno)
	then	select = 2
	else	select = 1	

	using r_type select
	(0),	xcall i_input(inp_id,"rolo_info", rolo,,,,,,,status)
	(1),	xcall i_input(inp_id,"rolo_sales", rolo,,,,,,,status)
	(2),	xcall i_input(inp_id,"rolo_shipping", rolo,,,,,,,status)
	endusing
	if (g_select)
		begin
		using G_entnam select
		("SAVE"),using select select
			(1),	begin
				r_cust = cusno
				store (r_chan,rolo,r_cust)
				xcall u_message ("Rolodex updated")
				end
			(2),	begin
				write (r_chan,rolo,r_cust)
				xcall u_message ("Rolodex updated")
				end
			endusing
		("CANCEL"),	 xcall u_beep
		endusing
		done = TRUE
		end


	;;;close r_chan
fini,	
	if (.not. %passed(chan) ) xcall u_close (r_chan)
	XCALL U_WINDOW(D_DELETE, inp_id)
	xreturn
	.end

.subroutine	cmd_save
	id	,i
	name	,a
	status	,d
.proc
	status = 0	;ok
	xcall m_signal("Save")
	return
.end

.subroutine	cmd_cancel
	id	,i
	name	,a
	status	,d
.proc
	status = 1	;abort
	xcall m_signal("Cancel")
	return
.end



