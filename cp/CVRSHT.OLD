;CVRSHT.CP
;
;	COVER SHEET
;
EXTERNAL FUNCTION
	GU_NAME	,A

RECORD	ORDHDR
	.INCLUDE 'DEF:RD044A.DEF'

RECORD	ORDLIN
	.INCLUDE 'DEF:RD045A.DEF'

RECORD	SALMAN
	.INCLUDE 'DEF:RD054A.DEF'

RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'

RECORD	DPTSTS
	.INCLUDE 'DEF:RD193A.DEF'

RECORD	DUCACC
	.INCLUDE 'DEF:RD175A.DEF'

RECORD	PRINT
;;;	TITLE	,A*,	'COVER SHEET'
	TITLE	,A*,	'NO TITLE'
	HD	,A6,	'NO HDR'
	LG	,A9,	'NO LEGEND'
	PLINE	,A80
	PRNTON	,D1
	LINCNT	,D2,60
	PGCNT	,D6
	LPSW	,D2
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	PRTCTL	,D3,080
	LPARG	,D1
	PRNTSW	,D1
	PRTCTR	,D1

RECORD	HLINE0
		,A66
		,A6,	'ORDER:'
		,A1
	HL0_ORD	,D6

RECORD	HLINE
	,A*,'DATE ENTERED  SALESMAN   EXT  PO NUMBER  JOB NUMBER  SHIP VIA'

RECORD	HLINE2
	,A*,'  DEPARTMENT           STATUS   COMMENT'

RECORD	DPT_DATA
	DPTARA	,20A2
	NUMDPT	,D2

RECORD	CHANNEL
	CHN044	,D2
	CHN045	,D2
	CHN054	,D2
	CHN175	,D2
	CHN182	,D2
	CHN193	,D2

RECORD	VARS
	UNAME	,A25
	TDATE	,D8		;CCYYMMDD
	TTIME	,D6		;HH:MM:SS
	I	,D5
	BLANKS	,A30
	XDATE	,D8
	D_LINE	,A60
	DCHAR	,D3
	TCHAR	,D3
	OPNOK	,D1
	XORD	,D6
	ROW	,D2
	COL	,D2
	ENTRY	,A30
	INXCTL	,D1
	CNGCTL	,D1
	WHATNO	,D2
	WDEPT	,A2
	LOKCTL	,D1

	READ	,D1,0
	WRITE	,D1,1
	STORE	,D1,2
	SWITCH	,D1
	V	,D1

.PROC
	XCALL TERID (V)

	UNAME = %GU_NAME
	UPCASE UNAME
	
	CALL OPENS
	IF (.NOT. OPNOK) GOTO ENDOFF

DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,'COVER SHEET',1)
	XCALL OUTPT (3,4,0,'ORDER #',1)
	XCALL INPUT (3, 14, 06, 00 , '#E', ENTRY, INXCTL, 1)
	GOTO (DISPLA, ENDOFF), INXCTL
	XORD = ENTRY(1,6)

	CALL GETORD
	IF (LOKCTL) GOTO DISPLA

	CALL DSPDPT
	IF (PRNTON.EQ.1)  
		BEGIN
		XCALL LPOFF(LPSW,SPLFIL,PGCNT)
		CALL TIME_STAMP
		END
	CLEAR PRNTON
	GOTO DISPLA

ENDOFF,
	CALL CLOSE
	IF (UNAME(1,5) .EQ. 'PLANT')
		BEGIN
		XCALL PGCHN('CP:SHPMNU',1)
		END
	XCALL PGCHN ('CP:CPLMNU',1)
	
DSPDPT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	PLINE = HLINE2
	CALL PRINT

	CLEAR DPT_DATA
	ROW = 8
	NUMDPT = 0
	FIND (CHN193, DPTSTS, OORDNO) [ERR=NXTDPT]
NXTDPT,
	XCALL IOS (CHN193, DPTSTS, READ, LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF_DPT
	IF (S_ORDN .NE. OORDNO) GOTO EOF_DPT

	CALL GET_TABLE

	PLINE(3,4) = DD_DEPT
	PLINE(6,20) = DD_DESC
	USING S_STAT SELECT
	(0),	PLINE(24,28) = 'OPEN '
	(1),	PLINE(24,28) = 'COMPL'
	ENDUSING
	IF(S_PACK.EQ.1) PLINE(24,29)='PACKED'
	PLINE(33,52) = S_COMM

	CALL PRINT

	INCR NUMDPT
	DPTARA(NUMDPT) = S_DEPT(1,1)
	GOTO NXTDPT

EOF_DPT,
	RETURN
;---------------------------------------------------------

GET_TABLE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		;;; READ DEPT FROM COPTBL
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR TBL_KEY
	TBLCOD = 'DD'
;;;	DD_DEPT = S_DEPT	
	DD_DEPT = S_DEPT(1,1)
	XCALL ISIO (CHN182, COPTBL, TBL_KEY, READ, LOKCTL)
	IF (LOKCTL) DD_DESC = '* NOT ON FILE *'
	RETURN
;--------------------------------------------------------


GETORD,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL ISIO (CHN044, ORDHDR, XORD, READ, LOKCTL)
	IF (LOKCTL)
		BEGIN
		XCALL MESAG ('ORDER NOT ON FILE',1)
		RETURN
		END
	XCALL IO (CHN054, SALMAN, OSLMAN, READ, LOKCTL)
	IF (LOKCTL) CLEAR SALMAN
	
	HL0_ORD=OORDNO
	PLINE = HLINE0
	CALL PRINT
	CALL PRINT

	PLINE(23,80)=OCUSNM
	CALL PRINT
;;;	PLINE(23,28) = 'ORDER:'
;;;	PLINE(30,35) = OORDNO,'XXXXXX'
;;;	CALL PRINT
	CALL PRINT

	PLINE = HLINE
	CALL PRINT
	CALL PRINT

	XDATE(1,4) = OORDDT(5,8)
	XDATE(5,8) = OORDDT(1,4)
	PLINE(3,12) = XDATE,	'XX/XX/XXXX'
	PLINE(15,24)= SLSNM(1,10)
	PLINE(26,28)= SLSTNO,	'XXX'
	PLINE(31,40)= OPONO
	PLINE(42,51)= OJOBNO
	
	TBLCOD = 'SC'
	SC_SCAC = OSCAC
	XCALL ISIO (CHN182, COPTBL, TBL_KEY, READ, LOKCTL)
	IF (LOKCTL) SC_NAME = '* NOT ON FILE *'

	PLINE(54,80) = SC_NAME
	CALL PRINT
	CALL PRINT

	RETURN
;---------------------------------------------------------

TIME_STAMP,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		;;; RECORD DATE AND TIME ORDER PRINTED.
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	SWITCH = 5
	XCALL FILES (44, 'SU', 044, SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	READ(44, ORDHDR, XORD) [ERR=TS_ERR]
	XCALL RDAT8(TDATE)
	XCALL TIME(TTIME)
	OSHDTE = TDATE
	OSHTIM = TTIME(1,4)
	WRITE(44, ORDHDR, XORD)
	CLOSE 44
	RETURN
TS_ERR,
	XCALL MESAG ('Another user is updating this order',1)
	RETURN
;---------------------------------------------------------

PRINT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (PRNTON .EQ. 0) CALL PRNTON
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HD,HD,HD
&		,LG,LG,LG,0,080,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;-------------------------------------------------------------
PRNTON,
	SPLFIL (5,6) = 'EF'
	LPSW = 1		;PRINT,SPOOL, OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTON = 1
	RETURN
;-------------------------------------------------------------

OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (4, 'SI', 044, SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN044 = 4

	XCALL FILES (5, 'SI', 045, SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN045 = 5

	XCALL FILES (6, 'I', 054, SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN054 = 6

	XCALL FILES (7, 'SI', 175, SWITCH)
	IF(SWITCH .EQ. 9) RETURN
	CHN175 = 7

	XCALL FILES (8, 'SI',  182, SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN182 = 8 

	XCALL FILES (9, 'SI', 193, SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN193 = 9

	OPNOK = 1
	RETURN
;---------------------------------------------

CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	IF (CHN044) CLOSE CHN044
	IF (CHN045) CLOSE CHN045
	IF (CHN054) CLOSE CHN054
	IF (CHN193) CLOSE CHN193
	IF (CHN175) CLOSE CHN175

	RETURN
;---------------------------------------------
.END

