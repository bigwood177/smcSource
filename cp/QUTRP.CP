subroutine qutrp
	stdat	,d
	endat	,d
	nite	,d	;1=nite batch, no screen i/o

;QUTRPT.CP
;
;	QUOTE TO ORDER REPORT
; called from either arqmnu or nite batch driver.
;
;
RECORD	D_STUFF
	D_IN	,D8		;DATE-IN, ANY FORMAT
	D_OUT	,D6		;RETURN VALUE MMDDYY
	D_OUTR	,D8		;RETURN VALUE CCYYMMDD
	D_FMT	,A10		;RETURN VALUE MM/DD/CCYY
	D_SW	,A2		;"99" = DATE CONVERSION ERROR


RECORD	ARQDAT
	.INCLUDE 'DEF:RD083A.DEF'
RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'
RECORD ORDHDR
	.INCLUDE 'DEF:RD044A.DEF'
RECORD ORDLIN
	.INCLUDE 'DEF:RD045A.DEF'
RECORD DUCACC
	.INCLUDE 'DEF:RD175A.DEF'
RECORD SALMAN
	.INCLUDE 'DEF:RD054A.DEF'

RECORD HDR1
	,A50,'----- ORDER -----        -------- CUSTOMER -------'
	,A50,'--------------                                    '
	,A32

RECORD HDR2
	,A50,'NUMBER     DATE          NUMBER   NAME            '
;	      12345678901234567890123456789012345678901234567890
	,A54,'                 PO NUM         SALE AMT   DEPARTMENTS'
;;;	,A54,'                 REASON         SALE AMT   DEPARTMENTS'
	,A28

RECORD LEG1
		,A14,'SALESPERSON:  '
	LSTSLM	,D2
		,A3
	SLMNAM	,A25
		,A10
	L1DAT1	,A10
		,A*, ' THRU '
	L1DAT2	,A10

RECORD	ARA
	CDPT	,11A2
	C1	,11D3,	094,097,100,103,106,109,112,115,118,121,124
	C2	,11D3,	095,098,101,104,107,110,113,116,119,122,125
	I	,D6

RECORD	CHANNEL
	CHN044	,D2
	CHN045	,D2
	CHN054	,D2
	CHN083	,D2
	CHN175	,D2
	CHN182	,D2
	CHN184	,D3
	CHN185	,D3
	CHN186	,D3
	CH_LIN	,D3
	CH_DUC	,D3

RECORD	VARS
	OPNOK	,D1
	XDATE	,D8	
	V	,D1
	A2	,A2
	TITLE	,A*,	'QUOTES TO ORDER REPORT'
	PLINE	,A132
	LPONSW	,D1
	LPSW	,D1
	SPLFIL	,A14
	LINCNT	,D2,62
	PGCNT	,D3
	RPTNUM	,D3
	PRTTYP	,A1
	LPARG	,D1
	SWITCH	,D1
	WRCNT	,D5
	READ	,D1,	0
	WRITE	,D1,	1
	LOKCTL	,D1
	FILE	,D1
	LSTORD	,D8
	DTMASK	,A8,	'XX/XX/XX'
	INXCTL	,D1
	ENTRY	,A30
	FSTOPN	,D6,	0
	RECNO	,D5
;;;	STORDR	,D8
;;;	ENORDR	,D8
	TOTSAL	,D8
	EXEPRC	,D8
	DECMAL	,D10
	ORDEST	,A1

PROC 
	ORDEST = 'O'		;THIS REPORT FOR ORDERS ONLY

BUILD,
	CALL OPENS
	IF (.NOT. OPNOK) GOTO ENDOFF

	XDATE(1,4) = STDAT(5,8)
	XDATE(5,8) = STDAT(1,4)
	L1DAT1 = XDATE, 'ZX/XX/XXXX'

	XDATE(1,4) = ENDAT(5,8)
	XDATE(5,8) = ENDAT(1,4)
	L1DAT2 = XDATE, 'ZX/XX/XXXX'

	if (.not. nite) XCALL WATE (2,2)
	LSTSLM = -1
	LSTORD = -1
	LPONSW = 0
	CLEAR AQ_KEY1
	FIND (7, ARQDAT, AQ_KEY1, KRF:1) [ERR=RDLOOP]
RDLOOP,
	LOKCTL = 1
	XCALL IOS (chn083, ARQDAT, READ, LOKCTL)
	IF (LOKCTL.NE.0) GOTO EOF
	IF (AQ_RC .NE. 'QO') GOTO RDLOOP
	IF (AQ_DATE.LT.STDAT) GOTO RDLOOP
	IF (AQ_DATE.GT.ENDAT) GOTO RDLOOP

	CH_DUC = CHN175
	CH_LIN = CHN045

	LOKCTL = 1
	XCALL ISIO (CHN044, ORDHDR, AQ_ORD, READ, LOKCTL)
	IF (LOKCTL .NE. 0)
		BEGIN
		LOKCTL = 1
		XCALL ISIO (CHN184, ORDHDR, AQ_ORD, READ, LOKCTL)
		IF (LOKCTL .NE. 0) GOTO RDLOOP
		CH_LIN = CHN185
		CH_DUC = CHN186
		END

	IF (ORDEST.NE.OLOC) GOTO RDLOOP		;;;

	IF (OSLMAN.NE.LSTSLM)
	BEGIN
	  LINCNT = 62
	  LOKCTL = 1
	  XCALL IO (8,SALMAN,OSLMAN,READ,LOKCTL)
	  LSTSLM = OSLMAN
	  SLMNAM = SLSNM
	END

OLP2,
	PLINE (1,6) = OORDNO

	XCALL DATE8(OORDDT, D_OUT, D_OUTR, D_FMT, D_SW)
	PLINE (10,17) = D_OUT, DTMASK
;;;	PLINE (21,22) = OSLMAN
	PLINE (26,31) = OCUSNO
	PLINE (35,64) = OCUSNM
	PLINE (68,78) = OPONO
	
	CALL GETSAL

	CALL OPN_OPS			;DEPTS

	CALL PRINT
	LSTORD = OORDNO
	LSTSLM = OSLMAN
	GOTO RDLOOP

GETSAL,
	FOR I FROM 1 THRU 10 CLEAR CDPT(I)
	TOTSAL =
	LORDNO = OORDNO
	LOKCTL = 1
	XCALL ISIO (CH_LIN,ORDLIN,LORDNO,READ,LOKCTL)
GS1A,
	if(ltype .ne. 'M')
		BEGIN
		IF (LOKCTL.OR.LORDNO.NE.OORDNO) GOTO GS1B
	;;;	DECMAL = ((LQTYOR*LPRICE) - (LDISC*LQTYOR*LPRICE)#2)#1
		DECMAL = (LQTYOR*LPRICE)#1
		EXEPRC = DECMAL ;;;- ((DECMAL*LODISC)#2)
		TOTSAL = TOTSAL + EXEPRC
		LOKCTL = 1
		CALL GET_DEPT
		END
	XCALL IOS (CH_LIN, ORDLIN,READ,LOKCTL)
	GOTO GS1A
GS1B,
	DUCTOR = OORDNO
	LOKCTL = 1
	XCALL ISIO (CH_DUC,DUCACC,DUCTOR,READ,LOKCTL)
GS2A,
	IF (LOKCTL.OR.DUCTOR.NE.OORDNO) GOTO GS2B
	DECMAL = ( POUNDS * GPRICE ) # 1
	TOTSAL = TOTSAL + DECMAL		;DUCT
	IF (SQFLIN.NE.0) 
	BEGIN
	  DECMAL = ( LINPRC * SQFLIN ) # 1
	  TOTSAL = TOTSAL + DECMAL		;LINER
	END
	LOKCTL = 1
	XCALL IOS (CH_DUC,DUCACC,READ,LOKCTL)
	GOTO GS2A
GS2B,
	PLINE (81,91) = TOTSAL,'ZZZ,ZZZ.XX-'

	RETURN		

GET_DEPT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		;;; FILL DEPT ARRAY FROM ORDLIN RECS
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	FOR I FROM 1 THRU 10
		BEGIN
		IF (CDPT(I) .EQ. LDEPT) EXITLOOP
		IF (CDPT(I) .EQ. A2)
			BEGIN
			CDPT(I) = LDEPT
			EXITLOOP
			END
		END

	RETURN
;---------------------------------------------------------

OPN_OPS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		;;; DEPTS
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
oo_loop,
	FOR I FROM 1 THRU 10
		BEGIN
		IF (CDPT(I) .NE. A2)
		THEN	PLINE (C1(I), C2(I)) = CDPT(I)
		ELSE	EXITLOOP
		END

	return
;-----------------------------------------------------------

PRINT,
	IF (LPONSW .EQ. 0) CALL LPON

	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,'NO HDR',LEG1,
&		'NO LEGEND',' ',0,0,132,1,LPSW,RPTNUM,PRTTYP)
	RETURN
;-----------------------------------------------------------

LPON,
	IF (NITE .EQ. 1)
	THEN	LPSW = 9	;COMPRESSED LOCAL
	ELSE	LPSW = 1	; PRINT, SPOOL OR DISPLAY

	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	if (.not. nite) XCALL WATE (LPARG,V)
	LPONSW = 1
	RETURN
;-----------------------------------------------------------

EOF,
	PLINE =
	CALL PRINT
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
ENDOFF,
	CALL CLOSE
	xreturn

;;;	XCALL PGCHN ('CP:ARQMNU',1)

OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK, CHANNEL

	SWITCH = 5
	XCALL FILES (4,'SI',44,SWITCH)
	IF (SWITCH.EQ.9) RETURN
	CHN044 = 4

	SWITCH = 5
	XCALL FILES (5,'SI',45,SWITCH)
	IF (SWITCH.EQ.9) RETURN
	CHN045 = 5

	SWITCH = 5
	XCALL FILES (7, 'SI', 083, SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN083 = 7

	SWITCH = 5
	XCALL FILES (8,'I',54,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN054 = 8

	SWITCH = 5
	XCALL FILES (9,'SI',175,SWITCH)
	IF (SWITCH.EQ.9) RETURN
	CHN175 = 9

	SWITCH = 5
	XCALL FILES (17, 'SI', 182, SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN182 = 17

	SWITCH = 5
	XCALL FILES (184, 'SI', 184, SWITCH)	;SLHHDR
	IF (SWITCH .EQ. 9) RETURN
	CHN184 = 184

	SWITCH = 5
	XCALL FILES (185, 'SI', 185, SWITCH)	;SLHLIN
	IF (SWITCH .EQ. 9) RETURN
	CHN185 = 185

	SWITCH = 5
	XCALL FILES (186, 'SI', 186, SWITCH)	;SLHDUC
	IF (SWITCH .EQ. 9) RETURN
	CHN186 = 186


	OPNOK = 1
	RETURN
;------------------------------------------------

CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (CHN044) CLOSE CHN044
	IF (CHN045) CLOSE CHN045
	IF (CHN054) CLOSE CHN054
	IF (CHN083) CLOSE CHN083
	IF (CHN175) CLOSE CHN175
	IF (CHN182) CLOSE CHN182
	IF (CHN184) CLOSE CHN184
	IF (CHN185) CLOSE CHN185
	IF (CHN186) CLOSE CHN186

	RETURN
;------------------------------------------------


END


