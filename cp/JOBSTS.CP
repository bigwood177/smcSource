;JOBSTS.CP
;
;	Check status of line items, update header, print report.
;

RECORD	JOBHDR
	.INCLUDE 'DEF:RD071A.DEF'

RECORD	JOBLIN
	.INCLUDE 'DEF:RD072A.DEF'
;
RECORD	PRINT
	TITLE	,A21,	'OPEN FAB TICKETS'
	HD	,A6,	'NO HDR'
	LG	,A9,	'NO LEGEND'
	PLINE	,A132
	PRNTON	,D1
	LINCNT	,D2,60
	PGCNT	,D6
	LPSW	,D2
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	PRTCTL	,D3,080
	LPARG	,D1
	PRNTSW	,D1
	PRTCTR	,D1

RECORD	HD1
		,A*,	'TICKET DPT   REL-DATE  SCHD-DATE'
		,A*,	'                               ORDERED  COMPLETE'


RECORD	CHANNEL
	CHN071	,D2
	CHN072	,D2
	
RECORD	KEY2
	K2_DEP	,A2
	K2_JOB	,D6

RECORD	VARS
	ST_DEP	,A2
	EN_DEP	,A2
	OPNOK	,D1
	RECVD	,D1	;1= RECEIVED
	XDATE	,D8
	SAVJOB	,D6
	ENTRY	,A30
	INXCTL	,D1
	CNGCTL	,D1
	WHATNO	,D2
	I	,D6
	LOKCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	SWITCH	,D1
	V	,D1
;
PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'FAB TICKET STATUS REPORT',1)
;
	CALL OPENS
	IF (.NOT. OPNOK) GOTO EOF

DISPLA,
	XCALL OUTPT (6,4,0,'DEPT AA   THRU: AA',1)
	XCALL INPUT (6,9,02,00,'AE',ENTRY,INXCTL,1)
	GOTO (DISPLA, ENDOFF),INXCTL
	ST_DEP = ENTRY(1,2)
	IF (ST_DEP .EQ. '  ')
		BEGIN
		EN_DEP = 'ZZ'
		XCALL OUTPT (6,9,1,'ALL',1)
		GOTO ANYCN
		END

	XCALL INPUT (6,20,02,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	EN_DEP = ENTRY(1,2)
	IF (EN_DEP .EQ. '  ')
		BEGIN
		EN_DEP = ST_DEP
		XCALL OUTPT (6,20,1,EN_DEP,1)
		END
ANYCN,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (DISPLA),CNGCTL


	CLEAR SAVJOB
	K2_DEP = ST_DEP
	K2_JOB =
	FIND (CHN071, JOBHDR, KEY2, KRF:1) [ERR=LOOP]
;;;	FIND (CHN071, JOBHDR, ^FIRST) [ERR=LOOP]
LOOP,
	LOKCTL = 1
	XCALL IOS (CHN071, JOBHDR, READ, LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF
	IF (JHSTS .EQ. 'R') GOTO LOOP		;ALREADY RECV'D
	IF (JHDEPT .GT. EN_DEP) GOTO EOF
	CALL LINES
	IF (.NOT. RECVD) GOTO LOOP	

	JHSTS = 'R'
	XCALL ISIO (CHN071, JOBHDR, JHNUM, WRITE, LOKCTL)
	GOTO LOOP

LINES,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CLEAR RECVD

	CLEAR JOBLIN
	JLNUM = JHNUM
	FIND (CHN072, JOBLIN, JLKEY) [ERR=J_LOP]
J_LOP,
	LOKCTL = 1
	XCALL IOS (CHN072, JOBLIN, READ, LOKCTL)
	IF (LOKCTL .NE. 0) GOTO J_EOF
	IF (JLNUM .NE. JHNUM) GOTO J_EOF

	USING JLSTS SELECT
	('R'),	RECVD = 1		;AT LEAST 1 LINE RECVD

	('P'),	BEGIN
		RECVD = 0		;AT LEAST 1 LINE STILL OPEN
		CALL P_JLINE		;PRINT THIS LINE
		END

	(),	NOP			;SKIP
	ENDUSING

	GOTO J_LOP
J_EOF,
	RETURN
;------------------------------------------------

P_JLINE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	IF (JLNUM .NE. SAVJOB) CALL NEWJOB

	PLINE (3,17) = JLITM
	PLINE (19,21) = JLIF1
	PLINE (23,25) = JLIF2
	PLINE (27,31) = JLIF3
	PLINE (33,62) = JLDES
	PLINE (64,70) = JLQTY,	'ZZZ,ZZX'
	PLINE (72,78) = JLRCV,	'ZZZ,ZZX'
	CALL PRINT
	RETURN

;------------------------------------------------

NEWJOB,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (SAVJOB .EQ. 0) GOTO OUTJOB
	CLEAR PLINE
	CALL PRINT

OUTJOB,
	PLINE (1,6) = JHNUM,	'ZZZZZX'
	PLINE (9,10) = JHDEPT

;;;	XDATE(1,4) = JHRLDT(5,8)
;;;	XDATE(5,8) = JHRLDT(1,4)
;;;	PLINE (12,21) = XDATE,	'ZX/XX/XXXX'

	XDATE(1,4) = JHSHDT(5,8)
	XDATE(5,8) = JHSHDT(1,4)
	PLINE (23,32) = XDATE,	'ZX/XX/XXXX'
	CALL PRINT

	SAVJOB = JLNUM

	RETURN
;------------------------------------------------
;TICKET DPT   REL-DATE  SCHD-DATE     ORDERED  COMPLETE
;XXXXXX  AA XX/XX/XXXX XX/XX/XXXX
;
;        AAAAAAAAAAAAAAA AAA-AAA-AAAAA ZZZ,ZZX  ZZZ,ZZX
;1234567890123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5         6         7
;

PRINT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (PRNTON .EQ. 0) CALL PRNTON
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HD1,HD,HD
&		,LG,LG,LG,0,080,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;-------------------------------------------------------------
PRNTON,
	SPLFIL (5,6) = 'EF'
	LPSW = 1		;PRINT,SPOOL, OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTON = 1
	RETURN
;------------------------------------------------
ENDOFF,
EOF,
	IF (PRNTON.EQ.1)  XCALL LPOFF(LPSW,SPLFIL,PGCNT)
	CALL CLOSE
	STOP 'cp:jobmnu'

OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK
	
	SWITCH = 5
	XCALL FILES (1,'SU',71, SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN071 = 1

	SWITCH = 5
	XCALL FILES (2,'SU',72, SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN072 = 2

	OPNOK = 1

	RETURN
;------------------------------------------------

CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	IF (CHN071) CLOSE CHN071
	IF (CHN072) CLOSE CHN072

	RETURN
;------------------------------------------------
