;OPTMNT.CP
;DPTMNT.CP
;	COP TABLE MAINTENANCE
;	DEPARTMENT OPERATIONS
;
RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'


RECORD	CHANNEL
	CHN182	,D2


RECORD	PRINT
	TITLE	,A*,	'JOB TICKET OPERATIONS'
	HD	,A6,	'NO HDR'
	LG	,A9,	'NO LEGEND'
	PLINE	,A80
	PRNTON	,D1
	LINCNT	,D2,60
	PGCNT	,D6
	LPSW	,D2
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	PRTCTL	,D3,080
	LPARG	,D1
	PRNTSW	,D1
	PRTCTR	,D1


RECORD	HD1
		,A*,	'DEPT  SEQ  DESCRIPTION                    WRK-DEPT'


RECORD	VARS
	A3	,A3
	ALPHA	,A12
	OPNOK	,D1
	BLANKS	,A30
	SEQ	,D3
	DEPT	,A2
	DESC	,A30
	DPTDSC	,A30
	ST_DPT	,A2
	EN_DPT	,A2
	PGM	,D1
	ROW	,D2
	ENTRY	,A30
	INXCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	STORE	,D1,2
	DELETE	,D1,3
	LOKCTL	,D1
	WHATNO	,D2
	SELECT	,D1
	CNGCTL	,D1
	I	,D3
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'JOB TICKETS OPERATIONS TABLE',1)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO CLOSE

MENU,
	XCALL OUTPT (1,1,2,'JOB TICKETS OPERATIONS TABLE',1)
	XCALL OUTPT (3,9,0,'PLEASE SELECT APPLICATION',1)
	XCALL OUTPT (5,15,0,'1. TABLE MAINTENANCE',1)
	XCALL OUTPT (6,15,0,'2. PRINT TABLE',1)
MINPUT,
	XCALL INPUT (3,36,1,1,'#E',ENTRY,INXCTL,1)
	GOTO (MINPUT,ENDOFF), INXCTL
	PGM = ENTRY(1,1)
	GOTO (DISPLA,PRINT_TABLE),PGM
	GOTO MINPUT

DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,'JOB TICKETS OPERATIONS TABLE',1)
	XCALL OUTPT ( 4,4,0,'1. DEPARTMENT',1)
	XCALL OUTPT ( 6,4,0,'2. SEQ #',1)
	XCALL OUTPT ( 8,4,0,'3. DESCTIPTION',1)
	XCALL OUTPT (12,4,0,'4. WORK DEPT (Y/N)',1)

DEPT,
	XCALL INPUT (4,24,02,00,'AE',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	DEPT = ENTRY(1,2)
	CLEAR TBL_KEY
	TBL_KEY(1,2) = 'DD'
	OP_DEPT = DEPT
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .NE. 0)
		BEGIN
		XCALL MESAG ('DEPARTMENT NOT FOUND',1)
		GOTO DEPT
		END
	DPTDSC = DD_DESC
	XCALL OUTPT (4,27,0,DD_DESC,1)

SEQ,
	XCALL INPUT (6,24,02,00,'#E',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	SEQ = ENTRY(1,2)
	CLEAR TBL_KEY
	TBL_KEY(1,2) = 'OP'
	OP_DEPT = DEPT
	OP_SEQ = SEQ
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .EQ. 0)
		BEGIN
		SELECT = 2
		CALL DSPREC
		GOTO ANYCNG
		END

	SELECT = 1			;ADD MODE
	CLEAR COPTBL

	OP_DEPT = DEPT
	OP_SEQ = SEQ
DESC,
	XCALL INPUT (8,24,30,00,'LE',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	OP_DESC = ENTRY(1,30)
	GOTO (ANYCNG),CNGCTL
;;;DFL,
;;;	XCALL INPUT (10,24,01,00,'A ',ENTRY,INXCTL,1)
;;;	GOTO (DISPLA),INXCTL
;;;	USING ENTRY(1,1) SELECT
;;;	('B'),	OP_DFL = 1
;;;	('F'),	OP_DFL = 2
;;;	('C'),	OP_DFL = 3
;;;	(),	CLEAR OP_DFL
;;;	ENDUSING
;;;	GOTO (ANYCNG),CNGCTL
WRK,
	XCALL INPUT (12,28,01,00,'YN',ENTRY,INXCTL,1)
	USING INXCTL SELECT
	(1),	OP_WRK = 1
	(),	OP_WRK = 0
	ENDUSING

ANYCNG,
	XCALL OUTPT (24,1,1,'FIELD # TO CHANGE            <TAB> = DELETE',1)
	XCALL INPUT (24,20,02,00,'#T',ENTRY,INXCTL,1)
	IF (INXCTL .EQ. 3)
		BEGIN
		XCALL OUTPT (24,1,1,'DELETE, ARE YOU SURE ?',1)
		XCALL INPUT (24,24,01,01,'YN',ENTRY,INXCTL,1)
		GOTO (ANYCNG),INXCTL-1
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,DELETE,LOKCTL)
		GOTO DISPLA
		END

	WHATNO = ENTRY(1,2)
	IF (WHATNO .EQ. 0)
	THEN 	CLEAR CNGCTL
	ELSE 	CNGCTL = 1

	GOTO (PROCES,CNGBR),CNGCTL+1
CNGBR,
	GOTO (DEPT, SEQ, DESC, WRK),WHATNO
	GOTO ANYCNG

PROCES,
	OP_DFL = 3		;ALWAYS COMPLETE
	CASE SELECT OF
	BEGINCASE
	1:	BEGIN
		TBLCOD = 'OP'
		OP_DEPT = DEPT
		OP_SEQ = SEQ
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,STORE,LOKCTL)
		END
	2:	XCALL ISIO (CHN182,COPTBL,TBL_KEY,WRITE,LOKCTL)
	ENDCASE
	GOTO DISPLA

ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('CP:TBLMNU',1)
	STOP


DSPREC,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; DISPLAY RECORD
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL OUTPT (4,24,1,OP_DEPT,1)
	A3 = OP_SEQ,	'ZZX' [LEFT]
	XCALL OUTPT (6,24,1,A3,1)
	XCALL OUTPT (8,24,1,OP_DESC,1)

	USING OP_DFL SELECT
	(1),	XCALL OUTPT (10,24,1,'B',1)
	(2),	XCALL OUTPT (10,24,1,'F',1)
	(3),	XCALL OUTPT (10,24,1,'C',1)
	(),	XCALL OUTPT (10,24,1,' ',1)
	ENDUSING

	USING OP_WRK SELECT
	(1),	XCALL OUTPT (12,28,1,'Y',1)
	(),	XCALL OUTPT (12,28,1,'N',1)
	ENDUSING


	RETURN
;-----------------------------------



;===================================================================
PRINT_TABLE,
;===================================================================
	LINCNT = 66
	PGCNT  = 0
	
PDISP,
	CNGCTL = 
	XCALL OUTPT (1,1,2,'PRINT JOB TICKETS OPERATIONS TABLE',1)
	XCALL OUTPT (4,4,0,'1. STARTING DEPT',1)
	XCALL OUTPT (6,4,0,'2. ENDING   DEPT',1)
ST_DPT,
	XCALL INPUT (4,27,02,00,'AE',ENTRY,INXCTL,1)
	GOTO (PDISP,PDONE),INXCTL
	ST_DPT = ENTRY(1,2)
	IF (ST_DPT .EQ. BLANKS)
		BEGIN
		EN_DPT = 'ZZ'
		XCALL OUTPT (4,25,1,'ALL',1)
		XCALL OUTPT (6,25,1,' ',1)
		GOTO P_ANY
		END
	GOTO (P_ANY),CNGCTL
EN_DPT,
	XCALL INPUT (6,25,02,00,'A ',ENTRY,INXCTL,1)
	GOTO (PDISP),INXCTL
	EN_DPT = ENTRY(1,2)
	IF (EN_DPT .EQ. BLANKS)
		BEGIN
		EN_DPT = ST_DPT
		XCALL OUTPT (6,25,0,EN_DPT,1)
		END
P_ANY,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (P_PRINT,P_CNGBR),CNGCTL + 1
P_CNGBR,
	GOTO (ST_DPT,EN_DPT),WHATNO
	GOTO P_ANY

P_PRINT,
	CLEAR TBL_KEY
	TBLCOD = 'OP'
	OP_DEPT = ST_DPT
	FIND (CHN182,COPTBL,TBL_KEY)[ERR=PLOOP]

PLOOP,
	XCALL IOS (CHN182,COPTBL,READ,LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF
	IF (TBLCOD .NE. 'OP') GOTO EOF
	IF (OP_DEPT .LT. ST_DPT) GOTO PLOOP
	IF (OP_DEPT .GT. EN_DPT) GOTO EOF


	CLEAR PLINE
	PLINE (2,3) = OP_DEPT	;DD_DEPT
	PLINE (7,9) = OP_SEQ,	'ZZX'
	PLINE (12,41) = OP_DESC

;	USING OP_DFL SELECT
;	(1),	PLINE (44,44) = 'B'
;	(2),	PLINE (44,44) = 'F'
;	(3),	PLINE (44,44) = 'C'
;	ENDUSING

	USING OP_WRK SELECT
	(1),	PLINE (47,47) = 'Y'
	(),	PLINE (50,50) = 'N'
	ENDUSING


;DEPT  SEQ  DESCRIPTION                    WRK-DEPT
; AA    XX  AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA     Y
;123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5
	CALL PRINT
	GOTO PLOOP

EOF,
	IF (PRNTON.EQ.1)  XCALL LPOFF(LPSW,SPLFIL,PGCNT)
	PRNTON  = 0

PDONE,
	GOTO MENU

PRINT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (PRNTON .EQ. 0) CALL PRNTON
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HD1,HD,HD
&		,LG,LG,LG,0,080,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;-------------------------------------------------------------
PRNTON,
	SPLFIL (5,6) = 'EF'
	LPSW = 1		;PRINT,SPOOL, OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTON = 1
	RETURN
;-------------------------------------------------------------
;===================================================================


OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (1,'SU',182,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN182 = 1

	OPNOK = 1
	RETURN
;----------------------------------------------------

CLOSE,
	CLOSE CHN182
	RETURN
;----------------------------------------------------
	

