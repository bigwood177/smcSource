; TPRICE.COP - ENTER PRICING ARRAY FOR T'S
;
;
RECORD TPRICE
	ARRAY	,80D5
RECORD 
	ATYPE	,16A29,	' 1. 26 GA MAIN PRICE         ',
&			' 2. 24 GA MAIN PRICE         ',
&			' 3. 22 GA MAIN PRICE         ',
&			' 4. 20 GA MAIN PRICE         ',
&			' 5. 26 GA 90 DEG SADDLE PRICE',
&			' 6. 24 GA 90 DEG SADDLE PRICE',
&			' 7. 22 GA 90 DEG SADDLE PRICE',
&			' 8. 20 GA 90 DEG SADDLE PRICE',
&			' 9. 26 GA 45 DEG SADDLE PRICE',
&			'10. 24 GA 45 DEG SADDLE PRICE',
&			'11. 22 GA 45 DEG SADDLE PRICE',
&			'12. 20 GA 45 DEG SADDLE PRICE',
&			'13. 26 GA REDUCER PRICE      ',
&			'14. 24 GA REDUCER PRICE      ',
&			'15. 22 GA REDUCER PRICE      ',
&			'16. 20 GA REDUCER PRICE      '
	BTYPE	,9A29,	' 1. GALVANIZED               ',
&			' 2. PVS                      ',
&			' 3. N/A                      ',
&			' 4. SWEDGE SADDLE - GALV     ',
&			' 5. CONICAL SADDLE - GALV    ',
&			' 6. N/A                      ',
&			' 7. SWEDGE SADDLE - PVS      ',
&			' 8. CONICAL SADDLE - PVS     ',
&			' 9. SHOE TAP                 '

RECORD HDR1
		,A40,'SZ"    PRICE        SZ"    PRICE        '
		,A40,'SZ"    PRICE        SZ"    PRICE        '
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2

RECORD
	I	,D2
		,A1,'"'
RECORD	,X
	AI	,A3


RECORD	VARS
	DTYPE	,D2
	MTYPE	,D2
	MST	,D2
	MEND	,D2
	INXCTL	,D2
	ENTRY	,A6
	CNGCTL	,D2
	WHATNO	,D2
	READ	,D1,0
	WRITE	,D1,1
	LOKCTL	,D1
	DLMASK	,A6,'ZZZ.XX'
	V	,D1
	ENTALL	,D1
	LINCNT	,D2
	PGCNT	,D3
	TITLE	,A20,	'PRICING ARRAY REPORT'
	PLINE	,A80
	RPTNUM	,D3
	PRTTYP	,A1
	SPLFIL	,A14
	LPARG	,D1
	LPSW	,D1

PROC
	XCALL TERID (V)
	GOTO ENTMAT
ENDTYP,
	CLOSE 1

ENTMAT,
	XCALL OUTPT (1,1,2,'PRICING MATRIX FOR SADDLES & TEES',1)
	XCALL OUTPT (4,10,0,'ENTER MATERIAL TYPE',1)
	FOR I FROM 1 THRU 9	XCALL OUTPT ( 5+I,15,0,BTYPE(I),1)
	CTL = '04,30,02,00,#E'
	CALL INPUT
	GOTO (ENTMAT,ENDOFF), INXCTL
	MTYPE = ENTRY

	USING MTYPE SELECT

	(1),	OPEN (1,U,'SMC:TPRICE.SMC')
	(2),	OPEN (1,U,'SMC:TVPRIC.SMC')
;;;	(3),	OPEN (1,U,'SMC:SPRICE.SMC')
	(3),	BEGIN
		  XCALL MESAG ('INVALID SELECTION',2)
		  GOTO ENTMAT
		END
	(4),	OPEN (1,U,'SMC:SWPRIC.SMC')
	(5),	OPEN (1,U,'SMC:SCPRIC.SMC')
;;;	(6),	OPEN (1,U,'SMC:SVPRIC.SMC')
	(6),	BEGIN
		  XCALL MESAG ('INVALID SELECTION',2)
		  GOTO ENTMAT
		END
	(7),	OPEN (1,U,'SMC:SWVPRI.SMC')
	(8),	OPEN (1,U,'SMC:SCVPRI.SMC')
	(9),	OPEN (1,U,'SMC:STPRIC.SMC')
	( ),	BEGIN
		  XCALL MESAG ('INVALID SELECTION',2)
		  GOTO ENTMAT
		END
	ENDUSING
	GOTO ENTTYP

CLOSES,
	CLOSE 1
	GOTO ENTMAT
ENDOFF,
	XCALL PGCHN ('CP:SPCFUN',1)
ENTTYP,
	XCALL OUTPT (1,40,1,'\',1)
	XCALL OUTPT (2,1,2,BTYPE(MTYPE),1)
	XCALL OUTPT (4,10,0,'ENTER TYPE OF DATA TO ENTER/EDIT',1)
;;;	IF (MTYPE.LE.2) FOR I FROM 1 THRU 16	XCALL OUTPT (5+I,15,0,ATYPE(I),1)
;;;	IF (MTYPE.GE.3) FOR I FROM 5 THRU 12	XCALL OUTPT (5+I,15,0,ATYPE(I),1)
	
	USING MTYPE SELECT
	(1,2),	FOR I FROM 1 THRU 16	XCALL OUTPT (5+I,15,0,ATYPE(I),1)
	(4,7),	FOR I FROM 5 THRU 12	XCALL OUTPT (5+I,15,0,ATYPE(I),1)
	(5,8,9),FOR I FROM 5 THRU 8	XCALL OUTPT (5+I,15,0,ATYPE(I),1)
	ENDUSING

	XCALL OUTPT (23,15,0,'99. PRINT OUT THIS CATEGORY',1)
	CTL = '04,45,02,00,#E'
	CALL INPUT
	GOTO (ENTTYP,CLOSES), INXCTL
	DTYPE = ENTRY
	IF (DTYPE.EQ.99) GOTO PRTFIL
	IF (DTYPE.LT.1.OR.DTYPE.GT.16) GOTO ENTTYP
;;;	IF (MTYPE.GE.3.AND.(DTYPE.LT.5.OR.DTYPE.GT.12)) GOTO ENTTYP

	USING MTYPE SELECT
	(4,7),	IF(DTYPE.LT.5 .OR. DTYPE.GT.12) GOTO ENTTYP
	(5,8,9),IF(DTYPE.LT.5 .OR. DTYPE.GT.8) GOTO ENTTYP
	ENDUSING

	LOKCTL = 1
	XCALL IO (1,TPRICE,DTYPE,READ,LOKCTL)
	INXCTL =
	CNGCTL =
DISPLA,
	XCALL OUTPT (1,42,2,BTYPE(MTYPE),1)
	XCALL OUTPT (1,50,2,ATYPE(DTYPE),1)
	XCALL OUTPT (3,1,0,HDR1,1)

	FOR I FROM 1 THRU 80
	BEGIN
	  CALL SETCUP
	  XCALL OUTPT (ROW,COL,1,AI,1)
	  XCALL OUTPT (0,0,0,'   ',1)
	  ENTRY(1,6) = ARRAY(I),DLMASK
	  XCALL OUTPT (0,0,0,ENTRY(1,6),1)
	END
	I =
	GOTO ANYCNG
INPLP,
	INCR I
	IF (I.GT.80) GOTO ANYCNG
GETPRC,
	CTL = '00,00,05,00,$X'
	CALL SETCUP
	COL = COL + 6
	CALL INPUT
	GOTO (DISPLA,ANYCNG,CLRALL), INXCTL
	ARRAY (I) = ENTRY
	ENTRY(1,6) = ARRAY(I),DLMASK
	XCALL OUTPT (ROW,COL,0,ENTRY(1,6),1)
	IF (ENTALL) GOTO INPLP
	GOTO ANYCNG
ANYCNG,
	ENTALL =
	XCALL ANYCN (CNGCTL,WHATNO)
	IF (CNGCTL.EQ.1)
	BEGIN
	  IF (WHATNO.GT.80) GOTO ANYCNG
	  I = WHATNO
	  GOTO GETPRC
	END
	LOKCTL = 1
	XCALL IO (1,TPRICE,DTYPE,WRITE,LOKCTL)
	GOTO ENTTYP
CLRALL,
	ENTALL =
	XCALL OUTPT (24,1,1,'CLEAR AND REENTER ALL PRICES IN THIS CATEGORY <N> ? ',1)
	CTL = '24,53,01,00,YN'
	CALL INPUT
	IF (INXCTL.NE.1) GOTO ANYCNG
	FOR I FROM 1 THRU 80
	BEGIN
	  CALL SETCUP
	  ARRAY(I) =
	  ENTRY(1,6) = ARRAY(I),DLMASK
	  XCALL OUTPT (ROW,COL,1,AI,1)
	  XCALL OUTPT (0,0,0,'   ',1)
	  XCALL OUTPT (0,0,0,ENTRY(1,6),1)
	END
	I=
	ENTALL = 1
	XCALL OUTPT (24,1,1,'\',1)
	GOTO INPLP
SETCUP,
	IF (I.GE.1.AND.I.LE.20)
	BEGIN
	  ROW = 4+((I-1)*1)
	  COL = 01
	END
	IF (I.GE.21.AND.I.LE.40)
	BEGIN
	  ROW = 4+((I-20-1)*1)
	  COL = 21
	END
	IF (I.GE.41.AND.I.LE.60)
	BEGIN
	  ROW = 4+((I-40-1)*1)
	  COL = 41
	END
	IF (I.GE.61.AND.I.LE.80)
	BEGIN
	  ROW = 4+((I-60-1)*1)
	  COL = 61
	END
	RETURN
INPUT,
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,1)
	RETURN
PRTFIL,
	LPSW = 1
	SPLFIL (5,6) = 
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO ENDPFI
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	LINCNT = 62
	PGCNT =

	USING MTYPE SELECT
	(1,2),	BEGIN
		MST = 1
		MEND = 16
		END
	(4,7),	BEGIN
		MST = 5
		MEND = 12
		END
	(5,8,9),BEGIN
		MST = 5
		MEND = 8
		END
	ENDUSING

;;;	FOR DTYPE FROM 1 THRU 16
	FOR DTYPE FROM MST THRU MEND
	BEGIN
	  XCALL IO (1,TPRICE,DTYPE,READ,LOKCTL)
	  IF (LINCNT+23.GT.60) LINCNT = 62
	  PLINE (1,30) = BTYPE(MTYPE)
	  CALL PRINT
	  PLINE (1,30) = ATYPE(DTYPE)
	  CALL PRINT
	  FOR I FROM 1 THRU 20
	  BEGIN
	    PLINE ( 1, 3) = AI
	    PLINE ( 7,12) = ARRAY(I), DLMASK
	    I = I + 20
	    PLINE (21,23) = AI
	    PLINE (27,32) = ARRAY(I), DLMASK
	    I = I + 20
	    PLINE (41,43) = AI
	    PLINE (47,52) = ARRAY(I), DLMASK
	    I = I + 20
	    PLINE (61,63) = AI
	    PLINE (67,72) = ARRAY(I), DLMASK
	    I = I - 60
	    CALL PRINT
	  END
	  XCALL LINFD (2)
	  LINCNT = LINCNT + 2
	END
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
ENDPFI,
	GOTO ENTTYP

PRINT,
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,' ',' ',
&		'NO LEG',' ',' ',0,80,80,0,LPSW,RPTNUM,PRTTYP)
	RETURN
END

