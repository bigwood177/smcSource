;  BILLS / COP 
;
;
;	6-25-99 SSQ: Build and sort unique work files for
;			biledt and invoic instead of using
;			"tmpidx"

GLOBAL ORDH,	INIT
RECORD ORDHDR   	
		.INCLUDE 'DEF:RD044A.DEF'
ENDGLOBAL

GLOBAL DUCK	,INIT
		.INCLUDE 'DEF:RD175D.DEF'
ENDGLOBAL

GLOBAL PAR	,INIT
RECORD PARAM
	.INCLUDE 'DEF:PARAM.DEF'
ENDGLOBAL

GLOBAL CPCHAN	,INIT
RECORD	CHANNEL
	CHN001	,D2
	CHN002	,D2
	CHN007	,D2
	CHN041	,D2
	CHN042	,D2
	CHN044	,D2
	CHN045	,D2
	CHN054	,D2
	CHN060	,D2
	CHN169	,D2
	CHN170	,D2
	CHN171	,D2
	CHN172	,D2
	CHN175	,D2
	CHN182	,D2
ENDGLOBAL
;;; ----- END GLOBAL SECTION -----


RECORD	FFILE
	FILNUM	,D2
	REDFIL	,A14
	CLCTL	,D1

RECORD ARACCT		
		.INCLUDE 'DEF:RD007A.DEF'
RECORD	,X		
		.INCLUDE 'DEF:RD007B.DEF'
RECORD COPCTL
		.INCLUDE 'DEF:RD060A.DEF'
RECORD ARTCDE
		.INCLUDE 'DEF:RD169A.DEF'
RECORD ARTCTL,X
		.INCLUDE 'DEF:RD169B.DEF'

RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2

RECORD	WRK_DATE
	MM	,D2
	DD	,D2
	YY	,D2

RECORD
	MDAYS,	12D2,	31,28,31,30,31,30,31,31,30,31,30,31


RECORD	VARS
	cngtax	,d1
	TAXFRT	,D8
	BSEND	,D5
	BSMID	,D5
	SRCCTL	,D1
	KEY	,A7
	SRCHFL	,D1
	DEFACT	,D7
	SYSTEM	,D1
	DCHAR	,D3
	TCHAR	,D3
	SWITCH	,D1
	V	,D1
	D	,D1
	ORDNOA	,A6
	CUSNOA	,A6
	SAVCOM	,A70
	ENTRY	,A10
	INXCTL	,D1
	CNGCTL	,D1
	WHATNO	,D2
	CTR	,D2
	SLSMAX	,D2, 08		;SET FOR MAX # OF COMMISSIONS
				;IN SALESMAN RECORDS
	SELECT	,D1
	COMPCT	,D4
	DECMAL	,D18
	OPTION	,D1
	MASK	,A10,'ZZZ,ZZZ.XX'
	MASK1	,A7,'XXXXXXX'
	MASK2	,A8,'XXXX-XXX'
	MASK3	,A6,'ZZZZZX'
	RUNTOT	,D8
	TAX	,D6
	TAXTOT	,D8
	MSCTAX	,D6
	TOTWGT	,D6
	WHLORD	,D1
	SKPORD	,D1
	NOITMS	,D1
	ENDLIN	,D1
	ALPHA	,A10
	BLANKS	,A35
	CNGCOM	,D1
	COL2	,D2
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	PRGNAM	,A10
	OPENED	,D1
	MANTAX	,A1
	ORGART	,D5
	TODAY	,D6
	D_DAYS	,D2
	D_DATE	,D6
	D_AMT	,D6
	D_LIN1	,A50
	D_LIN2	,A50
	D_LIN3	,A50
	DI	,D2
;;;	TXFLAG	,A3
PROC
	XCALL TERID (V)
	XCALL ENVRN (SYSTEM)
	XCALL RDATE(TODAY)

	XCALL OUTPT (1,1,1,'BILLING',V)
	XCALL OUTPT (2,1,1,'\',1)
	CALL OPENS
DISPLA,
	XCALL BLMNU (V)
	CTL = '08,47,01,01,#E'
	CALL INPUT
	GO TO (END), INXCTL - 1
	SELECT = ENTRY(1,1)
	GO TO (SELCT,UNSEL,LIST,INVOIC), SELECT
	GO TO DISPLA
END,
	CALL CLOSES
END1,
	XCALL PGCHN ('CP:CPMENU',1)
SELCT,
	XCALL OUTPT (1,11,1,'\',V)
	XCALL FILES (5,'SU',45,5)		;FILE # 45 - ORDLIN FILE
;;;	XCALL ORDBL (ORDHDR,SAVCOM,WHLORD,SKPORD,DSTFLG,MLARFG,INXCTL,D,V)
	XCALL ORDBL (ORDHDR,SAVCOM,WHLORD,SKPORD,DSTFLG,MLARFG,INXCTL,
&		RUNTOT, TAXTOT, TOTWGT, OCOST)

	CLOSE 5
	IF (INXCTL.EQ.2) GO TO DISPLA
	IF (SKPORD) GO TO SELCT
	ORDNOA = OORDNO, MASK3
	CUSNOA = OCUSNO, MASK3
	NOITMS = 1

	XCALL FILES (5,'SU',45,5)		;FILE # 45 - ORDLIN FILE
;;;	XCALL LINBL(OORDNO,ODISC,NOITMS,RUNTOT,TAXTOT,TOTWGT,OCOST,WHLORD,D,V)
	XCALL LNBIL(RUNTOT,TAXTOT,TOTWGT,OCOST)
	CLOSE 5

	XCALL FILES (5,'SU',175,5)		;FILE # 175 - DUCACC FILE
	XCALL DUCBL(OORDNO,ODISC,RUNTOT,TAXTOT,WHLORD,OTAXFL)
	CLOSE 5

MSC,
	IF (DSTFLG.NE.'Y') GO TO MSC1
	LOKCTL = 1
	XCALL IO (7,ARACCT,1,READ,LOKCTL)
	BSEND = ORG007
MSC1,
	CALL ORDMSC
	GO TO SELCT
UNSEL,
	CALL CLOSES
	XCALL PGCHN ('CP:UNBILL',1)
LIST,
	IF (OPENED.EQ.1) CALL CLOSES

;-----------------------------------------------------
;;;	SWITCH = 5			;;;
;;;	XCALL SNMSG ('CP:BILEDT',SWITCH);;;
;;;	XCALL PGCHN ('CP:BILIDX',1)	;;;
	CALL BIL_IDX
	XCALL PGCHN ('CP:BILEDT',1)
;-----------------------------------------------------

INVOIC,
	IF (OPENED.EQ.1) CALL CLOSES
	SWITCH = 1
	XCALL FILES (10,'I',41,SWITCH)
	IF (SWITCH.EQ.9) GO TO DISPLA
	CLOSE 10
	SWITCH = 6				;;;WAS 3 NOW 6
	XCALL FILES (4,'SI',44,SWITCH)
	IF (SWITCH.EQ.9) XCALL FILES (10,'I',41,4)
	IF (SWITCH.EQ.9) GO TO DISPLA
	SWITCH = 6				;;;WAS 3 NOW 6
	XCALL FILES (5,'SI',45,SWITCH)
	IF (SWITCH.NE.9) GO TO OPEN2
	CALL CLOSE1
	GO TO DISPLA
OPEN2,
	SWITCH = 1
	XCALL FILES (2,'I',02,SWITCH)
	IF (SWITCH.NE.9) GO TO OPEN3
	CALL CLOSE2
	GO TO DISPLA
OPEN3,
	SWITCH = 1
	XCALL FILES (3,'I',60,SWITCH)
	IF (SWITCH.NE.9) GO TO OPEN4
	CALL CLOSE3
	GO TO DISPLA
OPEN4,
	SWITCH = 6
	XCALL FILES(8,'I',172,SWITCH)
	IF (SWITCH.NE.9) GO TO OPEN5
	XCALL FILES (3,'I',60,4)
	CALL CLOSE3
	GO TO DISPLA
OPEN5,
	SWITCH = 6
	XCALL FILES(9,'I',170,SWITCH)
	IF (SWITCH.NE.9) GO TO OPEN6
	XCALL FILES (3,'I',60,4)
	XCALL FILES (8,'I',172,4)
	CALL CLOSE3
	GO TO DISPLA
OPEN6,
	SWITCH = 6
	XCALL FILES (9,'SI',175,SWITCH)
	IF (SWITCH.NE.9) GOTO SNDMSG
	XCALL FILES (3,'I',60,4)
	XCALL FILES (8,'I',172,4)
	XCALL FILES (9,'I',170,4)
	CALL CLOSE3
	GOTO DISPLA
SNDMSG,
	CLOSE 2
	CLOSE 3

;-----------------------------------------------------
;;;	SWITCH = 5			;;;
;;;	XCALL SNMSG ('CP:ALNINV',SWITCH);;;
;;;	XCALL PGCHN ('CP:BILIDX',1)	;;;
	CALL INV_IDX
	XCALL PGCHN ('CP:ALNINV',1)
;-----------------------------------------------------
;
OPENS,
	OPENED = 1
	SWITCH = 1
	XCALL FILES (9,'I',041,SWITCH)		;FILE # 41 - ITMMAS FILE
	IF (SWITCH.EQ.9) GO TO END1
	XCALL FILES (10,'I',042,SWITCH)
	IF (SWITCH.EQ.9) CALL INU042
	IF (SWITCH.EQ.9) GO TO END1	
	SWITCH = 1
	XCALL FILES (4,'SU',44,SWITCH)		;FILE # 44 - ORDHDR FILE
	IF (SWITCH.EQ.9) CALL INU041
	IF (SWITCH.EQ.9) GO TO END1
	SWITCH = 6
	XCALL FILES (5,'SU',45,SWITCH)		;FILE # 45 - ORDLIN FILE
	IF (SWITCH.EQ.9) GOTO END2
;;;	SWITCH = 1
;;;	XCALL FILES (8,'I',54,SWITCH)		;FILE # 54 - SALMAN FILE
;;;	IF (SWITCH.EQ.9) GO TO END3
	SWITCH = 1
	XCALL FILES (3,'I',60,SWITCH)		;FILE # 60 - COPCTL FILE
	IF (SWITCH.EQ.9) GO TO END4
	LOKCTL = 1
	XCALL IOS (3,COPCTL,READ,LOKCTL)
	CLOSE 3
	SAVCOM (1,35) = ORDCOM (1)
	SAVCOM (36,70) = ORDCOM (2)
	SWITCH = 1
	XCALL FILES (7,'I',07,SWITCH)		;FILE # 07 - ARACCT FILE
	IF (SWITCH.EQ.9) GO TO END5
	SWITCH = 6
	XCALL FILES (1,'I',169,SWITCH)		;FILE # 169 -- ARTCDE
	IF (SWITCH.EQ.9) GO TO END6
	SWITCH = 6
	XCALL FILES (1,'I',170,SWITCH)		;FILE # 170 -- ARTERM
	IF (SWITCH.EQ.9) GO TO END7
	SWITCH = 6
	XCALL FILES (1,'I',171,SWITCH)		;FILE # 171 -- SHIPTO
	IF (SWITCH.EQ.9) GO TO END8
	SWITCH = 6
	XCALL FILES (1,'I',172,SWITCH)		;FILE # 172 -- SHPVIA
	IF (SWITCH.EQ.9) GO TO END9
	SWITCH = 6
	XCALL FILES (1,'I',002,SWITCH)		;FILE # 002 -- CUSIDX
	IF (SWITCH.EQ.9) GO TO END10
	SWITCH = 6
	XCALL FILES (1,'I',001,SWITCH)		;FILE # 001 -- CUSMAS
	IF (SWITCH.EQ.9) GO TO END11
	SWITCH = 6
	XCALL FILES (5,'SU',175,SWITCH)		;FILE # 175 -- DUCACC
	IF (SWITCH.EQ.9) GOTO END12
	RETURN
END12,
	CALL CLOS11
END11,
	CALL CLOS10
	GO TO END1
END10,
	CALL CLOSE9
	GO TO END1
END9,
	CALL CLOSE8
	GO TO END1
END8,
	CALL CLOSE7
	GO TO END1
END7,
	CALL CLOSE6
	GO TO END1
END6,
	CALL CLOSE5
	GO TO END1
END5,
	CALL CLOSE4
	GO TO END1
END4,
	CALL CLOSE3
	GO TO END1
END3,
	CALL CLOSE2
	GO TO END1
END2,
	CALL CLOSE1
	GO TO END1
CLOSES,
	OPENED =
	XCALL WATE (3,V)
CLOS12,
	XCALL FILES (1,'I',175,4)
CLOS11,
	XCALL FILES (1,'I',001,4)
CLOS10,
	XCALL FILES (1,'I',002,4)
CLOSE9,
	XCALL FILES (1,'I',172,4)
CLOSE8,
	XCALL FILES (1,'I',171,4)
CLOSE7,
	XCALL FILES (1,'I',170,4)
CLOSE6,
	XCALL FILES (1,'I',169,4)
CLOSE5,
	XCALL FILES (7,'I',7,4)
CLOSE4,
	XCALL FILES (3,'I',60,4)
CLOSE3,
;;;	XCALL FILES (8,'I',54,4)
CLOSE2,
	XCALL FILES (5,'U',45,4)
CLOSE1,
	XCALL FILES (4,'U',44,4)
INU041,
	XCALL FILES (10,'I',042,4)
INU042,
	XCALL FILES (9,'I',041,4)
	RETURN
INPUT,
	XCALL INPUT(ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,1)
	RETURN
INPUT2,
	XCALL INPUT(ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,1)
	RETURN
;
ORDMSC,
	OFLAG = 1
	XCALL OE3B (ORDNOA,CUSNOA,OCUSNM,MLMSFG,MLSTFG,MLFRFG,V)

;------------------------------------------------
; add this code to oe3b later...
	XCALL OUTPT (21,7,0,'6. DISC TEXT',1)
	XCALL OUTPT (22,7,0,'7. DISC AMT',1)
;------------------------------------------------

	XCALL OUTPT (18,40,0,OTAXFL,1)
;;;	XCALL OUTPT (19,40,0,TXFLAG,1)
	CTL = '04,25,08'
	DECMAL = RUNTOT
	CALL DSPDLR
	ALPHA = TOTWGT,'Z,ZZZ.XX'
	XCALL OUTPT (2,57,0,ALPHA,V)
	ROW = 6
	DECMAL = TAXTOT
	CALL DSPDLR
DISPL3,
	WHATNO =
	CNGCTL =
;;;ssq 7-28-99	OFRGHT =
	OMISC =
	OTAX =
	MANTAX = 
	OMSACT = DEFMSA
	OTXACT(1) = DEFSTA
	OFRACT = DEFRET

;;; SSQ 01-03-00 moved here from below...
	IF (OSCAC .EQ. '2')		;SSQ 9-30-98
	THEN	OFRACT = 6043000
	ELSE	OFRACT = DEFRET
;;; SSQ 01-03-00 moved here from below...

	CTL = '08,22,06,00,$-'
	IF (OFRGHT .NE. 0)
	   BEGIN
	   ENTRY(1,8) = OFRGHT,	'Z,ZZZ.XX-' [LEFT]
	   XCALL OUTPT (ROW, COL, 1, ENTRY(1,8), 1)
	   CALL ACCEPT
	   IF (TCHAR.NE.13) 
		BEGIN
		CALL INPUT
		GO TO (ORDMSC), INXCTL
		OFRGHT = ENTRY
	;;;	IF (OSCAC .EQ. '2')		;SSQ 9-30-98
	;;;	THEN	OFRACT = 6043000
	;;;	ELSE	OFRACT = DEFRET
		END
	   GOTO FACCT
	   END
FRGHT,
	CTL = '08,22,06,00,$-'
	CALL INPUT
	GO TO (ORDMSC), INXCTL
	OFRGHT = ENTRY
FACCT,
	DEFACT = OFRACT
;;;	IF (MLFRFG.NE.'Y'.OR.OFRGHT.EQ.0) GO TO FRGHT1
	IF (OFRGHT.EQ.0) GO TO FRGHT1
	CALL GETACT
	GO TO (ORDMSC), INXCTL
FRGHT1,
	OFRACT = DEFACT
	GO TO (ORDTOT), CNGCTL
MISC,
	CTL = '10,22,06,00,$ '
	CALL INPUT
	GOTO (ORDMSC), INXCTL
	OMISC = ENTRY
	IF (OMISC.NE.0)
	BEGIN
	  XCALL MESAG ('SORRY CHARLIE, NO MISC CHARGES ALLOWED HERE',1)
	  GOTO MISC
	END
	DEFACT = OMSACT
	IF (MLMSFG.NE.'Y'.OR.OMISC.EQ.0) GO TO MISC1
	CALL GETACT
	GO TO (ORDMSC), INXCTL
MISC1,
	OMSACT = DEFACT
	MSCTAX =
;;;	IF (RUNTOT.EQ.0) GOTO NOMTAX
;;;	MSCTAX = ((TAXTOT*OMISC*10)/RUNTOT) #1
;;;NOMTAX,
	CTL = '12,26,06'
	DECMAL = MSCTAX
	CALL DSPDLR
	CTL = '14,22,07,00,$ '
	IF (CNGCTL.EQ.1) GOTO ORDTOT
TAX,
	SWITCH = 5
	CLOSE 1			;SSQ 9-12-97
	XCALL FILES (1,'I',169,SWITCH)
	OTXACT(1) = DEFSTA
	MANTAX = 'N'
	ARTCDE =
FNDTCD,
	LOKCTL = 1 
	XCALL IO (1,ARTCTL,1,READ,LOKCTL)
	ORGART = ORG169
	CTL = '14,22,07,00,$-'
	IF (OTAXFL.EQ.'   '.OR.SWITCH.EQ.9) GOTO SKP1 ;CUSTOMER HAS NO TAX RATE
	KEY(1,3) = OTAXFL
	SRCCTL = 2
	XCALL SERCH (1,ARTCDE,KEY(1,3),1,3,ORGART,BSMID,SRCCTL,4,4,9,0,0,0,0)
	IF (SRCCTL.EQ.0) 
		BEGIN
		XCALL OUTPT (18,45,0,ARTDSC,1)	;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;SSQ 2-20-98 WISC FREIGHT IS TAXABLE...
		IF (OTAXFL(1,1) .EQ. 'W')
		THEN	TAXFRT = OFRGHT
		ELSE	TAXFRT =
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		END
SKP1,
	IF (SRCCTL.OR.OTAXFL.EQ.'   ')
	BEGIN
	  XCALL OUTPT (24,1,1,'INVALID TAX CODE OR TAX CODE NOT FOUND - REENTER <N> ? ',1)
	  XCALL INPUT (24,56,01,00,'YN',ENTRY,INXCTL,1)
	  IF (INXCTL.EQ.1) 
	  BEGIN
	    XCALL INPUT (18,40,03,00,'AT',ENTRY,INXCTL,1)
	    OTAXFL = ENTRY
	    GOTO FNDTCD
	  END
	  MANTAX = 'Y'
	  ARTCDE =
	END
;;;
ASK,
	CLOSE 1
	CTL = '14,22'
	OTAX(1) = ((MSCTAX + TAXTOT) * ARTPRT(1) / 100) # 3
	XCALL DSPLY (MAX,14,COL,OTAX(1),3,1)	;
	OTXACT(1) = ARTGLN(1)
	DEFACT = OTXACT(1)
	SRCHFL = 1
	CALL SERCH
	CALL DSPACT

	CTL = '15,22'
	OTAX(2) = ((MSCTAX + TAXTOT) * ARTPRT(2) / 100) # 3
	XCALL DSPLY (MAX,15,COL,OTAX(2),3,1)	;
	OTXACT(2) = ARTGLN(2)
	DEFACT = OTXACT(2)
	SRCHFL = 1
	CALL SERCH
	CALL DSPACT

	CTL = '16,22'
	OTAX(3) = ((MSCTAX + TAXTOT) * ARTPRT(3) / 100) # 3
	XCALL DSPLY (MAX,ROW,COL,OTAX(3),3,1)	;
	OTXACT(3) = ARTGLN(3)
	DEFACT = OTXACT(3)
	SRCHFL = 1
	CALL SERCH
	CALL DSPACT

	TAX = OTAX(1) + OTAX(2) + OTAX(3)
	XCALL DSPLY (MAX,18,COL,TAX,3,1)	;Display default tax amount
;;;	GOTO ORDTOT

	cngtax = 0
OTAX1,
	CTL = '14,22,07,00,$-'
	CALL ACCEP2
	IF (TCHAR.EQ.13.AND.CNGCTL.EQ.0) GO TO OTAX2
	IF (TCHAR.EQ.13) GO TO OTXAC1
	IF (TCHAR.NE.9) GOTO OTAX1
	CALL INPUT2
	GOTO (DISPLA), INXCTL
	OTAX(1) = ENTRY
	TAX = OTAX(1) + OTAX(2) + OTAX(3)
	XCALL DSPLY (MAX,18,COL,TAX,3,1)	;Display default tax amount
	cngtax = 1	;ssq 6-5-98
OTXAC1,
	COL = 40
	CALL ACCEP2
	IF (TCHAR.EQ.13) GOTO (OTAX2,ORDTOT), CNGCTL+1
	IF (TCHAR.NE.9) GOTO OTXAC1
	CALL GETAC1
	GOTO (OTAX1), INXCTL
	OTXACT(1) = DEFACT
	GOTO (ANYCN3), CNGCTL
OTAX2,
	CTL = '15,22,07,00,$-'
	CALL ACCEP2
	IF (TCHAR.EQ.13.AND.CNGCTL.EQ.0) GO TO OTAX3
	IF (TCHAR.EQ.13) GO TO OTXAC2
	IF (TCHAR.NE.9) GOTO OTAX2
	CALL INPUT2
	GOTO (DISPLA), INXCTL
	OTAX(2) = ENTRY
	TAX = OTAX(1) + OTAX(2) + OTAX(3)
	XCALL DSPLY (MAX,18,COL,TAX,3,1)	;Display default tax amount
	cngtax = 1	;ssq 6-5-98
OTXAC2,
	COL = 40
	CALL ACCEP2
	IF (TCHAR.EQ.13) GO TO (OTAX3,ORDTOT), CNGCTL+1
	IF (TCHAR.NE.9) GOTO OTXAC2
	CALL GETAC1
	GOTO (OTAX2), INXCTL
	OTXACT(2) = DEFACT
	GOTO (ANYCN3), CNGCTL
OTAX3,
	CTL = '16,22,07,00,$-'
	CALL ACCEP2
	IF (TCHAR.EQ.13.AND.CNGCTL.EQ.0) GO TO ORDTOT
	IF (TCHAR.EQ.13) GO TO OTXAC3
	IF (TCHAR.NE.9) GOTO OTAX3
	CALL INPUT2
	GOTO (DISPLA), INXCTL
	OTAX(3) = ENTRY
	TAX = OTAX(1) + OTAX(2) + OTAX(3)
	XCALL DSPLY (MAX,18,COL,TAX,3,1)	;Display default tax amount
	cngtax = 1	;ssq 6-5-98
OTXAC3,
	COL = 40
	CALL ACCEP2
	IF (TCHAR.EQ.13) GO TO (ORDTOT,ORDTOT), CNGCTL+1
	IF (TCHAR.NE.9) GOTO OTXAC3
	CALL GETAC1
	GOTO (OTAX3), INXCTL
	OTXACT(3) = DEFACT
	GOTO (ANYCN3), CNGCTL
	GOTO ORDTOT

;;;OK,
;;;TAX0,
;;;	IF (OTAX(1).EQ.0) GOTO TAX1
;;;	DEFACT = OTXACT(1)
;;;	IF (MLSTFG.NE.'Y'.OR.TAX.EQ.0) GO TO TAX1
;;;
;;;	CALL GETACT
;;;	MANTAX = 
;;;	GO TO (ORDMSC), INXCTL
;;;TAX1,
;;;	OTXACT(1) = DEFACT

ORDTOT,
;;;	IF (.NOT.CNGCTL .and. .not. cngtax) CALL COMP_TAX
	IF (.not. cngtax) CALL COMP_TAX	;SSQ 4-28-99

	CTL = '20,22,10'
	DECMAL = RUNTOT + OFRGHT + OMISC + TAX
	CALL DSPDLR
	OSALE = RUNTOT
	GO TO (ANYCN3), CNGCTL

	DSCODE = 1
	DSAMT = 0
	CALL DISPLA_DISC
	GOTO ANYCN3
DSCODE,
	XCALL INPUT (21,22,01,00,'# ',ENTRY,INXCTL,1)
	GOTO (ORDMSC),INXCTL
	IF (ENTRY(1,1) .EQ. ' ') ENTRY(1,1) = '1'
	DSCODE = ENTRY(1,1)
	IF (DSCODE .GT. 5) GOTO DSCODE
	CALL DISPLA_DISC
	GO TO (ANYCN3), CNGCTL
DSAMT,
	XCALL INPUT (22,22,06,00,'$ ',ENTRY,INXCTL,1)
	DSAMT = ENTRY(1,6)
	CALL DISPLA_DISC
	GO TO ANYCN3

DISPLA_DISC,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR D_LIN1, D_LIN2, D_LIN3

	USING DSCODE SELECT
	(0),	BEGIN
		D_LIN1 = OCOMNT(1)
		D_LIN2 = OCOMNT(2)
		END
	(1),	BEGIN
		D_DAYS = 15
		CALL MAKE_D_DATE
		IF (DSAMT.EQ.0)
		THEN	D_AMT = (2*OSALE)#2
		ELSE	D_AMT = DSAMT

		D_LIN1 = "A discount of $"
		D_LIN1(16,23) = D_AMT, 'ZZZX.XX' [LEFT]
		DI = %TRIM(D_LIN1)
		D_LIN1(DI+1, 50) = " may be taken only if"
		
		D_LIN2 = "payment is received and in our office by"
		D_LIN2(42,49) = D_DATE, 'ZX/XX/XX' [LEFT]
		END
	(2),	BEGIN
		D_DAYS = 30
		CALL MAKE_D_DATE
		D_LIN1 = "Payment must be received in our office by"
		D_LIN1(43,50) = D_DATE, 'ZX/XX/XX' [LEFT]

		D_LIN2 = "for discounted delivered prices to be valid."
		END
	(3),	BEGIN
		D_DAYS = 30
		CALL MAKE_D_DATE
		D_LIN1 = "Payment must be received in our office by"
		D_LIN1(43,50) = D_DATE, 'ZX/XX/XX' [LEFT]

		D_LIN2 = "for discount to apply."
		END
	(4),	BEGIN
		D_DAYS = 30
		CALL MAKE_D_DATE
		D_LIN1 = "A discount of $"
		D_LIN1(16,23) = DSAMT, 'ZZZX.XX' [LEFT]
		DI = %TRIM(D_LIN1)
		D_LIN1(DI+1, 50) = " and the freight charges"

		D_LIN2 = "may be deducted only if this invoice"
		
		D_LIN3 = "is paid and in our office by"
		D_LIN3(30,37) = D_DATE, 'ZX/XX/XX' [LEFT]
		END
		
	(5),	BEGIN
		D_DAYS = 30
		CALL MAKE_D_DATE
		IF (DSAMT.EQ.0)
		THEN	D_AMT = (2*OSALE)#2
		ELSE	D_AMT = DSAMT

		D_LIN1 = "A discount of $"
		D_LIN1(16,23) = D_AMT, 'ZZZX.XX' [LEFT]
		DI = %TRIM(D_LIN1)
		D_LIN1(DI+1, 50) = " may be taken only if"
		
		D_LIN2 = "payment is received and in our office by"
		D_LIN2(42,49) = D_DATE, 'ZX/XX/XX' [LEFT]
		END
	ENDUSING

		XCALL OUTPT (21,28,1,D_LIN1(1,%TRIM(D_LIN1)),1)
		XCALL OUTPT (22,28,1,D_LIN2(1,%TRIM(D_LIN2)),1)
		XCALL OUTPT (23,28,1,D_LIN3(1,%TRIM(D_LIN3)),1)
	RETURN
;-------------------------------------------------------
MAKE_D_DATE,
	WRK_DATE = TODAY
	DD = DD + D_DAYS
	IF (DD .GT. MDAYS(MM))
	   BEGIN
	   DD = DD - MDAYS(MM)
	   MM = MM + 1
	   IF (MM .GT. 12)
	      BEGIN
	      YY = YY + 1
	      MM = 1
	      END
	   END
	D_DATE = WRK_DATE
	RETURN
;-------------------------------------------------------

GETACT,
	COL = 40
	SRCHFL = 1
	CALL SERCH
;;;	IF (MANTAX.EQ.'N') RETURN  ;NO OVERRIDE OF ACCT # IF TAX FROM TAX FILE
	SRCHFL =
	COL = 39
	CALL ACCEPT
	IF (TCHAR.EQ.13) RETURN
GETAC1,
	COL = 40
	XCALL OUTPT (ROW,40,1,'\',1)
	CALL ENTACT
	RETURN
ENTACT,
	XCALL OUTPT (ROW,COL,1,'\',1)
	CTL (7,14) = '04,03,# '
	CALL INPUT
	IF (INXCTL.EQ.1) RETURN
	DEFACT (1,4) = ENTRY (1,4)
	COL2 = COL+4
	XCALL OUTPT (ROW,COL2,0,'-',1)
	CTL (7,11) = '03,00'
	COL = COL + 5
	CALL INPUT
	COL = COL - 5
	IF (INXCTL.EQ.1) RETURN
	DEFACT (5,7) = ENTRY (1,3)
	CALL SERCH
	IF (SRCCTL.EQ.0) RETURN
	GO TO ENTACT
ACCEPT,
	XCALL FLAGS (00010000,1)		;DISABLE CHARACTER ECHOING
	XCALL OUTPT (ROW,COL,0,'\',1)
	ACCEPT (15,TCHAR)
	IF (SYSTEM.NE.1.AND.TCHAR.EQ.13) ACCEPT (15,DCHAR)
	XCALL FLAGS (00010000,0)		;RE-ENABLE CHARACTER ECHOING
	RETURN
ACCEP2,
	XCALL FLAGS (00010000,1)		;DISABLE CHARACTER ECHOING
	XCALL OUTPT (ROW,COL,0,'\',1)
	ACCEPT (15,TCHAR)
	IF (SYSTEM.NE.1.AND.TCHAR.EQ.13) ACCEPT (15,DCHAR)
	XCALL FLAGS (00010000,0)		;RE-ENABLE CHARACTER ECHOING
	RETURN
CLR45,
	XCALL OUTPT (9,22,1,'\',V)
	XCALL OUTPT (10,22,1,'\',V)
	RETURN
CNGBR3,
	GO TO (FRGHT,MISC,OTAX1,OTAX2,OTAX3,DSCODE,DSAMT), WHATNO
	XCALL MESAG (' ',6)
	GOTO ANYCN3
PRCES3,
	LOKCTL = 1
	XCALL ISIO (4,ORDHDR,OORDNO,WRITE,LOKCTL)
	RETURN
SERCH,
	KEY = DEFACT, MASK1
	ALPHA (1,8) = DEFACT, MASK2
	XCALL SERCH (7,ARACCT,KEY,1,7,BSEND,BSMID,SRCCTL,4,32,37,0,0,0,0)
	IF (SRCCTL.EQ.1) CALL BADACT
	IF (SRCCTL.EQ.0) CALL DSPACT
	RETURN
ANYCN3,
	XCALL ANYCN (CNGCTL,WHATNO)
	GOTO (PRCES3,CNGBR3,ORDMSC), CNGCTL+1
BADACT,
	IF (SRCHFL.EQ.1) ARACDS = 'ACCOUNT NOT ON FILE'
	IF (SRCHFL.EQ.0) XCALL MESAG('ACCOUNT NOT ON FILE',1)
	IF (SRCHFL.EQ.1) SRCCTL = 0
	RETURN
DSPACT,
	XCALL OUTPT (ROW,40,0,ALPHA(1,8),1)
	XCALL OUTPT (ROW,50,0,ARACDS,1)
	RETURN
DSPNUM,
	OPTION = 1
	GOTO CALDSP
DSPDTE,
	OPTION = 2
	GOTO CALDSP
DSPDLR,
	OPTION = 3
CALDSP,
	XCALL DSPLY(MAX,ROW,COL,DECMAL,OPTION,1)
	RETURN

COMP_TAX,
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;SSQ 2-20-98 WISC FREIGHT IS TAXABLE...
;;; SSQ 4-28-99 NE FREIGHT IS TAXABLE...
	USING OTAXFL(1,1) SELECT
	('W'),	TAXFRT = OFRGHT			;WISC
	('N'),	TAXFRT = OFRGHT			;NEB
	(),	TAXFRT =
	ENDUSING
;;;		IF (OTAXFL(1,1) .EQ. 'W')
;;;		THEN	TAXFRT = OFRGHT
;;;		ELSE	TAXFRT =
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CTL = '14,22'
	OTAX(1) = ((TAXFRT + MSCTAX + TAXTOT) * ARTPRT(1) / 100) # 3
	XCALL DSPLY (MAX,14,COL,OTAX(1),3,1)	;

	CTL = '15,22'
	OTAX(2) = ((TAXFRT + MSCTAX + TAXTOT) * ARTPRT(2) / 100) # 3
	XCALL DSPLY (MAX,15,COL,OTAX(2),3,1)	;

	CTL = '16,22'
	OTAX(3) = ((TAXFRT + MSCTAX + TAXTOT) * ARTPRT(3) / 100) # 3
	XCALL DSPLY (MAX,16,COL,OTAX(3),3,1)	;

	TAX = OTAX(1) + OTAX(2) + OTAX(3)
	XCALL DSPLY (MAX,18,COL,TAX,3,1)	;Display default tax amount
	RETURN
;-----------------------------------------------------
BIL_IDX,
	XCALL FFILE (44, REDFIL, CLCTL)		;OPEN ON SAME DEVICE AS ORDHDR
	IF (CLCTL) RETURN
	REDFIL(5,14) = 'BILEDT.WRK'
	GOTO MAKIDX

INV_IDX,
	XCALL FFILE (44, REDFIL, CLCTL)		;OPEN ON SAME DEVICE AS ORDHDR
	IF (CLCTL) RETURN
	REDFIL(5,14) = 'INVOIC.WRK'
	GOTO MAKIDX
MAKIDX,
	OPEN (8, O, REDFIL)

	SWITCH = 5
	XCALL FILES (4,'SI',44,SWITCH)

	ORDHDR =
	LOKCTL = 1
	XCALL IOS (8,ORDHDR,WRITE,LOKCTL)
RDLOOP,
	LOKCTL = 1
	XCALL IOS (4,ORDHDR,READ,LOKCTL)
	IF (LOKCTL.NE.0) GOTO DONE_IDX

	IF (OCUSNM(1,9).EQ.']]]CANCEL')GOTO RDLOOP
;;;	IF (OFLAG.NE.1) GOTO RDLOOP
	IF (OFLAG.EQ.0) GOTO RDLOOP
	LOKCTL = 1
	XCALL IOS (8,ORDHDR,WRITE,LOKCTL)
	GOTO RDLOOP
DONE_IDX,
	ORDHDR = ']]]]]]]]]]]]]]]]]]]]]]]]]'
	LOKCTL = 1
	XCALL IOS (8,ORDHDR,WRITE,LOKCTL)
	CLOSE 8
	XCALL FILES (4,'I',44,4)

	SORT (IN=REDFIL,
&		RECORD=ORDHDR,
&		KEY = (OCUSNO,OORDNO)
&		)
	RETURN
;-----------------------------------------------

END
