;DUCPRT.DBL


RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'

RECORD	PARAMS
	.INCLUDE 'DEF:PARAM.DEF'

RECORD	DPKEY
	K_CODE	,A2
	K_GAUGE	,D3
	K_TYPE	,D1
	K_CAT	,D1
	K_STYLE	,D1

RECORD	HD1
	,A*,	'  TYPE             GAUGE  STYLE              '
	,A*,	'CAT                     PRICE'

RECORD	PRINT
	TITLE	,A*,	'DUCT PRICE TABLE'
	HD	,A6,	'NO HDR'
	LG	,A9,	'NO LEGEND'
	PLINE	,A80
	PRNTON	,D1
	LINCNT	,D2,60
	PGCNT	,D6
	LPSW	,D2
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	PRTCTL	,D3,080
	LPARG	,D1
	PRNTSW	,D1
	PRTCTR	,D1

RECORD	VARS
	FILNAM	,A14
	PDESC	,A15
	ENTRY	,A30
	INXCTL	,D1
	WHATNO	,D2
	CNGCTL	,D1
	STG	,D3
	ENG	,D3
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'PRINT DUCT PRICE TABLE',1)
	SWITCH = 5
	XCALL FILES (1,'SI',182,SWITCH)
	IF (SWITCH .EQ. 9) XCALL PGCHN ('CP:TBLMNU',1)

	CLEAR PRNTON
DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,'PRINT DUCT PRICE TABLE',1)
	XCALL OUTPT (4,4,0,'1. START GAUGE:',1)
	XCALL OUTPT (6,4,0,'2. END   GAUGE:',1)
STG,
	XCALL INPUT (4,21,03,00,'#E',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	STG = ENTRY(1,3)
	IF (STG .EQ. 0)
		BEGIN
		ENG = 999
		XCALL OUTPT (4,21,1,'ALL',1)
		XCALL OUTPT (6,21,1,' ',1)
		GOTO ANYCNG
		END
	GOTO (ANYCNG),CNGCTL
ENG,
	XCALL INPUT (6,21,03,00,'#E',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	ENG = ENTRY(1,3)
	IF (ENG .EQ. 0)
		BEGIN
		ENG = STG
		ENTRY(1,3) = ENG,	'ZZX' [LEFT]
		XCALL OUTPT (6,21,1,ENTRY(1,3),1)
		END
ANYCNG,
	XCALL ANYCN (CNGCTL,WHATNO)
	GOTO (PROCES,CNGBR),CNGCTL+1
CNGBR,
	GOTO (STG,ENG),WHATNO
	GOTO ANYCNG
PROCES,
	XCALL FFILE(182,FILNAM,INXCTL)
	FILNAM(12,14) = 'SRT'
	OPEN(10,O,FILNAM)

	CLEAR DPKEY
	K_CODE = 'DP'
;;;	K_GAUGE = STG

	FIND (1,COPTBL,DPKEY) [ERR=LOOP]
LOOP,		
	XCALL IOS (1,COPTBL,READ,LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF
	IF (TBLCOD .NE. 'DP') GOTO EOF
	IF (DP_GAUGE.LT.STG .OR. DP_GAUGE.GT.ENG) GOTO LOOP
	WRITES (10,COPTBL)
	GOTO LOOP
EOF,
	CLOSE 10
	SORT (IN=FILNAM, RECORD=COPTBL, KEY=(DP_TYPE,DP_GAUGE,DP_STYLE,DP_CAT) )
	OPEN (10,I,FILNAM)

PLOOP,
	READS (10,COPTBL,P_EOF)

	PLINE (3,3) = DP_TYPE,	'X'
	IF (DP_TYPE .LE. 7)
	THEN	BEGIN
		PDESC = DTYPE(DP_TYPE)
		PLINE (5,19) = PDESC(3,15)
		END
	ELSE	PLINE (5,19) = '*OUT OF RANGE*'

	PLINE (22,24) = DP_GAUGE,	'ZXX'

	PLINE (27,27) = DP_STYLE,	'X'
	IF (DP_STYLE .LE. 7)
	THEN	BEGIN
		PDESC = DSTY(DP_STYLE)
		PLINE (29,43) = PDESC(3,15)
		END
	ELSE	PLINE (29,43) = '*OUT OF RANGE*'

	PLINE (46,46) = DP_CAT,	'X'
	IF (DP_CAT .LE. 5)
	THEN	BEGIN
		PDESC = DCAT(DP_CAT)
		PLINE (48,62) = PDESC(3,15)
		END
	ELSE	PLINE (48,62) = '*OUT OF RANGE*'

	PLINE (66,74) = DP_PRICE,	'Z,ZZX.XXX'
	CALL PRINT
	GOTO PLOOP
;  TYPE             GAUGE  STYLE              CAT                     PRICE
;  X AAAAAAAAAAAAAAA  ZZX  X AAAAAAAAAAAAAAA  X AAAAAAAAAAAAAAA   ZZ,ZZX.XX
;123456789012345678901234567890123456789012345678901234567890123456789012345678
;         1         2         3         4         5         6         7        

P_EOF,
	CLOSE 10
ENDOFF,
	IF (PRNTON.EQ.1)  XCALL LPOFF(LPSW,SPLFIL,PGCNT)
	XCALL FILES (1,'SI',182,4)
	XCALL PGCHN ('CP:TBLMNU',1)

PRINT,
	IF (PRNTON .EQ. 0) CALL PRNTON
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HD1,HD,HD
&		,LG,LG,LG,0,080,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;------------------------------------------------------------------------------

PRNTON,
	SPLFIL (5,6) = 'EF'
	LPSW = 1		;PRINT,SPOOL, OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTON = 1
	RETURN
;------------------------------------------------------------------------------

END
;==============================================================================
;  TYPE               STYLE              CAT              GAUGE       PRICE
;  X AAAAAAAAAAAAAAA  X AAAAAAAAAAAAAAA  X AAAAAAAAAAAAAAA  ZZX   ZZ,ZZX.XX
;123456789012345678901234567890123456789012345678901234567890123456789012345678
;         1         2         3         4         5         6         7        



