;STRMNT.CP
;	COPIED 8-24-11
;DUCPRC.COP
;
;		AUTHOR: SHERWOOD S. QUIRING
;		DATE  : 18-DEC-1996
;
RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'


RECORD	PRINT
	TITLE	,A*,	'SQUARE TO ROUND'
	HD	,A6,	'NO HDR'
	LG	,A9,	'NO LEGEND'
	PLINE	,A132
	PRNTON	,D1
	LINCNT	,D2,60
	PGCNT	,D6
	LPSW	,D2
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	PRTCTL	,D3,132
	LPARG	,D1
	PRNTSW	,D1
	PRTCTR	,D1

RECORD	HD1
		,A*,	'ASP. RATIO  LEN+WIDTH   DEPT    PRICE'

RECORD	LPKEY
	K_AR	,D1
	K_KEY	,D3
	K_FILL	,A11

RECORD	CHANNEL
	CHN182	,D2

RECORD	CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2

RECORD	VARS
	I	,D5
	J	,D5
	PGM	,D1
	ST_AR	,D1
	EN_AR	,D1
	SELECT	,D1
	OPNOK	,D1
	ENTRY	,A30
	INXCTL	,D1
	WHATNO	,D2
	CNGCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	STORE	,D1,2
	DELETE	,D1,3
	LOKCTL	,D1
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	V = 1

	XCALL OUTPT (1,1,2,'SQUARE TO ROUND PRICING TABLE',1)
	CALL OPENS
	IF (.NOT. OPNOK) GOTO ENDOFF

MENU,
	XCALL OUTPT (1,1,2,'SQUARE TO ROUND PRICING TABLE',1)
	XCALL OUTPT (3,9,0,'PLEASE SELECT APPLICATION',1)
	XCALL OUTPT (5,15,0,'1. TABLE MAINTENANCE',1)
	XCALL OUTPT (6,15,0,'2. PRINT TABLE',1)
MINPUT,
	XCALL INPUT (3,36,1,1,'#E',ENTRY,INXCTL,1)
	GOTO (MINPUT,ENDOFF), INXCTL
	PGM = ENTRY(1,1)
	GOTO (DISPLA,PRINT_TABLE),PGM
	GOTO MINPUT

DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,'SQUARE TO ROUND PRICING TABLE',1)
	XCALL OUTPT ( 4,4,0,'1. ASPECT RATIO (1,2,3)',1)
	XCALL OUTPT ( 6,5,0,'2. L+W',1)
	XCALL OUTPT ( 8,5,0,'3. PRICE',1)
	XCALL OUTPT (10,5,0,'4. DEPT',1)
	XCALL OUTPT (12,5,0,'5. SUFFIX',1)
AR,
	CTL = '04,29,01,00,#E'
	CALL INPUT
	GOTO (DISPLA,MENU),INXCTL
	K_AR = ENTRY(1,1)
	IF (K_AR .EQ. 0) 
		BEGIN
		K_AR = ST_AR
		ENTRY(1,1) = K_AR, 'Z'
		XCALL OUTPT (ROW, COL, 0, ENTRY(1,1), 1)
		END

	USING K_AR SELECT
	(1,2,3),	ST_AR = K_AR
	(),		BEGIN
			XCALL BEEP
			GOTO AR
			END
	ENDUSING

KEY,
	XCALL INPUT (6,14,03,00,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	K_KEY = ENTRY(1,3)

	TBLCOD = 'SQ'
	SQ_AR = K_AR
	SQ_LW = K_KEY
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .EQ. 0)
	THEN	BEGIN
		SELECT = 2		;EXISTING RECORD
		K_KEY = SQ_LW
		CALL DSPREC
		GOTO ANYCNG
		END
	ELSE	BEGIN
		SELECT = 1
		CLEAR COPTBL
		END
	GOTO (ANYCNG),CNGCTL

SQ_PRICE,
	CTL = '08,14,06,00,$ '
	CALL INPUT
	GOTO (DISPLA),INXCTL
	SQ_PRICE = ENTRY(1,6)
	GOTO (ANYCNG),CNGCTL
SQ_DEPT,
	CTL = '10,14,02,00,A '
	CALL INPUT
	GOTO (DISPLA),INXCTL
	SQ_DEPT = ENTRY(1,2)
	GOTO (ANYCNG),CNGCTL
SQ_SUFX,
	CTL = '12,16,02,00,A '
	CALL INPUT
	GOTO (DISPLA),INXCTL
	SQ_SUFX = ENTRY(1,2)
	GOTO (ANYCNG),CNGCTL
ANYCNG,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (PROCES,CNGBR),CNGCTL + 1
CNGBR,
	GOTO (AR, KEY, SQ_PRICE, SQ_DEPT, SQ_SUFX),WHATNO
	GOTO ANYCNG
PROCES,
	TBLCOD = 'SQ'
	SQ_AR = K_AR
	SQ_LW = K_KEY

	CASE SELECT OF
	BEGINCASE
;;;	1:	XCALL ISIO (CHN182,COPTBL,TBL_KEY,STORE,LOKCTL)
	1:	STORE (CHN182, COPTBL, TBL_KEY)
	2:	XCALL ISIO (CHN182,COPTBL,TBL_KEY,WRITE,LOKCTL)
	3:	BEGIN
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,DELETE,LOKCTL)
		XCALL MESAG ('RECORD DELETED !',1)
		END
	ENDCASE

	GOTO DISPLA

DSPREC,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR ENTRY

	ENTRY(1,3) = SQ_LW,	'ZZX' [LEFT]
	XCALL OUTPT (6,14,0,ENTRY(1,3),1)
	
	CLEAR ENTRY
	ENTRY(1,7) = SQ_PRICE,	'ZZZX.XX' [LEFT]
	XCALL OUTPT (08,14,0,ENTRY(1,7),1)

	XCALL OUTPT (10,14,0,SQ_DEPT,1)

	XCALL OUTPT (12,16,0,SQ_SUFX,1)
	RETURN
;--------------------------------------------------------

INPUT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL INPUT(ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,1)
	RETURN
;--------------------------------------------------------

ENDOFF,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CALL CLOSE
	XCALL PGCHN ('CP:TBLMNU',1)
;------------------------------------

;===================================================================
PRINT_TABLE,
;===================================================================
	LINCNT = 66
	PGCNT  = 0
	
PDISP,
	CNGCTL = 
	XCALL OUTPT (1,1,2,'PRINT SQUARE TO ROUND PRICING TABLE',1)
	XCALL OUTPT (4,4,0,'1. STARTING A/R',1)
	XCALL OUTPT (6,4,0,'2. ENDING   A/R',1)
ST_ITEM,
	XCALL INPUT (4,25,01,00,'#E',ENTRY,INXCTL,1)
	GOTO (PDISP,PDONE),INXCTL
	ST_AR = ENTRY(1,1)
	IF (ST_AR .EQ. 0)
		BEGIN
		EN_AR = 9

		XCALL OUTPT (4,25,1,'ALL',1)
		XCALL OUTPT (6,25,1,' ',1)
		GOTO P_ANY
		END
	GOTO (P_ANY),CNGCTL
EN_ITEM,
	XCALL INPUT (6,25,01,00,'# ',ENTRY,INXCTL,1)
	GOTO (PDISP),INXCTL
	EN_AR = ENTRY(1,1)
	IF (EN_AR .EQ. 0)
		BEGIN
		EN_AR = ST_AR
		ENTRY(1,1) = EN_AR,'X'		
		XCALL OUTPT (6,25,0,ENTRY(1,1),1)
		END
P_ANY,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (P_PRINT,P_CNGBR),CNGCTL + 1
P_CNGBR,
	GOTO (ST_ITEM,EN_ITEM),WHATNO
	GOTO P_ANY

P_PRINT,
	CLEAR TBL_KEY
	TBLCOD = 'SQ'
	SQ_AR = ST_AR
	FIND (CHN182,COPTBL,TBL_KEY)[ERR=PLOOP]

PLOOP,
	XCALL IOS (CHN182,COPTBL,READ,LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF
	IF (TBLCOD .NE. 'SQ') GOTO EOF
	IF (SQ_AR .LT. ST_AR) GOTO PLOOP
	IF (SQ_AR .GT. EN_AR) GOTO EOF

	PLINE(6,6) = SQ_AR,	'X'	
	PLINE(16,18) = SQ_LW,	'ZZX'
	PLINE(26,27) = SQ_DEPT	
	PLINE(31,37) = SQ_PRICE,	'ZZZX.XX'
	PLINE(40,41) = SQ_SUFX
	CALL PRINT
	GOTO PLOOP

;ASP. RATIO  LEN+WIDTH   DEPT    PRICE
;     X         ZZX       AA   ZZZX.XX
;123456789012345678901234567890123456789
;         1         2         3
EOF,
	IF (PRNTON.EQ.1)  XCALL LPOFF(LPSW,SPLFIL,PGCNT)
	PRNTON  = 0

PDONE,
	GOTO MENU

PRINT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (PRNTON .EQ. 0) CALL PRNTON
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HD1,HD,HD
&		,LG,LG,LG,0,132,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;-------------------------------------------------------------
PRNTON,
	SPLFIL (5,6) = 'EF'
	LPSW = 1		;PRINT,SPOOL, OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTON = 1
	RETURN
;-------------------------------------------------------------
;===================================================================
OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (1,'SU',182,SWITCH)		;182 - COPTBL
	IF (SWITCH .EQ. 9) RETURN
	CHN182 = 1
	OPNOK = 1
	RETURN
;------------------------------------

CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (CHN182) CLOSE CHN182
	RETURN
;------------------------------------


