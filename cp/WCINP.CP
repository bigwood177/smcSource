SUBROUTINE WCINP
	WID	,D
	ROW	,D
	COL	,D
	MAX	,D
	MIN	,D
	TYPE	,A
	ENTRY	,A
	INXCTL	,D
	POS	,D

.INCLUDE 'DEF:WINDOWS.DEF'

RECORD	VARS
	DEC3	,D3
	DCHAR	,D3
	ACHAR	,A10
	BLANKS	,A40
	STAT	,D1
	DECMAL	,D10
	J	,D2
PROC
	IF (POS.EQ.0) 
	THEN	BEGIN
		ENTRY =
		XCALL W_DISP(WID,WD_POS,ROW,COL,BLANKS(1,MAX+3-POS))
		XCALL W_DISP(WID,WD_POS,ROW,COL)
		END
	ELSE	XCALL W_DISP(WID,WD_POS,ROW,COL,ENTRY(1,POS))

	XCALL W_UPDT
ACCPT,
	XCALL W_DISP(WID,WD_ACCEPT,DCHAR)
	IF (DCHAR .EQ. 0)	;FUNCTION KEYS
		BEGIN
		CLEAR DEC3
		XCALL FLAGS (10000,1)
		XCALL TTSTS (STAT)
		IF (STAT)	XCALL W_DISP(WID,WD_ACCEPT,DEC3)
		IF (DEC3.EQ.133) DCHAR = 27		;<F11>
		IF (DEC3.EQ.79)  DCHAR = 27		;<END>	
		XCALL FLAGS (10000,0)
		END

	USING DCHAR SELECT
	(8),	BEGIN			;<BS> = DELETE
		POS = POS - 1
		IF (POS.LT.1)
		   BEGIN
		   ENTRY =
		   POS =
		   END
		GOTO RETERN
		END
	(9),				;TAB
		IF (TYPE(2,2).EQ.'T'.OR.TYPE(2,2).EQ.'X') GOTO RETERN
	(13),	BEGIN			;<CR>
		XCALL W_DISP(WID,WD_ACCEPT,DEC3)
		GOTO RETERN
		END
	(23),	GOTO RETERN		;^W
	(27),				;<ESC>, <END>, <F11>
		IF (TYPE(2,2).EQ.'E'.OR.TYPE(2,2).EQ.'X') GOTO RETERN
	ENDUSING

	IF (TYPE(1,1).EQ.'#' .AND. (DCHAR.LT.48 .OR. DCHAR.GT.57)) GOTO NUMONL
	INCR POS
	IF (POS.GT.MAX) GOTO TOOLONG
	XCALL ASCII (DCHAR,ENTRY(POS,POS))
	XCALL W_DISP(WID,WD_POS,ROW,COL,ENTRY(1,MAX))

RETERN,
	INXCTL =
	IF (DCHAR.EQ.23) INXCTL = 1
	IF (DCHAR.EQ.27) INXCTL = 2
	IF (DCHAR.EQ. 9) INXCTL = 3
	IF (DCHAR.EQ.13) INXCTL = 4
	RETURN
TOOLONG,
;;;	XCALL MESAG ('TOO LONG',2)
	xcall wnmsg(wid,23,'too long',2)
	XCALL W_DISP(WID,WD_ACCEPT,DEC3)	;added ssq 5/8/02
	POS = POS - 1
	RETURN	
NUMONL,
;;;	XCALL MESAG ('MUST BE NUMERIC',2)
	xcall wnmsg(wid,23,'must be numeric',2)
	XCALL W_DISP(WID,WD_ACCEPT,DEC3)	;added ssq 5/13/02
	RETURN

