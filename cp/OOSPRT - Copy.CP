;OOSPRT.CP
;
RECORD	OOSMSG
	.INCLUDE 'DEF:OOSMSG.DEF'

RECORD	HD1
	,	A*,'    DATE ORDER#     CUSTOMER             ITEM #          QTY-ORD  QTY-AVL'

RECORD	PRINT
	TITLE	,A*,	'OUT OF STOCK ITEMS'
	HD	,A6,	'NO HDR'
	LG	,A9,	'NO LEGEND'
	PLINE	,A132
	PRNTON	,D1
	LINCNT	,D2,60
	PGCNT	,D6
	LPSW	,D2
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	PRTCTL	,D3,132
	LPARG	,D1
	PRNTSW	,D1
	PRTCTR	,D1

RECORD	FILOOS
	FS_DEV	,A3
		,A1,	':'
	FS_NAME	,A6
		,A1,	'.'
	FS_EXT	,A3

RECORD	CHANNEL
	CHNOOS	,D2

RECORD	VARS
	OPNOK	,D1
	ENTRY	,A30
	INXCTL	,D1
	READ	,D1,0
	LOKCTL	,D1
	XDATE	,D6
	SAVRFA	,A6
	OPT	,D1
	CLCTL	,D1
	I	,D6
	V	,D1

PROC
	XCALL TERID(V)
	V=1
	CALL OPENS
	IF (.NOT. OPNOK) GOTO ENDOFF
DISPLA,
	XCALL OUTPT (1,1,2,'PRINT OUT OF STOCK ITEMS',1)
	XCALL OUTPT (4,20,2,'PLEASE SELECT ',V)
	XCALL OUTPT (5,25,0,'1. PRINT AND DELETE ITEMS',V)
	XCALL OUTPT (6,25,0,'2. PRINT ITEMS',V)
	XCALL INPUT (4,34,01,00,'#E',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	OPT = ENTRY(1,1)
	IF(OPT.LT.1 .OR. OPT.GT.2) GOTO DISPLA

PROCES,
	FIND (CHNOOS,OOSMSG,^LAST) [ERR=EXIT_OOS]
OOS_LOOP,
;;;	READS (CHNOOS,OOSMSG,EXIT_OOS,REVERSE,GETRFA:SAVRFA)
	READS (CHNOOS,OOSMSG,EXIT_OOS,REVERSE)[EOF=EXIT_OOS]
	IF(F_FLAG.EQ.9)GOTO OOS_LOOP	;MARKED FOR DELETION

	XDATE(1,4)=F_DATE(5,8)
	XDATE(5,6)=F_DATE(3,4)
	PLINE(1,8)=XDATE,	'XX/XX/XX'
	PLINE(10,15)=F_ORD,	'ZZZZZX'
	PLINE(21,40)=F_NAME
	PLINE(42,56)=F_ITEM
	PLINE(58,64)=F_QTYOR,	'ZZZ,ZZX'
	PLINE(67,74)=F_QTYAV,	'ZZZ,ZZX-'
	CALL PRINT
	IF(OPT.EQ.2)GOTO OOS_LOOP

	F_FLAG=9
	WRITE(CHNOOS,OOSMSG,F_DATE)
	GOTO OOS_LOOP
PRINT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (PRNTON .EQ. 0) CALL PRNTON
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HD1,HD,HD
&		,LG,LG,LG,0,132,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;-------------------------------------------------------------
PRNTON,
	SPLFIL (5,6) = 'EF'
	LPSW = 1		;PRINT,SPOOL, OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTON = 1
	RETURN
;-------------------------------------------------------------


EXIT_OOS,
	IF (PRNTON.EQ.1)  XCALL LPOFF(LPSW,SPLFIL,PGCNT)
ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('CP:IMMENU',1)
	XCALL FLAGS (7000000)	;suppress program termination message
	STOP
;=========================================================

OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL FFILE (45, FILOOS, CLCTL)

;;;	OPEN(19, SU, 'SMC:OOSMSG.ISM')
	FS_NAME = 'OOSMSG'
	FS_EXT(3,3) = 'M'
	OPEN(19,SU,FILOOS)

	CHNOOS = 19

;;;	OPEN(1,SU,'SMC:OOSMSG.ISM')
;;;	CHNOOS = 1
	OPNOK=1
	RETURN
;------------------------------------------------------

CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLOSE CHNOOS
	RETURN
;------------------------------------------------------
;--------------------------------------------------

;    DATE ORDER# SLM CUSTOMER             ITEM #          QTY-ORD  QTY-AVL
;XX/XX/XX ZZZZZX AAA AAAAAAAAAAAAAAAAAAAA AAAAAAAAAAAAAAA ZZZ,ZZX  ZZZ,ZZX- 
;123456789012345678901234567890123456789012345678901234567890123456789012345
;         1         2         3         4         5         6         7

