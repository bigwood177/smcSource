;OPNOR2.CP
;
RECORD	ORDHDR
	.INCLUDE 'DEF:RD044A.DEF'
;
RECORD	ORDLIN
	.INCLUDE 'DEF:RD045A.DEF'

RECORD	TEMP
	T_DEPT	,A2
	T_OPN	,D10	;OPEN
	T_CPL	,D10	;COMPLETED

RECORD	HDR2
		,A*,	'DEPT         OPEN    COMPLETED        TOTAL'

RECORD	HDR1
		,A*,	'FROM: '
	H2_DAT1	,A10
		,A*,	' THRU: '
	H2_DAT2	,A10


;AA     ZZZZ,ZZX.XX- ZZZZ,ZZX.XX- ZZZZ,ZZX.XX-
;1234567890123456789012345678901234567890
;         1         2         3         4
RECORD	PRINT
	TITLE	,A*,	'DEPARTMENT SUMMARY'
	HD	,A6,	'NO HDR'
	LINCNT	,D2,	60
	SPLFIL	,A14
	LPONSW	,D1
	LPARG	,D1
	RPTNUM	,D3
	PRTTYP	,A1
	PGCNT	,D6,	000000
	PLINE	,A80
	PRTCTL	,D3
	LPSW	,D2

RECORD	DIS
	II	,D6

RECORD	CHANNEL
	CHN044	,D2
	CHN045	,D2
	CHNTMP	,D2

RECORD	VARS
	TOT_OPN	,D9
	TOT_CPL	,D9
	tmpfil	,a14,	'SPL:OPNOR2.ISM'
	REDFIL	,A14
	CLCTL	,D1
	YEAR	,D4
	YR	,D2
	O_AMT	,D9
	C_AMT	,D9
	SAVDPT	,A2
	SDATE	,D8	;START DATE
	EDATE	,D8	;END DATE
	XDATE	,D8
	TODAY	,D8	;TODAY'S DATE
	DATE1	,D8	;FIRST DAY OF THE YEAR
	SYEAR	,D4
	EYEAR	,D4
;
	ENTRY	,A30
	OPNOK	,D1
	WHATNO	,D2
	CNGCTL	,D1
	INXCTL	,D1
	LOKCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	STORE	,D1,2

	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'DEPARTMENT SUMMARY',1)
;
;;;	CALL OPENS
;;;	IF (.NOT. OPNOK) GOTO ENDOFF
	XCALL RDAT8(TODAY)

DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,'DEPARTMENT SUMMARY REPORT',1)
	XCALL OUTPT (4,4,0,'1. START DATE',1)
	XCALL OUTPT (6,4,0,'2. END   DATE',1)
SDATE,
	XCALL INPUT (4, 19, 08, 00, 'DE', ENTRY, INXCTL, 1)
	GOTO (DISPLA,ENDOFF),INXCTL
	SDATE = ENTRY(1,8)
	IF (SDATE .EQ. 0)
		BEGIN
		SDATE = TODAY
		XDATE(1,4) = SDATE(5,8)
		XDATE(5,8) = SDATE(1,4)
		ENTRY(1,10) = XDATE, 'ZX/XX/XXXX'
		XCALL OUTPT (4,19,1,ENTRY(1,10),1)
		END

	GOTO (ANYCNG),CNGCTL
EDATE,
	XCALL INPUT (6, 19, 08, 00, 'DE', ENTRY, INXCTL, 1)
	GOTO (DISPLA),INXCTL
	EDATE = ENTRY(1,8)
	IF (EDATE .EQ. 0)
		BEGIN
		EDATE = SDATE
		XDATE(1,4) = EDATE(5,8)
		XDATE(5,8) = EDATE(1,4)
		ENTRY(1,10) = XDATE, 'ZX/XX/XXXX'
		XCALL OUTPT (6,19,1,ENTRY(1,10),1)
		END

ANYCNG,
	XCALL ANYCN (CNGCTL, WHATNO)
	GOTO (PROCES, CNGBR),CNGCTL+1
CNGBR,
	GOTO (SDATE, EDATE),WHATNO
	GOTO ANYCNG

PROCES,
	CLEAR PGCNT
	LINCNT = 60
	CLEAR TOT_OPN, TOT_CPL, T_OPN, T_CPL

	XDATE(1,4) = SDATE(5,8)
	XDATE(5,8) = SDATE(1,4)
	H2_DAT1 = XDATE,	'ZX/XX/XXXX'

	XDATE(1,4) = EDATE(5,8)
	XDATE(5,8) = EDATE(1,4)
	H2_DAT2 = XDATE,	'ZX/XX/XXXX'

	SYEAR = SDATE(1,4)
	EYEAR = EDATE(1,4)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO ENDOFF
	

	CALL GET_OPEN			;OPEN ORDERS
	FOR YEAR FROM SYEAR THRU EYEAR CALL GET_COMPLETE
;PRINT REPORT...
;;;	CLOSE CHNTMP

	CALL CLOSE

	XCALL LPON (LPSW, SPLFIL)
	FIND (CHNTMP, TEMP, ^FIRST) [ERR=P_LOOP]
P_LOOP,
	READS (CHNTMP, TEMP, P_EOF)
	PLINE (1,4) = 	T_DEPT
	PLINE (8,18) = T_OPN, 'ZZZZ,ZZX.XX-'
	PLINE (21,31) = T_CPL,  'ZZZZ,ZZX.XX-'
	PLINE (34,44) = (T_OPN+T_CPL),  'ZZZZ,ZZX.XX-'
	CALL PRINT

	TOT_OPN = TOT_OPN + T_OPN
	TOT_CPL = TOT_CPL + T_CPL
	GOTO P_LOOP

P_EOF,
	CALL PRINT
	PLINE (1,5) = 'TOTAL'
	PLINE (8,18) = TOT_OPN, 'ZZZZ,ZZX.XX-'
	PLINE (21,31) = TOT_CPL, 'ZZZZ,ZZX.XX-'
	PLINE (34,44) = (TOT_OPN+TOT_CPL),  'ZZZZ,ZZX.XX-'
	CALL PRINT
	
	XCALL LPOFF (LPSW, SPLFIL, PGCNT)

	CLOSE CHNTMP
	XCALL DELET (TMPFIL)

	GOTO DISPLA

PRINT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	PRTCTL = 70
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,HD,
&			'NO LEGEND',' ',' ',0,080,PRTCTL,0,LPSW,RPTNUM,PRTTYP)

	RETURN
;-------------------------------------------


GET_OPEN,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR ORDLIN, C_AMT, O_AMT, II
	SAVDPT = '-1'

FIND_NEXT,
	LORDNO = LORDNO + 1	
	FIND (CHN045, ORDLIN, LORDNO) [ERR=GO_LOOP]
GO_LOOP,
	INCR II
	IF (II/1000*1000.EQ.II) DISPLAY (15,$SCR_POS(1,70),DIS)
	XCALL IOS (CHN045, ORDLIN, READ, LOKCTL)
	IF (LOKCTL .NE. 0) GOTO GO_EOF
	IF (LTYPE .EQ. 'M') GOTO GO_LOOP	;SKIP MEMOS
	IF (LORDNO .NE. OORDNO) 
		BEGIN
		XCALL ISIO (CHN044, ORDHDR, LORDNO, READ, LOKCTL)
		IF (LOKCTL.NE.0) GOTO FIND_NEXT
		IF (OLOC.NE.'O') GOTO FIND_NEXT
		END

	IF (OORDDT .LT. SDATE)	GOTO FIND_NEXT
	IF (OORDDT .GT. EDATE) GOTO FIND_NEXT

	IF (LDEPT .NE. SAVDPT) CALL GETDPT
	O_AMT = O_AMT + (LQTYOR*LPRICE)#1
	GOTO GO_LOOP
GO_EOF,
	CALL GETDPT
	RETURN
;-------------------------------------------

GET_COMPLETE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CALL CLOSE 

	SWITCH = 5

	USING YEAR SELECT
	(.EQ.TODAY(1,4) ), BEGIN
		 	   XCALL FILES (84, 'SI', 184, SWITCH)
			   CHN044 = 84
		 	   XCALL FILES (85, 'SI', 185, SWITCH)
			   CHN045 = 85
			   END

	(.GT. TODAY(1,4)), 	RETURN		;IN THE FUTURE
	(.GT. 2006),	   BEGIN		;ARBITRARY, DON'T PROC PREV
			   YR=YEAR(3,4)		;SLHHXX, SLHLXX
			   XCALL FFILE (184, REDFIL, CLCTL)
			   REDFIL(9,10) = YR, 'XX'
			   REDFIL(14,14) = 'M'
			   OPEN (84, SI, REDFIL)
			   CHN044 = 84

			   XCALL FFILE (185, REDFIL, CLCTL)
			   REDFIL(9,10) = YR, 'XX'
			   REDFIL(14,14) = 'M'
			   OPEN (85, SI, REDFIL)
			   CHN045 = 85
			   END

	ENDUSING

	CLEAR ORDLIN, C_AMT, O_AMT, II
	SAVDPT = '-1'

GET_NEXT,
	LORDNO = LORDNO + 1	
	FIND (CHN045, ORDLIN, LORDNO) [ERR=GC_LOOP]
GC_LOOP,
	INCR II
	IF (II/1000*1000.EQ.II) DISPLAY (15,$SCR_POS(1,70),DIS)
	XCALL IOS (CHN045, ORDLIN, READ, LOKCTL)
	IF (LOKCTL .NE. 0) GOTO GC_EOF
	IF (LTYPE .EQ. 'M') GOTO GC_LOOP	;SKIP MEMOS
	IF (LORDNO .NE. OORDNO) 
		BEGIN
		XCALL ISIO (CHN044, ORDHDR, LORDNO, READ, LOKCTL)
		IF (LOKCTL.NE.0) GOTO GET_NEXT
		END

	IF (OORDDT .LT. SDATE)	GOTO GET_NEXT
	IF (OORDDT .GT. EDATE) GOTO GET_NEXT

	IF (LDEPT .NE. SAVDPT) CALL GETDPT
	C_AMT = C_AMT + (LQTYSH*LPRICE)#1
;;;	C_AMT = C_AMT + (LQTYOR*LPRICE)#1
	GOTO GC_LOOP
GC_EOF,
	CALL GETDPT
	RETURN
;-------------------------------------------

GETDPT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	IF (SAVDPT .EQ. '-1') GOTO OUTDPT
	XCALL ISIO (CHNTMP, TEMP, SAVDPT, READ, LOKCTL)
	IF (LOKCTL .NE. 0) GOTO NEWDPT

	T_OPN = T_OPN + O_AMT
	T_CPL = T_CPL + C_AMT

	XCALL ISIO (CHNTMP, TEMP, LDEPT, WRITE, LOKCTL)
	CLEAR O_AMT, C_AMT

	SAVDPT = LDEPT
	CLEAR O_AMT, C_AMT
	RETURN
;-------------------------------------------
NEWDPT,
	CLEAR TEMP
	T_DEPT = SAVDPT
	T_OPN = O_AMT
	T_CPL = C_AMT
	
	XCALL ISIO (CHNTMP, TEMP, LDEPT, STORE, LOKCTL)
	CLEAR O_AMT, C_AMT

OUTDPT,
	SAVDPT = LDEPT
	CLEAR O_AMT, C_AMT
	RETURN
;-------------------------------------------


ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('CP:IMMENU',1)
	STOP


OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK
	SWITCH = 5
	XCALL FILES (4, 'SI', 44, SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN044 = 4

	SWITCH = 5
	XCALL FILES (5,'SI',45, SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN045 = 5

	ISAMC (TMPFIL, 22, 1, 'START=1, LENGTH=2, NODUPS, ASCEND')
	OPEN (10, SU, TMPFIL)
	CHNTMP = 10

	OPNOK = 1
	RETURN
;-------------------------------------------

CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (CHN044) CLOSE CHN044
	IF (CHN045) CLOSE CHN045

	RETURN
;-------------------------------------------

