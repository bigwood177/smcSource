;TI1MNT.COP
;
;	INVENTORY QTY PRICE BREAK TABLE
;
RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'

RECORD	ITMMAS
	.INCLUDE 'DEF:RD041A.DEF'

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;RECORD,X
;;;	.INCLUDE 'DEF:RD041B.DEF'
;;;
;;;RECORD	ITMIDX
;;;	.INCLUDE 'DEF:RD042A.DEF'
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

RECORD	CHANNEL
	CHN041	,D2
	CHN042	,D2
	CHN182	,D2

RECORD	TMPLT
	TN1	,D1
		,A2,	'. '
	TD1	,A4,	'QTY:'
		,A1
	TV1	,A6	;ZZ,ZZX
		,A4
	TN2	,D1
		,A2,	'. '
	TD2	,A6,	'PRICE:'
		,A1
	TV2	,A8	;ZZX.XXX

RECORD	PRINT
	TITLE	,A*,	'INVENTORY PRICE TABLE'
	HD	,A6,	'NO HDR'
	LG	,A9,	'NO LEGEND'
	PLINE	,A80
	PRNTON	,D1
	LINCNT	,D2,60
	PGCNT	,D6
	LPSW	,D2
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	PRTCTL	,D3,080
	LPARG	,D1
	PRNTSW	,D1
	PRTCTR	,D1

RECORD	HD1
		,A*,	'ITEM #          DESCR'

RECORD	VARS
	OPNOK	,D1
	PRS	,4D2,	01,21,41,61
	PRE	,4D2,	20,40,60,80
	BQTY	,D5
	EQTY	,D5
	PRANG	,A20
	ST_ITEM	,A15
	EN_ITEM	,A15
	BLANKS	,A15
	PGM	,D1
	ROW	,D2
	ITEM	,A15
	NUMITM	,D2
	BSEND	,D5
	BSMID	,D5
	SRCCTL	,D1
	ENTRY	,A30
	INXCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	STORE	,D1,2
	DELETE	,D1,3
	LOKCTL	,D1
	WHATNO	,D2
	SELECT	,D1
	CNGCTL	,D1
	I	,D3
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'INVENTORY PRICE TABLE',1)

	NUMITM = 4

	CALL OPENS
	IF (.NOT. OPNOK) GOTO CLOSE

MENU,
	XCALL OUTPT (1,1,2,'INVENTORY PRICE TABLE',1)
	XCALL OUTPT (3,9,0,'PLEASE SELECT APPLICATION',1)
	XCALL OUTPT (5,15,0,'1. TABLE MAINTENANCE',1)
	XCALL OUTPT (6,15,0,'2. PRINT PRICE TABLE',1)
MINPUT,
	XCALL INPUT (3,36,1,1,'#E',ENTRY,INXCTL,1)
	GOTO (MINPUT,ENDOFF), INXCTL
	PGM = ENTRY(1,1)
	GOTO (DISPLA,PRINT_TABLE),PGM
	GOTO MINPUT

DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,'INVENTORY PRICE TABLE',1)
	XCALL OUTPT (2,1,0,'Use this price if quantity ordered is less than this qty',1)
	XCALL OUTPT (4,05,0,'1. ITEM #',1)

	ROW = 
	TN1 = 1
	CLEAR TV1, TV2
	FOR I FROM 1 THRU NUMITM
		BEGIN
		CLEAR I1_QTY(I), I1_PRICE(I)
		INCR TN1
		TN2 = TN1 + 1
		ROW = (I+1)*2+2
		XCALL OUTPT (ROW,5,0,TMPLT,1)
		TN1 = TN2
		END

ITEM,
	XCALL INPUT (04,17,15,00,'AE',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	ITEM = ENTRY(1,15)

	LOKCTL = 1
	XCALL ISIO (CHN041, ITMMAS, ITEM, READ, LOKCTL)
	IF (LOKCTL .NE. 0)
	THEN	BEGIN
		XCALL MESAG ('ITEM NOT ON FILE',1)
		GOTO ITEM
		END
	ELSE	BEGIN
		XCALL OUTPT (04,33,1,DESCR,1)
		ENTRY(1,18) = AVGCST,	'ZZZZ.XXX  Avg Cost'
		XCALL OUTPT (5,33,1,ENTRY(1,18),1)
		END

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;	XCALL SERCH (CHN042,ITMIDX,ITEM,1,15,BSEND,BSMID,SRCCTL,4,16,20,0,0,0,0)
;;;	IF (SRCCTL.NE.0 .OR. IRC041.EQ.0)
;;;	THEN	BEGIN
;;;		XCALL MESAG ('ITEM NOT ON FILE',1)
;;;		GOTO ITEM
;;;		END
;;;	ELSE	BEGIN
;;;		XCALL IO (CHN041,ITMMAS,IRC041,READ,LOKCTL)
;;;		XCALL OUTPT (04,33,1,DESCR,1)
;;;		END
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CLEAR TBL_KEY

	TBL_KEY(1,2) = 'I1'
	I1_KEY = ITEM
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .EQ. 0)
		BEGIN
		SELECT = 2
		CALL DSPREC
		GOTO ANYCNG
		END


	SELECT = 1			;ADD MODE
	CLEAR COPTBL


	FOR I FROM 1 THRU NUMITM	
		BEGIN
		CALL GET_INPUTQ
		CALL GET_INPUTP
		GOTO (DISPLA,ENDOFF),INXCTL
		END
ANYCNG,
;;;	XCALL ANYCN(CNGCTL,WHATNO)
	XCALL OUTPT (24,1,1,'FIELD # TO CHANGE            <TAB> = DELETE',1)
	XCALL INPUT (24,20,02,00,'#T',ENTRY,INXCTL,1)
	IF (INXCTL .EQ. 3)
		BEGIN
		XCALL OUTPT (24,1,1,'DELETE, ARE YOU SURE ?',1)
		XCALL INPUT (24,24,01,01,'YN',ENTRY,INXCTL,1)
		GOTO (ANYCNG),INXCTL-1
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,DELETE,LOKCTL)
		GOTO DISPLA
		END

	WHATNO = ENTRY(1,2)
	IF (WHATNO .EQ. 0)
	THEN 	CLEAR CNGCTL
	ELSE 	CNGCTL = 1

	GOTO (PROCES,CNGBR),CNGCTL+1
CNGBR,
	USING WHATNO SELECT
	(1),	  GOTO ITEM
	(2,4,6,8),  BEGIN
		  I = WHATNO/2
		  CALL GET_INPUTQ
		  GOTO (DISPLA),INXCTL
		  END
	(3,5,7,9),  BEGIN
		  I = WHATNO/2
		  CALL GET_INPUTP
		  GOTO (DISPLA),INXCTL
		  END
	ENDUSING
	GOTO ANYCNG

PROCES,
	CASE SELECT OF
	BEGINCASE
	1:	BEGIN
		TBLCOD = 'I1'
		TBLKEY = ITEM
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,STORE,LOKCTL)
		END
	2:	XCALL ISIO (CHN182,COPTBL,TBL_KEY,WRITE,LOKCTL)
	ENDCASE
	GOTO DISPLA

ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('CP:PRCMNU',1)
	STOP


DSPREC,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; DISPLAY RECORD
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	ROW = 
	TN1 = 1
	FOR I FROM 1 THRU NUMITM
		BEGIN
		INCR TN1
		TN2 = TN1 + 1
		ROW = (I+1)*2+2
		TV1 = I1_QTY(I),  'ZZ,ZZX' [LEFT]
		TV2 = I1_PRICE(I),'ZZZX.XXX' [LEFT]
		XCALL OUTPT (ROW,5,0,TMPLT,1)
		TN1 = TN2
		END
	RETURN
;-----------------------------------


GET_INPUTQ,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		;;; ACCEPT QTY INPUT FOR ARRAY ELEMENT I.
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	ROW = I + 1
	ROW = ROW * 2 + 2
	XCALL INPUT (ROW,13,05,00,'# ',ENTRY,INXCTL,1)
	IF (INXCTL .NE. 0) RETURN
	I1_QTY(I) = ENTRY(1,5)
	RETURN
GET_INPUTP,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		;;; ACCEPT PRICE INPUT FOR ARRAY ELEMENT I.
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	ROW = I + 1
	ROW = ROW * 2 + 2
	XCALL INPUT (ROW,33,07,00,'# ',ENTRY,INXCTL,1)
	IF (INXCTL .NE. 0) RETURN
	I1_PRICE(I) = ENTRY(1,7)
	ENTRY(1,8) = I1_PRICE(I),	'ZZZZ.XXX' [LEFT]
	XCALL OUTPT (ROW,33,0,ENTRY(1,7),1)
	RETURN
;-------------------------------------------------------------------

;===================================================================
PRINT_TABLE,
;===================================================================
	LINCNT = 66
	PGCNT  = 0
	
PDISP,
	CNGCTL = 
	XCALL OUTPT (1,1,2,'PRINT INVENTORY PRICE TABLE',1)
	XCALL OUTPT (4,4,0,'1. STARTING ITEM #',1)
	XCALL OUTPT (6,4,0,'2. ENDING   ITEM #',1)
ST_ITEM,
	XCALL INPUT (4,25,15,00,'AE',ENTRY,INXCTL,1)
	GOTO (PDISP,PDONE),INXCTL
	ST_ITEM = ENTRY(1,15)
	IF (ST_ITEM .EQ. BLANKS)
		BEGIN
		EN_ITEM = 'ZZZZZZZZZZZZZZZ'
		XCALL OUTPT (4,25,1,'ALL',1)
		XCALL OUTPT (6,25,1,' ',1)
		GOTO P_ANY
		END
	GOTO (P_ANY),CNGCTL
EN_ITEM,
	XCALL INPUT (6,25,15,00,'A ',ENTRY,INXCTL,1)
	GOTO (PDISP),INXCTL
	EN_ITEM = ENTRY(1,15)
	IF (EN_ITEM .EQ. BLANKS)
		BEGIN
		EN_ITEM = ST_ITEM
		XCALL OUTPT (6,25,0,EN_ITEM,1)
		END
P_ANY,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (P_PRINT,P_CNGBR),CNGCTL + 1
P_CNGBR,
	GOTO (ST_ITEM,EN_ITEM),WHATNO
	GOTO P_ANY

P_PRINT,
	CLEAR TBL_KEY
	TBLCOD = 'I1'
	I1_KEY = ST_ITEM
	FIND (CHN182,COPTBL,TBL_KEY)[ERR=PLOOP]

PLOOP,
	XCALL IOS (CHN182,COPTBL,READ,LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF
	IF (TBLCOD .NE. 'I1') GOTO EOF
	IF (I1_KEY .LT. ST_ITEM) GOTO PLOOP
	IF (I1_KEY .GT. EN_ITEM) GOTO EOF

	LOKCTL = 1
	XCALL ISIO (CHN041, ITMMAS, I1_KEY, READ, LOKCTL)
	IF (LOKCTL .NE. 0) DESCR = '*** NOT ON FILE ***'

;;;	XCALL SERCH (CHN042,ITMIDX,I1_KEY,1,15,BSEND,BSMID,SRCCTL,4,16,20,0,0,0,0)
;;;	IF (SRCCTL.NE.0 .OR. IRC041.EQ.0)
;;;	THEN	DESCR = '*** NOT ON FILE ***'
;;;	ELSE	XCALL IO (CHN041,ITMMAS,IRC041,READ,LOKCTL)

;;;	PLINE (1,15) = ITEMNO
	PLINE (1,15) = I1_KEY		;in case not found
	PLINE (17,46) = DESCR
	CALL PRINT
	CLEAR PRANG
	BQTY = 0
	I = 1
	WHILE (I1_QTY(I).NE.0 .AND. I.LE.NUMITM)
	  BEGIN
	  CLEAR PRANG
	  IF (I1_QTY(I) .EQ. 99999)
	  THEN	BEGIN
		PRANG(1,1) = '('
		PRANG(2,6) = 'OVER '
		PRANG(7,11) = BQTY, 'ZZZZX' [LEFT]
		END
	  ELSE	BEGIN
		EQTY = I1_QTY(I)-1
		PRANG(1,1) = '('
		PRANG(2,20) = BQTY, 'ZZZZX' [LEFT]
		PRANG(%TRIM(PRANG)+1,20) = '-'
		PRANG(%TRIM(PRANG)+1,20) = EQTY, 'ZZZZX' [LEFT]
	;;;;	PRANG(%TRIM(PRANG)+1,20) = ')'
	;;;;	PRANG(%TRIM(PRANG)+2,20) = I1_PRICE(I), 'ZZZ.XXX' [LEFT]
		END
	  PRANG(%TRIM(PRANG)+1,20) = ')'
	  PRANG(%TRIM(PRANG)+2,20) = I1_PRICE(I), 'ZZZZ.XXX' [LEFT]
	  PLINE (PRS(I), PRE(I)) = PRANG
	  BQTY = I1_QTY(I)
	  INCR I
	  END
	CALL PRINT
	CALL PRINT
	GOTO PLOOP

EOF,
	IF (PRNTON.EQ.1)  XCALL LPOFF(LPSW,SPLFIL,PGCNT)
	PRNTON = 0
PDONE,
	GOTO MENU

PRINT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (PRNTON .EQ. 0) CALL PRNTON
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HD1,HD,HD
&		,LG,LG,LG,0,080,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;-------------------------------------------------------------
PRNTON,
	SPLFIL (5,6) = 'EF'
	LPSW = 1		;PRINT,SPOOL, OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTON = 1
	RETURN
;-------------------------------------------------------------
;===================================================================


OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (1,'SU',182,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN182 = 1
	
	XCALL FILES (2,'SI',041,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN041 = 2

;;;	XCALL FILES (3,'I',042,SWITCH)
;;;	IF (SWITCH .EQ. 9) RETURN
;;;	CHN042 = 3

;;;	XCALL IO (CHN041,ITMMAS,1,READ,LOKCTL)
;;;	BSEND = ORG041

	OPNOK = 1
	RETURN
;----------------------------------------------------

CLOSE,
	CLOSE CHN041
;;;	CLOSE CHN042
	CLOSE CHN182
	RETURN
;----------------------------------------------------
	
	
