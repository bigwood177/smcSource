;VV3MNT.CP

;	COP TABLE MAINTENANCE
;	VULCAN SEAM TABLE
;
RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'


RECORD	CFGMAT
	.INCLUDE 'DEF:CFGMAT.DEF'

RECORD	CHANNEL
	CHN182	,D2


RECORD	PRINT
	TITLE	,A*,	'VULCAN SEAM TABLE'
	HD	,A6,	'NO HDR'
	LG	,A9,	'NO LEGEND'
	PLINE	,A80
	PRNTON	,D1
	LINCNT	,D2,60
	PGCNT	,D6
	LPSW	,D2
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	PRTCTL	,D3,080
	LPARG	,D1
	PRNTSW	,D1
	PRTCTR	,D1


RECORD	HD1
		,A*,	'KEY   GA  MAT G-SEAM          L-SEAM          S-SEAM'


RECORD	VARS
	A4	,A4
	ALPHA	,A12
	OPNOK	,D1
	BLANKS	,A30
	DEPT	,D4
	GA	,D2
	MAT	,D1
	DESC	,A30
	LOC	,A20	
	ST_DPT	,D4
	EN_DPT	,D4
	PGM	,D1
	ROW	,D2
	ENTRY	,A30
	INXCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	STORE	,D1,2
	DELETE	,D1,3
	LOKCTL	,D1
	WHATNO	,D2
	SELECT	,D1
	CNGCTL	,D1
	I	,D3
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'VULCAN SEAM TABLE',1)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO CLOSE

MENU,
	XCALL OUTPT (1,1,2,'VULCAN SEAM TABLE',1)
	XCALL OUTPT (3,9,0,'PLEASE SELECT APPLICATION',1)
	XCALL OUTPT (5,15,0,'1. TABLE MAINTENANCE',1)
	XCALL OUTPT (6,15,0,'2. PRINT TABLE',1)
MINPUT,
	XCALL INPUT (3,36,1,1,'#E',ENTRY,INXCTL,1)
	GOTO (MINPUT,ENDOFF), INXCTL
	PGM = ENTRY(1,1)
	GOTO (DISPLA,PRINT_TABLE),PGM
	GOTO MINPUT

DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,'VULCAN KEYS TABLE',1)
	XCALL OUTPT ( 4,4,0,'1. KEY',1)
	XCALL OUTPT ( 6,4,0,'2. GAUGE',1)
	XCALL OUTPT ( 8,4,0,'3. MATL',1)
	XCALL OUTPT (10,4,0,'4. G-SEAM',1)
	XCALL OUTPT (12,4,0,'5. L-SEAM',1)
	XCALL OUTPT (14,4,0,'6. S-SEAM',1)

DEPT,
	XCALL INPUT (4,24,04,00,'#E',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	DEPT = ENTRY(1,4)

	CLEAR TBL_KEY
	TBLCOD = 'VD'
	VD_KEY = DEPT
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .EQ. 0)
	THEN	XCALL OUTPT (4,32,0,VD_DESC,1)
	ELSE	BEGIN
		XCALL MESAG ('INVALID KEY',1)
		GOTO DEPT
		END

GA,
	XCALL INPUT (6,24,02,00,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	GA = ENTRY(1,2)
	USING GA SELECT
	(16,18,20,22,24,26),NOP
	(),	BEGIN
		XCALL MESAG ('INVALID GAUGE',1)
		GOTO GA
		END
	ENDUSING
MAT,
	clear mat
	XCALL INPUT (8,24,01,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL-1
	FOR I FROM 1 THRU C_MAX
		BEGIN
		IF (ENTRY(1,1) .EQ. C_MAT(I))
			BEGIN
			MAT = I
			EXITLOOP
			END			
		END
	IF (MAT .LE. 0)
		BEGIN
		XCALL MESAG ('MUST BE "G", "A", "P", "S" OR "V"',1)
		GOTO MAT
		END

	

	CLEAR TBL_KEY
	TBLCOD = 'V3'
	V3_KEY = DEPT
	V3_GA = GA
	V3_MAT = MAT

	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .EQ. 0)
		BEGIN
		SELECT = 2
		CALL DSPREC
		GOTO ANYCNG
		END
	SELECT = 1			;ADD MODE
	CLEAR COPTBL
	V3_KEY = DEPT
	V3_GA = GA
	V3_MAT = MAT
GSEAM,
	XCALL INPUT (10,24,15,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	V3_GSEAM = ENTRY(1,15)
	GOTO (ANYCNG),CNGCTL
LSEAM,
	XCALL INPUT (12,24,15,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	V3_LSEAM = ENTRY(1,15)
	GOTO (ANYCNG),CNGCTL
SSEAM,
	XCALL INPUT (14,24,15,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	V3_SSEAM = ENTRY(1,15)
	GOTO (ANYCNG),CNGCTL

ANYCNG,
	XCALL OUTPT (24,1,1,'FIELD # TO CHANGE            <TAB> = DELETE',1)
	XCALL INPUT (24,20,02,00,'#T',ENTRY,INXCTL,1)
	IF (INXCTL .EQ. 3)
		BEGIN
		XCALL OUTPT (24,1,1,'DELETE, ARE YOU SURE ?',1)
		XCALL INPUT (24,24,01,01,'YN',ENTRY,INXCTL,1)
		GOTO (ANYCNG),INXCTL-1
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,DELETE,LOKCTL)
		GOTO DISPLA
		END

	WHATNO = ENTRY(1,2)
	IF (WHATNO .EQ. 0)
	THEN 	CLEAR CNGCTL
	ELSE 	CNGCTL = 1

	GOTO (PROCES,CNGBR),CNGCTL+1
CNGBR,
	GOTO (DEPT, GA, MAT, GSEAM, LSEAM, SSEAM),WHATNO
	GOTO ANYCNG

PROCES,
	CASE SELECT OF
	BEGINCASE
	1:	BEGIN
		TBLCOD = 'V3'
		V3_KEY = DEPT
		V3_GA = GA
		V3_MAT = MAT
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,STORE,LOKCTL)
		END
	2:	XCALL ISIO (CHN182,COPTBL,TBL_KEY,WRITE,LOKCTL)
	ENDCASE
	GOTO DISPLA

ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('CP:VULMNU',1)
	STOP


DSPREC,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; DISPLAY RECORD
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	A4 = VD_KEY,	'ZZZX' [LEFT]
	XCALL OUTPT (4,24,1,A4,1)
;;;	XCALL OUTPT (6,24,1,VD_DESC,1)
	USING V3_MAT SELECT
	(1),	XCALL OUTPT (8,24,1,C_MAT(1),1)
	(2),	XCALL OUTPT (8,24,1,C_MAT(2),1)
	(3),	XCALL OUTPT (8,24,1,C_MAT(3),1)
	(4),	XCALL OUTPT (8,24,1,C_MAT(4),1)
	(5),	XCALL OUTPT (8,24,1,C_MAT(5),1)
	(),	XCALL OUTPT (8,24,1,'INVALID MATL',1)
	ENDUSING

	XCALL OUTPT (10,24,1,V3_GSEAM,1)
	XCALL OUTPT (12,24,1,V3_LSEAM,1)
	XCALL OUTPT (14,24,1,V3_SSEAM,1)

	RETURN
;-----------------------------------



;===================================================================
PRINT_TABLE,
;===================================================================
	LINCNT = 66
	PGCNT  = 0
	
PDISP,
	CNGCTL = 
	XCALL OUTPT (1,1,2,'PRINT VULCAN KEYS TABLE',1)
	XCALL OUTPT (4,4,0,'1. STARTING KEY',1)
	XCALL OUTPT (6,4,0,'2. ENDING   KEY',1)
ST_DPT,
	XCALL INPUT (4,27,04,00,'#E',ENTRY,INXCTL,1)
	GOTO (PDISP,PDONE),INXCTL
	ST_DPT = ENTRY(1,4)
	IF (ST_DPT .EQ. 0)
		BEGIN
		EN_DPT = 9999
		XCALL OUTPT (4,25,1,'ALL',1)
		XCALL OUTPT (6,25,1,' ',1)
		GOTO P_ANY
		END
	GOTO (P_ANY),CNGCTL
EN_DPT,
	XCALL INPUT (6,25,04,00,'# ',ENTRY,INXCTL,1)
	GOTO (PDISP),INXCTL
	EN_DPT = ENTRY(1,4)
	IF (EN_DPT .EQ. 0)
		BEGIN
		EN_DPT = ST_DPT
		A4 = EN_DPT,	'ZZZX' [LEFT]
		XCALL OUTPT (6,25,0,A4,1)
		END
P_ANY,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (P_PRINT,P_CNGBR),CNGCTL + 1
P_CNGBR,
	GOTO (ST_DPT,EN_DPT),WHATNO
	GOTO P_ANY

P_PRINT,
	CLEAR TBL_KEY
	TBLCOD = 'V3'
	V3_KEY = ST_DPT
	FIND (CHN182,COPTBL,TBL_KEY)[ERR=PLOOP]

PLOOP,
	XCALL IOS (CHN182,COPTBL,READ,LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF
	IF (TBLCOD .NE. 'V3') GOTO EOF
	IF (V3_KEY .LT. ST_DPT) GOTO PLOOP
	IF (V3_KEY .GT. EN_DPT) GOTO EOF


	PLINE (2,4) = V3_KEY, 'ZZZX'	;DD_DEPT
	PLINE (7,8) = V3_GA,	'XX'
	
	USING V3_MAT SELECT
	(1 THRU 5),	PLINE (12,12) = C_MAT(V3_MAT)
	(),		PLINE (12,12) = '?'
	ENDUSING

	PLINE (15,29) = V3_GSEAM
	PLINE (31,45) = V3_LSEAM
	PLINE (47,61) = V3_SSEAM

;KEY   GA  MAT G-SEAM          L-SEAM          S-SEAM
; AA   XX   A  AAAAAAAAAAAAAAA AAAAAAAAAAAAAAA AAAAAAAAAAAAAAA
;123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5
	CALL PRINT
	GOTO PLOOP

EOF,
	IF (PRNTON.EQ.1)  XCALL LPOFF(LPSW,SPLFIL,PGCNT)
	PRNTON  = 0

PDONE,
	GOTO MENU

PRINT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (PRNTON .EQ. 0) CALL PRNTON
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HD1,HD,HD
&		,LG,LG,LG,0,080,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;-------------------------------------------------------------
PRNTON,
	SPLFIL (5,6) = 'EF'
	LPSW = 1		;PRINT,SPOOL, OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTON = 1
	RETURN
;-------------------------------------------------------------
;===================================================================


OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (1,'SU',182,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN182 = 1

	OPNOK = 1
	RETURN
;----------------------------------------------------

CLOSE,
	CLOSE CHN182
	RETURN
;----------------------------------------------------
	


