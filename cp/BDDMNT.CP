;BDDMNT.CP
;		BACKDRAFT DAMPER PRICING

RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'

RECORD	CHANNEL
	CHN182	,D2

RECORD	PRINT
	TITLE	,A*,	'BACKDRAFT DAMPER TABLE'
	HD	,A6,	'NO HDR'
	LG	,A9,	'NO LEGEND'
	PLINE	,A132
	PRNTON	,D1
	LINCNT	,D2,60
	PGCNT	,D6
	LPSW	,D2
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	PRTCTL	,D3,080
	LPARG	,D1
	PRNTSW	,D1
	PRTCTR	,D1

RECORD	HD1
;;;	,	A*,	' MIN-PRICE  PRICE/SQIN  SST-MULT  ALUM-MULT  PROF-MULT'
	,	A*,	' MIN-PRICE  PRICE/SQFT  SST-MULT  ALUM-MULT  PROF-MULT    SL/CB FLANGE HI VEL'
RECORD	VARS
	OPNOK	,D1
	PGM	,D1
	ENTRY	,A30
	INXCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	STORE	,D1,2
	DELETE	,D1,3
	LOKCTL	,D1
	WHATNO	,D2
	SELECT	,D1
	CNGCTL	,D1
	I	,D3
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'BACKDRAFT DAMPER TABLE',1)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO CLOSE

MENU,
	XCALL OUTPT (1,1,2,'BACKDRAFT DAMPER TABLE',1)
	XCALL OUTPT (3,9,0,'PLEASE SELECT APPLICATION',1)
	XCALL OUTPT (5,15,0,'1. TABLE MAINTENANCE',1)
	XCALL OUTPT (6,15,0,'2. PRINT TABLE',1)
MINPUT,
	XCALL INPUT (3,36,1,1,'#E',ENTRY,INXCTL,1)
	GOTO (MINPUT,ENDOFF), INXCTL
	PGM = ENTRY(1,1)
	GOTO (DISPLA,PRINT_TABLE),PGM
	GOTO MINPUT

DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,'BACKDRAFT DAMPER TABLE',1)
	XCALL OUTPT ( 4,4,0,'1. MINIMUM PRICE',1)
	XCALL OUTPT ( 6,4,0,'2. PRICE/SQFT',1)
	XCALL OUTPT (08,4,0,'3. STAINLESS MULT',1)
	XCALL OUTPT (10,4,0,'4. ALUMINUM MULT',1)
	XCALL OUTPT (12,4,0,'5. PROFIT MULT',1)
	XCALL OUTPT (14,4,0,'6. SPRING LOAD/COUNTERBALANCE (per unit)',1)
;                            45678901234567890123456789012345678901234567890
;                                  1         2         3         4         5
	XCALL OUTPT (16,4,0,'7. FLANGED MODEL (per sq ft)',1)
	XCALL OUTPT (18,4,0,'8. MED VELOCITY MODEL (per sq ft)',1)
;;;	XCALL OUTPT (18,4,0,'8. HIGH VELOCITY MODEL (per sq ft)',1)
	CLEAR TBL_KEY
	TBL_KEY(1,2) = 'BD'
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .EQ. 0)
		BEGIN
		SELECT = 2
		CALL DSPREC
		GOTO ANYCNG
		END


	SELECT = 1			;ADD MODE
	CLEAR COPTBL
MIN,
	XCALL INPUT (4,24,05,00,'$E',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	BD_MIN = ENTRY(1,5)
	GOTO (ANYCNG),CNGCTL
PCUN,
	XCALL INPUT (6,24,05,00,'$ ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	BD_PCUN = ENTRY(1,5)
	GOTO (ANYCNG),CNGCTL
SMUL,
	XCALL INPUT (8,24,03,00,'$ ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	BD_SMUL = ENTRY(1,3)
	GOTO (ANYCNG),CNGCTL
AMUL,
	XCALL INPUT (10,24,03,00,'$ ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	BD_AMUL = ENTRY(1,3)
	GOTO (ANYCNG),CNGCTL
PMUL,
	XCALL INPUT (12,24,03,00,'$ ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	BD_PMUL = ENTRY(1,3)
	GOTO (ANYCNG),CNGCTL
SL,
	XCALL INPUT (14,49,05,00,'$ ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	BD_SL = ENTRY(1,5)
	GOTO (ANYCNG),CNGCTL
FM,
	XCALL INPUT (16,49,05,00,'$ ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	BD_FM = ENTRY(1,5)
	GOTO (ANYCNG),CNGCTL
HV,
	XCALL INPUT (18,49,05,00,'$ ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	BD_HV = ENTRY(1,5)
	GOTO ANYCNG

ANYCNG,
	XCALL OUTPT (24,1,1,'FIELD # TO CHANGE            <TAB> = DELETE',1)
	XCALL INPUT (24,20,02,00,'#T',ENTRY,INXCTL,1)
	IF (INXCTL .EQ. 3)
		BEGIN
		XCALL OUTPT (24,1,1,'DELETE, ARE YOU SURE ?',1)
		XCALL INPUT (24,24,01,01,'YN',ENTRY,INXCTL,1)
		GOTO (ANYCNG),INXCTL-1
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,DELETE,LOKCTL)
		GOTO DISPLA
		END

	WHATNO = ENTRY(1,2)
	IF (WHATNO .EQ. 0)
	THEN 	CLEAR CNGCTL
	ELSE 	CNGCTL = 1

	GOTO (PROCES,CNGBR),CNGCTL+1
CNGBR,
	GOTO (MIN,PCUN,SMUL,AMUL,PMUL,SL,FM,HV),WHATNO
	GOTO ANYCNG

PROCES,
	CASE SELECT OF
	BEGINCASE
	1:	BEGIN
		TBLCOD = 'BD'
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,STORE,LOKCTL)
		END
	2:	XCALL ISIO (CHN182,COPTBL,TBL_KEY,WRITE,LOKCTL)
	ENDCASE
	GOTO MENU

ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('CP:TBLMNU',1)
	STOP


DSPREC,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; DISPLAY RECORD
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	ENTRY(1,6) = BD_MIN,	'ZZX.XX' [LEFT]
	XCALL OUTPT (4,24,1,ENTRY(1,6),1)

	ENTRY(1,6) = BD_PCUN,	'ZZX.XX' [LEFT]
	XCALL OUTPT (6,24,1,ENTRY(1,6),1)

	ENTRY(1,4) = BD_SMUL,	'X.XX'
	XCALL OUTPT (8,24,1,ENTRY(1,4),1)

	ENTRY(1,4) = BD_AMUL,	'X.XX'
	XCALL OUTPT (10,24,1,ENTRY(1,4),1)

	ENTRY(1,4) = BD_PMUL,	'X.XX'
	XCALL OUTPT (12,24,1,ENTRY(1,4),1)

	ENTRY(1,6) = BD_SL,	'ZZX.XX' [LEFT]
	XCALL OUTPT (14,49,1,ENTRY(1,6),1)

	ENTRY(1,6) = BD_FM,	'ZZX.XX' [LEFT]
	XCALL OUTPT (16,49,1,ENTRY(1,6),1)

	ENTRY(1,6) = BD_HV,	'ZZX.XX' [LEFT]
	XCALL OUTPT (18,49,1,ENTRY(1,6),1)
	RETURN
;-----------------------------------


;===================================================================
PRINT_TABLE,
;===================================================================

	LINCNT = 66
	PGCNT  = 0
	
PDISP,
	CLEAR TBL_KEY
	TBLCOD = 'BD'
	FIND (CHN182,COPTBL,TBL_KEY)[ERR=PT_LOOP]
PT_LOOP,
	XCALL IOS (CHN182,COPTBL,READ,LOKCTL)
	IF (LOKCTL .NE. 0) GOTO PT_EOF
	IF (TBLCOD .NE. 'BD') GOTO PT_EOF
	
	PLINE(5,10) = BD_MIN,	'ZZX.XX'
	PLINE(17,22) = BD_PCUN,	'ZZX.XX'
	PLINE(29,32) = BD_SMUL,	'X.XX'
	PLINE(40,43) = BD_AMUL,	'X.XX'
	PLINE(51,54) = BD_PMUL,	'X.XX'
	PLINE(58,63) = BD_SL,	'ZZX.XX'
	PLINE(65,70) = BD_FM,	'ZZX.XX'
	PLINE(72,77) = BD_HV,	'ZZX.XX'
; MIN-PRICE  PRICE/SQFT  SST-MULT  ALUM-MULT  PROF-MULT    SL/CB FLANGE HI VEL
;    ZZX.XX      ZZX.XX      X.XX       X.XX       X.XX   ZZX.XX ZZX.XX ZZX.XX
;12345678901234567890123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5         6         7         8
	CALL PRINT

PT_EOF,
EOF,
	IF (PRNTON.EQ.1)  XCALL LPOFF(LPSW,SPLFIL,PGCNT)
	PRNTON  = 0
	CLOSE 10
PDONE,
	GOTO MENU

PRINT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (PRNTON .EQ. 0) CALL PRNTON
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HD1,HD,HD
&		,LG,LG,LG,0,080,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;-------------------------------------------------------------
PRNTON,
	SPLFIL (5,6) = 'EF'
	LPSW = 1		;PRINT,SPOOL, OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTON = 1
	RETURN
;-------------------------------------------------------------
;===================================================================


OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (1,'SU',182,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN182 = 1

	OPNOK = 1
	RETURN
;----------------------------------------------------

CLOSE,
	CLOSE CHN182
	RETURN
;----------------------------------------------------
	
	
