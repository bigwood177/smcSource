;EMPLBS.CP
;
;	PRINT EMPLOYEE POUNDS REPORT

RECORD	EMPWRK
	EMP_NO	,D3
	EMP_ORD	,D8
	EMP_LBS	,D8

RECORD	DPTLBS
	.INCLUDE 'DEF:RD128A.DEF'

RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'


RECORD TITLE
		,A39,	'EMPLOYEE POUNDS REPORT'
RECORD HDR1
		,A*,	'NBR EMPLOYEE                        ORERS PACKED  TOTAL LBS  AVG LBS/ORDER'
RECORD	PRINT
	NOHD	,A6,	'NO HDR'
	LINCNT	,D2,	60
	SPLFIL	,A14
	LPONSW	,D1
	LPARG	,D1
	RPTNUM	,D3
	PRTTYP	,A1
	PGCNT	,D6,	000000
	PLINE	,A132
	PRTCTL	,D3
	LPSW	,D2

RECORD	TOTALS
	EMPLBS	,D8
	EMPORD	,D8
	TOTLBS	,D8
	TOTORD	,D8
	AVG	,D8

RECORD	CHANNEL
	CHN128	,D2
	CHN182	,D2
	CHNWRK	,D2

RECORD	VARS
	WRKFIL	,A14,	'SPL:EMPLBS.ISM'
	KEY_SPEC,A*,	'START=1, LENGTH=3, ORDER=A'
	OPNOK	,D1
	SAVNBR	,D3
	ST_DAT	,D8
	EN_DAT	,D8
	XDATE	,D8
	ENTRY	,A30
	INXCTL	,D1
	CNGCTL	,D1
	WHATNO	,D2
	LOKCTL	,D1
	READ	,D1,0
	SWITCH	,D1
	V	,D1


PROC
	XCALL TERID (v)
	XCALL OUTPT (1,1,2,'EMPLOYEE POUNDS REPORT',1)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO ENDOFF
DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,'EMPLOYEE POUNDS REPORT',1)
	XCALL OUTPT (4,4,1,'1. START DATE',1)
	XCALL OUTPT (6,4,1,'2. END   DATE',1)
ST_DAT,
	XCALL INPUT (4,20,08,00,'DE',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	ST_DAT = ENTRY(1,8)
	IF (ST_DAT .EQ. 0)
		BEGIN
		EN_DAT = 99999999
		XCALL OUTPT (4,20,1,'ALL',1)
		GOTO ANYCN
		END
	GOTO (ANYCN), CNGCTL

EN_DAT,
	XCALL INPUT (6,20,08,00,'DE',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	EN_DAT = ENTRY(1,8)
	IF (EN_DAT .EQ. 0)
		BEGIN
		EN_DAT = ST_DAT
		XDATE(1,4) = EN_DAT(5,8)
		XDATE(5,8) = EN_DAT(1,4)
		ENTRY(1,10) = XDATE, 'ZX/XX/XXXX'
		XCALL OUTPT (6,20,1,ENTRY(1,10),1)
		END

ANYCN,
	XCALL ANYCN (CNGCTL, WHATNO)
	GOTO (PROCES, CNGBR), CNGCTL+1

CNGBR,
	GOTO (ST_DAT, EN_DAT), WHATNO
	GOTO ANYCN

PROCES,
	SAVNBR = -1
	FIND (CHN128, DPTLBS, ^FIRST, KRF=2) [ERR=LOOP]
LOOP,
	XCALL IOS (CHN128, DPTLBS, READ, LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF

	IF (DI_DAT .LT. ST_DAT) GOTO LOOP
	IF (DI_DAT .GT. EN_DAT) GOTO EOF

	READ (CHNWRK, EMPWRK, DI_NBR)[ERR=NO_REC]
	EMP_ORD = EMP_ORD + 1
	EMP_LBS = EMP_LBS + DI_LBS
	WRITE (CHNWRK, EMPWRK, DI_NBR)
	GOTO LOOP

NO_REC,
	CLEAR EMPWRK
	EMP_NO = DI_NBR
	EMP_ORD = 1
	EMP_LBS = DI_LBS
	STORE (CHNWRK, EMPWRK, EMP_NO)

	GOTO LOOP
EOF,
	CLEAR PLINE
	FIND (CHNWRK, EMPWRK, ^FIRST) [ERR=LOOP2]
LOOP2,
	READS (CHNWRK, EMPWRK, EOF2)
	
	CLEAR COPTBL
	TBLCOD = 'EI'
	EI_TNBR = EMP_NO
	XCALL ISIO (CHN182, COPTBL, TBL_KEY, READ, LOKCTL)
	IF (LOKCTL .NE. 0) EI_NAME = '*NOT ON FILE*'
	PLINE (1,3) = EMP_NO,	'ZZX'
	PLINE (5,34) = EI_NAME
	PLINE (43,48) = EMP_ORD,	'ZZ,ZZX'
	PLINE (53,59) = EMP_LBS,	'ZZZ,ZZX'

	AVG =
	IF (EMP_ORD .GT. 0) AVG = (EMP_LBS*100/EMP_ORD)#1

	PLINE (67,74) = AVG,	'ZZ,ZZX.X'
	CALL PRINT

	TOTLBS = TOTLBS + EMP_LBS
	TOTORD = TOTORD + EMP_ORD

	GOTO LOOP2

EOF2,
	PLINE = 'TOTALS:'
	PLINE (42,48) = TOTORD,	'ZZZ,ZZX'
	PLINE (53,59) = TOTLBS,	'ZZZ,ZZX'

	AVG =
	IF (TOTORD .GT. 0) AVG = (TOTLBS*100/TOTORD)#1

	PLINE (67,74) = AVG,	'ZZ,ZZX.X'
	CALL PRINT

	GOTO ENDOFF



PRINT,
	IF (LPONSW.EQ.0) CALL PRNTON
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,NOHD,NOHD,
&			'NO LEGEND',' ',' ',1,80,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
PRNTON,
	SPLFIL (5,6) = 'EL'
	LPSW = 1	; PRINT, SPOOL OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPONSW = 1
	RETURN
;************************************************************************



ENDOFF,
	CALL CLOSE
	IF (LPONSW.EQ.1) XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	LPONSW = 0
	LINCNT = 60
	PGCNT =
	XCALL FLAGS (7000000)
	STOP

OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	OPNOK =

	SWITCH = 5
	XCALL FILES (28, 'SI', 128, SWITCH)	;128 = DPTLBS
	IF (SWITCH .EQ. 9) RETURN
	CHN128 = 28

	SWITCH = 5
	XCALL FILES (17, 'SI', 182, SWITCH)	;182 - COPTBL
	IF (SWITCH .EQ. 9) RETURN
	CHN182 = 17

	XCALL ISAMC (WRKFIL, 19, 1, KEY_SPEC)
	OPEN (19, 'SU', WRKFIL)
	CHNWRK = 19

	OPNOK = 1
	RETURN
;-----------------------------------------------

CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	IF (CHN128) CLOSE CHN128
	IF (CHN182) CLOSE CHN182
	IF (CHNWRK)
		BEGIN
 		CLOSE CHNWRK
		XCALL DELET (WRKFIL)
		END
	RETURN
;-------------------------------------------------

END

;NBR EMPLOYEE                        ORERS PACKED  TOTAL LBS  AVG LBS/ORDER
;ZZX AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA        Z,ZZX    ZZZ,ZZX           ZX.X
;12345678901234567890123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5         6         7

