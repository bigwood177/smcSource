;DESQTY.CP
;
RECORD	ORDLIN
	.INCLUDE 'DEF:RD045A.DEF'

RECORD	HD1
	,	A*,	'FOR ITEM '
	HD_ITEM	,A15

RECORD	HD2
	,	A*,	'ITEM                             QTY-SOLD'


RECORD	PRINT
	TITLE	,A*,	'QTY SOLD BY DESCRIPTION'
	HD	,A6,	'NO HDR'
	LG	,A9,	'NO LEGEND'
	PLINE	,A132
	PRNTON	,D1
	LINCNT	,D2,60
	PGCNT	,D6
	LPSW	,D2
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	PRTCTL	,D3,132
	LPARG	,D1
	PRNTSW	,D1
	PRTCTR	,D1

RECORD	CHANNEL
	CHNLIN	,D2
	CHNWRK	,D2

RECORD	VARS
	OPNOK	,D1
	WRKFIL	,A14
	ENTRY	,A30
	INXCTL	,D1
	CNGCTL	,D1
	WHATNO	,D2
	READ	,D1,0
	LOKCTL	,D1
	SAVDSC	,A30
	SRCKEY	,A15
	D_QTY	,D8
	T_QTY	,D8
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID(V)
	V=1
	XCALL OUTPT (1,1,2,'QTY SOLD BY DESCRIPTION',1)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO ENDOFF
DISPLA,
	XCALL OUTPT (1,1,2,'QTY SOLD BY DESCRIPTION',1)
	CLEAR CNGCTL
	XCALL OUTPT (6,4,0,'1. ITEM #',1)
	XCALL INPUT (6,15,15,00,'AE',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	SRCKEY=ENTRY(1,15)
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO(DISPLA),CNGCTL
PROCES,
	HD_ITEM = SRCKEY
	CLEAR D_QTY, T_QTY

	SAVDSC = '-1'
	FIND (CHNLIN, ORDLIN, SRCKEY, KRF=1) [ERR=EOF]
BLDWRK,
	XCALL IOS(CHNLIN, ORDLIN, READ, LOKCTL)
	IF (LOKCTL .NE. 0) GOTO WRK_EOF
	IF (LITMNO .NE. SRCKEY) GOTO WRK_EOF
	WRITES(10,ORDLIN)
	GOTO BLDWRK

WRK_EOF,
	CLOSE CHNWRK
	SORT(INPUT=WRKFIL, RECORD=ORDLIN, KEY=(LDESCR))
	OPEN(10,I,WRKFIL)
		
LOOP,
	XCALL IOS(CHNWRK, ORDLIN, READ, LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF
	IF(LDESCR .NE. SAVDSC) CALL NEWDSC
	D_QTY = D_QTY + LQTYSH			;QTY SHIPPED
	GOTO LOOP

NEWDSC,
	IF(SAVDSC .EQ. '-1') GOTO ENDDSC
	PLINE(1,30) = SAVDSC
	PLINE(32,41)= D_QTY,	'ZZ,ZZZ,ZZX-'
	CALL PRINT
	T_QTY = T_QTY + D_QTY

ENDDSC,
	CLEAR D_QTY
	SAVDSC = LDESCR
	RETURN

EOF,
	CALL NEWDSC
	CALL PRINT
	PLINE(1,30) = 'TOTAL FOR ALL DESCRIPTIONS:'
	PLINE(32,41)= T_QTY,	'ZZ,ZZZ,ZZX-'
	CALL PRINT
	IF (PRNTON.EQ.1)  XCALL LPOFF(LPSW,SPLFIL,PGCNT)
	
ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('CP:COPRPT',1)
;===========================================

PRINT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (PRNTON .EQ. 0) CALL PRNTON
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HD1,HD2,HD
&		,LG,LG,LG,0,132,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;-------------------------------------------------------------
PRNTON,
	SPLFIL (5,6) = 'EF'
	LPSW = 1		;PRINT,SPOOL, OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTON = 1
	RETURN
;-------------------------------------------------------------

OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK
	XCALL FILES(1,'SI',185,SWITCH)
	IF (SWITCH.EQ.9) RETURN
	CHNLIN = 1

	WRKFIL='SPL:DESQTY.DAT'
	OPEN(10,O,WRKFIL)
	CHNWRK = 10

	OPNOK = 1
	RETURN
;-----------------------------------------

CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CLOSE CHNLIN
	CLOSE CHNWRK

	RETURN
;-----------------------------------------


;ITEM                             QTY-SOLD
;AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA ZZ,ZZZ,ZZX-
;1234567890123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5         6         7
