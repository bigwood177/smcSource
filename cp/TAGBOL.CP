;TAGBOL.CP
;
;	FIND TAG-A-LONG BOL
;
RECORD	BLHEAD
	.INCLUDE 'DEF:RD178A.DEF'

RECORD	BLLINE
	.INCLUDE 'DEF:RD179A.DEF'
;
RECORD	CHANNEL
	CHN178	,D2
	CHN179	,D2

RECORD	FOUND_MSG
		,A*,	'THIS ORDER IS SHIPPING WITH: '
	FM_ORDNO	,A6

RECORD	VARS
	ORDNO	,D6
	AORDNO	,A7
	OPNOK	,D1
	ENTRY	,A30
	INXCTL	,D1
	CNGCTL	,D1
	WHATNO	,D2
	READ	,D1,0
	LOKCTL	,D1
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'FIND A TAG-A-LONG',1)
	CALL OPENS
	IF (.NOT. OPNOK) GOTO ENDOFF
DISPLA,
	XCALL OUTPT (1,1,2,'FIND A TAG-A-LONG',1)
	CLEAR CNGCTL
	XCALL OUTPT (5,5,0,'ORDER #',1)
	XCALL INPUT (5,14,06,00,'#E',ENTRY,INXCTL,1)
	GOTO (DISPLA, ENDOFF), INXCTL
	ORDNO = ENTRY(1,6)
	CNGCTL = 1
	XCALL ANYCN(CNGCTL, WHATNO)
	GOTO (DISPLA),CNGCTL
PROCES,
	AORDNO = ORDNO,	'XXXXXX'
	READ (CHN178, BLHEAD, ORDNO) [ERR=CHECK_LINE]
	IF (BHORDN .EQ. ORDNO)
		BEGIN
		XCALL MESAG ('This order has its own Bill of Lading',1)
		GOTO DISPLA
		END
CHECK_LINE,
	FIND (CHN179, BLLINE, ^FIRST) [ERR=NO_BL]
LOOP,
	READS (CHN179, BLLINE, EOF)
	IF (BLCLAS .NE. 888) GOTO LOOP
	IF (BLUOM .NE. AORDNO) GOTO LOOP
	FM_ORDNO = BLORDN,	'XXXXXX'
	XCALL MESAG (FOUND_MSG,1)
	GOTO DISPLA
EOF,

NO_BL,
	XCALL MESAG ('This order is not on ANY Bill of Lading',1)
	GOTO DISPLA
ENDOFF,
	CALL CLOSE
	STOP
OPENS,
	CLEAR OPNOK
	SWITCH = 5
	XCALL FILES (1, 'SI', 178, SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN178 = 1

	XCALL FILES (2,'SI',179, SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN179 = 2

	OPNOK = 1
	RETURN
CLOSE,
	IF (CHN178) CLOSE CHN178
	IF (CHN179) CLOSE CHN179
	RETURN
	
