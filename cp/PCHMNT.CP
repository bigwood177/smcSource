;PCHMNT.CP
;
;		AUTHOR: SHERWOOD S. QUIRING
;		DATE  : 18-DEC-1996
;
RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'

;-
RECORD	PRINT
	TITLE	,A*,	'PRESSURE CHART TABLE'
	HD	,A6,	'NO HDR'
	LG	,A9,	'NO LEGEND'
	PLINE	,A80
	PRNTON	,D1
	LINCNT	,D2,60
	PGCNT	,D6
	LPSW	,D2
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	PRTCTL	,D3,080
	LPARG	,D1
	PRNTSW	,D1
	PRTCTR	,D1

RECORD	HD1
	,A*,	'   CHART LENGTH DIM GAUGE  PUNCH PATTERN'

RECORD	CHKEY
	K_CODE	,A2
	K_CHART	,D3
	K_LEN	,D1
	K_DIM	,D3

RECORD	CHANNEL
	CHN182	,D2

RECORD	CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2

RECORD	VARS
	SAVCHT	,D3
	SAVLEN	,D1
	ST_CHART,	D3
	EN_CHART,	D3
	I	,D5
	J	,D5
	SELECT	,D1
	PGM	,D1
	OPNOK	,D1
	ENTRY	,A30
	INXCTL	,D1
	WHATNO	,D2
	CNGCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	STORE	,D1,2
	DELETE	,D1,3
	LOKCTL	,D1
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	V = 1

	XCALL OUTPT (1,1,2,'PRESSURE CHART TABLE',1)
	CALL OPENS
	IF (.NOT. OPNOK)
		BEGIN
		CALL CLOSE
		XCALL PGCHN ('CP:TBLMNU',1)
		END

MENU,
	XCALL OUTPT (1,1,2,'PRESSURE CHART TABLE',1)
	XCALL OUTPT (3,9,0,'PLEASE SELECT APPLICATION',1)
	XCALL OUTPT (5,15,0,'1. TABLE MAINTENANCE',1)
	XCALL OUTPT (6,15,0,'2. PRINT TABLE',1)
MINPUT,
	XCALL INPUT (3,36,1,1,'#E',ENTRY,INXCTL,1)
	GOTO (MINPUT,ENDOFF), INXCTL
	PGM = ENTRY(1,1)
	GOTO (DISPLA,PRINT_TABLE),PGM
	GOTO MINPUT
DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,'PRESSURE CHART TABLE',1)
	XCALL OUTPT ( 4,4,0,'1. CHART',1)
	XCALL OUTPT ( 6,4,0,'2. LENGTH',1)
	XCALL OUTPT ( 8,4,0,'3. MAX DIM',1)
	XCALL OUTPT (10,4,0,'4. GAUGE',1)
	XCALL OUTPT (12,4,0,'5. PUNCH',1)

PC_CHART,
	CTL = '04,16,03,00,#E'
	CALL INPUT
	GOTO (DISPLA,MENU),INXCTL
	PC_CHART = ENTRY(1,3)
	IF (PC_CHART .EQ. 0) PC_CHART = SAVCHT
	IF (PC_CHART .EQ. 0) GOTO PC_CHART
	ENTRY(1,4) = PC_CHART,	'ZX.X' [LEFT]
	XCALL OUTPT(ROW,COL,1,ENTRY(1,4),1)
	SAVCHT = PC_CHART
	GOTO (ANYCNG),CNGCTL

PC_LEN,
	CTL = '06,16,01,00,# '
	CALL INPUT
	GOTO(DISPLA),INXCTL
	PC_LEN = ENTRY(1,1)
	IF (PC_LEN .EQ. 0) 
		BEGIN
		PC_LEN = SAVLEN
		ENTRY(1,1)=PC_LEN,'X'
		XCALL OUTPT(ROW,COL,1,ENTRY(1,1),1)
		END
	IF (PC_LEN .EQ. 0) GOTO PC_LEN
	SAVLEN = PC_LEN
	GOTO(ANYCNG),CNGCTL
PC_DIM,
	CTL = '08,16,03,00,# '
	CALL INPUT
	GOTO(DISPLA),INXCTL
	PC_DIM = ENTRY(1,3)
	K_CODE = 'PC'
	K_CHART = PC_CHART
	K_LEN = PC_LEN
	K_DIM = PC_DIM
	XCALL ISIO (CHN182,COPTBL,CHKEY,READ,LOKCTL)
	IF (LOKCTL .EQ. 0)
	THEN	BEGIN
		SELECT = 2		;EXISTING RECORD
		CALL DSPREC
		GOTO ANYCNG
		END
	ELSE	BEGIN
		SELECT = 1
		PC_CHART = K_CHART
		PC_LEN   = K_LEN
		PC_DIM   = K_DIM
		END
	GOTO (ANYCNG),CNGCTL

PC_GAUGE,
	CTL = '10,16,02,00,# '
	CALL INPUT
	GOTO (ANYCNG),CNGCTL
	PC_GAUGE = ENTRY(1,2)
	IF(PC_GAUGE.LT.18 .OR. PC_GAUGE.GT.26) GOTO PC_GAUGE
	GOTO (ANYCNG),CNGCTL

PC_PUNCH,
	CTL = '12,16,06,00,A '
	CALL INPUT
	GOTO (DISPLA),INXCTL
	PC_PUNCH = ENTRY(1,6)
	GOTO (ANYCNG),CNGCTL
ANYCNG,
;;;	XCALL ANYCN(CNGCTL,WHATNO)
	XCALL OUTPT (24,1,1,'FIELD # TO CHANGE            <TAB> = DELETE',1)
	XCALL INPUT (24,20,02,00,'#T',ENTRY,INXCTL,1)
	IF (INXCTL .EQ. 3)
		BEGIN
		XCALL OUTPT (24,1,1,'DELETE, ARE YOU SURE ?',1)
		XCALL INPUT (24,24,01,01,'YN',ENTRY,INXCTL,1)
		GOTO (ANYCNG),INXCTL-1
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,DELETE,LOKCTL)
		GOTO DISPLA
		END

	WHATNO = ENTRY(1,2)
	IF (WHATNO .EQ. 0)
	THEN 	CLEAR CNGCTL
	ELSE 	CNGCTL = 1
;-
	GOTO (PROCES,CNGBR),CNGCTL + 1
CNGBR,
	CASE SELECT OF
	BEGINCASE
	1:	GOTO (PC_CHART,PC_LEN,PC_DIM,PC_GAUGE,PC_PUNCH),WHATNO
	2:	GOTO (ANYCNG,ANYCNG,ANYCNG,PC_GAUGE,PC_PUNCH),WHATNO
	ENDCASE

	GOTO ANYCNG
PROCES,
	TBLCOD = 'PC'
	
	CASE SELECT OF
	BEGINCASE
	1:	XCALL ISIO (CHN182,COPTBL,TBL_KEY,STORE,LOKCTL)
	2:	XCALL ISIO (CHN182,COPTBL,TBL_KEY,WRITE,LOKCTL)
	3:	BEGIN
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,DELETE,LOKCTL)
		XCALL MESAG ('RECORD DELETED !',1)
		END
	ENDCASE
	GOTO DISPLA

DSPREC,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR ENTRY
	ENTRY(1,4) = PC_CHART,	'ZZ.X' [LEFT]
	XCALL OUTPT (4,16,0,ENTRY(1,4),1)
	
	CLEAR ENTRY
	ENTRY(1,1) = PC_LEN,	'X'
	XCALL OUTPT (6,16,0,ENTRY(1,1),1)

	CLEAR ENTRY
	ENTRY(1,3) = PC_DIM,	'ZZX' [LEFT]
	XCALL OUTPT (8,16,0,ENTRY(1,3),1)

	CLEAR ENTRY
	ENTRY(1,2) = PC_GAUGE,	'ZX'
	XCALL OUTPT (10,16,0,ENTRY,1)

	XCALL OUTPT (12,13,0,PC_PUNCH,1)

	RETURN
;--------------------------------------------------------

INPUT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL INPUT(ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,1)
	RETURN
;--------------------------------------------------------

ENDOFF,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CALL CLOSE
	XCALL PGCHN ('CP:TBLMNU',1)
;------------------------------------

;===================================================================
PRINT_TABLE,
;===================================================================
	LINCNT = 66
	PGCNT  = 0
	
PDISP,
	CNGCTL = 
	XCALL OUTPT (1,1,2,'PRINT PRESSURE CHART TABLE',1)
	XCALL OUTPT (4,4,0,'1. STARTING CHART',1)
	XCALL OUTPT (6,4,0,'2. ENDING   CHART',1)
ST_CHART,
	XCALL INPUT (4,25,03,00,'#E',ENTRY,INXCTL,1)
	GOTO (PDISP,PDONE),INXCTL
	ST_CHART = ENTRY(1,3)
	IF (ST_CHART .EQ. 0)
		BEGIN
		EN_CHART = 999
		XCALL OUTPT (4,25,1,'ALL',1)
		XCALL OUTPT (6,25,1,' ',1)
		GOTO P_ANY
		END
	ENTRY(1,4)=ST_CHART,'ZX.X' [LEFT]
	XCALL OUTPT(4,25,1,ENTRY(1,4),1)
	GOTO (P_ANY),CNGCTL
EN_CHART,
	XCALL INPUT (6,25,03,00,'# ',ENTRY,INXCTL,1)
	GOTO (PDISP),INXCTL
	EN_CHART = ENTRY(1,3)
	IF (EN_CHART .EQ. 0)
		BEGIN
		EN_CHART = ST_CHART
		ENTRY(1,4)=EN_CHART,'ZX.X' [LEFT]
		XCALL OUTPT (6,25,0,ENTRY(1,4),1)
		END
P_ANY,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (P_PRINT,P_CNGBR),CNGCTL + 1
P_CNGBR,
	GOTO (ST_CHART,EN_CHART),WHATNO
	GOTO P_ANY

P_PRINT,
	CLEAR TBL_KEY
	TBLCOD = 'PC'
	PC_CHART = ST_CHART
	FIND (CHN182,COPTBL,TBL_KEY)[ERR=PLOOP]

PLOOP,
	XCALL IOS (CHN182,COPTBL,READ,LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF
	IF (TBLCOD .NE. 'PC') GOTO EOF
	IF (PC_CHART .LT. ST_CHART) GOTO PLOOP
	IF (PC_CHART .GT. EN_CHART) GOTO EOF

	PLINE (5,8) = PC_CHART, 'ZX.X'
	PLINE(12,12) = PC_LEN,	'X'
	PLINE(17,19) = PC_DIM,	'ZZX'
	PLINE(24,25) = PC_GAUGE,'ZX'
	PLINE(28,52) = PC_PUNCH
	CALL PRINT
	GOTO PLOOP
;   CHART LENGTH DIM GAUGE  PUNCH PATTERN
;    XX.X   X    XXX    XX  AAAAAAAAAAAAAAAAAAAAAAAAA
;123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5
EOF,
	IF (PRNTON.EQ.1)  
		BEGIN
		XCALL LPOFF(LPSW,SPLFIL,PGCNT)
		CLEAR PRNTON
		END
PDONE,
	GOTO MENU

PRINT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (PRNTON .EQ. 0) CALL PRNTON
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HD1,HD,HD
&		,LG,LG,LG,0,080,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;-------------------------------------------------------------
PRNTON,
	SPLFIL (5,6) = 'EF'
	LPSW = 1		;PRINT,SPOOL, OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTON = 1
	RETURN
;-------------------------------------------------------------
;===================================================================
OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (1,'SU',182,SWITCH)		;182 - COPTBL
	IF (SWITCH .EQ. 9) RETURN
	CHN182 = 1
	OPNOK = 1
	RETURN
;------------------------------------

CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (CHN182) CLOSE CHN182
	RETURN
;------------------------------------


