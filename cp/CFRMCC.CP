;cfrmcc.cp		- common carrier
;CONFRM.CP
;SHIPMENT CONFIRMATIONS
;
RECORD	CONFRM
	.INCLUDE 'DEF:RD158A.DEF'

RECORD	ORDHDR
	.INCLUDE 'DEF:RD044A.DEF'

RECORD	BLHTAG	;TAG-ALONG FILE
	.INCLUDE 'DEF:RD066A.DEF'

RECORD
	DT	,A20
RECORD,X
	YYYY	,D4
	MM	,D2
	DD	,D2
	HH	,D2
	MIN	,D2

RECORD	ADT
	AYYYY	,A4
		,A1, '/'
	AMM	,A2
		,A1, '/'
	ADD	,A2
		,A1, ' '
	AHH	,A2
		,A1, ':'
	AMIN	,A2


RECORD	CHANNEL
	CHN044	,D2
	CHN066	,D2
	CHN158	,D2


RECORD	VARS
	OPNOK	,D1
	A1	,A1
	BS	,A1	; BACKSPACE 8
	EN	,A1	; END 127
	CW	,A1	; ^W 23
	PGM_SRC	,A1
	XORD	,D6
	SELECT	,D1
	ENTRY	,A30
	INXCTL	,D1
	CNGCTL	,D1
	WHATNO	,D2
	READ	,D1,0
	WRITE	,D1,1
	STORE	,D1,2
	LOKCTL	,D1
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'SHIPPING CONFIRMATION',1)
;
	XCALL ASCII (8, BS)
	XCALL ASCII (127, EN)
	XCALL ASCII (23, CW)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO ENDOFF
	PGM_SRC = 'P'	;common carrier

DISPLA,
	XCALL OUTPT (1,1,2,'SHIPPING CONFIRMATION',1)
	XCALL OUTPT ( 4,4,0,'1. ORDER #',1)
	XCALL OUTPT ( 8,4,0,'2. PRO #',1)		;PRO #, ETC

ORD,
	XCALL INPUT (4, 15, 08, 00, 'AE', ENTRY, INXCTL, 1)
	
	GOTO (DISPLA, ENDOFF), INXCTL
	IF (ENTRY(1,2) .EQ. 'S~') 
		BEGIN
		ENTRY = ENTRY(3,30)
		XCALL OUTPT (4,15,0,ENTRY,1)
		END
	
	xcall alpdc (entry(1,6), xord, switch)
	goto (ord), switch
	XCALL ISIO (CHN044, ORDHDR, XORD, READ, LOKCTL)
	IF (LOKCTL .NE. 0)
		BEGIN
		XCALL MESAG ('ORDER NOT ON FILE',1)
		GOTO DISPLA
		END

;;;	IF (OC_NBR .LE. 0)
;;;		BEGIN
;;;		XCALL MESAG ('NOT A CONFIRMING ORDER',1)
;;;		GOTO DISPLA
;;;		END

	XCALL OUTPT (4,25,0,OCUSNM,1)

	XCALL ISIO (CHN158, CONFRM, XORD, READ, LOKCTL)
	IF (LOKCTL .EQ. 0)
		BEGIN
		XCALL MESAG ('ALREADY CONFIRMED',1)
		SELECT = 2
		CALL DSPREC
		GOTO ANYCNG
		END

	CLEAR CONFRM
	SELECT = 1

REF,
	XCALL INPUT (8, 14, 20, 00, 'A ', ENTRY, INXCTL, 1)
	GOTO (DISPLA),INXCTL
	SP_REF = ENTRY(1,20)
	GOTO (ANYCNG), CNGCTL

ANYCNG,
	XCALL ANYCN (CNGCTL, WHATNO)
	GOTO (PROCES, CNGBR), CNGCTL + 1
CNGBR,
	GOTO (ORD, REF), WHATNO
	GOTO ANYCNG

DSPREC,
	ENTRY(1,6) = SP_ORD, 'XXXXXX'
;;;	XCALL OUTPT (4, 14, 1, ENTRY(1,6), 1)
;;;	XCALL OUTPT (6, 14, 1, SP_SRC, 1)
	XCALL OUTPT (8, 14, 1, SP_REF, 1)
	
	DT(1,12) = SP_DT
	AYYYY = YYYY,	'XXXX'
	AMM = MM,	'ZX' [left]
	ADD = DD,	'ZX' [left]
	AHH = HH,	'ZX'
	AMIN = MIN,	'XX'
	XCALL OUTPT (8, 38, 0, ADT, 1)

	RETURN
	
PROCES,
	USING SELECT SELECT
	(1),	BEGIN
		DT = %DATETIME
		SP_DT = DT(1,12)
		SP_ORD = XORD
		SP_CUST = OCUSNO
		SP_MNBR = OC_NBR
		SP_SRC = PGM_SRC
		XCALL ISIO (CHN158, CONFRM, SP_ORD, STORE, LOKCTL)
		CALL TAG_ALONG
		END
	(2),	BEGIN
		XCALL ISIO (CHN158, CONFRM, SP_ORD, WRITE, LOKCTL)
		END

	ENDUSING

	GOTO DISPLA

ENDOFF,
	CALL CLOSE
	xcall flags (7000000)
	STOP
;
TAG_ALONG,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		;;; CHECK FOR TAG ALONG ORDERS
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	FIND (CHN066, BLHTAG, SP_ORD, KRF=1) [ERR=TA_RETURN]
TA_LOOP,
	XCALL IOS (CHN066, BLHTAG, READ, LOKCTL)
	IF (LOKCTL .NE. 0) RETURN
	IF (BT_ORD .NE. XORD) RETURN

	;SET ABOVE
;;;	DT = %DATETIME
;;;	SP_DT = DT(1,12)
;;;	SP_ORD = XORD
;;;	SP_CUST = OCUSNO
;;;	SP_MNBR = OC_NBR
;;;	SP_SRC = PGM_SRC

	SP_ORD = BT_TAG	;TAG ALONG ORDER
	XCALL ISIO (CHN158, CONFRM, SP_ORD, STORE, LOKCTL)

	GOTO TA_LOOP

TA_RETURN,
	RETURN
;----------------------------------------------

OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (4, 'SI', 044, SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN044 = 4

	SWITCH = 5
	XCALL FILES (5, 'SU', 158, SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN158 = 5

	SWITCH = 5
	XCALL FILES (6, 'SI', 066, SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN066 = 6

	OPNOK = 1

	RETURN
;----------------------------------------------

CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	
	IF (CHN044) CLOSE CHN044
	IF (CHN158) CLOSE CHN158
	IF (CHN066) CLOSE CHN066

	RETURN
;----------------------------------------------


