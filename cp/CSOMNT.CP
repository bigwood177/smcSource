;CSOMNT.CP
;OPTMNT.CP
;	COP TABLE MAINTENANCE
;	COMPLETION STATUS OPERATIONS
;
RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'

RECORD	DEPART
;TABLE.DEF	(147 BYTES)
; file # 182 - COPTBL.ISM

	TBLCOX		,A2	;'DD' 
	DZ_DEPT		,A2	;DEPT
			,A13	;KEY FILLER
			,A30	;DESC FILLER
	DZ_DESC		,A30	;DEPT DESCRIPTION
	DZ_LOC		,A20	;DEFAULT LOCATION
	DZ_PQUE		,A12	;PRINT QUE NAME FOR WORKORDERS
			,A38	;FILLER

RECORD,X
	TBL_KEX		,A17


RECORD	CHANNEL
	CHN182	,D2
	CHN183	,D2	;2ND CHAN TO COPTBL FOR BAR CODES


RECORD	PRINT
	TITLE	,A*,	'COMLETION STATUS OPERATIONS'
	HD	,A6,	'NO HDR'
	LG	,A9,	'NO LEGEND'
	PLINE	,A80
	PRNTON	,D1
	LINCNT	,D2,60
	PGCNT	,D6
	LPSW	,D2
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	PRTCTL	,D3,080
	LPARG	,D1
	PRNTSW	,D1
	PRTCTR	,D1


RECORD	HD1
;;;		,a*,	'DEPT  SEQ  DESCRIPTION                    '
		,a*,	'DEPT  SEQ  DESCRIPTION                    B/F/C WORK-DEPT  '
RECORD	B_CODE
		,A2,	'D~'
	BC_DEPT	,A2	
		,A1,	'~'
	BC_SEQ	,A3

RECORD	HLINE
		,A14,	'--------------'
	H_DEPT	,A30
		,A14,	'--------------'

RECORD	VARS
	LEN	,D6
	SAVDPT	,A2
	LN	,D6
	BAR_CODE	,D1
	B_DATA	,A100
	B_STRNG	,A100
	A3	,A3
	ALPHA	,A12
	OPNOK	,D1
	BLANKS	,A30
	SEQ	,D3
	DEPT	,A2
	DESC	,A30
	DPTDSC	,A30
	ST_DPT	,A2
	EN_DPT	,A2
	PGM	,D1
	ROW	,D2
	ENTRY	,A30
	INXCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	STORE	,D1,2
	DELETE	,D1,3
	LOKCTL	,D1
	WHATNO	,D2
	SELECT	,D1
	CNGCTL	,D1
	I	,D3
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'COMPLETION STATUS OPERATIONS TABLE',1)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO CLOSE

MENU,
	BAR_CODE = 0
	
	XCALL OUTPT (1,1,2,'COMPLETION STATUS OPERATIONS TABLE',1)
	XCALL OUTPT (3,9,0,'PLEASE SELECT APPLICATION',1)
	XCALL OUTPT (5,15,0,'1. TABLE MAINTENANCE',1)
	XCALL OUTPT (6,15,0,'2. PRINT TABLE',1)
	XCALL OUTPT (7,15,0,'3. PRINT BARCODES',1)
MINPUT,
	XCALL INPUT (3,36,1,1,'#E',ENTRY,INXCTL,1)
	GOTO (MINPUT,ENDOFF), INXCTL
	PGM = ENTRY(1,1)
	GOTO (DISPLA,PRINT_TABLE,PRINT_BARCODES),PGM
	GOTO MINPUT

DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,'COMPLETION STATUS OPERATIONS TABLE',1)
	XCALL OUTPT ( 4,4,0,'1. DEPARTMENT',1)
	XCALL OUTPT ( 6,4,0,'2. SEQ #',1)
	XCALL OUTPT ( 8,4,0,'3. DESCRIPTION',1)
	XCALL OUTPT (10,4,0,'4. B/F/C',1)
	XCALL OUTPT (12,4,0,'5. WORK DEPT (Y/N)',1)
DEPT,
	XCALL INPUT (4,24,02,00,'AE',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	DEPT = ENTRY(1,2)
	CLEAR TBL_KEY
	TBL_KEY(1,2) = 'DD'
	OP_DEPT = DEPT
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .NE. 0)
		BEGIN
		XCALL MESAG ('DEPARTMENT NOT FOUND',1)
		GOTO DEPT
		END
	DPTDSC = DD_DESC
	XCALL OUTPT (4,27,0,DD_DESC,1)

SEQ,
	XCALL INPUT (6,24,02,00,'#E',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	SEQ = ENTRY(1,2)
	CLEAR TBL_KEY
	TBL_KEY(1,2) = 'CS'
	OP_DEPT = DEPT
	OP_SEQ = SEQ
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .EQ. 0)
		BEGIN
		SELECT = 2
		CALL DSPREC
		GOTO ANYCNG
		END

	SELECT = 1			;ADD MODE
	CLEAR COPTBL

	OP_DEPT = DEPT
	OP_SEQ = SEQ
DESC,
	XCALL INPUT (8,24,20,00,'LE',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	OP_DESC = ENTRY(1,20)
	GOTO (ANYCNG),CNGCTL

DFL,
	XCALL INPUT (10,24,01,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	USING ENTRY(1,1) SELECT
	('B'),	OP_DFL = 1
	('F'),	OP_DFL = 2
	('C'),	OP_DFL = 3
	(),	CLEAR OP_DFL
	ENDUSING
	GOTO (ANYCNG),CNGCTL
WRK,
	XCALL INPUT (12,28,01,00,'YN',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	USING INXCTL SELECT
	(1),	OP_WRK = 1
	(),	OP_WRK = 0
	ENDUSING
	
ANYCNG,
	XCALL OUTPT (24,1,1,'FIELD # TO CHANGE            <TAB> = DELETE',1)
	XCALL INPUT (24,20,02,00,'#T',ENTRY,INXCTL,1)
	IF (INXCTL .EQ. 3)
		BEGIN
		XCALL OUTPT (24,1,1,'DELETE, ARE YOU SURE ?',1)
		XCALL INPUT (24,24,01,01,'YN',ENTRY,INXCTL,1)
		GOTO (ANYCNG),INXCTL-1
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,DELETE,LOKCTL)
		GOTO DISPLA
		END

	WHATNO = ENTRY(1,2)
	IF (WHATNO .EQ. 0)
	THEN 	CLEAR CNGCTL
	ELSE 	CNGCTL = 1

	GOTO (PROCES,CNGBR),CNGCTL+1
CNGBR,
	GOTO (DEPT, SEQ, DESC, DFL, WRK),WHATNO
	GOTO ANYCNG

PROCES,
	CASE SELECT OF
	BEGINCASE
	1:	BEGIN
		TBLCOD = 'CS'
		OP_DEPT = DEPT
		OP_SEQ = SEQ
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,STORE,LOKCTL)
		END
	2:	XCALL ISIO (CHN182,COPTBL,TBL_KEY,WRITE,LOKCTL)
	ENDCASE
	GOTO DISPLA

ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('CP:TBLMNU',1)
	STOP


DSPREC,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; DISPLAY RECORD
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL OUTPT (4,24,1,OP_DEPT,1)
	A3 = OP_SEQ,	'ZZX' [LEFT]
	XCALL OUTPT (6,24,1,A3,1)
	XCALL OUTPT (8,24,1,OP_DESC,1)

	USING OP_DFL SELECT
	(1),	XCALL OUTPT (10,24,1,'B',1)
	(2),	XCALL OUTPT (10,24,1,'F',1)
	(3),	XCALL OUTPT (10,24,1,'C',1)
	(),	XCALL OUTPT (10,24,1,' ',1)
	ENDUSING

	USING OP_WRK SELECT
	(1),	XCALL OUTPT (12,28,1,'Y',1)
	(),	XCALL OUTPT (12,28,1,'N',1)
	ENDUSING

	RETURN
;-----------------------------------


PRINT_BARCODES,
	BAR_CODE = 1
	SAVDPT = '-1'

	SWITCH = 5
	XCALL FILES (16,'SI',182,SWITCH)
	CHN183 = 16

;===================================================================
PRINT_TABLE,
;===================================================================
	LINCNT = 66
	PGCNT  = 0
	
PDISP,
	CNGCTL = 
	XCALL OUTPT (1,1,2,'PRINT WORK ORDER COMPLETION STATUS TABLE',1)
	XCALL OUTPT (4,4,0,'1. STARTING DEPT',1)
	XCALL OUTPT (6,4,0,'2. ENDING   DEPT',1)
ST_DPT,
	XCALL INPUT (4,27,02,00,'AE',ENTRY,INXCTL,1)
	GOTO (PDISP,PDONE),INXCTL
	ST_DPT = ENTRY(1,2)
	IF (ST_DPT .EQ. BLANKS)
		BEGIN
		EN_DPT = 'ZZ'
		XCALL OUTPT (4,25,1,'ALL',1)
		XCALL OUTPT (6,25,1,' ',1)
		GOTO P_ANY
		END
	GOTO (P_ANY),CNGCTL
EN_DPT,
	XCALL INPUT (6,25,02,00,'A ',ENTRY,INXCTL,1)
	GOTO (PDISP),INXCTL
	EN_DPT = ENTRY(1,2)
	IF (EN_DPT .EQ. BLANKS)
		BEGIN
		EN_DPT = ST_DPT
		XCALL OUTPT (6,25,0,EN_DPT,1)
		END
P_ANY,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (P_PRINT,P_CNGBR),CNGCTL + 1
P_CNGBR,
	GOTO (ST_DPT,EN_DPT),WHATNO
	GOTO P_ANY

P_PRINT,
	CLEAR TBL_KEY
	TBLCOD = 'CS'
	OP_DEPT = ST_DPT
	FIND (CHN182,COPTBL,TBL_KEY)[ERR=PLOOP]

PLOOP,
	XCALL IOS (CHN182,COPTBL,READ,LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF
	IF (TBLCOD .NE. 'CS') GOTO EOF
	IF (OP_DEPT .LT. ST_DPT) GOTO PLOOP
	IF (OP_DEPT .GT. EN_DPT) GOTO EOF

	IF (BAR_CODE .AND. OP_DEPT.NE.SAVDPT)
		BEGIN
		CLEAR TBL_KEX
		TBL_KEX(1,2) = 'DD'
		DZ_DEPT = OP_DEPT 
		XCALL ISIO (CHN183,DEPART,TBL_KEX,READ,LOKCTL)
		IF (LOKCTL .NE. 0) DZ_DESC = '** NOT ON FILE **'
		LEN = %TRIM (DZ_DESC)
		LEN = (30 - LEN)/2
		IF (LEN.GT.0) XCALL FILL ('-', H_DEPT(1,LEN))
		H_DEPT(LEN+1,30) = DZ_DESC
		LEN = %TRIM(H_DEPT)

		IF (LEN.LT.30) XCALL FILL('-',H_DEPT(LEN+1,30))
		PLINE = HLINE
		CALL PRINT	
		CALL PRINT	
		SAVDPT = OP_DEPT
		END

	PLINE (2,3) = OP_DEPT	;DD_DEPT
	PLINE (7,9) = OP_SEQ,	'ZZX'
	PLINE (12,41) = OP_DESC

	USING OP_DFL SELECT
	(1),	PLINE (45,45) = 'B'
	(2),	PLINE (45,45) = 'F'
	(3),	PLINE (45,45) = 'C'
	(),	PLINE (45,45) = 
	ENDUSING

	IF (OP_WRK .EQ. 0)
	THEN	PLINE (53,53) = 'N'
	ELSE	PLINE (53,53) = 'Y'

	IF (BAR_CODE) 
		BEGIN
		call print
		BC_DEPT = OP_DEPT
		BC_SEQ = OP_SEQ,	'XXX'
		XCALL B128 (B_CODE, B_STRNG, 70)
		LN = %TRIM (B_STRNG)
		PLINE (43,80) = B_STRNG(1,LN)
		call print
		call print		
		call print		
		call print		
		END

;DEPT  SEQ  DESCRIPTION                    B/F/C WORK-DEPT  
; AA    XX  AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA   A       A
;123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5
	CALL PRINT
	GOTO PLOOP

EOF,
	if (bar_code) 
		begin
;;;		xcall fill ('_', pline)
;;;		call print
;;;		call print
;;;		call print
		goto pdisp
		end

PDONE,
	IF (PRNTON.EQ.1)  XCALL LPOFF(LPSW,SPLFIL,PGCNT)
	IF (CHN183) CLOSE CHN183
;;;	IF (BAR_CODE) XCALL OF128
	PRNTON  = 0

;;;PDONE,
	GOTO MENU

PRINT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (PRNTON .EQ. 0) CALL PRNTON
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HD1,HD,HD
&		,LG,LG,LG,0,080,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;-------------------------------------------------------------
PRNTON,
	SPLFIL (5,6) = 'EF'
	LPSW = 1		;PRINT,SPOOL, OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTON = 1
	RETURN
;-------------------------------------------------------------
;===================================================================


OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (1,'SU',182,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN182 = 1

	CHN183 = 0
	OPNOK = 1
	RETURN
;----------------------------------------------------

CLOSE,
	CLOSE CHN182
	RETURN
;----------------------------------------------------
	


