SUBROUTINE MEMBL


	.INCLUDE 'WND:WINDOWS.DEF'

RECORD	POP
	.INCLUDE 'DEF:POP1.DEF'

RECORD ORDLIN		
	.INCLUDE 'DEF:RD045A.DEF'
RECORD,X
	.INCLUDE 'DEF:RD045M.DEF'
RECORD,X
	.INCLUDE 'DEF:RD045E.DEF'
RECORD,X
	.INCLUDE 'DEF:RD045D.DEF'
	
RECORD SAVLIN
	.INCLUDE 'DEF:RD045F.DEF'


GLOBAL ORDH
RECORD	ORDHDR
	.INCLUDE 'DEF:RD044A.DEF'
ENDGLOBAL

GLOBAL CPCHAN
RECORD	CHANNEL
	CHN001	,D2
	CHN002	,D2
	CHN007	,D2
	CHN041	,D2
	CHN042	,D2
	CHN044	,D2
	CHN045	,D2
	CHN054	,D2
	CHN060	,D2
	CHN169	,D2
	CHN170	,D2
	CHN171	,D2
	CHN172	,D2
	CHN174	,D2
	CHN175	,D2
	CHN178	,D2
	CHN182	,D2
	CHN066	,D2		;SSQ 5-24-05
	CHN101	,D3		;MISCUS 12-9-19
	CHN102	,D3		;MISSHP 12-9-19
ENDGLOBAL

RECORD	WN_NAME
		,A5,	'MEMBL'
	WN_TNMBR,D4

RECORD	VARS
	W_ID	,D4
	WND_1	,D4
	SAVRFA	,A6
	ENTRY	,A30
	INXCTL	,D1
	CNGCTL	,D1
	WHATNO	,D2
	A1	,A1
	SEQ1	,D2
	SEQ2	,D2
	SEQ3	,D2
	READ	,D1,0
	WRITE	,D1,1
	LOKCTL	,D1
	I	,D5
;
PROC
	CALL INIT_WINDOW

	CALL GET_MEMOS
PLOOP,
	IF (NUMARA .EQ. 0) RETURN
	DLINE = '  MEMO  DESCRIPTION'
	XCALL W_DISP(WND_1, WD_POS,14,1,WD_CLR, WDC_EOW)
	XCALL POP1(POP)
	USING P_ACTION SELECT
	(0),	GOTO EXIT
	(1,4),	BEGIN
		CALL SELECT
		READS (15, A1)
		END
	(3),	BEGIN
		CALL DELETE
		READS (15, A1)
		END
	ENDUSING
	GOTO PLOOP
EXIT,
	XCALL W_PROC(WP_DELETE, WND_1)
	RETURN
;================================================
SELECT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	READ(CHN045, ORDLIN, RFA:PARRFA(PI)) [ERR=LINE_ERR]
	CALL DSP_LINE

	GOTO ANYCN
M1,
	XCALL WINPT(W_ID, 14, 22, 30, 00, 'AE', ENTRY, INXCTL)
	GOTO (SELECT,END_SEL),INXCTL
	M_DESARA(1) = ENTRY(1,30)
	GOTO ANYCN
M2,
	XCALL WINPT(W_ID, 15, 22, 30, 00, 'AE', ENTRY, INXCTL)
	GOTO (SELECT,END_SEL),INXCTL
	M_DESARA(2) = ENTRY(1,30)
	GOTO ANYCN
M3,
	XCALL WINPT(W_ID, 16, 22, 30, 00, 'AE', ENTRY, INXCTL)
	GOTO (SELECT,END_SEL),INXCTL
	M_DESARA(3) = ENTRY(1,30)
	GOTO ANYCN
ANYCN,
	XCALL WANCN(W_ID, 23, CNGCTL, WHATNO)
	GOTO(PROCES, CNGBR),CNGCTL+1
CNGBR,
	GOTO(M1,M2,M3), WHATNO
	GOTO ANYCN
PROCES,
	WRITE(CHN045, ORDLIN, ORDKEY) [ERR=LINE_ERR]
	CALL GET_MEMOS
END_SEL,
LINE_ERR,
	RETURN
;-----------------------------------------------

DELETE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	READ(CHN045, ORDLIN, RFA:PARRFA(PI)) [ERR=LINE_ERR]
	CALL DSP_LINE

	XCALL W_DISP(WND_1, WD_POS, 23, 1, 'DELETE THIS MEMO ?')
	XCALL WINPT(W_ID, 23, 23, 01, 01, 'YN', ENTRY, INXCTL)
	IF(ENTRY(1,1) .EQ. 'N') RETURN

	SEQ1 = M_LMSQ1
	SEQ2 = M_LMSQ2
	SEQ3 = M_LMSQ3
		
	DELETE (CHN045) 	;DELETE THE MEMO RECORD
	FIND(CHN045, ORDLIN, OORDNO) [ERR=D_LOOP]
D_LOOP,
	XCALL IOS (CHN045, ORDLIN, READ, LOKCTL)
	IF (LORDNO .NE. OORDNO) GOTO D_EOF
	IF (LTYPE .EQ. 'M') GOTO D_LOOP

	IF(SEQ1 .EQ. 0) GOTO CHK2
	IF (LMSQ1 .NE. SEQ1) GOTO D_LOOP

	LMSQ1 = 0
	XCALL ISIO(CHN045, ORDLIN, ORDKEY, WRITE, LOKCTL)
	GOTO D_LOOP
CHK2,
	IF(SEQ2 .EQ. 0) GOTO CHK3
	IF (LMSQ2 .NE. SEQ2) GOTO D_LOOP

	LMSQ2 = 0
	XCALL ISIO(CHN045, ORDLIN, ORDKEY, WRITE, LOKCTL)
	GOTO D_LOOP
CHK3,
	IF(SEQ3 .EQ. 0) GOTO D_LOOP
	IF (LMSQ3 .NE. SEQ3) GOTO D_LOOP

	LMSQ3 = 0
	XCALL ISIO(CHN045, ORDLIN, ORDKEY, WRITE, LOKCTL)
	GOTO D_LOOP
D_EOF,
	CALL GET_MEMOS
	RETURN
;-----------------------------------------------

DSP_LINE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL W_DISP(WND_1, WD_POS,14,1,WD_CLR, WDC_EOW)
	XCALL W_DISP(WND_1, WD_POS, 14, 1, '1. ')
	XCALL W_DISP(WND_1, WD_POS, 15, 1, '2. ')
	XCALL W_DISP(WND_1, WD_POS, 16, 1, '3. ')
	XCALL W_DISP(WND_1, WD_POS, 14, 22, M_DESARA(1))
	XCALL W_DISP(WND_1, WD_POS, 15, 22, M_DESARA(2))
	XCALL W_DISP(WND_1, WD_POS, 16, 22, M_DESARA(3))
	XCALL W_UPDT
	RETURN
;-----------------------------------------------
	
GET_MEMOS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	FOR I FROM 1 THRU MAXARA
		BEGIN
		CLEAR PARRY(I)
		CLEAR PARRFA(I)
		CLEAR PFLAGS(I)
		END
	CLEAR I

	FIND(CHN045, ORDLIN, OORDNO) [ERR=M_LOOP]
M_LOOP,
	READS(CHN045, ORDLIN, M_EOF, GETRFA:SAVRFA)
	IF (LORDNO .NE. OORDNO) GOTO M_EOF
	IF (LTYPE .NE. 'M') GOTO M_LOOP

	DLINE(1,5) = M_LITMNO
	DLINE(7,36) = M_LDESCR(1,30)
	INCR I
	PARRY(I) = DLINE
	PARRFA(I) = SAVRFA
	GOTO M_LOOP
M_EOF,
	NUMARA = I
	RETURN
;------------------------------------------------


INIT_WINDOW,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		;;; SET UP SCREEN 1 WINDOW
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL TNMBR (WN_TNMBR)
	XCALL W_PROC(WP_FIND,WND_1,WN_NAME)
	IF (.NOT. WND_1)
		BEGIN
		XCALL W_PROC(WP_CREATE,WND_1,WN_NAME,24,78)
		END
	XCALL W_BRDR(WND_1,WB_TITLE,'SELECT FOR BILLING',
&			WB_TPOS,WBT_TOP,WBT_CENTER)
	XCALL W_PROC(WP_PLACE,WND_1,1,1)	
	XCALL W_DISP(WND_1,WD_CLEAR)

	W_ID = WND_1
	xcall u_logwnd(wnd_1)

;; POP info...

	MAXARA = 20		;9-19-97 NOT MORE THAN 1 FULL WINDOW
	PLEN = 36
	NUMROW = 10
	WX = 2
	WY = 7
	POP_WID(1,5) = "BLPOP"
	POP_WID(6,8) = WN_TNMBR,	'XXX'
	POP_TITLE = "MEMOS"
	RETURN
;-------------------------------------------------------------------
;MEMO  DESCRIPTION
;AAAAA AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
;12345678901234567890123456789012345678901234567890
;          1          2          3          4
