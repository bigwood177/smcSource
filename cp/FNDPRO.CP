;FNDPRO.COP
;
;	FIND SLBHDR RECORD BY PRO #
;
;
	.DEFINE POOLSIZE	,25000
	.DEFINE WNDCHNL		,15
	.DEFINE MAXWINS		,10
	.INCLUDE 'DEF:WINDOWS.DEF'

RECORD	D_STUFF
	D_IN	,D8		;DATE-IN, ANY FORMAT
	D_OUT	,D6		;RETURN VALUE MMDDYY
	D_OUTR	,D8		;RETURN VALUE CCYYMMDD
	D_FMT	,A10		;RETURN VALUE MM/DD/CCYY
	D_SW	,A2		;"99" = DATE CONVERSION ERROR


RECORD	BLHEAD
	.INCLUDE 'DEF:RD178A.DEF'

RECORD	SLBIDX
	.INCLUDE 'DEF:RD999A.DEF'

RECORD	POPAR
	.INCLUDE 'DEF:OEPOP.DEF'

RECORD	WARS
	WND_1	,D4
	W_ID	,D4

RECORD	CHANNEL
	CHN178	,D2
	CHN188	,D2
	CHN999	,D2
	CHNWRK	,D2

RECORD	FILPRC
	FL_DEV	,A3
		,A1,	':'
	FL_NAME	,A6
		,A1,	'.'
	FL_EXT	,A3

RECORD	VARS
	OPNOK	,D1
	I	,D5
	YEAR	,D2
	DATE	,D8
	FILNAM	,A14
	BLKEY	,D9
	BLEN	,D3
	ENTRY	,A30
	INXCTL	,D1
	CNGCGL	,D1
	PRO	,A15
	CPRO	,A15
	READ	,D1,0
	LOKCTL	,D1
	SWITCH	,D1
	V	,D1
PROC
	XCALL TERID (V)

	XCALL RDAT8(DATE)
	YEAR = DATE(3,4)
	YEAR = YEAR -1

	CALL OPENS
	IF (.NOT. OPNOK) GOTO ENDOFF

	CALL INIT_WINDOW

DISPLA,
	XCALL W_DISP(WND_1,WD_CLEAR)
	XCALL W_DISP(WND_1,WD_POS,4,4,"PRO #")
	XCALL W_UPDT
	XCALL WINPT (W_ID,4,11,15,00,'AE',ENTRY,INXCTL)
	GOTO (DISPLA,ENDOFF),INXCTL
	PRO = ENTRY(1,15)
	XCALL CMPRS(PRO,CPRO)

	CALL GET_PRO
	IF (NUMARA .LE. 0) GOTO DISPLA
	READS (15,ENTRY)
	DLINE = '  PRO #           CUSTOMER                  SHIP DATE'
	XCALL PROPOP (POPAR)
	GOTO DISPLA
;=====================================================

ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('CP:CPMENU',1)
	STOP
;=======================================================
GET_PRO,
	FIND (CHN999,SLBIDX,CPRO)[ERR=FNEXT]
FNEXT,
	CLEAR I
	XCALL IOS (CHN999,SLBIDX,READ,LOKCTL)
	USING SI_SRC SELECT
	(1),	XCALL ISIO (CHN178,BLHEAD,SI_BL,READ,LOKCTL)	;BLHEAD
	(2),	XCALL ISIO (CHN188,BLHEAD,SI_BL,READ,LOKCTL)	;SLBHDR
	(3),	XCALL ISIO (CHNWRK,BLHEAD,SI_BL,READ,LOKCTL)	;SLBHXX
	ENDUSING

	WHILE (LOKCTL.EQ.0 .AND.I.LT.MAXARA)
		BEGIN
		CLEAR DLINE
		DLINE(1,15) = BHPRON
		DLINE(17,40) = BHCUSNM
		XCALL DATE8(BHSHPD, D_OUT, D_OUTR, D_FMT, D_SW)
		DLINE (42,49) = D_OUT, 'XX/XX/XX'
		CLEAR BLKEY
		ONERROR NOTNUM
		BLKEY = KEY178
	NOTNUM,	OFFERROR
;;;		DLINE (51,61) = BLKEY,	'ZZZZZX.X.XX'
		DLINE (54,64) = BLKEY,	'ZZZZZX.X.XX'
		INCR I
		PARRY(I) = DLINE
		CLEAR DLINE	
		DLINE (1,25) = BHSHAD1
		XCALL TRIM (BHSHAD2,BLEN)
		IF (BLEN .LE. 0) BLEN = 1
		DLINE (26,46) = BHSHAD2(1,BLEN)

		XCALL TRIM (BHSHAD3,BLEN)
		IF (BLEN .LE. 0) BLEN = 1
		DLINE (47,75) = BHSHAD3(1,BLEN)
		P_ADD(I) = DLINE
		XCALL IOS (CHN999,SLBIDX,READ,LOKCTL)
		USING SI_SRC SELECT
		(1),	XCALL ISIO (CHN178,BLHEAD,SI_BL,READ,LOKCTL)	;BLHEAD
		(2),	XCALL ISIO (CHN188,BLHEAD,SI_BL,READ,LOKCTL)	;SLBHDR
		(3),	XCALL ISIO (CHNWRK,BLHEAD,SI_BL,READ,LOKCTL)	;SLBHXX
		ENDUSING
		END

	NUMARA = I
	IF (I.EQ.0 .OR. I.EQ.MAXARA) RETURN
	
	FOR I FROM NUMARA+1 THRU MAXARA	CLEAR PARRY(I)
	RETURN
;-------------------------------------------------------

OPENS,
	OPNOK = 1

	XCALL FFILE(1,FILPRC,SWITCH)	;SSQ 01-13-04
	FL_NAME = 'SLBIDX'
	FL_EXT(3,3) = 'M'

	OPEN(1,SI,FILPRC)
;;;	OPEN (1,SI,'SMC:SLBIDX.SMM')
	CHN999 = 1
	
	SWITCH = 5
	XCALL FILES (2,'SI',178,SWITCH)
;;;	OPEN (2,SI,'SMC:BLHEAD.SMM')
	CHN178 = 2

	SWITCH = 5
	XCALL FILES (3,'SI',188,SWITCH)
;;;	OPEN(3,SI,'SMC:SLBHDR.SMM')
	CHN188 = 3

	FILNAM = 'SMC:SLBHDR.SMM'
	FILNAM(9,10) = YEAR, 'XX'

	onerror lastyear		;1-3-19
	OPEN (4,SI,FILNAM)
	offerror
	CHNWRK = 4
	return

lastyear,
	offerror
	year = year - 1		;incase year end not done yet
	FILNAM(9,10) = YEAR, 'XX'
	OPEN (4,SI,FILNAM)
	CHNWRK = 4


	RETURN
;-------------------------------------------------------

CLOSE,
	CLOSE CHN999
	CLOSE CHN178
	CLOSE CHN188
	CLOSE CHNWRK

	RETURN
;-------------------------------------------------------




INIT_WINDOW,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL W_INIT(POOLSIZE,WNDCHNL,MAXWINS)
	XCALL W_PROC(WP_CREATE,WND_1,"FNDPRO",24,80)
	XCALL W_PROC(WP_PLACE,WND_1,1,1)	
	XCALL W_DISP(WND_1,WD_CLEAR)
	W_ID = WND_1


;; POP info...

	MAXARA = 10		;9-19-97 NOT MORE THAN 1 FULL WINDOW
	PLEN = 49
	NUMROW = 10
	WX = 8
	WY = 2
	POP_WID(1,5) = "OEPOP"
	POP_TITLE = "BOL HISTORY"
	RETURN
;-------------------------------------------------------------------
END
