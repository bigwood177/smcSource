;ORDMNT.DBL
;
;		AUTHOR: SHERWOOD S. QUIRING
;		DATE  : 3-DEC-1999
;
;	CUSTOMER ORDER MAINTENANCE
;
	.INCLUDE 'WND:TOOLS.DEF'
	.INCLUDE 'WND:WINDOWS.DEF'


;--------- GLOBALS -------------------------
GLOBAL ORDH,	INIT
RECORD ORDHDR   	
		.INCLUDE 'DEF:RD044A.DEF'
ENDGLOBAL
;------------------------------------------

GLOBAL ORDL,	INIT
RECORD ORDLIN		
		.INCLUDE 'DEF:RD045A.DEF'
RECORD	,X
		.INCLUDE 'DEF:RD045D.DEF'
RECORD	,X
		.INCLUDE 'DEF:RD045M.DEF'
ENDGLOBAL
;------------------------------------------

GLOBAL DUCK,	INIT
	.INCLUDE 'DEF:RD175D.DEF'
ENDGLOBAL
;------------------------------------------

GLOBAL IMS,	INIT
RECORD INVMAS
	.INCLUDE 'DEF:RD041A.DEF'
RECORD DUMINV,X
	.INCLUDE 'DEF:RD041B.DEF'
RECORD ITMIDX 
	.INCLUDE 'DEF:RD042A.DEF'
RECORD
	FIL041	,A14		;ITMMAS FILE NAME
ENDGLOBAL
;------------------------------------------

GLOBAL PAR	,INIT
RECORD PARAM
	.INCLUDE 'DEF:PARAM.DEF'
ENDGLOBAL
;------------------------------------------

;----- END GLOBALS -----


	.INCLUDE 'DEF:HPSUB.DEF'


RECORD	COPCTL
	.INCLUDE 'DEF:RD060A.DEF'

RECORD	FUNKEY
	.INCLUDE 'DEF:FUNKEY.DEF'


RECORD	CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2

RECORD	WINS
	W_ID	,D4
	WND_1	,D4

RECORD	WN_NAME
		,A5,	'ORDMM'
	WN_TNMBR,D4

RECORD	CHANNEL
	CHN044	,D2
	CHN060	,D2

RECORD	VARS
	F_KEY	,D3
	OPNOK	,D1
	BLANKS	,A30
	ENTRY	,A30
	INXCTL	,D1
	LOKCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	TMPORD	,D6
	FIRST	,D1
	NXTORD	,D6
	DEFFLG	,D1
	ESTIMA	,D1
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)


BEGIN_ORDER,
	CALL INIT_WINDOW
	CALL OPENS
	IF (.NOT. OPNOK) GOTO ENDOFF

DISPLA,
	FIRST = 1
	LOKCTL = 0
	XCALL IO (3,COPCTL,1,READ,LOKCTL)
	IF (LOKCTL) GO TO ENDOFF
	UNLOCK 3

	INXCTL =
	XCALL OE1W (DSTFLG,W_ID)
	DEFFLG =
	ESTIMA =
	UNLOCK 1
ORDNO,
	CTL = '04,16,06,00,#X'
	CALL INPUT
	GO TO (DISPLA,ENDOFF), INXCTL
	USING F_KEY SELECT
	(END_KEY,F_11), GOTO ENDOFF
	ENDUSING
	IF (INXCTL .EQ. 3)
	THEN	ESTIMA = 1
	ELSE	CLEAR ESTIMA

	TMPORD = ENTRY (1,6)
	IF (TMPORD.EQ.0.AND.ENTRY.NE.BLANKS) GO TO ORDNO
	IF (ENTRY.EQ.BLANKS) CALL DEFLNX
	OORDNO = TMPORD
	LOKCTL = 1
	XCALL ISIO (CHN044,ORDHDR,OORDNO,READ,LOKCTL)
	CLOSE CHN044
	CLOSE CHN060

	XCALL W_PROC(WP_DELETE, WND_1)
	XCALL U_FINISH
	IF (LOKCTL .EQ. 0)
	THEN	XCALL S_CHANGE(OORDNO)
	ELSE	XCALL S_ORDADD(TMPORD, ESTIMA)
	
;;;	XCALL U_FINISH
	GOTO BEGIN_ORDER

ENDOFF,
	XCALL W_PROC(WP_DELETE, WND_1)
	XCALL U_FINISH
	CALL CLOSE
	XCALL PGCHN ('CP:SSQMNU',1)
;=====================================================


DEFLNX,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (FIRST.EQ.0) GO TO NEXT
	LOKCTL = 0
	XCALL IO (3,COPCTL,1,READ,LOKCTL)
	IF (LOKCTL) GO TO ENDOFF
	IF (LSTORD.EQ.999999) LSTORD =
	INCR LSTORD
	NXTORD = LSTORD
	LOKCTL = 1
	XCALL IO (3,COPCTL,1,WRITE,LOKCTL)
	FIRST =
NEXT,
	DEFFLG = 1
	ENTRY (1,6) = NXTORD, 'XXXXXX'
	TMPORD = NXTORD
	XCALL W_DISP(WND_1,WD_POS,4,16,ENTRY(1,6))
	RETURN
;-------------------------------------------------
INPUT,
	XCALL WINPT (W_ID,ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,F_KEY)
	RETURN




OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK
	SWITCH = 5
	XCALL FILES (4, 'SI', 044, SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN044 = 4

	XCALL FILES (3, 'U', 060, SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN060 = 3

	XCALL FFILE (1, FIL041, SWITCH)
	OPEN (1, I, FIL045)		;OPEN ITMMAS IN INPUT MODE,
					;CLOSE & RE-OPEN IN UPDATE MODE
					;FOR WRITES.

	OPNOK = 1
	RETURN
;------------------------------------------------
CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (CHN044) CLOSE CHN044
	IF (CHN060) CLOSE CHN060
	RETURN
;------------------------------------------------


INIT_WINDOW,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		;;; SET UP SCREEN 1 WINDOW
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL U_START("LIB:SMC_WND",,,,,,,100)
	XCALL E_SECT("Order Maintenance", D_CAPTION)
;;;	XCALL W_INIT(POOLSIZE,WNDCHNL,MAXWINS)
;;;	xcall u_start("lib:smc_wnd",,,,,,,35)
	XCALL TNMBR (WN_TNMBR)
	XCALL W_PROC(WP_FIND,WND_1,WN_NAME)
	IF (.NOT. WND_1)
		BEGIN
		XCALL W_PROC(WP_CREATE,WND_1,WN_NAME,24,78)
		END
	XCALL W_BRDR(WND_1,WB_TITLE,'ORDER MAINTENANCE',
&			WB_TPOS,WBT_TOP,WBT_CENTER)
	XCALL W_PROC(WP_PLACE,WND_1,1,1)	
	XCALL W_DISP(WND_1,WD_CLEAR)
	XCALL W_UPDT
	W_ID = WND_1
	xcall u_logwnd(wnd_1)
	RETURN

