;SOINFO.CP
; ORDER INFORMATION BY CUST & SHIPTO
;
RECORD	ORDHDR
	.INCLUDE 'DEF:RD044A.DEF'
;
RECORD	VARS
	YEAR	,D2
	CUST	,D6
	SHIP	,A20
	XDATE	,D8
	FOUND	,D3
	TL	,D6

RECORD TITLE
		,A*,	'CUSTOMER/SHIP-TO INFO'
RECORD HDR1
	,A*,	'ORDER    INV-DATE --------- CUSTOMER -------- '
	,A*,	'-------SHIP-TO-------   SALE AMT'

RECORD	VARS2
	ACTCNT	,D4
	RECNO	,D4
	STRTCT	,A2
	ENDCAT	,A2
	KEY   	,A2
	BSEND	,D5
	BSMID	,D5
	SRCCTL	,D1
	LINCNT	,D2,	60
	SPLFIL	,A14
	LPONSW	,D1
	LPARG	,D1
	RPTNUM	,D3
	PRTTYP	,A1
	PGCNT	,D6,	000000
	PLINE	,A132
	PRTCTL	,D3
	ENTRY	,A30
	INXCTL	,D1
	DECMAL	,D4
	MASK	,A8,	'XXXX-XXX'
	BLANKS	,A4
	ALL	,A4,	'ALL '
	CNGCTL	,D1
	WHATNO	,D1
	SWITCH	,D1
	LPSW	,D2
	V	,D1
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
PROC
	XCALL TERID (V)
	XCALL OUTPT (2,1,2,'PRINT OUT',1)


DISPLA,
	XCALL OUTPT ( 6,4,1,'1. YEAR',1)
	XCALL OUTPT ( 8,4,1,'2. CUST#',1)
	XCALL OUTPT (10,4,1,'3. SHIP-TO',1)
YEAR,
	XCALL INPUT (6,14,02,02,'#E',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	YEAR = ENTRY(1,2)
	CLOSE 1
	USING YEAR SELECT
	(10),	OPEN (1, SI, 'SMC:SLHHDR.SMM')
	(09),	OPEN (1, SI, 'SMC:SLHH09.SMM')
	ENDUSING
	
CUST,
	XCALL INPUT (8,17,06,00,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	CUST = ENTRY(1,6)
	GOTO (ANYCNG),CNGCTL
SHIP,
	XCALL INPUT (10,17,20,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	SHIP = ENTRY(1,20)
	GOTO ANYCNG

ANYCNG,
	CNGCTL = 2
	XCALL ANYCN (CNGCTL,WHATNO)
	GOTO (PROCES, CNGBR), CNGCTL + 1
CNGBR,
	GOTO (YEAR, CUST, SHIP),WHATNO
	GOTO ANYCNG

PROCES,
	TL = %TRIM (SHIP)
	FIND (1, ORDHDR, cust, krf=1) [ERR=READ]
READ,
	READS (1, ORDHDR, EOF)
	IF (OCUSNO .NE. CUST) GOTO eof
	FOUND = %INSTR(1, OSHPNM(1,120), SHIP(1,TL) )
	IF (FOUND .LE. 0) GOTO READ


	XDATE(1,4) = OINVDT(5,8)
	XDATE(5,8) = OINVDT(1,4)
	PLINE (1,6) = OORDNO,	'XXXXXX'
	PLINE (8,17) = XDATE,	'ZX/XX/XXXX'
	PLINE (19,24) = OCUSNO,	'ZZZZZX'
	PLINE (26,45)  = OCUSNM

	IF (FOUND.GE.1.AND.FOUND.LE.30) PLINE (47,67) = OSHPNM
	IF (FOUND.GE.31.AND.FOUND.LE.60) PLINE (47,67) = OSHAD1
	IF (FOUND.GE.61.AND.FOUND.LE.90) PLINE (47,67) = OSHAD2
	IF (FOUND.GE.91.AND.FOUND.LE.120) PLINE (47,67) = OSHAD3

;;;	PLINE (47,50) = OSHPTO,	'ZZZX'
;;;	PLINE (52,67) = OSHPNM
	PLINE (69,78) = OSALE,	'ZZZ,ZZX.XX'
	CALL PRINT

	GO TO READ

;ORDER    INV-DATE --------- CUSTOMER -------- -------SHIP-TO-------   SALE AMT
;XXXXXX XX/XX/XXXX XXXXXX AAAAAAAAAAAAAAAAAAAA XXXX AAAAAAAAAAAAAAAA ZZZ,ZZX.XX
;123456789012345678901234567890123456789012345678901234567890123456789012345678
;         1         2         3         4         5         6         7

PRINT,
	IF (LPONSW.EQ.0) CALL PRNTON
	PRTCTL = 80
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,'NO HDR2','NO HDR3',
&			'NO LEGEND',' ',' ',1,80,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
PRNTON,
	SPLFIL (5,6) = 'EL'
	LPSW = 1	; PRINT, SPOOL OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) close 1
	IF (LPSW.EQ.0) XCALL PGCHN ('CP:CPMENU',1)
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	LPONSW = 1
	RETURN
;************************************************************************
endoff,
EOF,
	CLOSE 1

	IF (LPONSW.EQ.1) XCALL LPOFF(LPSW,SPLFIL,PGCNT)
	XCALL PGCHN ('CP:COPRPT',1)

END
