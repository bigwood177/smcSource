;  INITCP / COP 
;
;
;		::PCPYCOP.DEF::
;*********************************************************************
;		CUSTOMER ORDER PROCESSING 
;		
;		RELEASED: AUGUST 1, 1984 (d70s10)
;***********************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		PROGRAM TO INITIALIZE ALL FILES
;		IN THE ACCOUNTS RECEIVABLE SYSTEM.
;		DO NOT LEAVE ON USER DISK.
;
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD FILES
	COP2	,A14,'   :COPCTL.   '
	COP3	,A14,'   :CRMHDR.   '
	COP4	,A14,'   :CRMLIN.   '
	COP5	,A14,'   :PRDACT.   '
	COP6	,A14,'   :SLSSUM.   '
	COP7	,A14,'   :SLSIDX.   '
	COP8	,A14,'   :SLSHST.   '
	COP9	,A14,'   :SHIPTO.   '
	COP10	,A14,'   :SHPVIA.   '
RECORD ,X
	COP	,9A14
RECORD FILBL
	FILNAM	,A14
RECORD
	PHYDEV	,A3
	CPYEXT	,A3
RECORD CTLVAR
		,D10,0000100001
	MAXREC	,D5
		,D3,000
RECORD ZERO
		,2D10
RECORD BRACKS
		,A50,']]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]'
		,A50,']]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]'
		,A50,']]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]'
		,A50,']]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]'
		,A50,']]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]'
		,A50,']]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]'
		,A51,']]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]'
RECORD
		.INCLUDE 'DEF:RD060S.DEF'
		.INCLUDE 'DEF:RD046S.DEF'
		.INCLUDE 'DEF:RD047S.DEF'
		.INCLUDE 'DEF:RD069S.DEF'
		.INCLUDE 'DEF:RD058S.DEF'
		.INCLUDE 'DEF:RD059S.DEF'
		.INCLUDE 'DEF:RD055S.DEF'
		.INCLUDE 'DEF:RD171S.DEF'
		.INCLUDE 'DEF:RD172S.DEF'
RECORD,X
	RECSIZ 	,9D4

RECORD
	WRTREC	,A351
	FILCNT	,D2,	00
	FILSIZ	,D5
	MAXCNT	,10D5
	BLKSIZ	,10D4
	ENTRY	,A6
	INXCTL	,D1
	MAX	,D1
	ROW	,D2
	COL	,D2
	TYPE	,A2
	CNGCTL	,D1
	SWITCH	,D1
	WHATNO	,D1
	RECCNT	,D5
	V	,D1
PROC 
	V = -1
	XCALL TERID (V)
BEGIN,
	OFFERROR
	V = 1
	XCALL OUTPT (1,1,2,'INITIALIZE COP FILES',V)
	XCALL OUTPT (3,5,0,'ENTER LOGICAL DEVICE THAT FILES WILL USE',V)
	XCALL OUTPT (3,55,0,'COMPANY CODE EXTENT',V)
INPTDV,
	XCALL INPUT (3,48,3,2,'AE',ENTRY,INXCTL,V)
	GO TO (BEGIN,END), INXCTL
	PHYDEV = ENTRY
INPTCE,
	XCALL INPUT (3,76,3,3,'A ',ENTRY,INXCTL,V)
	GO TO (BEGIN), INXCTL
	CPYEXT = ENTRY
	CNGCTL = 2
	XCALL ANYCN (CNGCTL,WHATNO)
	GO TO (REDDEV,BEGIN), CNGCTL + 1
REDDEV,
	COP2 (1,3) = PHYDEV
	COP2 (12,14) = CPYEXT
	COP3 (1,3) = PHYDEV
	COP3 (12,14) = CPYEXT
	COP4 (1,3) = PHYDEV
	COP4 (12,14) = CPYEXT
	COP5 (1,3) = PHYDEV
	COP5 (12,14) = CPYEXT
	COP6 (1,3) = PHYDEV
	COP6 (12,14) = CPYEXT
	COP7 (1,3) = PHYDEV
	COP7 (12,14) = CPYEXT
	COP8 (1,3) = PHYDEV
	COP8 (12,14) = CPYEXT
	COP9 (1,3) = PHYDEV
	COP9 (12,14) = CPYEXT
	COP10 (1,3) = PHYDEV
	COP10 (12,14) = CPYEXT
	XCALL OUTPT (5,20,0,'1. INITIALIZE COP CONTROL FILE ?',V)
	XCALL OUTPT (7,20,0,'PLEASE ENTER THE MAX # RECORDS',V)
	XCALL OUTPT (8,20,0,'TO ALLOW FOR EACH FILE BELOW:',V)
	XCALL OUTPT (10,20,0,'2. CREDIT MEMO HEADER',V)
	XCALL OUTPT (11,20,0,'3. CREDIT MEMO LINE ITEM',V)
	XCALL OUTPT (12,20,0,'4. PRODUCT CATEGORY ACCT',V)
	XCALL OUTPT (13,20,0,'5. SALES SUMMARY',V)
	XCALL OUTPT (14,20,0,'6. DETAIL SALES HISTORY',V)
	XCALL OUTPT (15,20,0,'7. SHIP-TO CODES',V)
	XCALL OUTPT (16,20,0,'8. SHIP VIA CODES',V)
	XCALL OUTPT (18,19,0,'(LEAVING A FIELD BLANK WILL',V)
	XCALL OUTPT (19,20,0,'SKIP CREATING THAT FILE.)',V)
	XCALL OUTPT (21,19,0,'(SEE INITIALIZATION DOCUMENTATION',V)
	XCALL OUTPT (22,20,0,'FOR INSTRUCTIONS ON CREATING',V)
	XCALL OUTPT (23,20,0,'ORDER HEADER AND ORDER LINE ITEM FILES',V)
INPUT0,
	XCALL INPUT (5,54,1,0,'YN',ENTRY,INXCTL,V)
	MAXCNT (1) = 0
	IF (INXCTL.EQ.1) MAXCNT (1) = 1
	GO TO (ANYCNG), CNGCTL
INPUT1,
	TYPE = '# '
	ROW = 10
	MAX = 4
	CALL INPUT
	GO TO (BEGIN), INXCTL
	MAXCNT (2) = ENTRY (1,4)
	GO TO (ANYCNG), CNGCTL
INPUT2,
	ROW = 11
	MAX = 4
	CALL INPUT
	GO TO (BEGIN), INXCTL
	MAXCNT (3) = ENTRY (1,4)
	GO TO (ANYCNG), CNGCTL
INPUT3,
	ROW = 12
	MAX = 4
	CALL INPUT
	GO TO (BEGIN), INXCTL
	MAXCNT (4) = ENTRY (1,4)
	GO TO (ANYCNG), CNGCTL
INPUT4,
	ROW = 13
	MAX = 5
	CALL INPUT
	GO TO (BEGIN), INXCTL
	MAXCNT (5) = ENTRY (1,5)
	MAXCNT (6) = ENTRY (1,5)
	GO TO (ANYCNG), CNGCTL
INPUT5,
	ROW = 14
	MAX = 5
	CALL INPUT
	GO TO (BEGIN), INXCTL
	MAXCNT (7) = ENTRY (1,5)
	GO TO (ANYCNG), CNGCTL
INPUT6,
	ROW = 15
	MAX = 5
	CALL INPUT
	GO TO (BEGIN), INXCTL
	MAXCNT (8) = ENTRY (1,5)
	GO TO (ANYCNG), CNGCTL
INPUT7,
	ROW = 16
	MAX = 5
	CALL INPUT
	GO TO (BEGIN), INXCTL
	MAXCNT (9) = ENTRY (1,5)
	GO TO ANYCNG

CNGBR,
	TYPE = '# '
	GO TO (INPUT0,INPUT1,INPUT2,INPUT3,INPUT4,INPUT5,INPUT6,INPUT7), WHATNO
	GO TO ANYCNG
PROCES,
	V = 2
	XCALL WATE (4,V)
	FILCNT =
BLOCKS,
	INCR FILCNT
	BLKSIZ(FILCNT) = ((RECSIZ(FILCNT) + 2)*(MAXCNT(FILCNT) + 2))/512 + 1
	IF (FILCNT.LT.9) GO TO BLOCKS
	IF (BLKSIZ(1).NE.0) BLKSIZ(1) = 1
	FILCNT =
WRLOOP,
	INCR FILCNT
	IF (FILCNT.GT.9) GO TO BEGIN
	IF (MAXCNT(FILCNT).EQ.0) GO TO WRLOOP
	MAXREC = MAXCNT (FILCNT) + 1
	IF (FILCNT.NE.6) WRTREC(RECSIZ(FILCNT)-17,RECSIZ(FILCNT)) = CTLVAR
	FILNAM = COP(FILCNT)
	FILSIZ = BLKSIZ(FILCNT)
	XCALL OPENF (1,'O',FILBL,FILSIZ,RECSIZ(FILCNT),SWITCH)
	IF (SWITCH) GO TO ERRMSG
	IF (FILCNT.EQ.1) RECCNT = 2
	IF (FILCNT.EQ.1) GO TO WRBRKS
	WRITES (1,WRTREC(1,RECSIZ(FILCNT)))
	RECCNT = 1
WRBRKS,
	INCR RECCNT
	WRITES (1,BRACKS(1,RECSIZ(FILCNT)))
	IF (RECCNT.LE.MAXREC) GO TO WRBRKS
	CLOSE 1
	WRTREC =
	GO TO WRLOOP
END,
	CLOSE 15
	XCALL PGMND
	STOP
INPUT,
	XCALL INPUT (ROW,46,MAX,0,TYPE,ENTRY,INXCTL,V)
	RETURN
ANYCNG,
	XCALL ANYCN (CNGCTL,WHATNO)
	GO TO (PROCES,CNGBR), CNGCTL+1
ERRMSG,
	XCALL OUTPT (11,1,0,'ERROR CREATING FILE ',V)
	XCALL OUTPT (0,0,0,COP(FILCNT),V)
	XCALL OUTPT (0,0,0,' - PROBABLY NOT ENOUGH ROOM',V)
	XCALL MESAG(' ',1)
	GO TO BEGIN
END
