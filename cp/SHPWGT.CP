;SHPWGT.CP
;
;		AUTHOR: SHERWOOD S. QUIRING
;		DATE  : 19-AUG-1998
;
;		SHIPPING WEIGHT REPORT
;
RECORD	D_STUFF
	D_IN	,D8		;DATE-IN, ANY FORMAT
	D_OUT	,D6		;RETURN VALUE MMDDYY
	D_OUTR	,D8		;RETURN VALUE CCYYMMDD
	D_FMT	,A10		;RETURN VALUE MM/DD/CCYY
	D_SW	,A2		;"99" = DATE CONVERSION ERROR


RECORD	BLHEAD
	.INCLUDE 'DEF:RD178A.DEF'

RECORD	BLLINE
	.INCLUDE 'DEF:RD179A.DEF'

RECORD	WORK
	DAYDAT	,D8
	WEIGHT	,D10

RECORD	PRINT_STUFF
	TITLE,	A*,	'SHIPPING WEIGHT REPORT'
	PLINE	,A80
	LINCNT	,D2,	60
	SPLFIL	,A14
	LPONSW	,D1
	LPARG	,D1
	RPTNUM	,D3
	PRTTYP	,A1
	PGCNT	,D6,	000000
	PRTCTL	,D3
	LPSW	,D2

RECORD	HD1
	H1_SDATE	,A10
		,A7,	'  THRU '
	H1_EDATE	,A10

RECORD	HD2
	,A80

RECORD	HD3
	,A*,	'    DATE         WEIGHT'

RECORD	CHANNEL
	CHN188	,D2
	CHN189	,D2
	CHNWRK	,D2

RECORD	DIS
	I	,D6

RECORD	VARS
	OPNOK	,D1
	STDAT	,D8
	ENDAT	,D8
	TODAY	,D8
	TDATE	,D6
	LSTDAT	,D8
	TOTWGT	,D10
	DAYWGT	,D10
	ENTRY	,A30
	INXCTL	,D1
	CNGCTL	,D1
	WHATNO	,D2
	LOKCTL	,D1
	READ	,D1,0
	SWITCH	,D1
	V	,D1
PROC
	XCALL TERID(V)
	XCALL OUTPT (1,1,2,'SHIPPING REPORT',1)
	XCALL RDATE(TDATE)
	XCALL DATE8(TDATE, D_OUT, TODAY, D_FMT, D_SW)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO ENDOFF
DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,'SHIPPING REPORT',1)
	XCALL OUTPT (4,4,0,'1. START DATE:',1)
	XCALL OUTPT (6,4,0,'2. END   DATE:',1)
STDAT,
	XCALL INPUT (4,19,08,00,'DE',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	STDAT = ENTRY(1,8)
	IF (STDAT .EQ. 0)
		BEGIN
		STDAT = TODAY
		XCALL DATE8(STDAT, D_OUT, D_OUTR, D_FMT, D_SW)
		XCALL OUTPT (4,19,1,D_FMT,1)
		END
	GOTO (ANYCNG),CNGCTL
ENDAT,
	XCALL INPUT (6,19,08,00,'DE',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	ENDAT = ENTRY(1,8)
	IF (ENDAT .EQ. 0)
		BEGIN
		ENDAT = STDAT
		XCALL DATE8(ENDAT, D_OUT, D_OUTR, D_FMT, D_SW)
		XCALL OUTPT (6,19,1,D_FMT,1)
		END
ANYCNG,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (PROCES,CNGBR),CNGCTL+1
CNGBR,
	GOTO (STDAT,ENDAT),WHATNO
	GOTO ANYCNG
PROCES,
	XCALL DATE8(STDAT, D_OUT, D_OUTR, H1_SDATE, D_SW)
	XCALL DATE8(ENDAT, D_OUT, D_OUTR, H1_EDATE, D_SW)
	
	CLEAR DAYWGT, TOTWGT, LSTDAT

	CLEAR I
LOOP,
	INCR I
	IF (I/500*500 .EQ. I) XCALL OUTPT (1,70,1,DIS,1)
	LOKCTL = 1
	XCALL IOS (CHN188,BLHEAD,READ,LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF

	IF (BHSHPD.LT.STDAT .OR. BHSHPD.GT.ENDAT) GOTO LOOP
	IF (BHSHPD .NE. LSTDAT) CALL WRT_WORK
	CALL GET_LINES
	GOTO LOOP
EOF,
	CALL WRT_WORK
	CLOSE 3
	SORT (IN='SPL:SHPWGT.DDF' ,RECORD=WORK,	KEY = (DAYDAT) )
	OPEN (3,I,'SPL:SHPWGT')

	CLEAR LSTDAT, TOTWGT
PRLOOP,
	READS (3,WORK,EOF_PR)
	IF (DAYDAT .NE. LSTDAT) CALL NEWDAT
;
	DAYWGT = DAYWGT + WEIGHT
	GOTO PRLOOP
EOF_PR,
	CALL NEWDAT

	CALL PRINT
	PLINE (1,8) = "TOTAL:"
	PLINE (13,23) = TOTWGT,	'ZZZ,ZZZ,ZZX'
	CALL PRINT
	IF (LPONSW.EQ.1) XCALL LPOFF(LPSW,SPLFIL,PGCNT)
	GOTO ENDOFF
;++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
	
GET_LINES,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		;;; SUM WEIGHT FOR ALL L/I CURRENT BL
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR KEY179
	BLORDN = BHORDN
	BLHDRN = BHHDRN
	BLSHPN = BHSHPN
	CLEAR BLLINN
	XCALL ISIO (CHN189,BLLINE,KEY179,READ,LOKCTL)

	WHILE (KEY179(1,9) .EQ. KEY178) 
		BEGIN
		DAYWGT = DAYWGT + BLWGHT
		XCALL IOS (CHN189,BLLINE,READ,LOKCTL)
		END
	RETURN
;--------------------------------------------------------

WRT_WORK,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (LSTDAT .EQ. 0) GOTO OUTWRK
	DAYDAT = LSTDAT
	WEIGHT = DAYWGT
	WRITES (CHNWRK,WORK)
OUTWRK,
	LSTDAT = BHSHPD
	CLEAR DAYWGT
	RETURN
;--------------------------------------------------------

NEWDAT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (LSTDAT .EQ. 0) GOTO OUTDAT

	XCALL DATE8(LSTDAT, D_OUT, D_OUTR, D_FMT, D_SW)
	PLINE (1,10) = D_FMT
	PLINE (13,23) = DAYWGT,	'ZZZ,ZZZ,ZZX'
	CALL PRINT
	TOTWGT = TOTWGT + DAYWGT
OUTDAT,
	LSTDAT = DAYDAT
	CLEAR DAYWGT
	RETURN
;--------------------------------------------------------
PRINT,
	IF (LPONSW.EQ.0) CALL PRNTON
	PRTCTL = 80
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HD1,HD2,'NO HDR3',
&			'NO LEGEND',' ',' ',1,080,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
PRNTON,
	SPLFIL (5,6) = 'EL'
	LPSW = 1	; PRINT, SPOOL OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) CALL CLOSE
;;;	IF (LPSW.EQ.0) XCALL PGCHN ('CP:CPMENU',1)
	IF (LPSW.EQ.0) XCALL PGCHN ('CP:coprpt',1)
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	LPONSW = 1
	RETURN

ENDOFF,
	CALL CLOSE
;;;	XCALL PGCHN ('CP:CPMENU',1)
	XCALL PGCHN ('CP:coprpt',1)

;====================================================

OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (1,'SI',188,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN188 = 1

	XCALL FILES (2,'SI',189,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN189 = 2

	OPEN (3,O,'SPL:SHPWGT.DDF')
	CHNWRK = 3
	OPNOK = 1
	RETURN
;----------------------------------------

CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (CHN188) CLOSE CHN188
	IF (CHN189) CLOSE CHN189
	CLOSE 3
	RETURN
;----------------------------------------
;XX/XX/XX  THRU XX/XX/XX
;
;    DATE         WEIGHT
;XX/XX/XX    ZZZ,ZZZ,ZZX
;
;12345678901234567890123456789012345678901234567890123456789012345678901234567
;         1         2         3         4         5         6         7
