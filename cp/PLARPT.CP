;PLARPT.CP
;
;
;		PLASMA DEPT, LBS, SQFT LINER, DOLLARS


RECORD	ORDHDR
	.INCLUDE 'DEF:RD044A.DEF'
RECORD	ORDLIN
	.INCLUDE 'DEF:RD045A.DEF'

RECORD	WORK
	W_ITEM	,A15
	W_PRICE	,D8
	W_GA	,D2
	W_DESCR	,A30
	W_UOFM	,A2
	W_QTY	,D8
	W_AMOUNT,D10
RECORD,X
	WRK_KEY	,A23

RECORD TITLE
		,A*,	'PLASMA DEPT DATA'
RECORD HDR1
		,A*,	'FROM '
	HSDAT	,A10
		,A*,	' THRU '
	HEDAT	,A10

RECORD HDR2
		,A*,	'ITEM #          DESCRIPTION                    '
		,A*,	'UM     QTY   PRICE       VALUE'
	
RECORD	SUBT
	GA_LBS	,D10
	GA_DLR	,D10

	T_LBS	,D10
	T_DLR	,D10
	T_SQF	,D10

RECORD	CHANNEL
	CHN044	,D2
	CHN045	,D2
	CHNWRK	,D2

RECORD	DIS
	II	,D6

RECORD	VARS
	OPNOK	,D1
	XDATE	,D8
	WRKFIL	,A14,	'SPL:PLASMA.ISM'
	A15	,A15
	GA	,D2
	SAVGA	,D2
	LN	,D6
	HD	,A6,	'NO HDR'
	LG	,A9,	'NO LEGEND'
	RECNO	,D4
	STDAT	,D8
	ENDAT	,D8
	KEY   	,A2
	BSEND	,D5
	BSMID	,D5
	SRCCTL	,D1
	LINCNT	,D2,	60
	SPLFIL	,A14
	LPONSW	,D1
	LPARG	,D1
	RPTNUM	,D3
	PRTTYP	,A1
	PGCNT	,D6,	000000
	PLINE	,A132
	PRTCTL	,D3
	ENTRY	,A15
	INXCTL	,D1
	DECMAL	,D18
	CNGCTL	,D1
	WHATNO	,D1
	SWITCH	,D1
	LPSW	,D2
	V	,D1
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
;
PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,TITLE,1)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO ENDOFF
	CLEAR A15
DISPLA,
	XCALL OUTPT (1,1,2,TITLE,1)
	CLEAR CNGCTL
	XCALL OUTPT (5,1,0,'1. STARTING DATE',1)
	XCALL OUTPT (7,1,0,'2. ENDING   DATE',1)
STDAT,
	XCALL INPUT (5,22,08,00,'DE',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	STDAT = ENTRY(1,8)
	GOTO (ANYCNG),CNGCTL
ENDAT,
	XCALL INPUT (7,22,08,00,'DE',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	ENDAT = ENTRY(1,8)
	GOTO ANYCNG
ANYCNG,
	XCALL ANYCN (CNGCTL,WHATNO)
	GOTO (PROCES, CNGBR),CNGCTL + 1
CNGBR,
	GO TO (STDAT, ENDAT), CNGCTL
	GOTO ANYCNG

PROCES,
	XDATE(1,4) = STDAT(5,8)
	XDATE(5,8) = STDAT(1,4)
	HSDAT = XDATE,	'ZX/XX/XXXX'

	XDATE(1,4) = ENDAT(5,8)
	XDATE(5,8) = ENDAT(1,4)
	HEDAT = XDATE,	'ZX/XX/XXXX'

	
	CLEAR II
	FIND (CHN044, ORDHDR, STDAT, KRF=5) [ERR=LOOP]
LOOP,
	INCR II
	IF (II/100*100 .EQ. II) XCALL OUTPT (1,70,1,DIS,1)
	LOKCTL = 
	XCALL IOS (CHN044, ORDHDR, READ, LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF
	IF (OKEYDT .GT. ENDAT) GOTO EOF

	FIND (CHN045, ORDLIN, OORDNO) [ERR=LOOP]
LINLOP,
	XCALL IOS (CHN045, ORDLIN, READ, LOKCTL)
	IF (LOKCTL .NE. 0) GOTO LOOP
	IF (LORDNO .NE. OORDNO) GOTO LOOP

	IF (LTYPE .EQ. 'M') GOTO LINLOP
	IF (LPRDCD .NE. 'L') GOTO LINLOP
	
	USING LITMNO SELECT
	('16' THRU '26'),	BEGIN		;FITTINGS
				GA = LITMNO(1,2)
				END

	('DLF'),		GA = 99		;LINER

	(),			GOTO LINLOP
	ENDUSING	

	CALL WRT_WORK
	GOTO LINLOP

EOF,
	SAVGA = -1
	FIND (CHNWRK, ^FIRST) [ERR=P_EOF]
P_LOOP,
	READS (CHNWRK, WORK, P_EOF)

	IF (W_GA .NE. SAVGA) CALL NEW_GA

	PLINE (1,15) = W_ITEM
	PLINE (17,46) = W_DESCR
	PLINE (48,49) = W_UOFM
	PLINE (51,57) = W_QTY,		'ZZZ,ZZX'
	PLINE (59,65) = W_PRICE,	'ZZX.XXX'
	PLINE (67,77) = W_AMOUNT,	'ZZZZ,ZZX.XX'
	CALL PRINT

	GA_LBS = GA_LBS + W_QTY
	GA_DLR = GA_DLR + W_AMOUNT

	GOTO P_LOOP

;ITEM #          DESCRIPTION                    UM     QTY   PRICE       VALUE
;AAAAAAAAAAAAAAA AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA AA ZZZ,ZZX ZZX.XXX ZZZZ,ZZX.XX
;12345678901234567890123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5         6         7

P_EOF,
; liner should trigger last call to new_ga
;;;	CALL NEW_GA

	CALL PRINT
	PLINE (22,43) = 'TOTALS FOR ALL GAUGES'
	PLINE (51,57) = T_LBS,	'ZZZ,ZZX'
	PLINE (67,77) = T_DLR,	'ZZZZ,ZZX.XX'
	CALL PRINT

	IF (LPONSW.EQ.1) XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	LPONSW = 0
	LINCNT = 60
	PGCNT =
	GO TO DISPLA


ENDOFF,
	CALL CLOSE
;;;	STOP
	XCALL PGCHN ('CP:COPRPT',1)

;================================================================

WRT_WORK,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	W_ITEM = LITMNO
	W_PRICE = LPRICE
	READ (CHNWRK, WORK, WRK_KEY) [ERR=NEW_WORK]
	W_QTY = W_QTY + LQTYSH
	W_AMOUNT = W_AMOUNT + (LQTYSH*LPRICE)#1
	WRITE (CHNWRK, WORK, LITMNO)
	RETURN
NEW_WORK,
	CLEAR WORK
	W_ITEM = LITMNO
	W_DESCR = LDESCR
	W_PRICE = LPRICE
	W_UOFM = LUOFM
	W_GA = GA
	W_QTY = LQTYSH
	W_AMOUNT = (LQTYSH*LPRICE)#1
	STORE (CHNWRK, WORK, WRK_KEY)
	RETURN
;-----------------------------------------------------------

NEW_GA,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (SAVGA .EQ. -1) GOTO END_GA
	CLEAR PLINE
	CALL PRINT
	PLINE (28,43) = 'TOTALS FOR GAUGE'
	PLINE (45,46) = SAVGA, 'XX'	
	PLINE (51,57) = GA_LBS,	'ZZZ,ZZX'
	PLINE (67,77) = GA_DLR,	'ZZZZ,ZZX.XX'
	CALL PRINT
	CALL PRINT

	T_LBS = T_LBS + GA_LBS
	T_DLR = T_DLR + GA_DLR
END_GA,
	SAVGA = W_GA
	CLEAR GA_LBS, GA_DLR
	RETURN
;------------------------------------------------------

PRINT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (LPONSW.EQ.0) CALL PRNTON
	PRTCTL = 80
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,HD,
&			LG,LG,LG,0,80,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
PRNTON,
	SPLFIL (5,6) = 'EL'
	LPSW = 1	; PRINT, SPOOL OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	LPONSW = 1
	RETURN
;--------------------------------------------------------------------


OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (1,'SI',184,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN044 = 1

	SWITCH = 5
	XCALL FILES (2,'SI',185,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN045 = 2

	XCALL ISAMC (WRKFIL, 75, 1, 'START=1, LENGTH=23, NODUPS, ASCEND')
	OPEN (33, SU, WRKFIL)
	CHNWRK = 33

	OPNOK = 1
	RETURN
;-----------------------------------------------

CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	IF (CHN044) CLOSE CHN044
	IF (CHN045) CLOSE CHN045
	IF (CHNWRK) CLOSE CHNWRK
	RETURN
;-----------------------------------------------

	END

