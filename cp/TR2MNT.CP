;tr2mnt.cp
;
;	COP TABLE MAINTENANCE
;	DEPARTMENT OPERATIONS
;
RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'


RECORD	CHANNEL
	CHN182	,D2


RECORD	PRINT
	TITLE	,A*,	'PRICE CODE MULTIPLIER'
	HD	,A6,	'NO HDR'
	LG	,A9,	'NO LEGEND'
	PLINE	,A80
	PRNTON	,D1
	LINCNT	,D2,60
	PGCNT	,D6
	LPSW	,D2
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	PRTCTL	,D3,080
	LPARG	,D1
	PRNTSW	,D1
	PRTCTR	,D1


RECORD	HD1
		,A*,	'F1    CODE    MULT'


RECORD	VARS
	memo	,d5
	A3	,A3
	A7	,A7
	ALPHA	,A12
	OPNOK	,D1
	BLANKS	,A30
	COD	,A3
	F1	,A3
	MULT	,D7		;XXX.XXX
	F1DSC	,A30
	C_DESC	,A30
	ST_F1	,D5
	EN_F1	,D5
	PGM	,D1
	ROW	,D2
	ENTRY	,A30
	INXCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	STORE	,D1,2
	DELETE	,D1,3
	LOKCTL	,D1
	WHATNO	,D2
	SELECT	,D1
	CNGCTL	,D1
	I	,D3
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'PRICE CODE MULTIPLIER TABLE',1)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO CLOSE

MENU,
	XCALL OUTPT (1,1,2,'PRICE CODE MULTIPLIER TABLE',1)
	XCALL OUTPT (3,9,0,'PLEASE SELECT APPLICATION',1)
	XCALL OUTPT (5,15,0,'1. TABLE MAINTENANCE',1)
	XCALL OUTPT (6,15,0,'2. PRINT TABLE',1)
MINPUT,
	XCALL INPUT (3,36,1,1,'#E',ENTRY,INXCTL,1)
	GOTO (MINPUT,ENDOFF), INXCTL
	PGM = ENTRY(1,1)
	GOTO (DISPLA,PRINT_TABLE),PGM
	GOTO MINPUT

DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,'PRICE CODE MULTIPLIER TABLE',1)
	XCALL OUTPT ( 4,4,0,'1. F1 NOTE',1)
	XCALL OUTPT ( 6,4,0,'2. CODE',1)
	XCALL OUTPT ( 8,4,0,'3. MULT',1)
F1,
	XCALL INPUT (4,24,03,00,'#E',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	F1 = ENTRY(1,3)
	CLEAR TBL_KEY
	TBL_KEY(1,3) = 'M1'
	R2_F1 = F1
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .NE. 0)
		BEGIN
		XCALL MESAG ('INVALID F1 NOTE',1)
		GOTO F1
		END
	F1DSC = M1_LONG
	XCALL OUTPT (4,29,0,M1_LONG,1)

COD,
	XCALL INPUT (6,24,03,00,'AE',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	COD = ENTRY(1,3)

	CLEAR TBL_KEY
	TBL_KEY(1,2) = 'R3'
	R3_COD = COD
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .NE. 0)
		BEGIN
		XCALL MESAG ('BAD PRICE CODE',1)
		GOTO COD
		END
	C_DESC = R3_DESC
	XCALL OUTPT (6,29,0,C_DESC,1)

	CLEAR TBL_KEY
	TBL_KEY(1,2) = 'R2'
	R2_F1 = F1
	R2_COD = COD
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .EQ. 0)
		BEGIN
		SELECT = 2
		CALL DSPREC
		GOTO ANYCNG
		END

	SELECT = 1			;ADD MODE
	CLEAR COPTBL

	R2_F1 = F1
	R2_COD = COD
MULT,
	XCALL INPUT (8,24,06,00,'#E',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	R2_MULT = ENTRY(1,6)
	A7 = R2_MULT, 'ZZX.XXX' [LEFT]
	XCALL OUTPT (8,24,0,A7,1)
	GOTO (ANYCNG),CNGCTL

ANYCNG,
	XCALL OUTPT (24,1,1,'FIELD # TO CHANGE            <TAB> = DELETE',1)
	XCALL INPUT (24,20,02,00,'#T',ENTRY,INXCTL,1)
	IF (INXCTL .EQ. 3)
		BEGIN
		XCALL OUTPT (24,1,1,'DELETE, ARE YOU SURE ?',1)
		XCALL INPUT (24,24,01,01,'YN',ENTRY,INXCTL,1)
		GOTO (ANYCNG),INXCTL-1
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,DELETE,LOKCTL)
		GOTO DISPLA
		END

	WHATNO = ENTRY(1,2)
	IF (WHATNO .EQ. 0)
	THEN 	CLEAR CNGCTL
	ELSE 	CNGCTL = 1

	GOTO (PROCES,CNGBR),CNGCTL+1
CNGBR,
	GOTO (F1, COD, MULT),WHATNO
	GOTO ANYCNG

PROCES,
	CASE SELECT OF
	BEGINCASE
	1:	BEGIN
		TBLCOD = 'R2'
		R2_F1 = F1
		R2_COD = COD
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,STORE,LOKCTL)
		END
	2:	XCALL ISIO (CHN182,COPTBL,TBL_KEY,WRITE,LOKCTL)
	ENDCASE
	GOTO DISPLA

ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('CP:PRCMNU',1)
	STOP


DSPREC,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; DISPLAY RECORD
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	A3 = R2_F1,	'ZZX' [LEFT]
	XCALL OUTPT (4,24,0,A3,1)

	XCALL OUTPT (6,24,1,R2_COD,1)

	A7 = R2_MULT,	'ZZX.XXX'
	XCALL OUTPT (8,24,1,A7,1)
	RETURN
;-----------------------------------



;===================================================================
PRINT_TABLE,
;===================================================================
	LINCNT = 66
	PGCNT  = 0
	
PDISP,
	CNGCTL = 
	XCALL OUTPT (1,1,2,'PRINT PRICE MULTIPLIER TABLE',1)
	XCALL OUTPT (4,4,0,'1. STARTING F1',1)
	XCALL OUTPT (6,4,0,'2. ENDING   F1',1)
ST_F1,
	XCALL INPUT (4,27,03,00,'#E',ENTRY,INXCTL,1)
	GOTO (PDISP,PDONE),INXCTL
	ST_F1 = ENTRY(1,3)
	IF (ST_F1 .EQ. 0)
		BEGIN
		EN_F1 = 999
		XCALL OUTPT (4,25,1,'ALL',1)
		XCALL OUTPT (6,25,1,' ',1)
		GOTO P_ANY
		END
	GOTO (P_ANY),CNGCTL
EN_F1,
	XCALL INPUT (6,25,03,00,'# ',ENTRY,INXCTL,1)
	GOTO (PDISP),INXCTL
	EN_F1 = ENTRY(1,3)
	IF (EN_F1 .EQ. 999)
		BEGIN
		EN_F1 = ST_F1
		A3 = EN_F1,	'ZZX'
		XCALL OUTPT (6,25,0,A3,1)
		END
P_ANY,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (P_PRINT,P_CNGBR),CNGCTL + 1
P_CNGBR,
	GOTO (ST_F1,EN_F1),WHATNO
	GOTO P_ANY

P_PRINT,
	switch = 5
	xcall files (18, 'SI', 182, switch)

	CLEAR TBL_KEY
	TBLCOD = 'R2'
	R2_F1 = ST_F1
	FIND (CHN182,COPTBL,TBL_KEY)[ERR=PLOOP]

PLOOP,
	XCALL IOS (CHN182,COPTBL,READ,LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF
	IF (TBLCOD .NE. 'R2') GOTO EOF
	IF (R2_F1 .LT. ST_F1) GOTO PLOOP
	IF (R2_F1 .GT. EN_F1) GOTO EOF


	PLINE (1,3) = R2_F1,	'ZZX'	;DD_MAT
	PLINE (7,9) = R2_COD
	PLINE (12,18) = R2_MULT,	'ZZX.XXX'

	F1 = R2_F1
	CLEAR TBL_KEY
	TBLCOD = 'M1'
	I3_MAT = F1
	XCALL ISIO (18,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .EQ. 0) PLINE (22,40) = M1_LONG

	COD = R2_COD
	CLEAR TBL_KEY
	TBLCOD = 'R3'
	R3_COD = COD
	XCALL ISIO (18,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .EQ. 0) PLINE (42,60) = R3_DESC

;MAT    GA  PRIC
; AA    XX  ZZX.XXX
;123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5
	CALL PRINT
	GOTO PLOOP

EOF,
	IF (PRNTON.EQ.1)  XCALL LPOFF(LPSW,SPLFIL,PGCNT)
	PRNTON  = 0
	xcall files (18, 'SI', 182, 4)
PDONE,
	GOTO MENU

PRINT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (PRNTON .EQ. 0) CALL PRNTON
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HD1,HD,HD
&		,LG,LG,LG,0,080,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;-------------------------------------------------------------
PRNTON,
	SPLFIL (5,6) = 'EF'
	LPSW = 1		;PRINT,SPOOL, OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTON = 1
	RETURN
;-------------------------------------------------------------
;===================================================================


OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (1,'SU',182,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN182 = 1

	OPNOK = 1
	RETURN
;----------------------------------------------------

CLOSE,
	CLOSE CHN182
	RETURN
;----------------------------------------------------
	



