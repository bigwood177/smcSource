; MSHMNT.CP
;	CASH-CUST SHIP-TO'S

;  SHTMNT / COP
;
; 5-30-18 ssq: make cusmas isam
;
;
;		PROGRAM TO MAINTAIN THE SHIP-TO CODES FILE IN CUSTOMER ORDER PROCESSING
;
RECORD SHIPTO
		.INCLUDE 'DEF:RD171A.DEF'
RECORD CUSCTL
		.INCLUDE 'DEF:RD001B.DEF'
RECORD CUSMAS
		.INCLUDE 'DEF:RD001A.DEF'
RECORD ARTCDE
		.INCLUDE 'DEF:RD169A.DEF'
RECORD ARTCTL
		.INCLUDE 'DEF:RD169B.DEF'
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2

RECORD MSGES
	TXCDNF	,A20,  	'TAX CODE NOT ON FILE'
	MSGCTL	,D1

RECORD KEY
	XCUSNO	,D6
	SHPNO	,D4

RECORD	ATAX
		,A1,	'('
	DTAX	,A3
		,A1,	')'

RECORD	CHANNEL
	CHN101	,D3
	CHN102	,D3
	CHN071	,D3
	CHN169	,D3

RECORD	VARS
	OPNOK	,D1
	PGM	,D2
	A10	,a10
	xdate	,d8
	ALPHA   ,A2
	BS171   ,D5
	BSEND	,D5
	BSMID	,D5
	CKEY	,A6
	CNGCTL	,D1
	ENTRY	,A30
	INXCTL	,D1
	LOKCTL	,D1
	OPNFLG  ,D1
	PAYDEP  ,D2
	READ	,D1	,0
	STORE	,D1	,2
	DELETE	,D1	,3
	RECCNT  ,D5
	SELECT  ,D1
	SORT    ,A1
	SRCCTL	,D1
	SWITCH	,D1
	TMPCOD	,A1
	V       ,D1
	WHATNO  ,D2
	WRITE	,D1	,1
	WRKDEP  ,A2
	WRKSAV  ,D5
PROC
	XCALL TERID(V)
	XCALL OUTPT (1,1,2,'CASH CUSTOMER SHIP-TO',1)
	CALL OPENS
	IF (.NOT. OPNOK) GOTO ENDOFF

MENU,
	XCALL OUTPT (1,1,2,'CASH CUSTOMER SHIP-TO',1)
	XCALL OUTPT (3,9,0,'PLEASE SELECT APPLICATION',1)
	XCALL OUTPT (5,15,0,'1. SHIP-TO MAINTENANCE',1)
	XCALL OUTPT (6,15,0,'2. PRINT SHIP-TO',1)
MINPUT,
	XCALL INPUT (3,36,1,1,'#E',ENTRY,INXCTL,1)
	GOTO (MINPUT,ENDOFF), INXCTL
	PGM = ENTRY(1,1)
	GOTO (DISPLA,PRINT_TABLE),PGM
	GOTO MINPUT

DISPLA,
	UNLOCK 1
	CNGCTL =
	XCALL OUTPT (1,1,2,'CASH CUSTOMER SHIP-TO FILE MAINTENANCE',1)
	XCALL OUTPT (04,17,0,'1. CUSTOMER NO',1)
	XCALL OUTPT (06,17,0,'2. SHIP-TO NO',1)
	xcall outpt (06,46,0,'LAST ORDER:',1)
	XCALL OUTPT (08,17,0,'3. SHIP-TO NAME',1)
	XCALL OUTPT (10,17,0,'4. SHIP-TO ADDR-1',1)
	XCALL OUTPT (12,17,0,'5. SHIP-TO ADDR-2',1)
	XCALL OUTPT (14,17,0,'6. CITY',1)
	XCALL OUTPT (15,17,0,'7. STATE',1)
	XCALL OUTPT (16,17,0,'8. ZIP',1)
	XCALL OUTPT (18,17,0,'9. SHIP-TO TAX CODE',1)
	XCALL OUTPT (20,16,0,'10. SHIP-TO PHONE #',1)
	XCALL OUTPT (22,16,0,'11. SHIP-TO FAX #',1)

SHCSNO,
	CTL = '04,38,06,01,#E'
	CALL INPUT
	GO TO (DISPLA,MENU), INXCTL
	XCUSNO = ENTRY	
	XCALL ISIO (CHN101, CUSMAS, XCUSNO, READ, LOKCTL)
	IF (LOKCTL .NE. 0)
		BEGIN
		XCALL MESAG ('CASH-CUST NOT ON FILE',1)
		GOTO DISPLA
		END

	XCALL OUTPT (4,46,0,NAME,1)

SHTONO,
	CTL = '06,38,04,01,#E'
	CALL INPUT
	GO TO (DISPLA,MENU), INXCTL
	SHPNO = ENTRY

	LOKCTL = 0
	XCALL ISIO (CHN102, SHIPTO, KEY, READ, LOKCTL)
	IF (LOKCTL .EQ. 0)
		BEGIN
		SELECT = 2
		CALL DSPREC
		GOTO ANYCNG
		END

	SELECT = 1
	CLEAR SHIPTO


SHNAME,
	SHCSNO = CUSNO
	SHTONO = SHPNO
	CTL = '08,38,30,00,A '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	SHTONA = ENTRY
	XCALL OUTPT (08,38,0,SHTONA,1)
	GO TO (ANYCNG), CNGCTL
SHADD1,
	CTL = '10,38,30,00,A '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	SHTOAD(1) = ENTRY
	XCALL OUTPT (10,38,0,SHTOAD(1),1)
	GO TO (ANYCNG), CNGCTL
SHADD2,
	CTL = '12,38,30,00,A '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	SHTOAD(2) = ENTRY
	XCALL OUTPT (12,38,0,SHTOAD(2),1)
	GO TO (ANYCNG), CNGCTL
SHTCTY,
	CTL = '14,38,16,00,A '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	SHTCTY = ENTRY
	XCALL OUTPT (14,38,0,SHTCTY,1)
	GO TO (ANYCNG), CNGCTL
SHTST,
	CTL = '15,38,02,02,A '
	CALL INPUT
	GOTO (DISPLA), INXCTL
	SHTST = ENTRY(1,2)
	GOTO (ANYCNG),CNGCTL
SHTZIP,
	CTL = '16,38,10,00,A '
	CALL INPUT
	GOTO (DISPLA), INXCTL
	SHTZIP = ENTRY(1,10)
	GOTO (ANYCNG), CNGCTL

SHTOTX,
	XCALL OUTPT (18,44,0,'                              ',1)
	DTAX = TAXFLG
	XCALL OUTPT (18,44,0,ATAX,1)
	CTL = '18,38,03,00,A '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	SHTOTC = ENTRY
	CALL GTTXDS
	IF (SRCCTL.EQ.0) GO TO DSPTAX
	MSGCTL = 1
	XCALL MESAG(TXCDNF,MSGCTL)
	GO TO SHTOTX
DSPTAX,
	XCALL OUTPT (18,38,0,SHTOTC,1)
	XCALL OUTPT (18,44,0,ARTDSC,1)
	GO TO (ANYCNG), CNGCTL

SHPHON,
	CTL = '20,38,10,00,# '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	SHPHON = ENTRY(1,10)
	ENTRY(1,13) = SHPHON,	'ZZZ XXX-XXXX'
	XCALL OUTPT (20,38,0,ENTRY(1,13),1)
	GO TO (ANYCNG), CNGCTL
SHFAX,
	CTL = '22,38,10,00,# '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	SHFAX = ENTRY(1,10)
	ENTRY(1,13) = SHFAX,	'ZZZ XXX-XXXX'
	XCALL OUTPT (22,38,0,ENTRY(1,13),1)
	GO TO (ANYCNG), CNGCTL

ANYCNG,
	XCALL ANYCN (CNGCTL,WHATNO)
	GO TO (PROCES,CNGBR,DISPLA), CNGCTL + 1
CNGBR,
	GO TO (SHNAME,SHADD1,SHADD2,SHTCTY,SHTST,SHTZIP,SHTOTX,SHPHON,SHFAX), WHATNO-2
	CNGCTL = 3
	GO TO ANYCNG

PROCES,
	USING SELECT SELECT
	(1),	BEGIN
		SHCSNO = XCUSNO
		SHTONO = SHPNO
		XCALL ISIO (CHN102, SHIPTO, SHTKEY, STORE, LOKCTL)
		END
	(2),	XCALL ISIO (CHN102, SHIPTO, SHTKEY, WRITE, LOKCTL)
	ENDUSING

	GOTO DISPLA


PRINT_TABLE,
	XCALL PGCHN ('CP:MSHPRT',1)

;**********************************************************************
DSPREC,
	XCALL ISIO (2, CUSMAS, XCUSNO, READ, LOKCTL)
	IF (LOKCTL .NE. 0) 
		BEGIN
		CUSMAS =
		NAME = '* NOT ON FILE *'
		END
		
	XCALL DSPLY (6,8,38,SHCSNO,1,1)

	XDATE(1,4) = SHODAT(5,8)
	XDATE(5,8) = SHODAT(1,4)
	A10 = XDATE,	'ZX/XX/XXXX'

	XCALL OUTPT (4,46,0,NAME,1)
	XCALL DSPLY (4,06,38,SHTONO,1,1)

	XCALL OUTPT (6,58,0,A10,1)

	XCALL OUTPT (08,38,0,SHTONA,1)
	XCALL OUTPT (10,38,0,SHTOAD(1),1)
	XCALL OUTPT (12,38,0,SHTOAD(2),1)
;;;	XCALL OUTPT (14,38,0,SHTOAD(3),1)
	XCALL OUTPT (14,38,0,SHTCTY,1)
	XCALL OUTPT (15,38,0,SHTST,1)
	XCALL OUTPT (16,38,0,SHTZIP,1)

	CALL GTTXDS
	XCALL OUTPT (18,38,0,SHTOTC,1)
	XCALL OUTPT (18,43,0,ARTDSC,1)

	ENTRY(1,13) = SHPHON,	'ZZZ XXX-XXXX'
	XCALL OUTPT (20,38,0,ENTRY(1,13),1)
	ENTRY(1,13) = SHFAX,	'ZZZ XXX-XXXX'
	XCALL OUTPT (22,38,0,ENTRY(1,13),1)

	RETURN
;-----------------------------------------------

	
CHANGE,
	IF (SELECT.EQ.3) GO TO DELETE
	LOKCTL = 1
	XCALL ISIO (1,SHIPTO,SHTKEY,WRITE,LOKCTL)
	GO TO DISPLA
DELETE,
	XCALL OUTPT (24,1,1,'OKAY TO DELETE  ?',1)
	CTL = '24,20,01,01,YN'
	CALL INPUT
	IF (INXCTL.NE.1) GO TO DISPLA
	XCALL WATE(3,V)
	LOKCTL = 1
	XCALL ISIO (1,SHIPTO,SHTKEY,DELETE,LOKCTL)


	XCALL MESAG ('RECORD DELETED',2)
	GO TO DISPLA

;**********************************************************************
INPUT,
	XCALL INPUT(ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,1)
	RETURN
;********************************************************************************
GTTXDS,
	IF (SHTOTC.NE.'   ') GO TO FINDTX
	SHTOTC = TAXFLG
FINDTX,
	BSEND = ORG169
	SRCCTL = 2
	XCALL SERCH (4,ARTCDE,SHTOTC,1,3,BSEND,BSMID,SRCCTL,1,4,9,0,0,0,0)
	IF (SRCCTL.NE.0) ARTDSC = 'TAX CODE NOT ON FILE'
	RETURN
;********************************************************************************
ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('CP:MISMNU',1)

OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	OPNOK = 0

	SWITCH = 5
	XCALL FILES (1,'SU',102, SWITCH)	;102 -- MISSHP.ISM
	IF (SWITCH .EQ. 9) RETURN
	CHN102 = 1

	SWITCH = 5
	XCALL FILES (2,'SI',101,SWITCH)		;01 -- MISCUS
	IF (SWITCH .EQ. 9) RETURN
	CHN101 = 2

	SWITCH = 5
	XCALL FILES (4,'I',169,SWITCH)		;169 -- ARTCDE
	IF (SWITCH .EQ. 9) RETURN
	CHN169 = 4

	XCALL IO (4,ARTCTL,1,READ,LOKCTL)

	OPNOK = 1
	RETURN
;---------------------------------------------------------------

CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	IF (CHN101) CLOSE CHN101
	IF (CHN102) CLOSE CHN102
	IF (CHN169) CLOSE CHN169

	RETURN
;---------------------------------------------------------------


END
