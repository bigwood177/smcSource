SUBROUTINE PREST
	OR_NUMBER	,D

; Version of wrkord to print (E)stimates locally...
;WRKORD.COP - PRINT WORK ORDER FORMS FROM DETACHED STATE
;
;
;
;	9-19-97 SCAC MOD
;	NT-SERVER VERSION.

	.INCLUDE 'DEF:HPSUB.DEF'


RECORD	O_FILE
		,A4,	'SPL:'
		,A1,	'W'
	O_TIME	,D6
		,A4,	'.WRK'


RECORD CUSMAS
	.INCLUDE 'DEF:RD001A.DEF'

RECORD CUSCTL	,X
	.INCLUDE 'DEF:RD001B.DEF'

RECORD CUSIDX
	.INCLUDE 'DEF:RD002A.DEF'

RECORD	SALMAN
	.INCLUDE 'DEF:RD054A.DEF'
RECORD ORDHDR
	.INCLUDE 'DEF:RD044A.DEF'

RECORD ORDLIN
	.INCLUDE 'DEF:RD045A.DEF'

GLOBAL DUCK	,INIT
	.INCLUDE 'DEF:RD175D.DEF'
ENDGLOBAL
;:
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;RECORD SHPVIA
;;;	.INCLUDE 'DEF:RD172A.DEF'
;;;RECORD SHVCTL	,X
;;;	.INCLUDE 'DEF:RD172B.DEF'

RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

RECORD PARAM
	.INCLUDE 'DEF:PARAM2.DEF'


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;RECORD MSREC
;;;	MSGNO	,D5,	00000
;;;		,A1,	':'
;;;	MESAGE	,A8
;;;RECORD ,X
;;;		,A6
;;;	ORDER	,D6
;;;	ORDEST	,A2
;;;
;;;RECORD	V_PRINTERS
;;;	LOCAL_PRINTER	,A13,	'LOCAL        '
;;;	WO_PRINTER	,A13,	'SERVER01_HP  '
;;;	PRINTER_NAME	,A13
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

RECORD
	MAXDUC	,D3,100
	QTY	,D5
	ITM	,A15
	DES	,A30
	PRC	,D7
	COMPA	,A15
	COMPB	,A15
RECORD ARRAYS
	SDUCRC	,100A32	;SUMMARIZED DUCT CONFIGURATIONS   (SEE DUCREC BELOW)
	LINSQF	,8D6	;SUMMARIZE LINER SQUARE FEET
RECORD DUCREC
	DUCCFG	,D7	;CONFIGURATION CODE
	DUCSQF	,D7	;SQ FEET OF MATERIAL
	DUCPND	,D7	;POUNDS OF MATERIAL
	DUCGPR	,D7	;GAUGE PRICE
RECORD ACCREC
	.INCLUDE 'DEF:ACCREC.DEF'
RECORD
	INIT	,20A3,	'JJM','TLE','LAM','DEG','JTH',
&			'DLH','JIM','SJM','CJM','BIL',
&			'MAW','PAT','BJZ','JPV','MJM',
&			'JEF','   ','   ','   ','   '

RECORD	BOLD
	B_ESC	,A1
		,A4,	'(s4B'		;extra bold
RECORD	NORMAL
	N_ESC	,A1
		,A4,	'(s0B'		;medium
RECORD	H_POS
	H_ESC	,A1
		,A5,	'&a24C'
RECORD	VARS
	CHN182	,D2
	CHN044	,D2
	CHN045	,D2
	CHN054	,D2
	CHN175	,D2
	STAT	,D1
	ORDER	,D6
	LOKCTL	,D1
	READ	,D1
	Lrow	,D2
	Lcol	,D3
	DECMAL	,D18
	PLINE	,A80
	PLINE2	,A95
	LINCNT	,D3
	TOTPRC	,D8
	BLANKS	,A30
	CONFIG	,D7
	PAGE	,D2
	KEY	,A6
	BSMID	,D5
;;;	BSEND	,D5
	SRCCTL	,D1
	SWITCH	,D1
	ORGCUS	,D5
	ORGSHV	,D5
	V	,D1
	ACCUOM	,A2
	LFEED	,D2
	DTMASK	,A8,	'XX/XX/XX'
	NUMASK	,A8,	'ZZZZZZX-'
	MAXLIN	,D2
	ENTRY	,A30
	INXCTL	,D1
	NO_DOLLARS	,D1

PROC
	CALL OPENS
;;;	XCALL ASCII(27,B_ESC)
;;;	N_ESC = B_ESC
;;;	H_ESC = B_ESC
	XCALL OUTPT (24,1,1,'SUPPRESS PRICE INFO ?',1)
	XCALL INPUT (24,23,01,00,'YN',ENTRY,INXCTL,1)
	IF (INXCTL.EQ.1) 
	THEN	NO_DOLLARS = 1
	ELSE	CLEAR NO_DOLLARS

RDHDR,
	LOKCTL = 1
	XCALL IO (36,CUSCTL,1,READ,LOKCTL)
	ORGCUS = ORG001

GETMSG,
;	create a unique spool file name and open it...
	XCALL TIME (O_TIME)
	OPEN (14,O,O_FILE)
	XCALL HP (14,hpDOTS,$false)

	MAXLIN = 35

	TOTPRC =
	ARRAYS =
	FOR I FROM 1 THRU MAXACC	SACCRC (I) =
	DUCREC =
	ACCREC =
	OORDNO = OR_NUMBER
	ORDER = OR_NUMBER

	LOKCTL = 1
	XCALL ISIO (CHN044,ORDHDR,OORDNO,READ,LOKCTL)
	IF (LOKCTL.NE.0.OR.OORDNO.NE.ORDER) GOTO INVORD

	CALL FNDCUS
	PAGE = 1
	CALL PRTHDR
GETLIN,
	LORDNO = ORDER
	LOKCTL = 1
	XCALL ISIO (CHN045,ORDLIN,LORDNO,READ,LOKCTL)
	IF (LOKCTL.NE.0.OR.LORDNO.NE.ORDER) GOTO GETDUC
	CALL PRTLIN
NXTLIN,
	IF (LINCNT.GE.36) CALL CONTIN
	LOKCTL = 1
	XCALL IOS (CHN045,ORDLIN,READ,LOKCTL)
	IF (LOKCTL.NE.0.OR.LORDNO.NE.ORDER) GOTO GETDUC
	CALL PRTLIN
	GOTO NXTLIN
GETDUC,
	DUCTOR = ORDER
	LOKCTL = 1
	XCALL ISIO (CHN175,DUCACC,DUCTOR,READ,LOKCTL)
	IF (LOKCTL.NE.0.OR.DUCTOR.NE.ORDER) GOTO ENDORD
	CALL PRTDUC
	GOTO SUMDUC
NXTDUC,
	LOKCTL = 1
	XCALL IOS (CHN175,DUCACC,READ,LOKCTL)
	IF (LOKCTL.NE.0.OR.DUCTOR.NE.ORDER) GOTO ENDORD
	CALL PRTDUC
SUMDUC,
	CONFIG(1,3) = GAUGE
	IF (DUTYPE.EQ.2.OR.DUTYPE.EQ.3) CONFIG(1,3) = THICK
	CONFIG(4,4) = DUTYPE
	CONFIG(5,5) = CAT
	CONFIG(6,6) = STY
	CONFIG(7,7) = SEAM

		; Check to see if the configuration has all ready 
		; been on this order
	I =
	DO BEGIN
	  INCR I
	  DUCREC = SDUCRC(I)
	END
	UNTIL (DUCCFG.EQ.CONFIG.OR.DUCCFG.EQ.0.OR.I.GE.MAXDUC)
	IF (I.GE.MAXDUC) GOTO BADCON

	DUCCFG = CONFIG
	DUCSQF = DUCSQF + SQFEET
	DUCPND = DUCPND + POUNDS
	DUCGPR = GPRICE
	SDUCRC(I) = DUCREC

	IF (LINER.GE.1.AND.LINER.LE.8.AND.LINER.NE.4)
&		LINSQF(LINER) = LINSQF(LINER) + SQFLIN

		;Summarize accessories

	IF (ACC.EQ.2) GOTO NXTDUC

	IF (SLIPS)
	BEGIN
	  QTY = SLIPS
	  ITM = SLPITM
	  PRC = SLPPRC
	  DES = SLPDES
	  CALL SUMACC
	END
	IF (DRIVES)
	BEGIN
	  QTY = DRIVES
	  ITM = DRVITM
	  PRC = DRVPRC
	  DES = DRVDES
	  CALL SUMACC
	END
	IF (TCORN)
	BEGIN
	  QTY = TCORN
	  ITM = CORITM
	  PRC = CORPRC
	  DES = CORDES
	  CALL SUMACC
	END
	IF (TNBQTY)
	BEGIN
	  QTY = TNBQTY
	  ITM = TNBITM
	  PRC = TNBPRC
	  DES = TNBDES
	  CALL SUMACC
	END
	IF (TGAQTY)
	BEGIN
	  QTY = TGAQTY
	  ITM = TGAITM
	  PRC = TGAPRC
	  DES = TGADES
	  CALL SUMACC
	END
	IF (TCLQTY)
	BEGIN
	  QTY = TCLQTY
	  ITM = TCLITM
	  PRC = TCLPRC
	  DES = TCLDES
	  CALL SUMACC
	END
	IF (TBAQTY)
	BEGIN
	  QTY = (JOINT * 4)
	  ITM = TBAITM
	  ITM (10,13) = SIZE1,'ZZX"'
	  PRC = TBAPRC
	  DES = TBADES
	  CALL SUMACC
	  QTY = (JOINT * 4)
	  ITM = TBAITM
	  ITM (10,13) = SIZE2,'ZZX"'
	  PRC = TBAPRC
	  DES = TBADES
	  CALL SUMACC
	END
	IF (TBNQTY)
	BEGIN
	  QTY = TBNQTY
	  ITM = TBNITM
	  PRC = TBNPRC
	  DES = TBNDES
	  CALL SUMACC
	END

	GOTO NXTDUC

SUMACC,
		; Check to see if the accessories have all ready 
		; been on this order
	I =
	DO BEGIN
	  INCR I
	  ACCREC = SACCRC(I)
	END
	UNTIL (ACCITM.EQ.ITM.OR.ACCITM.EQ.BLANKS.OR.I.GE.MAXACC)
	IF (I.GE.MAXACC) GOTO BADACC

	ACCQTY = ACCQTY + QTY
	ACCITM = ITM
	ACCPRC = PRC
	ACCDES = DES
	SACCRC(I) = ACCREC

	RETURN
BADCON,
;;;	LFEED = 8
;;;	XCALL LINFD (LFEED)
	LINCNT = LINCNT + 8
	PLINE (32,60) = 'DUCT INFORMATION IS '	
	CALL PRINT
	GOTO ERRMSG
BADACC,
;;;	LFEED = 8
;;;	XCALL LINFD (LFEED)
	LINCNT = LINCNT + 8
	PLINE (32,60) = 'ACCESSORY INFORMATION IS'	
	CALL PRINT
ERRMSG,
	PLINE (32,60) = 'PRESENTLY OVERFLOWING THE'
	CALL PRINT
	PLINE (32,60) = 'ARRAY BOUNDARIES.  PLEASE CALL'
	CALL PRINT
	PLINE (32,60) = 'SOFTWARE SUPPORT TO CORRECT THE'
	CALL PRINT
	PLINE (32,60) = 'PROBLEM'
	CALL PRINT
;;;	LFEED = 41 - LINCNT
;;;	IF (LFEED) XCALL LINFD (LFEED)
	GOTO ENDUP
INVORD,
	LFEED = 8
;;;	XCALL LINFD (LFEED)
	LINCNT =
	PLINE (32,60) = ORDER,'ORDER NUMBER: XXXXXX IS NOT'	
	CALL PRINT
	PLINE (32,60) = 'FOUND ON THE ORDER HEADER FILE'
	CALL PRINT
	PLINE (32,60) = 'PLEASE NOTE THIS AND CALL'
	CALL PRINT
	PLINE (32,60) = 'SOFTWARE SUPPORT TO CORRECT THE'
	CALL PRINT
	PLINE (32,60) = 'PROBLEM'
	LFEED = 41 - LINCNT
;;;	IF (LFEED) XCALL LINFD (LFEED)
	GOTO ENDUP

;------------------------------------------

ENDORD,
	CALL PRTSAC
	CALL PRTSDU
	LFEED = MAXLIN - LINCNT
;;;	IF (LFEED) XCALL LINFD (LFEED)
	IF (LFEED) LINCNT = MAXLIN

	CLEAR OCOMNT(1)
	CLEAR OCOMNT(2)

	PLINE (25,58) = OCOMNT(1) 
	CALL PRINT
	PLINE (25,58) = OCOMNT(2) 
	PLINE (56,68) = 'TOTAL ORDER: '
	PLINE (69,79) = TOTPRC,'ZZZ,ZZZ.ZZ-'
	CALL PRINT
ENDUP,
;;;	IF (ORDEST.EQ.'E') XCALL LINFD (0)
	CLOSE 14

;;; Send file to wo_printer, or local_printer (determined above)...
	LPQUE(O_FILE, DELETE)
	
STOPIT,
	CALL CLOSE
	RETURN

;=================================================

CONTIN,
	LFEED = MAXLIN - LINCNT
;;;	IF (LFEED) XCALL LINFD (LFEED)
	IF (LFEED) LINCNT = MAXLIN

	PLINE (25,58) = OCOMNT(1) 
	CALL PRINT
	PLINE (25,58) = OCOMNT(2) 
	PLINE (64,78) = '** CONTINUED **'
	CALL PRINT
;;;	IF (ORDEST.EQ.'E') XCALL LINFD (0)
;;;	XCALL LINFD(0)
	XCALL HP (14,hpFLUSH)

	INCR PAGE

PRTHDR,		; Print the order header information

	Lrow = 1
	XCALL HP (14,hpFONT,hp8CPI+hpBOLD)
	XCALL HP (14,hpPOS,Lrow,22,0,"Q U O T A T I O N")
	XCALL HP (14,hpFONT,hp10CPI+hpMEDIUM)
	INCR Lrow

	XCALL HP (14,hpPOS,Lrow,1,0,"SHEET METAL CONNECTORS, INC.")
	XCALL HP (14,hpPOS,Lrow,61,0,"Quotation No.")
	XCALL HP (14,hpFONT,hpBOLD)
	XCALL HP (14,hpPOS,Lrow,75,0,OORDNO)
	INCR Lrow

	XCALL HP (14,hpFONT,hpMEDIUM)
	XCALL HP (14,hpPOS,Lrow,1,0,"5850 MAIN STREET N. E.")
	INCR Lrow

	XCALL HP (14,hpPOS,Lrow,1,0,"MINNEAPOLIS,   MN 55432")
	XCALL HP (14,hpPOS,Lrow,68,0,"DATE")
	PLINE (73,80) = OORDDT,DTMASK
	XCALL HP (14,hpPOS,Lrow,73,0,PLINE(73,80))
	INCR Lrow

	XCALL HP(14,hpFONT,hpBOLD)
	XCALL HP(14,hpPOS,Lrow,1,0,"800-328-1966  612-572-0000")
	INCR Lrow

	XCALL HP(14,hpFONT,hpMEDIUM)
	XCALL HP(14,hpPOS,Lrow,1,0,'WEB SITE: WWW.SMDUCT.COM')
	INCR Lrow

	XCALL HP(14,hpFONT,hpBOLD)
	XCALL HP(14,hpPOS,Lrow,1,0,"FAX:612-572-1100")
	XCALL HP(14,hpFONT,hpMEDIUM)
	XCALL HP(14,hpPOS,Lrow,50,0,"SHIP TO:")
	INCR Lrow

	CLEAR PLINE
	PLINE (23,28) = CRDLMT,'ZZZZZZ'
	XCALL HP(14,hpPOS,Lrow,23,0,PLINE(23,28))
	INCR Lrow

	XCALL HP(14,hpPOS,Lrow,14,0,OCUSNM,hpPOS,Lrow,50,0,OSHPNM)
	INCR Lrow
	
	XCALL HP(14,hpPOS,Lrow,14,0,ADD1,hpPOS,Lrow,50,0,OSHAD1)
	INCR Lrow

	XCALL HP(14,hpPOS,Lrow,14,0,ADD2,hpPOS,Lrow,50,0,OSHAD2)
	INCR Lrow

	CLEAR PLINE
	PLINE (14,28) = CITY
	PLINE (31,32) = STATE
	PLINE (35,39) = ZIP
	PLINE (50,79) = OSHAD3
	XCALL HP(14,hpPOS,Lrow,1,0,PLINE)
	Lrow = Lrow + 4

	PLINE = 'Cust #   Sales-Rep      Job #       Freight FOB           Fab Time Terms    Page'
	XCALL HP (14,hpPOS,Lrow,1,0,PLINE)
	INCR Lrow

	CLEAR PLINE
	PLINE (1,6) = OCUSNO,NUMASK
	XCALL HP (14,hpPOS,Lrow,1,0,PLINE(1,6))
	XCALL HP (14,hpFONT,hpBOLD)

	CALL GET_SALESMAN
	XCALL HP (14,hpPOS,Lrow,9,0,SLSNM)
	XCALL HP (14,hpFONT,hpMEDIUM)


	PLINE (24,33) = OJOBNO
	IF (OCLPPD.EQ.'C') PLINE (36,48) = 'MPLS. - '
	IF (OCLPPD.EQ.'P') PLINE (36,48) = 'DEST. - '
	CALL GET_SCAC
	PLINE (44,56) = SC_NAME
	PLINE (58,60) = OPROMD,NUMASK
	PLINE (61,64) = 'DAYS'
	PLINE (68,74) = 'NET 30'
	PLINE (78,79) = PAGE,'XX'
	XCALL HP (14,hpPOS,Lrow,24,0,PLINE(24,79))
	Lrow = Lrow + 3

	XCALL HP (14,hpFONT,hpBOLD)
	PLINE (3,53) = 'We are pleased to quote on your inquiry as follows:'
	XCALL HP (14,hpPOS,Lrow,1,0,PLINE(3,53))
	XCALL HP(14,hpFONT,hpMEDIUM)
	Lrow = Lrow + 2


	XCALL HP (14,hpFONT,hp12CPI)
	PLINE = '    Qty          Item No.     Description            Unit Price  UM Ext. Price'
	XCALL HP(14,hpPOS,Lrow,1,0,PLINE)
	INCR Lrow

;;;	XCALL HP (14,hpTHICK,5)
;;;	XCALL HP (14,hpPOS,Lrow,1,hpHLINE,80)
;;;	INCR Lrow

	LINCNT =
	RETURN


PRTLIN,		; Print out line item detail
	IF ((LINCNT+2).GT.MAXLIN) CALL CONTIN

	IF (LCPFLG) 
	THEN	BEGIN
		PLINE2 (72,73) = '**'	
		END
	ELSE	BEGIN
		CLEAR PLINE2
		END
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	PLINE2 ( 18,30 ) = LITMNO
	IF ( LITMNO.EQ.'???' ) 
		BEGIN
		PLINE2 (18,30) = '*'
		PLINE2 (19,20) = LITMNO(4,5)
		END

	PLINE2 ( 31,60 ) = LDESCR
	IF (LITMNO.NE.'M')
	BEGIN
	  PLINE2 ( 1,7 ) = LQTYOR,NUMASK
	  PLINE2 (62,72) = LPRICE,'ZZZZZZ.XXX-'
	  PLINE2 (74,75) = LUOFM
	  DECMAL = ( ( LQTYOR * LPRICE ) # 1 )
&		- ( ( LQTYOR * ( LPRICE * LDISC ) #1 ) # 2 )
	  PLINE2 (77,87) = DECMAL,'ZZZZZZZ.XX-'
	  TOTPRC = TOTPRC + DECMAL		;INVENTORY ITEMS
	END
;    Qty          Item No.     Description            Unit Price  UM Ext. Price
;ZZZZZZX          AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAZZZZZZ.XXX- AA ZZZZZZZ.XX-
;12345678901234567890123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5         6         7

	IF (NO_DOLLARS)
		BEGIN
		PLINE2(63,72) = 
		PLINE2(77,87) =
		END

	XCALL HP (14,hpPOS,Lrow+LINCNT,1,0,PLINE2)
	INCR LINCNT
;;;	XCALL HP (14,hpFONT,hp10CPI)
	RETURN

PRTDUC,		;Print duct record
	XCALL HP (14,hpFONT,hp12CPI)

	IF ((LINCNT+3).GT.MAXLIN) CALL CONTIN
	INCR LINCNT		;SSQ 12-4-96

	PLINE2 (1,7) = JOINT,NUMASK
	PLINE2 (8,9) = 'JT'
	IF (DUTYPE.NE.2.AND.DUTYPE.NE.3)
	BEGIN
	  PLINE2 (13,14) = GAUGE,'XX'
	  PLINE2 (15,16) = 'GA'
	END
	IF (DUTYPE.EQ.2.OR.DUTYPE.EQ.3)
	BEGIN
	  PLINE2 (13,16) = THICK,'.XXX'
	END
	PLINE2 (18,20) = SIZE1,'ZZX'
	PLINE2 (21,21) = 'X'
	PLINE2 (22,24) = SIZE2,'ZZX'
	PLINE2 (25,25) = 'X'
	PLINE2 (26,27) = SIZE3,'ZX'
	PLINE2 (33,36) = DTYPE(DUTYPE)
	PLINE2 (38,43) = DCAT(CAT)
	PLINE2 (45,47) = DSTY(STY)
	PLINE2 (49,51) = DSEAM(SEAM)
	IF (SEAL.EQ.1) PLINE2 (53,56) = 'SEAL'
	PLINE2 (62,72) = GPRICE,'ZZZZZ.XXX-'
	DECMAL = ( POUNDS * GPRICE ) # 1
	PLINE2 (74,75) = 'LB'
	PLINE2 (77,87) = DECMAL,'ZZZZZZ.XX-'
	TOTPRC = TOTPRC + DECMAL		;DUCT

	IF (NO_DOLLARS)
		BEGIN
		PLINE2(62,72) = 
		PLINE2(85,95) =
		END
	XCALL HP (14,hpPOS,Lrow+LINCNT,1,0,PLINE2)
	INCR LINCNT

	PLINE2 (  1,7  ) = POUNDS,NUMASK
	PLINE2 (  8,8  ) = '#'
	PLINE2 ( 12,17 ) = SQFLIN,NUMASK
	PLINE2 ( 19,23 ) = 'SQ FT'
	PLINE2 ( 33,45 ) = '    NO LINER '
	IF (LINER.GE.1.AND.LINER.LE.8.AND.LINER.NE.4)
	BEGIN
	  PLINE2 ( 33,39 ) = DLINER( LINER )
	  PLINE2 ( 40,45 ) = 'LINER '
	END
	PLINE2 ( 46,60 ) = LOCAT
	PLINE2 (62,72) = LINPRC,'ZZZ.XXX-'
	PLINE2 (74,75) = 'SF'
	DECMAL = ( LINPRC * SQFLIN ) # 1
	PLINE2 (77,87) = DECMAL,'ZZZZZZZ.XX-'
	TOTPRC = TOTPRC + DECMAL		;LINER

	IF (NO_DOLLARS)
		BEGIN
		PLINE2(62,72) = 
		PLINE2(77,87) =
		END
	XCALL HP(14,hpPOS,Lrow+LINCNT,1,0,PLINE2)
	INCR LINCNT

	IF (ACC.EQ.1)
&	DECMAL = ( ( SLIPS  * SLPPRC ) # 1 ) +
&		 ( ( DRIVES * DRVPRC ) # 1 ) +
&		 ( ( TCORN  * CORPRC ) # 1 ) +
&		 ( ( TNBQTY * TNBPRC ) # 1 ) +
&		 ( ( TGAQTY * TGAPRC ) # 1 ) +
&		 ( ( TCLQTY * TCLPRC ) # 1 ) +
&		 ( ( TBAQTY * TBAPRC ) # 1 ) +
&		 ( ( TBNQTY * TBNPRC ) # 1 )

;;;	XCALL HP (14,hpFONT,hp10CPI)
	RETURN

PRTSAC,
		; Sort then Print summarized accessories from this order
;
;	BUBBLE SORT ACCREC ARRAY
;
	FOR I FROM 1 THRU MAXACC
	  BEGIN
	  ACCREC = SACCRC(I)
	  IF (ACCQTY.EQ.0) GOTO PRTSA2
	  COMPA = ACCITM
	  ACCREC = SACCRC(I+1)
	  IF (ACCQTY.EQ.0) GOTO PRTSA2
	  COMPB = ACCITM
	  IF (COMPA.GT.COMPB)
	  BEGIN
	    SACCRC(I+1) = SACCRC(I)
	    SACCRC(I) = ACCREC
	    I = I - 2
	    IF (I.LT.0) I = 0
	  END
	END
	GOTO BADACC
PRTSA2,
	FOR I FROM 1 THRU MAXACC
	  BEGIN
	  ACCREC = SACCRC(I)
	  IF (ACCITM.EQ.BLANKS) RETURN
	  IF ((LINCNT+1).GT.MAXLIN) CALL CONTIN
	  PLINE2 (1,7) = ACCQTY,NUMASK
	  PLINE2 (18,30) = ACCITM
	  PLINE2 (31,60) = ACCDES
	  ACCUOM = 'EA'
	  IF (ACCITM(4,6).EQ.'GS ') ACCUOM = 'FT'
	  IF (ACCITM(4,6).EQ.'BAR') 
	  BEGIN
	    ACCUOM = 'IN'
	    DECMAL = ACCITM(10,12)
	    ACCQTY = ACCQTY * DECMAL
	  END
	  PLINE2 (74,75) = ACCUOM
	  PLINE2 (62,72) = ACCPRC,'ZZZZZZ.XXX-'
	  DECMAL = (ACCQTY * ACCPRC) #1
	  PLINE2 (77,87) = DECMAL,'ZZZZZZZ.XX-'
	  TOTPRC = TOTPRC + DECMAL		;ACCESSORIES

	IF (NO_DOLLARS)
		BEGIN
		PLINE2(62,72) = 
		PLINE2(77,87) =
		END

	  XCALL HP (14,hpPOS,Lrow+LINCNT,1,0,PLINE2)
	  INCR LINCNT
	  SACCRC(I) =
	END
	IF (I.GE.MAXACC) GOTO BADACC
	RETURN

PRTSDU,
		;Print summarized duct from this order
	IF ((LINCNT+1).GT.MAXLIN) CALL CONTIN
;;;	XCALL LINFD (1)
	INCR LINCNT
	FOR I FROM 1 THRU MAXDUC
	  BEGIN
	  DUCREC = SDUCRC(I)
	  IF (DUCCFG.EQ.0) GOTO PRTSD2
	  IF ((LINCNT+1).GT.MAXLIN) CALL CONTIN
	  PLINE2 (1,7) = DUCPND,NUMASK
	  PLINE2 (8,8) = '#'
	  PLINE2 (18,19) = DUCCFG(1,3),'XX'
	  PLINE2 (21,22) = 'GA'
	  IF (DUCCFG(4,4).EQ.2.OR.DUCCFG(4,4).EQ.3)
	  BEGIN
	    PLINE2 (18,22) = DUCCFG(1,3),'.XXX'
	  END
	  PLINE2 (33,36) = DTYPE(DUCCFG(4,4))
	  PLINE2 (38,43) = DCAT(DUCCFG(5,5))
	  PLINE2 (45,47) = DSTY(DUCCFG(6,6))
	  PLINE2 (49,51) = DSEAM(DUCCFG(7,7))
	  CALL PRINT
	  SDUCRC(I) =
	END
PRTSD2,
	FOR I FROM 1 THRU 8
	  BEGIN
	  IF (LINSQF(I).NE.0.AND.I.NE.4)
	  BEGIN
	    IF ((LINCNT+1).GT.MAXLIN) CALL CONTIN
	    PLINE (1,7) = LINSQF(I),NUMASK
	    PLINE (8,10) = 'SQF'
	    PLINE (33,39) = DLINER(I)
	    PLINE (40,45) = ' LINER'
	    CALL PRINT
	    LINSQF(I) =
	  END
	END
	RETURN	

FNDCUS,
	IF (OCUSNO.NE.CUSNO)
	BEGIN
	  KEY = OCUSNO,'XXXXXX'
	  XCALL SERCH (37,CUSIDX,KEY,1,6,ORGCUS,BSMID,SRCCTL,4,7,11,0,0,0,0)
	  LOKCTL = 1
	  CASE SRCCTL OF 
	  BEGINCASE
	  0:	XCALL IO (36,CUSMAS,IRC001,READ,LOKCTL)
	  1:	BEGIN
		  CUSMAS =
		  NAME = '* CUSTOMER NOT ON FILE *'
		END
	  ENDCASE
	END
	RETURN
PRINT,
;;;	WRITES (14,PLINE)
	XCALL HP (14,hpPOS,Lrow+LINCNT,1,0,PLINE)
	PLINE =
	INCR LINCNT
	RETURN

GET_SCAC,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		;;; COP TABLE SCAC LOOK-UP
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR TBL_KEY
	TBLCOD = 'SC'
	TBLKEY = OSCAC
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .NE. 0)
		BEGIN
		CLEAR COPTBL
		SC_NAME = "* NOT ON FILE *"
		END
	RETURN
;-----------------------------------------------------

GET_SALESMAN,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL IO (CHN054,SALMAN,OSLMAN,READ,LOKCTL)
	IF (SLSNM.EQ.']]]]]]' .OR. SLSNM.EQ.']]]DEL' .OR. LOKCTL.NE.0) SLSNM =
	RETURN
;-----------------------------------------------------


OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	SWITCH = 5
	XCALL CHNOPN(24,STAT)
	IF (STAT .EQ. 0)
		BEGIN
		XCALL FILES (24,'SI',044,SWITCH)
		CHN044 = 24
		END

	XCALL CHNOPN(25,STAT)
	IF (STAT .EQ. 0)
		BEGIN
		XCALL FILES (25,'SI',045,SWITCH)
		CHN045 = 25
		END

	XCALL CHNOPN(26,STAT)
	IF (STAT .EQ. 0)
		BEGIN
		XCALL FILES (26,'SI',175,SWITCH)
		CHN175 = 26
		END

	XCALL CHNOPN(36,STAT)
	IF (STAT .EQ. 0) XCALL FILES (36,'I',001,SWITCH)

	XCALL CHNOPN(37,STAT)
	IF (STAT .EQ. 0) XCALL FILES (37,'I',002,SWITCH)

	XCALL CHNOPN(38,STAT)
	IF (STAT .EQ. 0)
		BEGIN
		XCALL FILES (38,'I',054,SWITCH)
		CHN054 = 38
		END

	CHN182 = 17	;COP TABLES
	RETURN


CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLOSE CHN044
	CLOSE CHN045
	CLOSE CHN054
	CLOSE CHN175
	CLOSE 36
	CLOSE 37

	RETURN

END

