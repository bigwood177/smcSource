;DPTMNT.CP
;	COP TABLE MAINTENANCE
;	DEPARTMENT DESCRIPTIONS
;
RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'


RECORD	CHANNEL
	CHN182	,D2


RECORD	PRINT
	TITLE	,A*,	'WORK ORDER DEPARTMENTS'
	HD	,A6,	'NO HDR'
	LG	,A9,	'NO LEGEND'
	PLINE	,A132
	PRNTON	,D1
	LINCNT	,D2,60
	PGCNT	,D6
	LPSW	,D2
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
;;;	PRTCTL	,D3,080
	PRTCTL	,D3,132
	LPARG	,D1
	PRNTSW	,D1
	PRTCTR	,D1


RECORD	HD1
		,A*,	'DEPT  DESCRIPTION                    DEFAULT LOCATION'
		,A*,	'      CAP  LBS/HR $/LB W/O PRINTER'
;;;		,A*,	'      CAP CNV-FAC EFF W/O PRINTER'
RECORD	VARS
	YESNO	,2A1,	'Y','N'
	ALPHA	,A12
	OPNOK	,D1
	BLANKS	,A30
	DEPT	,A2
	DESC	,A30
	LOC	,A20	
	QUE	,A12
	ST_DPT	,A2
	EN_DPT	,A2
	PGM	,D1
	ROW	,D2
	ENTRY	,A30
	INXCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	STORE	,D1,2
	DELETE	,D1,3
	LOKCTL	,D1
	WHATNO	,D2
	SELECT	,D1
	CNGCTL	,D1
	I	,D3
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'WORK ORDER DEPARTMENTS TABLE',1)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO CLOSE

MENU,
	XCALL OUTPT (1,1,2,'WORK ORDER DEPARTMENTS TABLE',1)
	XCALL OUTPT (3,9,0,'PLEASE SELECT APPLICATION',1)
	XCALL OUTPT (5,15,0,'1. TABLE MAINTENANCE',1)
	XCALL OUTPT (6,15,0,'2. PRINT TABLE',1)
MINPUT,
	XCALL INPUT (3,36,1,1,'#E',ENTRY,INXCTL,1)
	GOTO (MINPUT,ENDOFF), INXCTL
	PGM = ENTRY(1,1)
	GOTO (DISPLA,PRINT_TABLE),PGM
	GOTO MINPUT

DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,'WORK ORDER DEPARTMENTS TABLE',1)
	XCALL OUTPT ( 4,4,0, '1. DEPARTMENT',1)
	XCALL OUTPT ( 6,4,0, '2. DESCRIPTION',1)
	XCALL OUTPT ( 8,4,0, '3. DEFAULT LOC',1)
	XCALL OUTPT (10,4,0, '4. W/O PRINTER',1)
	XCALL OUTPT (12,4,0, '5. BURN?',1)
	XCALL OUTPT (12,28,0,'6. BURN CAP/LBS',1)

	XCALL OUTPT (14,4,0, '7. FORM?',1)
	XCALL OUTPT (14,28,0,'8. FORM CAP/LBS',1)

	XCALL OUTPT (16,4,0, '9. CAPACITY HRS/DAY',1)
	XCALL OUTPT (17,4,0,'10. CNV: LBS PER HR',1)
	XCALL OUTPT (18,4,0,"11. CNV: $'S TO XX.X LBS",1)
	XCALL OUTPT (19,4,0,'12. EFFICIENCY FACT',1)	
	XCALL OUTPT (20,4,0,'13. ADD TO SCHEDL?',1)
	XCALL OUTPT (21,4,0,'14. EXRESS # <',1)

DEPT,
	XCALL INPUT (4,24,02,00,'AE',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	DEPT = ENTRY(1,2)
	CLEAR TBL_KEY
	TBL_KEY(1,2) = 'DD'
	DD_DEPT = DEPT

	USING DEPT SELECT
	('I'),		XCALL OUTPT (21,4,1,'14. STOCK # <',1)
	ENDUSING

	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .EQ. 0)
		BEGIN
		SELECT = 2
		CALL DSPREC
		GOTO ANYCNG
		END
	SELECT = 1			;ADD MODE
	CLEAR COPTBL
	DD_DEPT = DEPT
DESC,
	XCALL INPUT (6,24,30,00,'AE',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	DD_DESC = ENTRY(1,30)
	GOTO (ANYCNG),CNGCTL
LOC,
	XCALL INPUT (8,24,20,00,'A ',ENTRY,INXCTL,1)
	GOTO(DISPLA),INXCTL
	DD_LOC = ENTRY(1,20)
	GOTO (ANYCNG),CNGCTL
QUE,
	XCALL INPUT(10,24,12,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	DD_PQUE = ENTRY(1,12)
	GOTO (ANYCNG),CNGCTL
BURN,
	XCALL INPUT(12,24,01,00,'YN',ENTRY,INXCTL,1)
	D_BURN = INXCTL
	XCALL OUTPT (12,24,0,YESNO(D_BURN),1)
	GOTO (ANYCNG),CNGCTL
B_CAP,
	IF (D_BURN .EQ. 1)
		BEGIN
		XCALL OUTPT (14,28,0,'8. FORM CAP/LBS',1)
		XCALL INPUT (12,46,06,00,'# ',ENTRY,INXCTL,1)
		GOTO (DISPLA),INXCTL
		DD_BLBS = ENTRY(1,6)
		GOTO (ANYCNG),CNGCTL
		END
FORM,
	XCALL INPUT (14,24,01,00,'YN',ENTRY,INXCTL,1)
	D_FORM = INXCTL
	XCALL OUTPT (14,24,0,YESNO(D_FORM),1)
	GOTO (ANYCNG),CNGCTL
F_CAP,
	IF (D_FORM .EQ. 1)
		BEGIN
		XCALL OUTPT (14,28,0,'8. FORM CAP/LBS',1)
		XCALL INPUT (14,46,06,00,'# ',ENTRY,INXCTL,1)
		GOTO (DISPLA),INXCTL
		DD_FLBS = ENTRY(1,6)
		GOTO (ANYCNG),CNGCTL
		END
CAP,
	XCALL INPUT (16,26,04,00,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	DD_CAP = ENTRY(1,4)
	GOTO (ANYCNG),CNGCTL
CNV,
	XCALL INPUT (17,26,06,00,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	DD_CNV = ENTRY(1,6)
	GOTO (ANYCNG), CNGCTL
DLR,
	XCALL INPUT (18,30,03,00,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	DD_DLR = ENTRY(1,3)
	ENTRY(1,4) = DD_DLR, 'ZZ.X'
	XCALL OUTPT (18,30,1,ENTRY(1,4),1)
	GOTO (ANYCNG),CNGCTL
EFF,
	XCALL INPUT (19,26,03,00,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	DD_EFF = ENTRY(1,3)
	GOTO (ANYCNG), CNGCTL
SCH,
	XCALL INPUT (20,26,01,00,'YN',ENTRY,INXCTL,1)
	USING INXCTL SELECT
	(1),	DD_SCH = 1
	(),	DD_SCH = 0
	ENDUSING

	GOTO (ANYCNG), CNGCTL
EXLB,
	XCALL INPUT (21,26,06,00,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	DD_EXLB = ENTRY(1,6)
	GOTO (ANYCNG), CNGCTL


ANYCNG,
	XCALL OUTPT (24,1,1,'FIELD # TO CHANGE            <TAB> = DELETE',1)
	XCALL INPUT (24,20,02,00,'#T',ENTRY,INXCTL,1)
	IF (INXCTL .EQ. 3)
		BEGIN
		XCALL OUTPT (24,1,1,'DELETE, ARE YOU SURE ?',1)
		XCALL INPUT (24,24,01,01,'YN',ENTRY,INXCTL,1)
		GOTO (ANYCNG),INXCTL-1
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,DELETE,LOKCTL)
		GOTO DISPLA
		END

	WHATNO = ENTRY(1,2)
	IF (WHATNO .EQ. 0)
	THEN 	CLEAR CNGCTL
	ELSE 	CNGCTL = 1

	GOTO (PROCES,CNGBR),CNGCTL+1
CNGBR,
	GOTO (DEPT,DESC,LOC,QUE,BURN,B_CAP,FORM,F_CAP,CAP,CNV,DLR,EFF,SCH,EXLB),WHATNO
	GOTO ANYCNG

PROCES,
	CASE SELECT OF
	BEGINCASE
	1:	BEGIN
		TBLCOD = 'DD'
		DD_DEPT = DEPT
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,STORE,LOKCTL)
		END
	2:	XCALL ISIO (CHN182,COPTBL,TBL_KEY,WRITE,LOKCTL)
	ENDCASE
	GOTO DISPLA

ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('CP:TBLMNU',1)
	STOP


DSPREC,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; DISPLAY RECORD
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL OUTPT (4,24,1,DD_DEPT,1)
	XCALL OUTPT (6,24,1,DD_DESC,1)
	XCALL OUTPT (8,24,1,DD_LOC,1)
	XCALL OUTPT(10,24,1,DD_PQUE,1)

	USING D_BURN SELECT
	(1),	XCALL OUTPT (12,24,0,'Y',1)
	(2),	XCALL OUTPT (12,24,1,'N',1)
	ENDUSING

	ENTRY(1,6) = DD_BLBS,	'ZZZZZZ' [left]
	XCALL OUTPT (12,46,1,ENTRY(1,6),1)

	USING D_FORM SELECT
	(1),	XCALL OUTPT (14,24,0,'Y',1)
	(2),	XCALL OUTPT (14,24,1,'N',1)
	ENDUSING

	ENTRY(1,6) = DD_FLBS,	'ZZZZZZ' [left]
	XCALL OUTPT (14,46,1,ENTRY(1,6),1)

	ENTRY(1,4) = DD_CAP, 'ZZZX' [LEFT]
	XCALL OUTPT (16,26,1,ENTRY(1,4),1)
	ENTRY(1,6) = DD_CNV, 'ZZZZZZ' [LEFT]
	XCALL OUTPT (17,26,1,ENTRY(1,6),1)
	ENTRY(1,4) = DD_DLR, 'ZZ.X' [LEFT]
	XCALL OUTPT (18,30,1,ENTRY(1,4),1)
	ENTRY(1,3) = DD_EFF, 'ZZX' [LEFT]
	XCALL OUTPT (19,26,1,ENTRY(1,3),1)

	USING DD_SCH SELECT
	(1),	XCALL OUTPT (20,26,1,'Y',1)
	(),	XCALL OUTPT (20,26,1,'N',1)
	ENDUSING

	ENTRY(1,6) = DD_EXLB, 'ZZZZZZ'
	XCALL OUTPT (21,26,1,ENTRY(1,6),1)

	RETURN
;-----------------------------------



;===================================================================
PRINT_TABLE,
;===================================================================
	LINCNT = 66
	PGCNT  = 0
	
PDISP,
	CNGCTL = 
	XCALL OUTPT (1,1,2,'PRINT WORK ORDER DEPARTMENTS TABLE',1)
	XCALL OUTPT (4,4,0,'1. STARTING DEPT',1)
	XCALL OUTPT (6,4,0,'2. ENDING   DEPT',1)
ST_DPT,
	XCALL INPUT (4,27,02,00,'AE',ENTRY,INXCTL,1)
	GOTO (PDISP,PDONE),INXCTL
	ST_DPT = ENTRY(1,2)
	IF (ST_DPT .EQ. BLANKS)
		BEGIN
		EN_DPT = 'ZZ'
		XCALL OUTPT (4,25,1,'ALL',1)
		XCALL OUTPT (6,25,1,' ',1)
		GOTO P_ANY
		END
	GOTO (P_ANY),CNGCTL
EN_DPT,
	XCALL INPUT (6,25,02,00,'A ',ENTRY,INXCTL,1)
	GOTO (PDISP),INXCTL
	EN_DPT = ENTRY(1,2)
	IF (EN_DPT .EQ. BLANKS)
		BEGIN
		EN_DPT = ST_DPT
		XCALL OUTPT (6,25,0,EN_DPT,1)
		END
P_ANY,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (P_PRINT,P_CNGBR),CNGCTL + 1
P_CNGBR,
	GOTO (ST_DPT,EN_DPT),WHATNO
	GOTO P_ANY

P_PRINT,
	CLEAR TBL_KEY
	TBLCOD = 'DD'
	DD_DEPT = ST_DPT
	FIND (CHN182,COPTBL,TBL_KEY)[ERR=PLOOP]

PLOOP,
	XCALL IOS (CHN182,COPTBL,READ,LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF
	IF (TBLCOD .NE. 'DD') GOTO EOF
	IF (DD_DEPT .LT. ST_DPT) GOTO PLOOP
	IF (DD_DEPT .GT. EN_DPT) GOTO EOF


	PLINE (2,3) = TBLKEY(1,2)	;DD_DEPT
	PLINE (7,36) = DD_DESC
	PLINE (38,57) = DD_LOC

	PLINE (59,62) = DD_CAP, 'ZZZX'
	PLINE (65,70) = DD_CNV, 'ZZZZZX'
	PLINE (72,75) = DD_DLR,	'ZZ.X'
	PLINE (77,79) = DD_EFF, 'ZZX'
	PLINE (81,92) = DD_PQUE


;DEPT  DESCRIPTION                    DEFAULT LOCATION      CAP CNV-FAC EFF
; AA   AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA AAAAAAAAAAAAAAAAAAAA ZZZX ZZZ.XXX ZZX
;12345678901234567890123456789012345678901234567890123456789012345678901234
;         1         2         3         4         5         6         7
	CALL PRINT
	GOTO PLOOP

EOF,
	IF (PRNTON.EQ.1)  XCALL LPOFF(LPSW,SPLFIL,PGCNT)
	PRNTON  = 0

PDONE,
	GOTO MENU

PRINT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (PRNTON .EQ. 0) CALL PRNTON
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HD1,HD,HD
&		,LG,LG,LG,0,080,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;-------------------------------------------------------------
PRNTON,
	SPLFIL (5,6) = 'EF'
	LPSW = 1		;PRINT,SPOOL, OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTON = 1
	RETURN
;-------------------------------------------------------------
;===================================================================


OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (1,'SU',182,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN182 = 1

	OPNOK = 1
	RETURN
;----------------------------------------------------

CLOSE,
	CLOSE CHN182
	RETURN
;----------------------------------------------------
	
