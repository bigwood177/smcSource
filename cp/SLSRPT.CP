;SLSRPT.CP
;		MONTHLY SUMMARY REPORT FOR TERRITORIES
; 6-04-18 ssq: make cusmas isam
;
RECORD	D_STUFF
	D_IN	,D8		;DATE-IN, ANY FORMAT
	D_OUT	,D6		;RETURN VALUE MMDDYY
	D_OUTR	,D8		;RETURN VALUE CCYYMMDD
	D_FMT	,A10		;RETURN VALUE MM/DD/CCYY
	D_SW	,A2		;"99" = DATE CONVERSION ERROR

	.INCLUDE 'DEF:RTF.DEF'

RECORD	TABLE
	.INCLUDE 'DEF:RD182A.DEF'
RECORD	TERWRK
	.INCLUDE 'DEF:TERWRK.DEF'

RECORD	ORDHDR
	.INCLUDE 'DEF:RD044A.DEF'

RECORD	ORDLIN
	.INCLUDE 'DEF:RD045A.DEF'

RECORD SLHDUC
	.INCLUDE 'DEF:RD186A.DEF'

RECORD	CUSMAS
	.INCLUDE 'DEF:RD001A.DEF'
RECORD,X
	.INCLUDE 'DEF:RD001B.DEF'

;;;RECORD	CUSIDX
;;;	.INCLUDE 'DEF:RD002A.DEF'


RECORD LEG1
		,A*,	'SALESMAN: '
	LMAN	,A2
		,A5
	LDATE1	,A10
		,A*,	' THRU '
	LDATE2	,A10
		,A5
		,A*,	'TERRITORY: '
	LTER	,A2

RECORD HDR1
		,A40,'-------------------- ITEM --------------'
		,A45,'------------  LAST SL  -------- SALE --------'
RECORD HDR2
		,A40,'CAT  NUMBER          DESCRIPTION        '
		,A45,'                 DATE      QTY       DOLLARS '

RECORD	PRINT
	SPLFIL	,A14
	LEG	,A9,	'NO LEGEND'
	HDR	,A6,	'NO HDR'
	BLANKS	,A30
	ALPHA	,A15
	LINCNT	,D2,60
	PGCNT	,D3
	PRTCTL	,D2
	LPSW	,D2
	LPARG	,D1
	PRTTYP	,A1
	RPTNUM	,D3
	PLINE	,A132
	TITLE	,A*,	'SALES HISTORY SUMMARY BY'
;;; CUSTOMER / PRODUCT CAT / ITEM'

RECORD	CUSLIN		;CUSTOMER
	STAR	,A4
	CUSNUM	,A6
		,A1
	CUSNAM	,A30
		,A10,	'SUBTOTAL: '

RECORD	CUSADR
		,A7
	C_ADR	,A25

;;;RECORD	SPLFIL
;;;		,A4
;;;	SPOPT	,D1
;;;		,A9


RECORD	CHANNEL
	CHNWRK	,D2
	CHN184	,D2	;SLHHDR
	CHN185	,D2	;SLHLIN
	CHN001	,D2	;CUSMAS
	CHN002	,D2	;CUSIDX
	CHN182	,D2	;COPTBL
	CHN186	,D2	;SLHDUC

RECORD	TOTALS
	MANTOT	,D10
	TERTOT	,D10
	CUSTOT	,D10
	CATTOT	,D10

RECORD	E_BOLD
	E_ESC	,A1
		,A4,	'(s3B'	;bold
RECORD	E_MED
	M_ESC	,A1
		,A4,	'(s0B'	;medium

RECORD	FILPRC
	FL_DEV	,A3
		,A1,	':'
	FL_NAME	,A6
		,A1,	'.'
	FL_EXT	,A3

RECORD	VARS
	NEW_CUST	,D1
	OPNOK	,D1
	STAT	,D1
	DASH	,A12,	'------------'
	SAVMAN	,D2
	SAVCUS	,D6
	STMAN	,D2
	SAVCAT	,A2
	SAVTER	,A2
	DTMASK	,A8,	'XX/XX/XX'
	NUMASK	,A8,	'ZZZ,ZZX-'
	DLMASK	,A12,	'ZZZZ,ZZZ.XX-'
	WITMNO	,A15
	WQTY	,D6
	WCAT	,A2
	DCAT	,D2
	WDESCR	,A30
	DECMAL	,D18
	EXEPRC	,D8
	TERR_EOF	,D1
	ENTRY	,A30
	INXCTL	,D1
	CNGCTL	,D1
	WHATNO	,D2
	XDATE	,D8
	STDAT	,D8
	ENDAT	,D8
	BSEND	,D5
	BSMID	,D5
	SRCCTL	,D1
	KEY	,A6
	READ	,D1
	LOKCTL	,D1,0
	WRITE	,D1,1
	FIND	,D1,2
	STORE	,D1,2
	DELETE	,D1,3
	SWITCH	,D1
	V	,D1
PROC
	XCALL TERID (V)
	XCALL ASCII (27, E_ESC)
	M_ESC = E_ESC
	XCALL OUTPT (1,1,2,'SALES BY TERRITORY/CATAGORY REPORT',1)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO ENDOFF
DISPLA,
	XCALL OUTPT (1,1,2,'SALES BY TERRITORY/CATAGORY REPORT',1)
	XCALL OUTPT (4,5,0,'START DATE:',1)
	XCALL OUTPT (5,5,0,'END   DATE:',1)

STDAT,
	XCALL INPUT (4,18,08,00,'DE',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	STDAT = ENTRY(1,8)
	GOTO (ANYCNG),CNGCTL
ENDAT,
	XCALL INPUT (5,18,08,00,'DE',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	ENDAT = ENTRY(1,8)
	GOTO (ANYCNG),CNGCTL
ANYCNG,
	XCALL ANYCN(CNGCTL, WHATNO)
	GOTO (PROCES, CNGBR),CNGCTL + 1
CNGBR,
	GOTO (STDAT, ENDAT), WHATNO
	GOTO ANYCNG
PROCES,

	XCALL OUTPT (2,1,2,'Build work file ...',1)

	FIND (CHN184, ORDHDR, STDAT, KRF=5) [ERR = LOOP]
LOOP,
	XCALL IOS (CHN184, ORDHDR, READ, LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF
	IF (OKEYDT.LT.STDAT) GOTO LOOP
	IF (OKEYDT.GT.ENDAT) GOTO EOF
	IF (OCUSNO .EQ. 63340) GOTO LOOP	;SKIP ROCKFORD SSQ 2-10-04
	CALL GET_TERRITORY

	CLEAR TERWRK
	T_TERR = TERR
	T_CUST = OCUSNO
	T_DATE = OKEYDT

	CALL LINES
	CALL DUCT

	GOTO LOOP
EOF,

	XCALL DATE8(STDAT, D_OUT, D_OUTR, LDATE1, D_SW)
	XCALL DATE8(ENDAT, D_OUT, D_OUTR, LDATE2, D_SW)

P_LOOP,
	CLEAR PGCNT
	LINCNT = 60

	CALL PRINT_REPORT
	CALL NEWMAN

	XCALL RTF(16,FINI)
	CLOSE 16
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	GOTO P_LOOP

;;;	GOTO DISPLA
;======================================================
;======================================================
PRINT_REPORT,
	XCALL OUTPT (6,5,2,'SALESMAN:',1)
STMAN,
	XCALL INPUT (6,18,02,00,'#E ',ENTRY,INXCTL,1)
	GOTO (PRINT_REPORT,DISPLA),INXCTL
	STMAN = ENTRY(1,2)
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (STMAN),CNGCTL

	LPSW = 1		; PRINT, SPOOL OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	OPEN(16,O,'SPL:CATRPT.DOC')
	IF (LPSW .EQ. 0) GOTO ENDOFF

	SAVCUS = -1
	SAVCAT = '-1'
	SAVTER = '-1'
	SAVMAN = -1
	CLEAR MANTOT, TERTOT, CUSTOT, CATTOT

	CLEAR TBL_KEY
	TBLCOD = 'TR'
	FIND (CHN182, TABLE, TBL_KEY) [ERR=PR_LOOP]
PR_LOOP,
	XCALL IOS (CHN182, TABLE, READ, LOKCTL)
	IF (LOKCTL .NE. 0) RETURN
	IF (STMAN.NE.0 .AND. TR_SLSM .NE. STMAN) GOTO PR_LOOP

;;;	FIND (CHNWRK, TERWRK, TR_TERR) [ERR=D_LOOP]
	FIND (CHNWRK, TERWRK, TR_TERR) [ERR=PR_LOOP]


D_LOOP,	;; Print all detail for current territory
	XCALL IOS (CHNWRK, TERWRK, READ, LOKCTL)
	IF (LOKCTL .NE. 0) GOTO DL_EOF

	IF (TR_SLSM .NE. SAVMAN)	CALL NEWMAN
	IF (TR_TERR .NE. SAVTER)	CALL NEWTER
	IF (T_TERR .NE. SAVTER) GOTO PR_LOOP

	IF (T_CUST .NE. SAVCUS) CALL NEWCUS
	IF (T_CAT  .NE. SAVCAT) CALL NEWCAT	
	IF (NEW_CUST)	CALL CUST_HEADER

	PLINE (2,3) = T_CAT
	PLINE (6,20) = T_ITEM
	PLINE (22,52) = T_DESC

	XCALL DATE8(T_DATE, D_OUT, D_OUTR, D_FMT, D_SW)
	PLINE (54,63) = D_FMT
	PLINE (64,71) = T_QTY,NUMASK
	PLINE (74,85) = T_AMT,DLMASK
	CALL PRINT
	CATTOT = CATTOT + T_AMT	
	GOTO D_LOOP

DL_EOF,
	GOTO PR_LOOP
;=============================================

CUST_HEADER,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		;;; PRINT CUSTOMER HEADER INFO
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	OCUSNO = SAVCUS
	CALL GET_TERRITORY
	CUSNAM = NAME
	CUSNUM = SAVCUS,	'ZZZZZZ'
	C_ADR = ADD1
	XCALL DATE8(ESTART, D_OUT, D_OUTR, D_FMT, D_SW)
	IF (D_OUTR .GT. STDAT)
	THEN	STAR = '***'
	ELSE	CLEAR STAR

	XCALL RTF(16,BOLD)
	DISPLAY (14, E_BOLD)

	PLINE (7,12) = SAVCUS,	'ZZZZZX' [LEFT]
	PLINE (15,27) = PHONE,	'ZZZ ZZX-XXXX'
	CALL PRINT
	PLINE (7,37) = NAME
	CALL PRINT
	PLINE (7,37) = ADD1
	CALL PRINT
	IF (ADD2 .NE. BLANKS)
		BEGIN
		PLINE(7,37) = ADD2
		CALL PRINT
		END
	PLINE (7,21) = CITY
	PLINE (23,24) = STATE
	PLINE (26,35) = ZIP
	CALL PRINT

	XCALL RTF(16, PLAIN)
	DISPLAY (14, E_MED)
	CLEAR NEW_CUST

	RETURN
;--------------------------------------------

NEWMAN,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	; Grand totals for salesman
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CALL NEWTER
	IF (SAVMAN .EQ. -1) GOTO OUTMAN
	PLINE (74,85) = DASH
	CALL PRINT
	PLINE (49,70) = 'SALESMAN XX SUBTOTAL: '
	PLINE (58,59) = SAVMAN, 'XX'
	PLINE (74,85) = MANTOT, DLMASK
	call print
OUTMAN,
	CLEAR MANTOT
	SAVMAN = TR_SLSM
	LMAN = TR_SLSM ,'ZX'
	LINCNT = 66
	RETURN
;----------------------------------------

NEWTER,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CALL NEWCUS
	IF (SAVTER .EQ. '-1') GOTO OUTTER
	MANTOT = MANTOT + TERTOT
	PLINE (74,85) = DASH
	CALL PRINT
	PLINE (48,70) = 'TERRITORY XX SUBTOTAL: '
	PLINE (58,59) = SAVTER
	PLINE (74,85) = TERTOT, DLMASK
	CALL PRINT
	CALL LINFD	
OUTTER,
	CLEAR TERTOT
	SAVTER = TR_TERR
	LTER = SAVTER	
	LINCNT = 66
	RETURN
;----------------------------------------

NEWCUS,;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CALL NEWCAT
	IF (SAVCUS .EQ. -1) GOTO OUTCUS
	TERTOT = TERTOT + CUSTOT
	PLINE (74,85) = DASH
	CALL PRINT
;------------------------------------------
;;;	OCUSNO = SAVCUS
;;;	CALL GET_TERRITORY
;;;	CUSNAM = NAME
;;;	CUSNUM = SAVCUS,	'ZZZZZZ'
;------------------------------------------
	PLINE = CUSLIN
	PLINE (74,85) = CUSTOT, DLMASK

	XCALL RTF(16, BOLD)
	DISPLAY (14, E_BOLD)
	CALL PRINT

	XCALL RTF(16, PLAIN)
	DISPLAY (14, E_MED)
	CALL LINFD
	CALL LINFD
OUTCUS,
	CLEAR CUSTOT
	SAVCUS = T_CUST
	NEW_CUST = 1
	RETURN
;----------------------------------------

NEWCAT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (SAVCAT .EQ. '-1') GOTO OUTCAT
	CUSTOT = CUSTOT + CATTOT
	PLINE (74,85) = DASH
	CALL PRINT
	PLINE (41,70) = 'PRODUCT CATEGORY XX SUBTOTAL: '
	PLINE (58,59) = SAVCAT
	PLINE (74,85) = CATTOT,DLMASK
	CALL PRINT
	CALL LINFD
OUTCAT,
	CLEAR CATTOT
	SAVCAT = T_CAT
	RETURN
;----------------------------------------


LINES,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; get line-items, write work records
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	FIND (CHN185, ORDLIN, OORDNO) [ERR = LINLOP]
LINLOP,
	XCALL IOS (CHN185, ORDLIN, READ, LOKCTL)
	IF (LOKCTL .NE. 0) RETURN
	IF (LORDNO .NE. OORDNO) RETURN
	IF (LTYPE .EQ. 'M') GOTO LINLOP
	IF (LQTYSH .EQ. 0) GOTO LINLOP
	IF (LPRICE .EQ. 0) GOTO LINLOP

	WITMNO = LITMNO
	WDESCR = LDESCR
	WCAT = LPRDCD
	WQTY = LQTYSH

	EXEPRC = ((LQTYSH*LPRICE))#1

	CALL UPDSLH

	GOTO LINLOP
;-------------------------------------------------------

DUCT,
	FIND (CHN186, SLHDUC, OORDNO) [ERR=DUCTLP]
DUCTLP,
	LOKCTL = 1
	XCALL IOS (CHN186,SLHDUC,READ,LOKCTL)
	IF (LOKCTL.NE.0.OR.HDUCTOR.NE.OORDNO) RETURN

	WITMNO = 'DUCT'
	WDESCR = 'DUCT WORK & ACCESSORIES'
	WCAT = '11'
	WQTY = HPOUNDS
	EXEPRC = ( HPOUNDS * HGPRICE ) # 1
	CALL UPDSLH

	IF (HLINER.NE.4)
	BEGIN
	  WITMNO =
	  WITMNO(1,5) = 'LINER'
	  WITMNO(6,6) = HLINER,'X'
	  WQTY = HSQFLIN
	  EXEPRC = (HSQFLIN * HLINPRC) # 1
	  CALL UPDSLH
	END

	GOTO DUCTLP

	RETURN
;-------------------------------------------------------

GET_TERRITORY,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL ISIO (CHN001, CUSMAS, OCUSNO, READ, LOKCTL)
	IF (LOKCTL .NE. 0)
		BEGIN
		CUSMAS =
		TERR = '??'
		END
		
;;;	KEY = OCUSNO, 'XXXXXX'
;;;	XCALL SERCH (CHN002,CUSIDX,KEY,1,6,BSEND,BSMID,SRCCTL,4,7,11,0,0,0,0)
;;;	IF (SRCCTL .EQ. 0 .AND. IRC001.NE.0)
;;;	THEN	BEGIN
;;;		LOKCTL = 1
;;;		XCALL IO (CHN001,CUSMAS,IRC001,READ,LOKCTL)
;;;		END
;;;	ELSE	TERR = '??'

	RETURN

;-------------------------------------------------------

UPDSLH,	;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; write slshst record
	;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (EXEPRC .EQ. 0) RETURN

	T_TERR = TERR
	T_CUST = OCUSNO
	T_ITEM = WITMNO
	CLEAR DCAT
	ONERROR NOT_NUM1
	DCAT = WCAT
NOT_NUM1,
	T_CAT = DCAT,	'XX'
	XCALL ISIO (CHNWRK, TERWRK, TERKEY, READ, LOKCTL)
	IF (LOKCTL .EQ. 0) 
		BEGIN
		T_QTY = T_QTY + WQTY
		T_AMT = T_AMT + EXEPRC
		XCALL ISIO (CHNWRK, TERWRK, TERKEY, WRITE, LOKCTL)
		RETURN
		END

	T_TERR = TERR
	T_CUST = OCUSNO
	T_DATE = OKEYDT

	CLEAR DCAT
	ONERROR NOT_NUM
	DCAT = WCAT
NOT_NUM,
	OFFERROR
	T_CAT = DCAT,	'XX'

	T_ITEM = WITMNO
	T_DESC = WDESCR
	T_QTY = WQTY
	T_AMT = EXEPRC

	XCALL ISIO (CHNWRK, TERWRK, TERKEY, STORE, LOKCTL)	

	RETURN
;-------------------------------------------------------

PRINT,
;;;	IF (RESTART) PLINE =
;;;	IF (RESTART) RETURN
	WRITES (16,PLINE)
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,HDR,
&			LEG1,LEG,LEG,0,0,132,1,LPSW,RPTNUM,PRTTYP)
	RETURN
LINFD,
;;;	IF (RESTART) RETURN
	XCALL LINFD (1)
	INCR LINCNT
	XCALL RTF(16,LN_FEED,1)

	RETURN
;-------------------------------------------------------

			
ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('CP:CPMENU',1)
	STOP

OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (1,'SI',001, SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN001 = 1

;;;	XCALL FILES (2,'I', 002, SWITCH)
;;;	IF (SWITCH .EQ. 9) RETURN
;;;	CHN002 = 2

	XCALL FILES (4, 'SI', 184, SWITCH)	;SLHHDR
	IF (SWITCH .EQ. 9) RETURN
	CHN184 = 4

	XCALL FILES (5, 'SI', 185, SWITCH)	;SLHLIN
	IF (SWITCH .EQ. 9) RETURN
	CHN185  = 5

	XCALL FILES (6, 'SI', 182, SWITCH)	;
	IF (SWITCH .EQ. 9) RETURN
	CHN182 = 6

	XCALL FILES (7, 'SI', 186, SWITCH)	;SLHDUC
	IF (SWITCH .EQ. 9) RETURN
	CHN186 = 7

	XCALL FFILE(1,FILPRC,SWITCH)	;SSQ 01-13-04
	FL_NAME = 'TERWRK'
	FL_EXT(3,3) = 'M'

	CHNWRK = 10
	XCALL ISCLR(CHNWRK, FILPRC ,STAT)
;;;	XCALL ISCLR(CHNWRK, 'SMC:TERWRK.ISM' ,STAT)
	IF (STAT .NE. 0)
		BEGIN
		XCALL MESAG ("CAN'T CLEAR TERWRK.ISM",1)
		RETURN
		END

	OPEN (10, SU, FILPRC)
;;;	OPEN (10, SU, 'SMC:TERWRK')

;;;	LOKCTL = 1
;;;	XCALL IO (CHN001, CUSMAS, 1, READ, LOKCTL)
;;;	BSEND = ORG001

	OPNOK = 1
	RETURN
;--------------------------------------

CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (CHN001) XCALL FILES (CHN001, 'I', 001, 4)
;;;	IF (CHN002) XCALL FILES (CHN002, 'I', 002, 4)
	IF (CHN182) XCALL FILES (CHN182, 'SI',182, 4)
	IF (CHN184) XCALL FILES (CHN184, 'SI',184, 4)
	IF (CHN185) XCALL FILES (CHN185, 'SI',185, 4)
	IF (CHNWRK) CLOSE CHNWRK

	RETURN
;--------------------------------------
	
