;CFGMNT.CP
;		GENERAL CONFIGURATOR PARAMETERS

RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'

RECORD	CHANNEL
	CHN182	,D2

RECORD	PRINT
	TITLE	,A*,	'GENERAL CONFIGURATOR PARAMETERS'
	HD	,A6,	'NO HDR'
	LG	,A9,	'NO LEGEND'
	MASK	,A5,	'ZX.XX'
	PLINE	,A132
	PRNTON	,D1
	LINCNT	,D2,60
	PGCNT	,D6
	LPSW	,D2
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	PRTCTL	,D3,080
	LPARG	,D1
	PRNTSW	,D1
	PRTCTR	,D1

RECORD	HD1
	,A*,	'    ---------------------------------MULTIPLIERS---------------------------------'
RECORD	HD2
	,A*,	'    PP-90   PP-40   EC-RED   20GA     18GA   16GA     SST    ALUM   PGRIP    PVS'

RECORD	VARS
	MSXCTL	,D1
	OPNOK	,D1
	PGM	,D1
	ENTRY	,A30
	INXCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	STORE	,D1,2
	DELETE	,D1,3
	LOKCTL	,D1
	WHATNO	,D2
	SELECT	,D1
	CNGCTL	,D1
	I	,D3
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'GENERAL CONFIGURATOR PARAMETERS',1)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO CLOSE

MENU,
	XCALL OUTPT (1,1,2,'GENERAL CONFIGURATOR PARAMETERS',1)
	XCALL OUTPT (3,9,0,'PLEASE SELECT APPLICATION',1)
	XCALL OUTPT (5,15,0,'1. TABLE MAINTENANCE',1)
	XCALL OUTPT (6,15,0,'2. PRINT TABLE',1)
	XCALL OUTPT (7,15,0,'3. RECALCULATE PRICE MATRIX VALUES FOR 16,18 & 20GA',1)
	XCALL OUTPT (8,15,0,'4. RECALCULATE PRICE MATRIX FOR MATERIALS',1)

MINPUT,
	XCALL INPUT (3,36,1,1,'#E',ENTRY,INXCTL,1)
	GOTO (MINPUT,ENDOFF), INXCTL
	PGM = ENTRY(1,1)
	GOTO (DISPLA, PRINT_TABLE, RE_CALC, PRCMAT),PGM
	GOTO MINPUT

DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,'GENERAL CONFIGURATOR PARAMETERS',1)
	XCALL OUTPT ( 4,4,0,'1. PAIR/PANTS 90 MULTIPLIER',1)
	XCALL OUTPT ( 6,4,0,'2. PAIR/PANTS 45 MULTIPLIER',1)
	XCALL OUTPT ( 8,4,0,'3. ECCENTRIC REDUCER MULTIPLIER',1)
;                            45678901234567890123456789012345678901234567890
;                                  1         2         3         4         5
	XCALL OUTPT (10,4,0,'4. 22 to 20GA MULTIPLIER',1)

	XCALL OUTPT (12,4,0,'5. 20 to 18GA MULTIPLIER',1)	;ssq 4-21-05
	XCALL OUTPT (14,4,0,'6. 20 to 16GA MULTIPLIER',1)	;ssq 4-21-05

;;;	XCALL OUTPT (12,4,0,'5. 22 to 18GA MULTIPLIER',1)	;ssq 4-21-05
;;;	XCALL OUTPT (14,4,0,'6. 22 to 16GA MULTIPLIER',1)	;ssq 4-21-05

	XCALL OUTPT (16,4,0,'7. STAINLESS STEEL MULTIPLIER',1)
	XCALL OUTPT (18,4,0,'8. ALUMINUM MULTIPLIER',1)
	XCALL OUTPT (20,4,0,'9. PAINT GRIP MULTIPLIER',1)
	XCALL OUTPT (22,3,0,'10. PVS MULTIPLIER',1)
	CLEAR TBL_KEY
	TBL_KEY(1,2) = 'CF'
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .EQ. 0)
		BEGIN
		SELECT = 2
		CALL DSPREC
		GOTO ANYCNG
		END


	SELECT = 1			;ADD MODE
	CLEAR COPTBL

PP9MUL,
	XCALL INPUT (4,36,04,00,'$E',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	PP9MUL = ENTRY(1,4)
	GOTO (ANYCNG),CNGCTL
PP4MUL,
	XCALL INPUT (6,36,04,00,'$E',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	PP4MUL = ENTRY(1,4)
	GOTO (ANYCNG),CNGCTL
ECRMUL,
	XCALL INPUT (8,36,04,00,'$E',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	ECRMUL = ENTRY(1,4)
	GOTO (ANYCNG),CNGCTL
G20MUL,
	XCALL INPUT (10,36,04,00,'$E',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	G20MUL = ENTRY(1,4)
	GOTO (ANYCNG),CNGCTL
G18MUL,
	XCALL INPUT (12,36,04,00,'$E',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	G18MUL = ENTRY(1,4)
	GOTO (ANYCNG),CNGCTL
G16MUL,
	XCALL INPUT (14,36,04,00,'$E',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	G16MUL = ENTRY(1,4)
	GOTO (ANYCNG),CNGCTL
SSTMUL,
	XCALL INPUT (16,36,04,00,'$E',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	SSTMUL = ENTRY(1,4)
	GOTO (ANYCNG),CNGCTL
ALUMUL,
	XCALL INPUT (18,36,04,00,'$E',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	ALUMUL = ENTRY(1,4)
	GOTO (ANYCNG),CNGCTL
PGRMUL,
	XCALL INPUT (20,36,04,00,'$E',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	PGRMUL = ENTRY(1,4)
	GOTO (ANYCNG),CNGCTL
PVSMUL,
	XCALL INPUT (22,36,04,00,'$E',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	PVSMUL = ENTRY(1,4)
	GOTO (ANYCNG),CNGCTL

ANYCNG,
	XCALL OUTPT (24,1,1,'FIELD # TO CHANGE            <TAB> = DELETE',1)
	XCALL INPUT (24,20,02,00,'#T',ENTRY,INXCTL,1)
	IF (INXCTL .EQ. 3)
		BEGIN
		XCALL OUTPT (24,1,1,'DELETE, ARE YOU SURE ?',1)
		XCALL INPUT (24,24,01,01,'YN',ENTRY,INXCTL,1)
		GOTO (ANYCNG),INXCTL-1
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,DELETE,LOKCTL)
		GOTO DISPLA
		END

	WHATNO = ENTRY(1,2)
	IF (WHATNO .EQ. 0)
	THEN 	CLEAR CNGCTL
	ELSE 	CNGCTL = 1

	GOTO (PROCES,CNGBR),CNGCTL+1
CNGBR,
	GOTO (PP9MUL,PP4MUL,ECRMUL,G20MUL,G18MUL,G16MUL,SSTMUL),WHATNO
	GOTO (ALUMUL,PGRMUL,PVSMUL),WHATNO-7
	GOTO ANYCNG

PROCES,
	CASE SELECT OF
	BEGINCASE
	1:	BEGIN
		TBLCOD = 'CF'
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,STORE,LOKCTL)
		END
	2:	XCALL ISIO (CHN182,COPTBL,TBL_KEY,WRITE,LOKCTL)
	ENDCASE
	GOTO MENU

ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('CP:TBLMNU',1)
	STOP


DSPREC,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; DISPLAY RECORD
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	ENTRY(1,5) = PP9MUL,	'ZX.XX' [LEFT]
	XCALL OUTPT (4,36,1,ENTRY(1,5),1)

	ENTRY(1,5) = PP4MUL,	'ZX.XX' [LEFT]
	XCALL OUTPT (6,36,1,ENTRY(1,5),1)

	ENTRY(1,5) = ECRMUL,	'ZX.XX' [LEFT]
	XCALL OUTPT (8,36,1,ENTRY(1,5),1)

	ENTRY(1,5) = G20MUL,	'ZX.XX' [LEFT]
	XCALL OUTPT (10,36,1,ENTRY(1,5),1)

	ENTRY(1,5) = G18MUL,	'ZX.XX' [LEFT]
	XCALL OUTPT (12,36,1,ENTRY(1,5),1)

	ENTRY(1,5) = G16MUL,	'ZX.XX' [LEFT]
	XCALL OUTPT (14,36,1,ENTRY(1,5),1)

	ENTRY(1,5) = SSTMUL,	'ZX.XX' [LEFT]
	XCALL OUTPT (16,36,1,ENTRY(1,5),1)

	ENTRY(1,5) = ALUMUL,	'ZX.XX' [LEFT]
	XCALL OUTPT (18,36,1,ENTRY(1,5),1)

	ENTRY(1,5) = PGRMUL,	'ZX.XX' [LEFT]
	XCALL OUTPT (20,36,1,ENTRY(1,5),1)

	ENTRY(1,5) = PVSMUL,	'ZX.XX' [LEFT]
	XCALL OUTPT (22,36,1,ENTRY(1,5),1)
	RETURN
;-----------------------------------


RE_CALC,	;;;;;;;;;;;;;;;;;;;;;;;;

	MSXCTL = 7
	XCALL MESAG ('ARE YOU SURE?',MSXCTL)
	IF (MSXCTL .EQ. 1) GOTO MENU

	XCALL PGCHN ('CP:PRCGAG',1)
;-----------------------------------


PRCMAT,	;;;;;;;;;;;;;;;;;;;;;;;;

	MSXCTL = 7
	XCALL MESAG ('ARE YOU SURE?',MSXCTL)
	IF (MSXCTL .EQ. 1) GOTO MENU

	XCALL PGCHN ('CP:PRCMAT',1)
;-----------------------------------

;===================================================================
PRINT_TABLE,
;===================================================================

	LINCNT = 66
	PGCNT  = 0
	
PDISP,
	CLEAR TBL_KEY
	TBLCOD = 'CF'
	FIND (CHN182,COPTBL,TBL_KEY)[ERR=PT_LOOP]
PT_LOOP,
	XCALL IOS (CHN182,COPTBL,READ,LOKCTL)
	IF (LOKCTL .NE. 0) GOTO PT_EOF
	IF (TBLCOD .NE. 'CF') GOTO PT_EOF
	
	PLINE(5,9) = PP9MUL,	MASK
	PLINE(13,17) = PP4MUL,	MASK
	PLINE(21,25) = ECRMUL,	MASK
	PLINE(29,33) = G20MUL,	MASK
	PLINE(37,41) = G18MUL,	MASK
	PLINE(45,49) = G16MUL,	MASK
	PLINE(53,57) = SSTMUL,	MASK
	PLINE(61,65) = ALUMUL,	MASK
	PLINE(69,73) = PGRMUL,	MASK
	PLINE(76,80) = PVSMUL,	MASK
;    -----------------------------MULTIPLIERS-----------------------------
;    PP-90   PP-40   EC-RED   20GA     18GA   16GA     SST    ALUM   PGRIP
;    ZX.XX   ZX.XX   ZX.XX   ZX.XX   ZX.XX   ZX.XX   ZX.XX   ZX.XX   ZX.XX
;12345678901234567890123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5         6         7         8
	CALL PRINT

PT_EOF,
EOF,
	IF (PRNTON.EQ.1)  XCALL LPOFF(LPSW,SPLFIL,PGCNT)
	PRNTON  = 0
	CLOSE 10
PDONE,
	GOTO MENU

PRINT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (PRNTON .EQ. 0) CALL PRNTON
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HD1,HD2,HD
&		,LG,LG,LG,0,080,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;-------------------------------------------------------------
PRNTON,
	SPLFIL (5,6) = 'EF'
	LPSW = 1		;PRINT,SPOOL, OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTON = 1
	RETURN
;-------------------------------------------------------------
;===================================================================


OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (1,'SU',182,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN182 = 1

	OPNOK = 1
	RETURN
;----------------------------------------------------

CLOSE,
	CLOSE CHN182
	RETURN
;----------------------------------------------------
	

