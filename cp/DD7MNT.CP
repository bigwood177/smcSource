;DD7MNT.CP
;OPTMNT.CP
;DPTMNT.CP
;	COP TABLE MAINTENANCE
;	DEPARTMENT OPERATIONS
;
RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'


RECORD	CHANNEL
	CHN182	,D2


RECORD	PRINT
	TITLE	,A*,	'DEPT CLASSES FOR BOL'
	HD	,A6,	'NO HDR'
	LG	,A9,	'NO LEGEND'
	PLINE	,A80
	PRNTON	,D1
	LINCNT	,D2,60
	PGCNT	,D6
	LPSW	,D2
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	PRTCTL	,D3,080
	LPARG	,D1
	PRNTSW	,D1
	PRTCTR	,D1


RECORD	HD1
		,A*,	'DEPT  SEQ  CLASS DESCRIPTION '


RECORD	VARS
	A3	,A3
	ALPHA	,A12
	OPNOK	,D1
	BLANKS	,A30
	SEQ	,D3
	DEPT	,A2
	DESC	,A30
	DPTDSC	,A30
	ST_DPT	,A2
	EN_DPT	,A2
	PGM	,D1
	ROW	,D2
	ENTRY	,A30
	INXCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	STORE	,D1,2
	DELETE	,D1,3
	LOKCTL	,D1
	WHATNO	,D2
	SELECT	,D1
	CNGCTL	,D1
	I	,D3
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'DEPT CLASSES FOR BOL',1)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO CLOSE

MENU,
	XCALL OUTPT (1,1,2,'DEPT CLASSES FOR BOL',1)
	XCALL OUTPT (3,9,0,'PLEASE SELECT APPLICATION',1)
	XCALL OUTPT (5,15,0,'1. TABLE MAINTENANCE',1)
	XCALL OUTPT (6,15,0,'2. PRINT TABLE',1)
MINPUT,
	XCALL INPUT (3,36,1,1,'#E',ENTRY,INXCTL,1)
	GOTO (MINPUT,ENDOFF), INXCTL
	PGM = ENTRY(1,1)
	GOTO (DISPLA,PRINT_TABLE),PGM
	GOTO MINPUT

DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,'DEPT CLASSES FOR BOL',1)
	XCALL OUTPT ( 4,4,0,'1. DEPARTMENT',1)
	XCALL OUTPT ( 6,4,0,'2. SEQ #',1)
	XCALL OUTPT ( 8,4,0,'3. CLASS',1)
	XCALL OUTPT (10,4,0,'4. DESCRIPTION',1)

DEPT,
	XCALL INPUT (4,24,02,00,'AE',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	DEPT = ENTRY(1,2)
	CLEAR TBL_KEY
	TBL_KEY(1,2) = 'DD'
	D7_DEPT = DEPT
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .NE. 0)
		BEGIN
		XCALL MESAG ('DEPARTMENT NOT FOUND',1)
		GOTO DEPT
		END
	DPTDSC = DD_DESC
	XCALL OUTPT (4,27,0,DD_DESC,1)

SEQ,
	XCALL INPUT (6,24,02,00,'#E',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	SEQ = ENTRY(1,2)
	CLEAR TBL_KEY
	TBL_KEY(1,2) = 'D7'
	D7_DEPT = DEPT
	D7_SEQ = SEQ
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .EQ. 0)
		BEGIN
		SELECT = 2
		CALL DSPREC
		GOTO ANYCNG
		END

	SELECT = 1			;ADD MODE
	CLEAR COPTBL

	D7_DEPT = DEPT
	D7_SEQ = SEQ
	
CLASS,
	XCALL INPUT (08,24,03,00,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU), INXCTL
	D7_CLAS = ENTRY(1,3)
	USING D7_CLAS SELECT
	  (50),	NOP
	  (60),	NOP
	  (70),	NOP
	  (125),NOP
	  (150),NOP
	  (200),NOP
	  (250),NOP
	  (300),NOP
	  (777),NOP
	  (888),NOP
	  (999),NOP
	  (),	BEGIN
		  XCALL MESAG ('INVALID FREIGHT CLASS',1)
		  GOTO CLASS
		END
	ENDUSING
	GOTO (ANYCNG),CNGCTL
	
DESC,
	XCALL INPUT (10,24,30,00,'LE',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	D7_DESC = ENTRY(1,30)
	GOTO (ANYCNG),CNGCTL


ANYCNG,
	XCALL OUTPT (24,1,1,'FIELD # TO CHANGE            <TAB> = DELETE',1)
	XCALL INPUT (24,20,02,00,'#T',ENTRY,INXCTL,1)
	IF (INXCTL .EQ. 3)
		BEGIN
		XCALL OUTPT (24,1,1,'DELETE, ARE YOU SURE ?',1)
		XCALL INPUT (24,24,01,01,'YN',ENTRY,INXCTL,1)
		GOTO (ANYCNG),INXCTL-1
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,DELETE,LOKCTL)
		GOTO DISPLA
		END

	WHATNO = ENTRY(1,2)
	IF (WHATNO .EQ. 0)
	THEN 	CLEAR CNGCTL
	ELSE 	CNGCTL = 1

	GOTO (PROCES,CNGBR),CNGCTL+1
CNGBR,
	GOTO (DEPT, SEQ, CLASS, DESC),WHATNO
	GOTO ANYCNG

PROCES,
	CASE SELECT OF
	BEGINCASE
	1:	BEGIN
		TBLCOD = 'D7'
		D7_DEPT = DEPT
		D7_SEQ = SEQ
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,STORE,LOKCTL)
		END
	2:	XCALL ISIO (CHN182,COPTBL,TBL_KEY,WRITE,LOKCTL)
	ENDCASE
	GOTO DISPLA

ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('CP:MMDEPT',1)
	STOP


DSPREC,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; DISPLAY RECORD
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL OUTPT (4,24,1,D7_DEPT,1)
	A3 = D7_SEQ,	'ZZX' [LEFT]
	XCALL OUTPT (6,24,1,A3,1)
	
	A3 = D7_CLAS,	'ZZX' [LEFT]
	XCALL OUTPT (8,24,1,A3,1)
	XCALL OUTPT (10,24,1,D7_DESC,1)

	RETURN
;-----------------------------------



;===================================================================
PRINT_TABLE,
;===================================================================
	LINCNT = 66
	PGCNT  = 0
	
PDISP,
	CNGCTL = 
	XCALL OUTPT (1,1,2,'DEPT CLASSES FOR BOL',1)
	XCALL OUTPT (4,4,0,'1. STARTING DEPT',1)
	XCALL OUTPT (6,4,0,'2. ENDING   DEPT',1)
ST_DPT,
	XCALL INPUT (4,27,02,00,'AE',ENTRY,INXCTL,1)
	GOTO (PDISP,PDONE),INXCTL
	ST_DPT = ENTRY(1,2)
	IF (ST_DPT .EQ. BLANKS)
		BEGIN
		EN_DPT = 'ZZ'
		XCALL OUTPT (4,25,1,'ALL',1)
		XCALL OUTPT (6,25,1,' ',1)
		GOTO P_ANY
		END
	GOTO (P_ANY),CNGCTL
EN_DPT,
	XCALL INPUT (6,25,02,00,'A ',ENTRY,INXCTL,1)
	GOTO (PDISP),INXCTL
	EN_DPT = ENTRY(1,2)
	IF (EN_DPT .EQ. BLANKS)
		BEGIN
		EN_DPT = ST_DPT
		XCALL OUTPT (6,25,0,EN_DPT,1)
		END
P_ANY,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (P_PRINT,P_CNGBR),CNGCTL + 1
P_CNGBR,
	GOTO (ST_DPT,EN_DPT),WHATNO
	GOTO P_ANY

P_PRINT,
	CLEAR TBL_KEY
	TBLCOD = 'D7'
	D7_DEPT = ST_DPT
	FIND (CHN182,COPTBL,TBL_KEY)[ERR=PLOOP]

PLOOP,
	XCALL IOS (CHN182,COPTBL,READ,LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF
	IF (TBLCOD .NE. 'D7') GOTO EOF
	IF (D7_DEPT .LT. ST_DPT) GOTO PLOOP
	IF (D7_DEPT .GT. EN_DPT) GOTO EOF


	CLEAR PLINE
	PLINE (2,3) = D7_DEPT	;DD_DEPT
	PLINE (7,9) = D7_SEQ,	'ZZX'
	PLINE (14,16) = D7_CLAS, 'ZZX'
	PLINE (18,47) = D7_DESC



;DEPT  SEQ  CLASS DESCRIPTION                    
; AA    XX    ZZX AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA     
;123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5
	CALL PRINT
	GOTO PLOOP

EOF,
	IF (PRNTON.EQ.1)  XCALL LPOFF(LPSW,SPLFIL,PGCNT)
	PRNTON  = 0

PDONE,
	GOTO MENU

PRINT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (PRNTON .EQ. 0) CALL PRNTON
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HD1,HD,HD
&		,LG,LG,LG,0,080,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;-------------------------------------------------------------
PRNTON,
	SPLFIL (5,6) = 'EF'
	LPSW = 1		;PRINT,SPOOL, OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTON = 1
	RETURN
;-------------------------------------------------------------
;===================================================================


OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (1,'SU',182,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN182 = 1

	OPNOK = 1
	RETURN
;----------------------------------------------------

CLOSE,
	CLOSE CHN182
	RETURN
;----------------------------------------------------
	

