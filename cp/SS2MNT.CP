;SS2MNT.CP
;SS1MNT.CP
;
;	COP TABLE MAINTENANCE
;	SAFETY SEAL PARAMETERS
;
;
RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'


RECORD	CHANNEL
	CHN182	,D2


RECORD	PRINT
	TITLE	,A*,	'COMPLETE SEAL PARAMETERS'
	HD	,A6,	'NO HDR'
	LG	,A9,	'NO LEGEND'
	PLINE	,A132
	PRNTON	,D1
	LINCNT	,D2,60
	PGCNT	,D6
	LPSW	,D2
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	PRTCTL	,D3,132
	LPARG	,D1
	PRNTSW	,D1
	PRTCTR	,D1


RECORD	HD1
		,A*,	'KEY       L-SEAM          G-SEAM          S-SEAM'
		,A*,	'          CONN T          CONN B'

RECORD	VLINE
		,A2
	VDIM	,A2
		,A1
	VD1	,A3
	VV1	,A7
		,A1
	VD2	,A3
	VV2	,A7
		,A1
	VD3	,A3
	VV3	,A7
		,A1
	VD4	,A3
	VV4	,A7
		,A1
	VD5	,A3
	VV5	,A7
		,A1
	VD6	,A3
	VV6	,A7
		,A1
	VD7	,A3
	VV7	,A7
		,A1
	VD8	,A3
	VV8	,A7
		,A1
	VD9	,A3
	VV9	,A7
		,A1
	VD10	,A3
	VV10	,A7
		,A1

RECORD	VALS
	LS	,A15
	GS	,A15
	SS	,A15
	CON1	,A15
	CON2	,A15
	AROW	,10D2,	16,17,18,19,20,16,17,18,19,20
	VAL	,12D6
	MAX	,D2

RECORD	VARS
	MAT	,A1
	GA	,D2
	KEY	,A5
	SAVKEY	,A5
	SAVTYP	,A1
	SAVMAT	,A1
	SAVGA	,D2
	A1	,A1
	A2	,A2
	A4	,A4
	A5	,A5
	A7	,A7
	B1	,A1	;BLANKS
	ALPHA	,A12
	OPNOK	,D1
	BLANKS	,A30
	TYPE	,A1
	DIM	,D2
	DESC	,A30
	I	,D3
	LOC	,A20	
	QUE	,A12
	ST_KEY	,A5
	EN_KEY	,A5
	PGM	,D1
	ROW	,D2
	COL	,D2
	ENTRY	,A30
	INXCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	STORE	,D1,2
	DELETE	,D1,3
	LOKCTL	,D1
	WHATNO	,D2
	SELECT	,D1
	CNGCTL	,D1
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'COMPLETE SEAL PARAMETERS TABLE',1)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO CLOSE

MENU,
	SAVTYP = '*'
	XCALL OUTPT (1,1,2,'COMPLETE SEAL PARAMETERS TABLE',1)
	XCALL OUTPT (3,9,0,'PLEASE SELECT APPLICATION',1)
	XCALL OUTPT (5,15,0,'1. TABLE MAINTENANCE',1)
	XCALL OUTPT (6,15,0,'2. PRINT TABLE',1)
MINPUT,
	XCALL INPUT (3,36,1,1,'#E',ENTRY,INXCTL,1)
	GOTO (MINPUT,ENDOFF), INXCTL
	PGM = ENTRY(1,1)
	GOTO (DISPLA,PRINT_TABLE),PGM
	GOTO MINPUT

DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,'COMPLETE SEAL PARAMETERS TABLE',1)
	XCALL OUTPT ( 4,4,0,'1. KEY',1)
	XCALL OUTPT ( 5,4,0,'2. MATERIAL (G,A,P,S,V)',1)
	XCALL OUTPT ( 6,4,0,'3. GAUGE (1=22-26, 2=16-20)',1)

	XCALL OUTPT ( 7,4,0,'4. COMPONENT (BODY, REDUCER ,TAP)      (B,R,T)',1)
	XCALL OUTPT ( 8,4,0,'5. DIM',1)

KEY,	
	XCALL INPUT (4,38,05,00,'AE',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	KEY = ENTRY(1,5)
	IF (KEY .EQ. A5) KEY = SAVKEY
	IF (KEY .EQ. A5) GOTO KEY
	XCALL OUTPT (4,38,1,KEY,1)
	SAVKEY = KEY

;;;	CLEAR TBL_KEY
;;;	TBLCOD = 'SK'
;;;	SK_KEY = KEY
;;;	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
;;;	IF (LOKCTL .EQ. 0)
;;;	THEN	XCALL OUTPT (4,44,1,SK_DESC,1)
;;;	ELSE	GOTO KEY
;;;	GOTO (ANYCNG), CNGCTL

MAT,
	XCALL INPUT (5,38,01,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	MAT = ENTRY(1,1)
	IF (MAT .EQ. B1) MAT = SAVMAT
	IF (MAT .EQ. B1) MAT = 'G'
	XCALL OUTPT (5,38,1,MAT,1)

	USING MAT SELECT
	('G'),	XCALL OUTPT (5,40,1,'GALV',1)
	('A'),	XCALL OUTPT (5,40,1,'ALUM',1)
	('P'),	XCALL OUTPT (5,40,1,'PAINT GRIP',1)
	('S'),	XCALL OUTPT (5,40,1,'STAINLESS',1)
	('V'),	XCALL OUTPT (5,40,1,'VINYL',1)
	ENDUSING
	SAVMAT = MAT
GA,
	XCALL INPUT (6,38,01,00,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	GA = ENTRY(1,1)
	IF (GA .EQ. 0) GA = SAVGA
	IF (GA .EQ. 0) GA = 1
	A1 = GA,	'X'
	XCALL OUTPT (6,38,1,A1,1)
	SAVGA = GA
TYPE,
	XCALL INPUT (7,38,01,00,'AE',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	TYPE = ENTRY(1,1)
	IF (TYPE .EQ. B1) 
		BEGIN
		TYPE = SAVTYP
		XCALL OUTPT (7,38,1,TYPE,1)
		END

	SAVTYP = TYPE

	XCALL OUTPT (10,3,0,' 6. LSEAM',1)
	XCALL OUTPT (11,3,0,' 7. GSEAM',1)
	XCALL OUTPT (12,3,0,' 8. SSEAM',1)
	XCALL OUTPT (13,3,0,' 9. CONN B',1)
	XCALL OUTPT (14,3,0,'10. CONN T',1)

	USING TYPE SELECT
	('B'),	BEGIN
		XCALL OUTPT (16,3,0,'11. W',1)
		XCALL OUTPT (17,3,0,'12. L',1)
		MAX = 2
		END
	('R'),	BEGIN
		XCALL OUTPT (16,3,0,'11. WB',1)
		XCALL OUTPT (17,3,0,'12. SB',1)
		XCALL OUTPT (18,3,0,'13. WT',1)
		XCALL OUTPT (19,3,0,'14. ST',1)
		XCALL OUTPT (20,3,0,'15. L ',1)
		MAX = 5
		END
	('T'),	BEGIN
		XCALL OUTPT (13,3,0,' 9. CONN L',1)
		XCALL OUTPT (14,3,0,'10. CONN R',1)
		XCALL OUTPT (16,3,0,'11. WL',1)
		XCALL OUTPT (17,3,0,'12. DL',1)
		XCALL OUTPT (18,3,0,'13. WR',1)
		XCALL OUTPT (19,3,0,'14. SR',1)
		XCALL OUTPT (20,3,0,'15. LT',1)

		XCALL OUTPT (16,27,0,'16. OW',1)
		XCALL OUTPT (17,27,0,'17. OD',1)
		XCALL OUTPT (18,27,0,'18. AT',1)
		XCALL OUTPT (19,27,0,'19. AR',1)
		XCALL OUTPT (20,27,0,'20. LB',1)
		MAX = 10
		END
	(),	BEGIN
		XCALL MESAG ('TYPE MUST BE B,R, or T',1)
		GOTO TYPE
		END
	ENDUSING

DIM,
	XCALL INPUT (8,12,02,01,'#E',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	DIM = ENTRY(1,2)

	CLEAR TBL_KEY
	TBLCOD = 'S2'
	S2_KEY = KEY
	S2_MAT = MAT
	S2_GA = GA
	S2_TYPE = TYPE
	S2_DIM = DIM
	S2_R = 1	;SEAM, CONN RECORD
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .EQ. 0)
		BEGIN
		SELECT = 2
		CALL DSPREC
		GOTO ANYCNG
		END
	SELECT = 1			;ADD MODE
	CLEAR COPTBL
	S2_DIM = DIM
LS,
	XCALL INPUT (10,16,15,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	LS = ENTRY(1,15)
	GOTO (ANYCNG),CNGCTL
GS,
	XCALL INPUT (11,16,15,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	GS = ENTRY(1,15)
	GOTO (ANYCNG),CNGCTL
SS,
	XCALL INPUT (12,16,15,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	SS = ENTRY(1,15)
	GOTO (ANYCNG),CNGCTL
CON1,
	XCALL INPUT (13,16,16,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	CON1 = ENTRY(1,15)
	GOTO (ANYCNG),CNGCTL
CON2,
	XCALL INPUT (14,16,16,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	CON2 = ENTRY(1,15)
	GOTO (ANYCNG),CNGCTL

	COL = 12
	FOR I FROM 1 THRU MAX		;DEPENDING ON TYPE...
		BEGIN
		IF (I.EQ.6) COL = 35

		ROW = AROW(I)

		CALL GET_VAL
		GOTO (DISPLA),INXCTL
		GOTO (ANYCNG),CNGCTL
		END
ANYCNG,
	XCALL OUTPT (24,1,1,'FIELD # TO CHANGE            <TAB> = DELETE',1)
	XCALL INPUT (24,20,02,00,'#T',ENTRY,INXCTL,1)
	IF (INXCTL .EQ. 3)
		BEGIN
		XCALL OUTPT (24,1,1,'DELETE, ARE YOU SURE ?',1)
		XCALL INPUT (24,24,01,01,'YN',ENTRY,INXCTL,1)
		GOTO (ANYCNG),INXCTL-1

		CLEAR TBL_KEY
		TBLCOD = 'S2'
		S2_KEY = KEY
		S2_MAT = MAT
		S2_GA = GA
		S2_TYPE = TYPE
		S2_DIM = DIM
		S2_R = 1	;SEAM, CONN RECORD
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
		IF (LOKCTL.EQ.0) XCALL ISIO (CHN182,COPTBL,TBL_KEY,DELETE,LOKCTL)

		S2_R = 2	;VALUES RECORD
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
		IF (LOKCTL.EQ.0) XCALL ISIO (CHN182,COPTBL,TBL_KEY,DELETE,LOKCTL)
		GOTO DISPLA
		END

	WHATNO = ENTRY(1,2)
	IF (WHATNO .EQ. 0)
	THEN 	CLEAR CNGCTL
	ELSE 	CNGCTL = 1

	GOTO (PROCES,CNGBR),CNGCTL+1
CNGBR,
	IF (WHATNO.GT.20) GOTO ANYCNG

	GOTO (KEY,MAT,GA,TYPE,DIM,LS,GS,SS,CON1,CON2),WHATNO
	I = WHATNO - 10

	USING TYPE SELECT
	('B'),	IF (I.GT.2) GOTO ANYCNG
	('R'),	IF (I.GT.5) GOTO ANYCNG
	ENDUSING 

	COL = 12
	IF (I.GE.6) COL = 35

	ROW = AROW(I)

	CALL GET_VAL

	GOTO ANYCNG

PROCES,

	USING TYPE SELECT
	('B'),	BEGIN
		S2_W = VAL(1)
		S2_L = VAL(2)
		END
	('R'),	BEGIN
		S2_WB = VAL(1)
		S2_SB = VAL(2)
		S2_WT = VAL(3)
		S2_ST = VAL(4)
		S2_RL = VAL(5)	;L
		END
	('T'),	BEGIN
		S2_WL = VAL(1)
		S2_DL = VAL(2)
		S2_WR = VAL(3)
		S2_SR = VAL(4)
		S2_LT = VAL(5)
		S2_OW = VAL(6)
		S2_OD = VAL(7)
		S2_AT = VAL(8)
		S2_AR = VAL(9)
		S2_LB = VAL(10)
		END
	ENDUSING

	USING SELECT SELECT
	(1),	BEGIN
		TBLCOD = 'S2'
		S2_KEY = KEY
		S2_MAT = MAT
		S2_GA = GA
		S2_TYPE = TYPE
		S2_DIM = DIM
		S2_R = 2

		XCALL ISIO (CHN182,COPTBL,TBL_KEY,STORE,LOKCTL)

		CLEAR COPTBL
		TBLCOD = 'S2'
		S2_KEY = KEY
		S2_MAT = MAT
		S2_GA = GA
		S2_TYPE = TYPE
		S2_DIM = DIM

		S2_LSEAM = LS
		S2_GSEAM = GS
		S2_SSEAM = SS
		S2_CONB = CON1
		S2_CONT = CON2
		S2_R = 1
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,STORE,LOKCTL)

		END
	(2),	BEGIN
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,WRITE,LOKCTL)

		CLEAR COPTBL
		TBLCOD = 'S2'
		S2_KEY = KEY
		S2_MAT = MAT
		S2_GA = GA
		S2_TYPE = TYPE
		S2_DIM = DIM
		S2_R = 1
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)

		S2_LSEAM = LS
		S2_GSEAM = GS
		S2_SSEAM = SS
		S2_CONB = CON1
		S2_CONT = CON2
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,WRITE,LOKCTL)
		END
	ENDUSING

	GOTO DISPLA

ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('CP:TBLMNU',1)
	STOP

GET_VAL,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		;;; INPUT THE VALUE
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL INPUT (ROW,COL,06,00,'# ',ENTRY,INXCTL,1)
	IF (INXCTL.EQ.1) RETURN
	VAL(I) = ENTRY(1,6)

DIS_VAL,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		;;; DISPLAY THE VALUE
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	A7 = VAL(I),	'ZZX.XXX' [LEFT]
	XCALL OUTPT (ROW,COL,0,A7,1)
	RETURN

DSPREC,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; DISPLAY RECORD
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	XCALL OUTPT (10,16,0,S2_LSEAM,1)
	XCALL OUTPT (11,16,0,S2_GSEAM,1)
	XCALL OUTPT (12,16,0,S2_SSEAM,1)
	XCALL OUTPT (13,16,0,S2_CONB,1)
	XCALL OUTPT (14,16,0,S2_CONT,1)

	LS = S2_LSEAM
	GS = S2_GSEAM
	SS = S2_SSEAM
	CON1 = S2_CONB
	CON2 = S2_CONT

	S2_R = 2	;VALUES RECORD
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .NE. 0)
		BEGIN
		XCALL mesag ('MISSING VALUE RECORD',1)
		RETURN
		END
	USING TYPE SELECT
	('B'),	BEGIN
		VAL(1) = S2_W 
		VAL(2) = S2_L 
		END
	('R'),	BEGIN
		VAL(1) = S2_WB 
		VAL(2) = S2_SB 
		VAL(3) = S2_WT 
		VAL(4) = S2_ST 
		VAL(5) = S2_RL 
		END
	('T'),	BEGIN
		VAL(1) = S2_WL 
		VAL(2) = S2_DL 
		VAL(3) = S2_WR 
		VAL(4) = S2_SR 
		VAL(5) = S2_LT 
		VAL(6) = S2_OW 
		VAL(7) = S2_OD 
		VAL(8) = S2_AT 
		VAL(9) = S2_AR 
		VAL(10) = S2_LB 
		END
	ENDUSING

	COL = 12
	FOR I FROM 1 THRU MAX		;DEPENDING ON TYPE...
		BEGIN
		IF (I.EQ.6) COL = 35

		ROW = AROW(I)
		CALL DIS_VAL
		END

	RETURN
;-----------------------------------



;===================================================================
PRINT_TABLE,
;===================================================================
	PGCNT  = 0
	
PDISP,
	CNGCTL = 
	XCALL OUTPT (1,1,2,'PRINT COMPLETE SEAL PARAMETERS TABLE',1)
	XCALL OUTPT (4,4,0,'1. STARTING KEY',1)
	XCALL OUTPT (6,4,0,'2. ENDING   KEY',1)
ST_KEY,
	XCALL INPUT (4,27,05,00,'AE',ENTRY,INXCTL,1)
	GOTO (PDISP,PDONE),INXCTL
	ST_KEY = ENTRY(1,5)
	IF (ST_KEY .EQ. A5)
		BEGIN
		EN_KEY = 'ZZZZZ'
		XCALL OUTPT (4,25,1,'ALL',1)
		XCALL OUTPT (6,25,1,' ',1)
		GOTO P_ANY
		END
	GOTO (P_ANY),CNGCTL
EN_KEY,
	XCALL INPUT (6,25,05,00,'A ',ENTRY,INXCTL,1)
	GOTO (PDISP),INXCTL
	EN_KEY = ENTRY(1,5)
	IF (EN_KEY .EQ. A5)
		BEGIN
		EN_KEY = ST_KEY
		XCALL OUTPT (6,25,0,EN_KEY,1)
		END
P_ANY,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (P_PRINT,P_CNGBR),CNGCTL + 1
P_CNGBR,
	GOTO (ST_KEY,EN_KEY),WHATNO
	GOTO P_ANY

P_PRINT,
	SWITCH = 5
	XCALL FILES (10,'SI',182,SWITCH)


	SAVTYP = '*'
	CLEAR TBL_KEY
	TBLCOD = 'S2'
	S2_KEY = ST_KEY
	FIND (10,COPTBL,TBL_KEY)[ERR=PLOOP]

PLOOP,
	XCALL IOS (10,COPTBL,READ,LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF
	IF (TBLCOD .NE. 'S2') GOTO EOF
	IF (S2_KEY .LT. ST_KEY) GOTO PLOOP
	IF (S2_KEY .GT. EN_KEY) GOTO EOF


	IF (S2_R .EQ. 1)
		BEGIN
		PLINE (1,5) = S2_KEY
		PLINE (7,7) = S2_MAT
		PLINE (9,9) = S2_GA,	'X'
		PLINE (11,25) = S2_LSEAM
		PLINE (27,41) = S2_GSEAM
		PLINE (43,57) = S2_SSEAM
		PLINE (59,73) = S2_CONB
		PLINE (75,89) = S2_CONT
		CALL PRINT
		GOTO PLOOP
		END

	CLEAR VLINE

	USING S2_TYPE SELECT
	('B'),	BEGIN
		VLINE(1,1) = 'B'
		VDIM = S2_DIM,	'ZX'
		VD1 = ' W:'
		VV1 = S2_W,	'ZZX.XXX' [left]
		VD2 = ' L:'
		VV2 = S2_L,	'ZZX.XXX' [left]
		END
	('R'),	BEGIN
		VLINE(1,1) = 'R'
		VDIM = S2_DIM,	'ZX'
		VD1 = 'WB:'
		VV1 = S2_WB,	'ZZX.XXX' [left]
		VD2 = 'SB:'
		VV2 = S2_SB,	'ZZX.XXX' [left]
		VD3 = 'WT:'
		VV3 = S2_WT,	'ZZX.XXX' [left]
		VD4 = 'ST:'
		VV4 = S2_ST,	'ZZX.XXX' [left]
		VD5 = 'RL:'
		VV5 = S2_RL,	'ZZX.XXX' [left]
		END
	('T'),	BEGIN
		VLINE(1,1) = 'T'
		VDIM = S2_DIM,	'ZX'
		VD1 = 'WL:'
		VV1 = S2_WL,	'ZZX.XXX' [left]
		VD2 = 'DL:'
		VV2 = S2_DL,	'ZZX.XXX' [left]
		VD3 = 'WR:'
		VV3 = S2_WR,	'ZZX.XXX' [left]
		VD4 = 'SR:'
		VV4 = S2_SR,	'ZZX.XXX' [left]
		VD5 = 'LT:'
		VV5 = S2_LT,	'ZZX.XXX' [left]
		VD6 = 'OW:'
		VV6 = S2_OW,	'ZZX.XXX' [left]
		VD7 = 'OD:'
		VV7 = S2_OD,	'ZZX.XXX' [left]
		VD8 = 'AT:'
		VV8 = S2_AT,	'ZZX.XXX' [left]
		VD9 = 'AR:'
		VV9 = S2_AR,	'ZZX.XXX' [left]
		VD10 = 'LB:'
		VV10 = S2_LB,	'ZZX.XXX' [left]
		END

	ENDUSING
	PLINE = VLINE
	CALL PRINT

;KEY       L-SEAM          G-SEAM          S-SEAM          CONN T          CONN B
;AAAAA A X AAAAAAAAAAAAAAA AAAAAAAAAAAAAAA AAAAAAAAAAAAAAA AAAAAAAAAAAAAAA AAAAAAAAAAAAAAA
;12345678901234567890123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5         6         7


	GOTO PLOOP

;------------------------------------------
EOF,
	IF (PRNTON.EQ.1)  XCALL LPOFF(LPSW,SPLFIL,PGCNT)
	PRNTON  = 0

	close 10
PDONE,
	GOTO MENU

PRINT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (PRNTON .EQ. 0) CALL PRNTON
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HD1,HD,HD
&		,LG,LG,LG,0,132,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;-------------------------------------------------------------
PRNTON,
	SPLFIL (5,6) = 'EF'
	LPSW = 1		;PRINT,SPOOL, OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTON = 1
	RETURN
;-------------------------------------------------------------
;===================================================================


OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (1,'SU',182,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN182 = 1

	OPNOK = 1
	RETURN
;----------------------------------------------------

CLOSE,
	CLOSE CHN182
	RETURN
;----------------------------------------------------
	


