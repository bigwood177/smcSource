; 3-20-07 "itmmas.smm" is hardcoded...

;DIKMNT.CP
;	COP TABLE MAINTENANCE
;	DEFAULT KEY CODES
;
RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'

RECORD	ITMMAS
	.INCLUDE 'DEF:RD041A.def'

RECORD	CHANNEL
	CHN182	,D2
	CHN041	,D2

RECORD	PRINT
	TITLE	,A*,	'DEFAULT KEY TABLE'
	HD	,A6,	'NO HDR'
	LG	,A9,	'NO LEGEND'
	PLINE	,A132
	PRNTON	,D1
	LINCNT	,D2,60
	PGCNT	,D6
	LPSW	,D2
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	PRTCTL	,D3,132
	LPARG	,D1
	PRNTSW	,D1
	PRTCTR	,D1

RECORD	HD1
		,A*,	'ITEM             F1  F2    F3'

RECORD	VARS
	mmf	,a2	;for memo-key
	savitm	,a15
	ALPHA	,A12
	OPNOK	,D1
	BLANKS	,A30
	MM_CODE	,D5
	A15	,A15
	F1	,D3
	F2	,D3
	F3	,D5
	ST_ITEM	,A15
	EN_ITEM	,A15
	ITEM	,A15
	DESC	,A30
	PGM	,D1
	ROW	,D2
	ENTRY	,A30
	INXCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	STORE	,D1,2
	DELETE	,D1,3
	LOKCTL	,D1
	WHATNO	,D2
	SELECT	,D1
	CNGCTL	,D1
	I	,D3
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'DEFAULT KEY TABLE',1)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO CLOSE

MENU,
	XCALL OUTPT (1,1,2,'DEFAULT KEY TABLE',1)
	XCALL OUTPT (3,9,0,'PLEASE SELECT APPLICATION',1)
	XCALL OUTPT (5,15,0,'1. TABLE MAINTENANCE',1)
	XCALL OUTPT (6,15,0,'2. PRINT TABLE',1)
MINPUT,
	XCALL INPUT (3,36,1,1,'#E',ENTRY,INXCTL,1)
	GOTO (MINPUT,ENDOFF), INXCTL
	PGM = ENTRY(1,1)
	GOTO (DISPLA,PRINT_TABLE),PGM
	GOTO MINPUT

DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,'DEFAULT KEY TABLE',1)
	XCALL OUTPT ( 4,4,0,'1. ITEM',1)
	XCALL OUTPT ( 6,4,0,'2. F1 KEY',1)
	XCALL OUTPT ( 8,4,0,'3. F2 KEY',1)
	XCALL OUTPT (10,4,0,'4. F3 KEY',1)

ITEM,
	XCALL INPUT (4,24,15,00,'AE',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	ITEM = ENTRY(1,15)
	if (item .eq. a15)
	then	begin
	ilop,	reads (chn041, itmmas, no_item)
		if (itemno .eq. savitm) goto ilop
		item = itemno
		xcall outpt (4,24,1,item,1)
		end
	else	read (chn041, itmmas, item) [err=no_item]
	savitm = itemno

	XCALL OUTPT (4,41,1,DESCR,1)
	GOTO ITEM_OK
NO_ITEM,
	XCALL MESAG ('ITEM NOT FOUND',1)
	GOTO ITEM
ITEM_OK,
	CLEAR TBL_KEY
	TBL_KEY(1,2) = 'IK'
	IK_ITEM = ITEM
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .EQ. 0)
		BEGIN
		SELECT = 2
		CALL DSPREC
		GOTO ANYCNG
		END


	SELECT = 1			;ADD MODE
	CLEAR COPTBL

F1,
	XCALL INPUT (6,24,03,00,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	F1 = ENTRY(1,3)
	MM_CODE = F1
	mmf = 'M1'
	CALL MEMO_KEY
	XCALL OUTPT (6,31,1,MM_LONG,1)
	GOTO (ANYCNG), CNGCTL
F2,
	XCALL INPUT (8,24,03,00,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	F2 = ENTRY(1,3)
	MM_CODE = F2
	mmf = 'M2'
	CALL MEMO_KEY
	XCALL OUTPT (8,31,1,MM_LONG,1)
	GOTO (ANYCNG), CNGCTL
F3,
	XCALL INPUT (10,24,05,00,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	F3 = ENTRY(1,5)

	MM_CODE = F3
	mmf = 'M3'
	CALL MEMO_KEY
	XCALL OUTPT (10,31,1,MM_LONG,1)

	GOTO (ANYCNG), CNGCTL

ANYCNG,
	XCALL OUTPT (24,1,1,'FIELD # TO CHANGE            <TAB> = DELETE',1)
	XCALL INPUT (24,20,02,00,'#T',ENTRY,INXCTL,1)
	IF (INXCTL .EQ. 3)
		BEGIN
		XCALL OUTPT (24,1,1,'DELETE, ARE YOU SURE ?',1)
		XCALL INPUT (24,24,01,01,'YN',ENTRY,INXCTL,1)
		GOTO (ANYCNG),INXCTL-1
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,DELETE,LOKCTL)
		GOTO DISPLA
		END

	WHATNO = ENTRY(1,2)
	IF (WHATNO .EQ. 0)
	THEN 	CLEAR CNGCTL
	ELSE 	CNGCTL = 1

	GOTO (PROCES,CNGBR),CNGCTL+1
CNGBR,
	GOTO (ITEM, F1, F2, F3), WHATNO
	GOTO ANYCNG

PROCES,
	CLEAR COPTBL
	CLEAR TBL_KEY
	TBL_KEY(1,2) = 'IK'
	IK_ITEM = ITEM
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)

	TBLCOD = 'IK'
	IK_ITEM = ITEM
	IK_F1 = F1
	IK_F2 = F2
	IK_F3 = F3

	CASE SELECT OF
	BEGINCASE
	1:	BEGIN
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,STORE,LOKCTL)
		END
	2:	XCALL ISIO (CHN182,COPTBL,TBL_KEY,WRITE,LOKCTL)
	ENDCASE
	GOTO DISPLA

ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('CP:TBLMNU',1)
	STOP


MEMO_KEY,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR TBL_KEY
;;;	TBLCOD = 'MM'
	TBLCOD = mmf
	MM_KEY = MM_CODE
	XCALL ISIO (CHN182, COPTBL, TBL_KEY, READ, LOKCTL)
	IF (LOKCTL .EQ. 0) RETURN
	IF (MM_CODE .NE. 0) 
	THEN	MM_LONG = '* KEY NOT FOUND *'
	ELSE	MM_LONG = 
	RETURN
;-----------------------------------------------------------

DSPREC,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; DISPLAY RECORD
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL OUTPT (4,24,1,IK_ITEM,1)
	XCALL OUTPT (4,41,1,DESCR,1)

	ENTRY(1,3) = IK_F1, 'ZZZ' [LEFT]
	XCALL OUTPT (6,24,1,ENTRY(1,3) ,1)

	ENTRY(1,3) = IK_F2, 'ZZZ' [LEFT]
	XCALL OUTPT (8,24,1,ENTRY(1,3),1)

	ENTRY(1,5) = IK_F3, 'ZZZZZ' [LEFT]
	XCALL OUTPT (10,24,1,ENTRY(1,5) ,1)

	F1 = IK_F1
	F2 = IK_F2
	F3 = IK_F3

	MM_CODE = F1
	mmf = 'M1'
	CALL MEMO_KEY
	XCALL OUTPT (6,31,1,MM_LONG,1)

	MM_CODE = F2
	mmf = 'M2'
	CALL MEMO_KEY
	XCALL OUTPT (8,31,1,MM_LONG,1)

	MM_CODE = F3
	mmf = 'M3'
	CALL MEMO_KEY
	XCALL OUTPT (10,31,1,MM_LONG,1)

	CLEAR TBL_KEY		;NEED TO RE-READ ORIGINAL RECORD
	TBL_KEY(1,2) = 'IK'
	IK_ITEM = ITEM
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	RETURN
;-----------------------------------


;===================================================================
PRINT_TABLE,
;===================================================================
	LINCNT = 66
	PGCNT  = 0
	
PDISP,
	CNGCTL = 
	XCALL OUTPT (1,1,2,'PRINT DEFAULT KEY TABLE',1)
	XCALL OUTPT (4,4,0,'1. STARTING ITEM',1)
	XCALL OUTPT (6,4,0,'2. ENDING   ITEM',1)
ST_ITEM,
	XCALL INPUT (4,25,15,00,'AE',ENTRY,INXCTL,1)
	GOTO (PDISP,PDONE),INXCTL
	ST_ITEM = ENTRY(1,15)
	IF (ST_ITEM .EQ. A15)
		BEGIN
		EN_ITEM = 'ZZZZZZZZZZZZZZZ'

		XCALL OUTPT (4,25,1,'ALL',1)
		XCALL OUTPT (6,25,1,' ',1)
		GOTO P_ANY
		END
	GOTO (P_ANY),CNGCTL
EN_ITEM,
	XCALL INPUT (6,25,15,00,'A ',ENTRY,INXCTL,1)
	GOTO (PDISP),INXCTL
	EN_ITEM = ENTRY(1,15)
	IF (EN_ITEM .EQ. A15)
		BEGIN
		EN_ITEM = ST_ITEM
		XCALL OUTPT (6,25,0,EN_ITEM,1)
		END
P_ANY,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (P_PRINT,P_CNGBR),CNGCTL + 1
P_CNGBR,
	GOTO (ST_ITEM,EN_ITEM),WHATNO
	GOTO P_ANY

P_PRINT,
	CLEAR TBL_KEY
	TBLCOD = 'IK'
	IK_ITEM = ST_ITEM
	FIND (CHN182,COPTBL,TBL_KEY)[ERR=PLOOP]

PLOOP,
	XCALL IOS (CHN182,COPTBL,READ,LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF
	IF (TBLCOD .NE. 'IK') GOTO EOF
	IF (IK_ITEM .LT. ST_ITEM) GOTO PLOOP
	IF (IK_ITEM .GT. EN_ITEM) GOTO EOF

	PLINE(1,15) = IK_ITEM	
	PLINE(17,19) = IK_F1,	'XXX'
	PLINE(21,23) = IK_F2,	'XXX'
	PLINE(25,29) = IK_F3,	'XXXXX'
	CALL PRINT
	GOTO PLOOP

;ITEM             F1  F2    F3
;AAAAAAAAAAAAAAA ZZX ZZX ZZZZX
;123456789012345678901234567890
;         1         2         3
EOF,
	IF (PRNTON.EQ.1)  XCALL LPOFF(LPSW,SPLFIL,PGCNT)
	PRNTON  = 0

PDONE,
	GOTO MENU

PRINT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (PRNTON .EQ. 0) CALL PRNTON
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HD1,HD,HD
&		,LG,LG,LG,0,132,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;-------------------------------------------------------------
PRNTON,
	SPLFIL (5,6) = 'EF'
	LPSW = 1		;PRINT,SPOOL, OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTON = 1
	RETURN
;-------------------------------------------------------------
;===================================================================


OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (1,'SU',182,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN182 = 1

	SWITCH = 5
	XCALL FILES (4, 'SI', 041, SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN041 = 4

	OPNOK = 1
	RETURN
;----------------------------------------------------

CLOSE,
	CLOSE CHN041
	CLOSE CHN182
	RETURN
;----------------------------------------------------
	
	
