;DESNEW.CP
;DESQTY.CP
;
RECORD	ORDLIN
	.INCLUDE 'DEF:RD045A.DEF'

RECORD	HD1
	,	A*,	'FOR ITEM '
	HD_ITEM	,A15
		,A2
		,A*,	'YEAR: '
	HD_YEAR	,A2
		,A2
	HD_DAMP	,A10
		
RECORD	HD2
	,	A*,	'ITEM                             QTY-SOLD'


RECORD	PRINT
	TITLE	,A*,	'QTY SOLD BY DESCRIPTION'
	HD	,A6,	'NO HDR'
	LG	,A9,	'NO LEGEND'
	PLINE	,A132
	PRNTON	,D1
	LINCNT	,D2,60
	PGCNT	,D6
	LPSW	,D2
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	PRTCTL	,D3,080
	LPARG	,D1
	PRNTSW	,D1
	PRTCTR	,D1

RECORD	CHANNEL
	CHNLIN	,D2
	CHNWRK	,D2

RECORD	VARS
	FILNAM	,A14
	YEAR	,D2
	CAT	,A2
	DAMP	,D1	;1=INCLUDE, 2=DON'T INCLUDE
	OPNOK	,D1
	WRKFIL	,A14
	ENTRY	,A30
	INXCTL	,D1
	CNGCTL	,D1
	WHATNO	,D2
	READ	,D1,0
	LOKCTL	,D1
	SAVDSC	,A30
	SRCKEY	,A15
	D_QTY	,D8
	T_QTY	,D8
	TL	,D3
	XL	,D3
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID(V)
	V=1
	XCALL OUTPT (1,1,2,'QTY SOLD BY DESCRIPTION',1)

DISPLA,
	XCALL OUTPT (1,1,2,'QTY SOLD BY DESCRIPTION',1)
	CLEAR CNGCTL
	XCALL OUTPT ( 6,4,0,'1. ITEM #',1)
	XCALL OUTPT ( 8,4,0,'2. INCLUDE W/DAMPER',1)
	XCALL OUTPT (10,4,0,'3. YEAR        <CR> = CURRENT YEAR',1)
ITEM,
	XCALL INPUT (6,15,15,00,'AE',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	SRCKEY=ENTRY(1,15)
	GOTO (ANYCNG),CNGCTL
CAT,
	XCALL INPUT (8,25,01,00,'YN',ENTRY,INXCTL,1)
	DAMP = INXCTL
	GOTO (ANYCNG),CNGCTL
YEAR,
	XCALL INPUT (10,15,02,00,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	YEAR = ENTRY(1,2)

ANYCNG,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (PROCES,CNGBR),CNGCTL+1
CNGBR,
	GOTO (ITEM,CAT,YEAR),WHATNO
	GOTO ANYCNG


PROCES,
	CALL OPENS
	HD_ITEM = SRCKEY
	HD_YEAR = YEAR,'XX'

	USING DAMP SELECT
	(1),	HD_DAMP = 'W/ DAMPER'
	(2),	HD_DAMP = 'NO DAMPERS'
	ENDUSING

	CLEAR D_QTY, T_QTY

	TL = %TRIM(SRCKEY)

	SAVDSC = '-1'
	find (CHNLIN, ORDLIN, SRCKEY, KRF=1) [ERR=BLDWRK]
BLDWRK,
	XCALL IOS(CHNLIN, ORDLIN, READ, LOKCTL)
	IF (LOKCTL .NE. 0) GOTO WRK_EOF
	IF (LITMNO(1,TL) .NE. SRCKEY(1,TL)) GOTO WRK_EOF
	GOTO (XLOOP),DAMP		;include damper, don't check
	IF (%INSTR(1,LCFGIM,'*') ) GOTO BLDWRK	;skip if damper
XLOOP,
	XL = %INSTR(1,LDESCR,'"')
	IF (XL .LE. 1) GOTO CONT
	LDESCR(XL,XL) = ' '
	GOTO XLOOP
CONT,

	WRITES(10,ORDLIN)
	GOTO BLDWRK

WRK_EOF,
	CLOSE CHNWRK
	SORT(INPUT=WRKFIL, RECORD=ORDLIN, KEY=(LDESCR))
	OPEN(10,I,WRKFIL)
		
LOOP,
	XCALL IOS(CHNWRK, ORDLIN, READ, LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF
	IF(LDESCR .NE. SAVDSC) CALL NEWDSC
	D_QTY = D_QTY + LQTYSH			;QTY SHIPPED
	GOTO LOOP

NEWDSC,
	IF(SAVDSC .EQ. '-1') GOTO ENDDSC
	PLINE(1,30) = SAVDSC
	PLINE(32,41)= D_QTY,	'ZZ,ZZZ,ZZX-'
	CALL PRINT
	T_QTY = T_QTY + D_QTY

ENDDSC,
	CLEAR D_QTY
	SAVDSC = LDESCR
	RETURN

EOF,
	CALL NEWDSC
	CALL PRINT
	PLINE(1,30) = 'TOTAL FOR ALL DESCRIPTIONS:'
	PLINE(32,41)= T_QTY,	'ZZ,ZZZ,ZZX-'
	CALL PRINT
	IF (PRNTON.EQ.1)  XCALL LPOFF(LPSW,SPLFIL,PGCNT)
	
ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('CP:COPRPT',1)
;===========================================

PRINT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (PRNTON .EQ. 0) CALL PRNTON
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HD1,HD2,HD
&		,LG,LG,LG,0,080,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;-------------------------------------------------------------
PRNTON,
	SPLFIL (5,6) = 'EF'
	LPSW = 1		;PRINT,SPOOL, OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTON = 1
	RETURN
;-------------------------------------------------------------

OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK
	XCALL FFILE(185,FILNAM,SWITCH)
	FILNAM(14,14) = 'M'

	IF (YEAR .NE. 0)FILNAM(9,10) = YEAR,'XX'
	OPEN(1,SI,FILNAM)

;;;	XCALL FILES(1,'SI',185,SWITCH)
;;;	IF (SWITCH.EQ.9) RETURN
	CHNLIN = 1

	WRKFIL='SPL:DESQTY.DAT'
	OPEN(10,O,WRKFIL)
	CHNWRK = 10

	OPNOK = 1
	RETURN
;-----------------------------------------

CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	IF (CHNLIN) CLOSE CHNLIN
	IF (CHNWRK) CLOSE CHNWRK

	RETURN
;-----------------------------------------


;ITEM                             QTY-SOLD
;AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA ZZ,ZZZ,ZZX-
;1234567890123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5         6         7
