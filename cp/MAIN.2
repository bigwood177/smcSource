;SLSRPT.CP
;		MONTHLY SUMMARY REPORT FOR TERRITORIES
;
RECORD	D_STUFF
	D_IN	,D8		;DATE-IN, ANY FORMAT
	D_OUT	,D6		;RETURN VALUE MMDDYY
	D_OUTR	,D8		;RETURN VALUE CCYYMMDD
	D_FMT	,A10		;RETURN VALUE MM/DD/CCYY
	D_SW	,A2		;"99" = DATE CONVERSION ERROR

RECORD	TABLE
	.INCLUDE 'DEF:RD182A.DEF'
RECORD	TERWRK
	.INCLUDE 'DEF:TERWRK.DEF'

RECORD	ORDHDR
	.INCLUDE 'DEF:RD044A.DEF'

RECORD	ORDLIN
	.INCLUDE 'DEF:RD045A.DEF'

RECORD DUCACC
		.INCLUDE 'DEF:RD175A.DEF'

RECORD	CUSMAS
	.INCLUDE 'DEF:RD001A.DEF'
RECORD,X
	.INCLUDE 'DEF:RD001B.DEF'

RECORD	CUSIDX
	.INCLUDE 'DEF:RD002A.DEF'


RECORD LEG1
		,A,	'SALESMAN: '
	LMAN	,A2

RECORD HDR1
		,A40,'-------------------- ITEM --------------'
		,A45,'------------  LAST SL  -------- SALE --------'
RECORD HDR2
		,A40,'CAT  NUMBER          DESCRIPTION        '
		,A45,'                 DATE      QTY       DOLLARS '

RECORD	PRINT
	LEG	,A9,	'NO LEGEND'
	HDR	,A6,	'NO HDR'
	BLANKS	,A15	
	ALPHA	,A15
	LINCNT	,D2,60
	PGCNT	,D3
	PRTCTL	,D2
	LPSW	,D2
	LPARG	,D1
	PRTTYP	,A1
	RPTNUM	,D3
	PLINE	,A132
	SPLFIL	,A14
	TITLE	,A54,	'SALES HISTORY SUMMARY BY CUSTOMER / PRODUCT CAT / ITEM'

RECORD	CHANNEL
	CHNWRK	,D2
	CHN184	,D2	;SLHHDR
	CHN185	,D2	;SLHLIN
	CHN001	,D2	;CUSMAS
	CHN002	,D2	;CUSIDX
	CHN182	,D2	;COPTBL
	CHN175	,D2	;DUCTOR

RECORD	TOTALS
	MANTOT	,D10
	TERTOT	,D10
	CUSTOT	,D10
	CATTOT	,D10

RECORD	VARS
	OPNOK	,D1
	STAT	,D1
	DASH	,A12,	'------------'
	SAVMAN	,D2
	SAVCUS	,D6
	STMAN	,D2
	SAVCAT	,A2
	SAVTER	,A2
	DTMASK	,A8,	'XX/XX/XX'
	NUMASK	,A8,	'ZZZ,ZZX-'
	DLMASK	,A12,	'ZZZZ,ZZZ.XX-'
	WITMNO	,A15
	WQTY	,D6
	WCAT	,A2
	DCAT	,D2
	WDESCR	,A30
	DECMAL	,D18
	EXEPRC	,D8
	TERR_EOF	,D1
	ENTRY	,A30
	INXCTL	,D1
	CNGCTL	,D1
	WHATNO	,D2
	STDAT	,D8
	ENDAT	,D8
	BSEND	,D5
	BSMID	,D5
	SRCCTL	,D1
	KEY	,A6
	READ	,D1
	LOKCTL	,D1,0
	WRITE	,D1,1
	FIND	,D1,2
	STORE	,D1,2
	DELETE	,D1,3
	SWITCH	,D1
	V	,D1
PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'SALES BY TERRITORY/CATAGORY REPORT',1)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO ENDOFF
DISPLA,
	XCALL OUTPT (1,1,2,'SALES BY TERRITORY/CATAGORY REPORT',1)
	XCALL OUTPT (4,5,0,'START DATE:',1)
	XCALL OUTPT (5,5,0,'END   DATE:',1)
	XCALL OUTPT (6,5,0,'SALESMAN:',1)

STDAT,
	XCALL INPUT (4,18,08,00,'DE',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	STDAT = ENTRY(1,8)
	GOTO (ANYCNG),CNGCTL
ENDAT,
	XCALL INPUT (5,18,08,00,'DE',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	ENDAT = ENTRY(1,8)
	GOTO (ANYCNG),CNGCTL
STMAN,
	XCALL INPUT (6,18,02,00,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	STMAN = ENTRY(1,2)
ANYCNG,
	XCALL ANYCN(CNGCTL, WHATNO)
	GOTO (PROCES, CNGBR),CNGCTL + 1
CNGBR,
	GOTO (STDAT, ENDAT, STMAN), WHATNO
	GOTO ANYCNG
PROCES,

	XCALL OUTPT (2,1,2,'Build work file ...',1)

	FIND (CHN184, ORDHDR, STDAT, KRF=5) [ERR = LOOP]
LOOP,
	XCALL IOS (CHN184, ORDHDR, READ, LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF
	IF (OKEYDT.LT.STDAT) GOTO LOOP
	IF (OKEYDT.GT.ENDAT) GOTO EOF

	CALL GET_TERRITORY

	CLEAR TERWRK
	T_TERR = TERR
	T_CUST = OCUSNO
	T_DATE = OKEYDT

	CALL LINES
	CALL DUCT

	GOTO LOOP
EOF,
	LPSW = 1		; PRINT, SPOOL OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW .EQ. 0) GOTO ENDOFF

	CALL PRINT_REPORT
	CALL NEWMAN

	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	GOTO DISPLA
;======================================================
;======================================================
PRINT_REPORT,
	SAVCUS = -1
	SAVCAT = '-1'
	SAVTER = '-1'
	SAVMAN = -1
	CLEAR MANTOT, TERTOT, CUSTOT, CATTOT

	CLEAR TBL_KEY
	TBLCOD = 'TR'
	FIND (CHN182, TABLE, TBL_KEY) [ERR=PR_LOOP]
PR_LOOP,
	XCALL IOS (CHN182, TABLE, READ, LOKCTL)
	IF (LOKCTL .NE. 0) RETURN
	IF (STMAN.NE.0 .AND. TR_SLSM .NE. STMAN) GOTO PR_LOOP
	IF (TR_SLSM .NE. SAVMAN)	CALL NEWMAN
	IF (TR_TERR .NE. SAVTER)	CALL NEWTER

	FIND (CHNWRK, TERWRK, TR_TERR) [ERR=D_LOOP]

D_LOOP,	;; Print all detail for current territory
	XCALL IOS (CHNWRK, TERWRK, READ, LOKCTL)
	IF (LOKCTL .NE. 0) GOTO DL_EOF
	IF (T_TERR .NE. SAVTER) GOTO DL_EOF

	IF (T_CUST .NE. SAVCUS) CALL NEWCUS
	IF (T_CAT  .NE. SAVCAT) CALL NEWCAT	
	PLINE (2,3) = T_CAT
	PLINE (6,20) = T_ITEM
	PLINE (22,52) = T_DESC

	XCALL DATE8(T_DATE, D_OUT, D_OUTR, D_FMT, D_SW)
	PLINE (54,63) = D_FMT
	PLINE (64,71) = T_QTY,NUMASK
	PLINE (74,85) = T_AMT,DLMASK
	CALL PRINT
	CATTOT = CATTOT + T_AMT	
	GOTO D_LOOP

DL_EOF,
	GOTO PR_LOOP
;=============================================

NEWMAN,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	; Grand totals for salesman
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CALL NEWTER
	IF (SAVMAN .EQ. -1) GOTO OUTMAN
	PLINE (74,85) = DASH
	CALL PRINT
	PLINE (49,70) = 'SALESMAN XX SUBTOTAL: '
	PLINE (58,59) = SAVMAN, 'XX'
	PLINE (74,85) = MANTOT, DLMASK
OUTMAN,
	CLEAR MANTOT
	SAVMAN = TR_SLSM
	LMAN = TR_SLSM ,'ZX'
	LINCNT = 66
	RETURN
;----------------------------------------

NEWTER,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CALL NEWCUS
	IF (SAVTER .EQ. '-1') GOTO OUTTER
	MANTOT = MANTOT + TERTOT
	PLINE (74,85) = DASH
	CALL PRINT
	PLINE (48,70) = 'TERRITORY XX SUBTOTAL: '
	PLINE (58,59) = SAVTER
	PLINE (74,85) = TERTOT, DLMASK
	CALL PRINT
	CALL LINFD	
OUTTER,
	CLEAR TERTOT
	SAVTER = TR_TERR
	LINCNT = 66
	RETURN
;----------------------------------------

NEWCUS,;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CALL NEWCAT
	IF (SAVCUS .EQ. -1) GOTO OUTCUS
	TERTOT = TERTOT + CUSTOT
	PLINE (74,85) = DASH
	CALL PRINT
	PLINE (46,70) = 'CUSTOMER XXXXXX SUBTOTAL: '
	PLINE (55,60) = SAVCUS,	'XXXXXX'
	PLINE (74,85) = CUSTOT, DLMASK
	CALL PRINT
OUTCUS,
	CLEAR CUSTOT
	SAVCUS = T_CUST
	OCUSNO = SAVCUS
	RETURN
;----------------------------------------

NEWCAT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (SAVCAT .EQ. '-1') GOTO OUTCAT
	CUSTOT = CUSTOT + CATTOT
	PLINE (74,85) = DASH
	CALL PRINT
	PLINE (41,70) = 'PRODUCT CATEGORY XX SUBTOTAL: '
	PLINE (58,59) = SAVCAT
	PLINE (74,85) = CATTOT,DLMASK
	CALL PRINT
	CALL LINFD
OUTCAT,
	CLEAR CATTOT
	SAVCAT = T_CAT
	RETURN
;----------------------------------------


LINES,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; get line-items, write work records
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	FIND (CHN185, ORDLIN, OORDNO) [ERR = LINLOP]
LINLOP,
	XCALL IOS (CHN185, ORDLIN, READ, LOKCTL)
	IF (LOKCTL .NE. 0) RETURN
	IF (LORDNO .NE. OORDNO) RETURN
	IF (LQTYSH .EQ. 0) GOTO LINLOP
	IF (LPRICE .EQ. 0) GOTO LINLOP

	WITMNO = LITMNO
	WDESCR = LDESCR
	WCAT = LPRDCD
	WQTY = LQTYSH

	DECMAL = ((LQTYSH*LPRICE) - ((LDISC*LQTYSH*LPRICE)#2))#1
	EXEPRC = DECMAL - ((DECMAL*LODISC)#2)

	CALL UPDSLH

	GOTO LINLOP
;-------------------------------------------------------

DUCT,
	FIND (CHN175, DUCACC, OORDNO) [ERR=DUCTLP]
DUCTLP,
	LOKCTL = 1
	XCALL IOS (6,DUCACC,READ,LOKCTL)
	IF (LOKCTL.NE.0.OR.DUCTOR.NE.OORDNO) RETURN

	WITMNO = 'DUCT'
	WCAT = '11'
	WQTY = POUNDS
	EXEPRC = ( POUNDS * GPRICE ) # 1
	CALL UPDSLH
LINER,
	IF (LINER.NE.4)
	BEGIN
	  WITMNO =
	  WITMNO(1,5) = 'LINER'
	  WITMNO(6,6) = LINER,'X'
	  WQTY = SQFLIN
	  EXEPRC = (SQFLIN * LINPRC) # 1
	  CALL UPDSLH
	END

	IF (ACC.EQ.2) GOTO DUCTLP

A1,
	IF (SLIPS.NE.0)
	BEGIN
	  WITMNO = SLPITM
	  WQTY = SLIPS
	  EXEPRC = (SLIPS * SLPPRC) # 1
	  CALL UPDSLH
	END
A2,
	IF (DRIVES.NE.0)
	BEGIN
	  WITMNO = DRVITM
	  WQTY = DRIVES
	  EXEPRC = (DRIVES * DRVPRC) # 1
	  CALL UPDSLH
	END
A3,
	IF (TCORN.NE.0)
	BEGIN
	  WITMNO = CORITM
	  WQTY = TCORN
	  EXEPRC = (TCORN * CORPRC) # 1
	  CALL UPDSLH
	END
A4,
	IF (TNBQTY.NE.0)
	BEGIN
	  WITMNO = TNBITM
	  WQTY = TNBQTY
	  EXEPRC = (TNBQTY * TNBPRC) # 1
	  CALL UPDSLH
	END
A5,
	IF (TGAQTY.NE.0)
	BEGIN
	  WITMNO = TGAITM
	  WQTY = TGAQTY
	  EXEPRC = (TGAQTY * TGAPRC) # 1
	  CALL UPDSLH
	END
A6,
	IF (TCLQTY.NE.0)
	BEGIN
	  WITMNO = TCLITM
	  WQTY = TCLQTY
	  EXEPRC = (TCLQTY * TCLPRC) # 1
	  CALL UPDSLH
	END
A7,
	IF (TBAQTY.NE.0)
	BEGIN
	  WITMNO = TBAITM
	  WQTY = TBAQTY
	  EXEPRC = (TBAQTY * TBAPRC) # 1
	  CALL UPDSLH
	END
A8,
	IF (TBNQTY.NE.0)
	BEGIN
	  WITMNO = TBNITM
	  WQTY = TBNQTY
	  EXEPRC = (TBNQTY * TBNPRC) # 1
	  CALL UPDSLH
	END

	GOTO DUCTLP

	RETURN
;-------------------------------------------------------

GET_TERRITORY,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	KEY = OCUSNO, 'XXXXXX'
	XCALL SERCH (CHN002,CUSIDX,KEY,1,6,BSEND,BSMID,SRCCTL,4,7,11,0,0,0,0)
	IF (SRCCTL .EQ. 0 .AND. IRC001.NE.0)
	THEN	BEGIN
		LOKCTL = 1
		XCALL IO (CHN001,CUSMAS,IRC001,READ,LOKCTL)
		END
	ELSE	TERR = '??'
	RETURN
;-------------------------------------------------------

UPDSLH,	;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; write slshst record
	;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (EXEPRC .EQ. 0) RETURN

	XCALL ISIO (CHNWRK, TERWRK, TERKEY, READ, LOKCTL)
	IF (LOKCTL .EQ. 0) 
		BEGIN
		T_QTY = T_QTY + WQTY
		T_AMT = T_AMT + EXEPRC
		XCALL ISIO (CHNWRK, TERWRK, TERKEY, WRITE, LOKCTL)
		RETURN
		END

	T_TERR = TERR
	T_CUST = OCUSNO
	T_DATE = OKEYDT

	CLEAR DCAT
	ONERROR NOT_NUM
	DCAT = WCAT
NOT_NUM,
	OFFERROR
	T_CAT = DCAT,	'XX'

	T_ITEM = WITMNO
	T_DESC = WDESCR
	T_QTY = WQTY
	T_AMT = EXEPRC

	XCALL ISIO (CHNWRK, TERWRK, TERKEY, STORE, LOKCTL)	

	RETURN
;-------------------------------------------------------

PRINT,
;;;	IF (RESTART) PLINE =
;;;	IF (RESTART) RETURN
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,HDR,
&			LEG1,LEG,LEG,0,0,132,1,LPSW,RPTNUM,PRTTYP)
	RETURN
LINFD,
;;;	IF (RESTART) RETURN
	XCALL LINFD (1)
	INCR LINCNT
	RETURN
;-------------------------------------------------------

			
ENDOFF,
	CALL CLOSE
	STOP

OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (1,'I',001, SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN001 = 1

	XCALL FILES (2,'I', 002, SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN002 = 2

	XCALL FILES (4, 'SI', 184, SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN184 = 4

	XCALL FILES (5, 'SI', 185, SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN185  = 5

	XCALL FILES (6, 'SI', 182, SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN182 = 6

	XCALL FILES (7, 'SI', 175, SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN175 = 7

	CHNWRK = 10
	XCALL ISCLR(CHNWRK, 'SMC:TERWRK.ISM' ,STAT)
	IF (STAT .NE. 0)
		BEGIN
		XCALL MESAG ("CAN'T CLEAR TERWRK.ISM",1)
		RETURN
		END

	OPEN (10, SU, 'SMC:TERWRK')

	LOKCTL = 1
	XCALL IO (CHN001, CUSMAS, 1, READ, LOKCTL)
	BSEND = ORG001

	OPNOK = 1
	RETURN
;--------------------------------------

CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (CHN001) XCALL FILES (CHN001, 'I', 001, 4)
	IF (CHN002) XCALL FILES (CHN002, 'I', 002, 4)
	IF (CHN182) XCALL FILES (CHN182, 'SI',182, 4)
	IF (CHN184) XCALL FILES (CHN184, 'SI',184, 4)
	IF (CHN185) XCALL FILES (CHN185, 'SI',185, 4)
	IF (CHNWRK) CLOSE CHNWRK

	RETURN
;--------------------------------------
	
