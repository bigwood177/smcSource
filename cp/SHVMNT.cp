;  SHVMNT / COP
;
;
RECORD SHPVIA
		.INCLUDE 'DEF:RD172A.DEF'

RECORD ,X
		.INCLUDE 'DEF:RD172C.DEF'

RECORD SHVCTL	
		.INCLUDE 'DEF:RD172B.DEF'

RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD SRTMSG
	PRGNAM	,A9,	'CP:SHVCNT'
	RCNT	,D5
	OCNT	,D5
RECORD
	NXTPGM	,A9
	SRTPGM	,A9,	'CP:SRTSHV'
	ORGPGM	,A9,	'CP:ORGSHV'
	PRTPGM	,A9,	'CP:SHVPRT'
RECORD
	ALPHA   ,A2
	BS172   ,D5
	BSEND	,D5
	BSMID	,D5
	CNGCTL	,D1
	ENTRY	,A15
	INXCTL	,D1
	KEY     ,A4
	LOKCTL	,D1
	OPNFLG  ,D1
	PAYDEP  ,D2
	READ	,D1	,0
	RECCNT  ,D5
	SELECT  ,D1
	SORT    ,A1
	SRCCTL	,D1
	SWITCH	,D1
	TMPCOD	,A1
	V       ,D1
	WHATNO  ,D2
	WRITE	,D1	,1
	WRKDEP  ,A2
	WRKSAV  ,A2
PROC
	XCALL TERID(V)
MENU,
	XCALL OUTPT (1,1,2,'SHIP VIA CODE FILE MAINTENANCE',V)
	XCALL OUTPT (4,20,0,'PLEASE SELECT APPLICATION',V)
	XCALL OUTPT (5,25,0,'1. ADD SHIP VIA CODES',V)
	XCALL OUTPT (6,25,0,'2. CHANGE/INQUIRE SHIP VIA CODES',V)
	XCALL OUTPT (7,25,0,'3. DELETE SHIP VIA CODES',V)
	XCALL OUTPT (8,25,0,'4. PRINT SHIP VIA CODES LIST',V)
	CTL = '08,47,01,01,#E'
	CALL INPUT
	GO TO (MENU,ENDOFF), INXCTL
	SELECT = ENTRY
	IF (SELECT.LT.1 .OR. SELECT.GT.4) GO TO MENU
	IF (SELECT.EQ.4) GO TO ASKSRT
	IF (OPNFLG.EQ.0) GO TO OPEN
	IF (SELECT.EQ.1 .OR. SELECT.EQ.2 .OR. SELECT.EQ.3) GO TO DISPLA
OPEN,
	XCALL WATE (3,V)
	SWITCH = 3
	XCALL FILES (1,'U',172,SWITCH)		;FILE 172 SHPVIA
	IF (SWITCH.EQ.9) GO TO INU172
	SWITCH = 5
	XCALL FILES (1,'U',172,SWITCH)		;FILE 172 SHPVIA
	IF (SWITCH.EQ.9) GO TO CLO172
	LOKCTL = 0
	XCALL IO (1,SHVCTL,1,READ,LOKCTL)
	RECCNT = REC172
	BS172 = ORG172
	UNLOCK 1
	OPNFLG = 1
	GO TO DISPLA
;**********************************************************************
CLO172,
	XCALL FILES (1,'U',172,4)
INU172,
	XCALL PGCHN ('CP:SPCFUN',1)
;**********************************************************************
ASKSRT,
	XCALL OUTPT (1,1,2,'SHIP VIA CODE FILE MAINTENANCE',1)
	XCALL OUTPT (2,1,0,'PRINT SHIP VIA CODE LIST',1)
	XCALL OUTPT (12,20,0,'SORT BEFORE PRINTING ?',1)
SRTQRY,
	CTL = '12,44,01,00,A '
	CALL INPUT
	IF (ENTRY.NE.'Y' .AND. ENTRY.NE.'N') ENTRY = 'N'
	XCALL OUTPT (6,44,0,ENTRY,V)
	SORT = ENTRY
ENDOFF,
	XCALL WATE(4,V)
	XCALL FILES (1,'U',172,4)
	IF (SORT.EQ.'Y') GO TO ORGNIZ
	IF (DEL172.GE.95.AND.SELECT.NE.4) GO TO ORGNIZ
	IF (REC172-ORG172.GE.50.AND.SELECT.NE.4) GO TO ORGNIZ
PRTEND,
	IF (SELECT.EQ.4) XCALL PGCHN ('CP:SHVPRT',1)
	XCALL PGCHN ('CP:SPCFUN',1)
ORGNIZ,
	NXTPGM = 'CP:SPCFUN'
	IF (SELECT.EQ.4) NXTPGM = 'CP:SHVPRT'
	SWITCH = 2
	XCALL FILES (1,'U',172,SWITCH)
	IF (SWITCH.EQ.9) XCALL PGCHN (NXTPGM,0)
	LOKCTL = 1
	XCALL IO (1,SHVCTL,1,READ,LOKCTL)
	UNLOCK 1
	CLOSE 1
	RCNT = REC172
	OCNT = ORG172
	SWITCH = 5
	XCALL SNMSG (' ',SWITCH)
	IF ((RCNT-OCNT.GE.50) .OR. SORT.EQ.'Y') XCALL SNMSG (SRTMSG,2)
	IF (DEL172.GE.95) XCALL SNMSG (ORGPGM,2)
	IF (SELECT.EQ.4) XCALL SNMSG (PRTPGM,2)
	IF ((RCNT-OCNT.GE.50) .OR. SORT.EQ.'Y') XCALL PGCHN (SRTPGM,0)
	XCALL PGCHN (ORGPGM,0)
;**********************************************************************
DISPLA,
	UNLOCK 1
	CNGCTL =
	XCALL OUTPT (1,1,2,'SHIP VIA CODE FILE MAINTENANCE',1)
	IF (SELECT.EQ.1) XCALL OUTPT (2,1,1,'ADD',1)
	IF (SELECT.EQ.1 .AND. REC172.GE.MAX172) GO TO FULL
	IF (SELECT.EQ.2) XCALL OUTPT (2,1,1,'CHANGE/INQUIRE',1)
	IF (SELECT.EQ.2 .OR. SELECT.EQ.3) XCALL OUTPT (11,18,1,'*',1)
	IF (SELECT.EQ.3) XCALL OUTPT (2,1,1,'DELETE',1)
	XCALL OUTPT (11,20,0,'1.  SHIP VIA CODE',1)
	XCALL OUTPT (13,20,0,'2.  DESCRIPTION',1)

WORK,
	IF (CNGCTL.EQ.1 .AND. SELECT.NE.1) GO TO BADCNG
	CTL = '11,38,01,01,AE'
	CALL INPUT
	GO TO (DISPLA,MENU), INXCTL
	TMPCOD = ENTRY
	BSEND = BS172
	SRCCTL = 2
	XCALL SERCH (1,SHPVIA,ENTRY,1,1,BSEND,BSMID,SRCCTL,1,2,7,0,0,0,0)
	IF (SRCCTL.EQ.0.AND.SELECT.EQ.1) XCALL MESAG ('RECORD ALREADY EXISTS',1)
	IF (SRCCTL.EQ.0 .AND. SELECT.EQ.1) GO TO WORK
	IF (SRCCTL.EQ.1 .AND. SELECT.NE.1) XCALL MESAG ('RECORD NOT FOUND',1)
	IF (SRCCTL.EQ.1 .AND. SELECT.NE.1) GO TO WORK
	WRKSAV = BSMID
	IF (SELECT.EQ.2 .OR. SELECT.EQ.3) GO TO DSPREC
DESC,
	SHPVCD = TMPCOD
	CTL = '13,38,15,00,A '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	SHPVDS = ENTRY
	XCALL OUTPT (13,38,0,SHPVDS,1)
ANYCNG,
	XCALL ANYCN (CNGCTL,WHATNO)
	GO TO (ADD,CNGBR,DISPLA), CNGCTL + 1
CNGBR,
	GO TO (DESC), WHATNO-1
	CNGCTL = 3
	GO TO ANYCNG
BADCNG,
	XCALL MESAG ('CAN''T CHANGE KEY FIELD',1)
	GO TO ANYCNG
;**********************************************************************
DSPREC,
	LOKCTL = 1
	XCALL IO (1,SHPVIA,WRKSAV,READ,LOKCTL)
	XCALL OUTPT (11,38,0,SHPVCD,1)
	XCALL OUTPT (13,38,0,SHPVDS,1)
	IF (SELECT.EQ.2) GO TO ANYCNG
	GO TO DELETE
ADD,
	IF (SELECT.EQ.2) GO TO CHANGE
	LOKCTL = 1
	XCALL IO (1,SHVCTL,1,READ,LOKCTL)
	INCR REC172
	RECCNT = REC172
	LOKCTL = 1
	XCALL IO (1,SHVCTL,1,WRITE,LOKCTL)
	LOKCTL = 1
	XCALL IO (1,SHPVIA,RECCNT,WRITE,LOKCTL)
	SHPVIA =
	GO TO DISPLA
	
FULL,
	UNLOCK 1
	XCALL OUTPT (23,1,0,'SHPVIA FILE IS NOW FULL. USE EXPAND',1)
	XCALL MESAG ('OPTION IN SYSTEM FUNCTIONS',2)
	GO TO MENU
CHANGE,
	IF (SELECT.EQ.3) GO TO DELETE
	LOKCTL = 1
	XCALL IO (1,SHPVIA,WRKSAV,WRITE,LOKCTL)
	GO TO DISPLA
DELETE,
	XCALL OUTPT (24,1,1,'OKAY TO DELETE  ?',1)
	CTL = '24,20,01,01,YN'
	CALL INPUT
	IF (INXCTL.NE.1) GO TO DISPLA
	XCALL WATE(3,V)
	LOKCTL = 1
	XCALL IO (1,SHVCTL,1,READ,LOKCTL)
	INCR DEL172
	LOKCTL = 1
	XCALL IO (1,SHVCTL,1,WRITE,LOKCTL)
	DFL172 = ']]]DEL'
	SHPVIA(8,18) =
	LOKCTL = 1
	XCALL IO (1,SHPVIA,WRKSAV,WRITE,LOKCTL)
	XCALL MESAG ('RECORD DELETED',2)
	IF (DEL172.GE.95) GO TO PURGE
	GO TO DISPLA
PURGE,
	XCALL MESAG ('DELETED RECORDS MUST BE PURGED', 1)
	XCALL WATE(4,V)
	GO TO ENDOFF
;**********************************************************************
INPUT,
	XCALL INPUT(ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,1)
	RETURN
END
