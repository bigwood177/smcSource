;DSUMNT.CP
; LOGIN: USER SECURITY
;
;
RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'


RECORD	CHANNEL
	CHN182	,D2


RECORD	PRINT
	TITLE	,A*,	'LOGIN: USER SECURITY'
	HD	,A6,	'NO HDR'
	LG	,A9,	'NO LEGEND'
	PLINE	,A80
	PRNTON	,D1
	LINCNT	,D2,60
	PGCNT	,D6
	LPSW	,D2
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	PRTCTL	,D3,080
	LPARG	,D1
	PRNTSW	,D1
	PRTCTR	,D1


RECORD	HD1
		,A*,	'LOGIN           DESC'

RECORD	VARS
	ALPHA	,A15
	A1	,A1
	OPNOK	,D1
	BLANKS	,A15
	LOGIN	,A15
	NAME	,A20
	GRP		,D1
	DEPT	,A1
	PGM	,D1
	ROW	,D2
	ENTRY	,A30
	INXCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	STORE	,D1,2
	DELETE	,D1,3
	LOKCTL	,D1
	WHATNO	,D2
	SELECT	,D1
	CNGCTL	,D1
	I	,D3
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,TITLE,1)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO CLOSE

MENU,
	XCALL OUTPT (1,1,2,TITLE,1)
	XCALL OUTPT (3,9,0,'PLEASE SELECT APPLICATION',1)
	XCALL OUTPT (5,15,0,'1. TABLE MAINTENANCE',1)
	XCALL OUTPT (6,15,0,'2. PRINT TABLE',1)
MINPUT,
	XCALL INPUT (3,36,1,1,'#E',ENTRY,INXCTL,1)
	GOTO (MINPUT,ENDOFF), INXCTL
	PGM = ENTRY(1,1)
	GOTO (DISPLA,PRINT_TABLE),PGM
	GOTO MINPUT

DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,TITLE,1)
	XCALL OUTPT ( 4,4,0,'1. USER NAME',1)
	XCALL OUTPT ( 6,4,0,'2. FULL NAME',1)
	XCALL OUTPT ( 8,4,0,'3. GROUP',1)
	CALL DISPLAY_GROUPS
	
LOGIN,
	XCALL INPUT (4,24,15,00,'AE',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	LOGIN = ENTRY(1,15)
	CLEAR TBL_KEY
	TBLCOD = 'US'
	D5_LOGIN = LOGIN
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .EQ. 0)
		BEGIN
		SELECT = 2
		CALL DSPREC
		GOTO ANYCNG
		END

	SELECT = 1			;ADD MODE
	CLEAR COPTBL
	GOTO (ANYCNG),CNGCTL
NAME,
	XCALL INPUT (6,24,20,00,'AE',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	NAME = ENTRY(1,20)
	GOTO (ANYCNG),CNGCTL
	
GRP,
	XCALL INPUT (8,24,01,01,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	GRP = ENTRY(1,1)
	IF (GRP.LT.1 .OR. GRP.GT.5) GOTO GRP
	
ANYCNG,
	XCALL OUTPT (24,1,1,'FIELD # TO CHANGE            <TAB> = DELETE',1)
	XCALL INPUT (24,20,02,00,'#T',ENTRY,INXCTL,1)
	IF (INXCTL .EQ. 3)
		BEGIN
		XCALL OUTPT (24,1,1,'DELETE, ARE YOU SURE ?',1)
		XCALL INPUT (24,24,01,01,'YN',ENTRY,INXCTL,1)
		GOTO (ANYCNG),INXCTL-1
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,DELETE,LOKCTL)
		GOTO DISPLA
		END

	WHATNO = ENTRY(1,2)
	IF (WHATNO .EQ. 0)
	THEN 	CLEAR CNGCTL
	ELSE 	CNGCTL = 1

	GOTO (PROCES,CNGBR),CNGCTL+1
CNGBR,
	GOTO (LOGIN, NAME, GRP),WHATNO
	GOTO ANYCNG

PROCES,
	CASE SELECT OF
	BEGINCASE
	1:	BEGIN
		CLEAR COPTBL
		TBLCOD = 'US'
		US_LOGIN = LOGIN
		US_NAME = NAME
		US_GRP = GRP
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,STORE,LOKCTL)
		END
	2:	BEGIN
		CLEAR TBL_KEY
		TBL_KEY(1,2) = 'US'
		US_LOGIN = LOGIN
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
		US_NAME = NAME
		US_GRP = GRP
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,WRITE,LOKCTL)
		END
	ENDCASE
	GOTO DISPLA

ENDOFF,
	CALL CLOSE
;;;	XCALL PGCHN ('CP:SECMNU',1)
	xcall flags (7000000)
	STOP


DSPREC,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; DISPLAY RECORD
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	NAME = US_NAME
	GRP = US_GRP

	XCALL OUTPT (4,24,1,US_LOGIN,1)
	XCALL OUTPT (6,24,1,US_NAME,1)
	A1 = US_GRP, 'X'
	XCALL OUTPT (8,24,1,A1,1)
	CALL DISPLAY_GROUPS

	RETURN
;-----------------------------------

DISPLAY_GROUPS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL OUTPT ( 8,48,0,'1 = ADMINISTRATIVE',1)
	XCALL OUTPT ( 9,48,0,'2 = INVENTORY',1)
	XCALL OUTPT (10,48,0,'3 = POWER USER',1)
	XCALL OUTPT (11,48,0,'4 = SALES',1)
	XCALL OUTPT (12,48,0,'5 = PLANT',1)
	RETURN
;-----------------------------------------------------


;===================================================================
PRINT_TABLE,
;===================================================================
	LINCNT = 66
	PGCNT  = 0
	

P_PRINT,
	CLEAR TBL_KEY
	TBLCOD = 'US'
	FIND (CHN182,COPTBL,TBL_KEY)[ERR=PLOOP]

PLOOP,
	XCALL IOS (CHN182,COPTBL,READ,LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF
	IF (TBLCOD .NE. 'US') GOTO EOF

	PLINE (1,15) = US_LOGIN
	PLINE (17,36) = US_NAME
	USING US_GRP SELECT
	(1),	PLINE (38,63) = 'ADMINISTRATIVE'
	(2),	PLINE (38,63) = 'INVENTORY'
	(3),	PLINE (38,63) = 'POWER USER'
	(4),	PLINE (38,63) = 'SALES'
	(5),	PLINE (38,63) = 'PLANT'
	(),	PLINE (38,63) = '???'
	ENDUSING

	CALL PRINT
	GOTO PLOOP

EOF,
	IF (PRNTON.EQ.1)  XCALL LPOFF(LPSW,SPLFIL,PGCNT)
	PRNTON  = 0

PDONE,
	GOTO MENU

PRINT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (PRNTON .EQ. 0) CALL PRNTON
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HD1,HD,HD
&		,LG,LG,LG,0,080,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;-------------------------------------------------------------
PRNTON,
	SPLFIL (5,6) = 'EF'
	LPSW = 1		;PRINT,SPOOL, OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTON = 1
	RETURN
;-------------------------------------------------------------
;===================================================================


OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (1,'SU',182,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN182 = 1

	OPNOK = 1
	RETURN
;----------------------------------------------------

CLOSE,
	CLOSE CHN182
	RETURN
;----------------------------------------------------
	
	

