;  IMFPRT / COP	isam

;	LPON.CP, LPOFF.CP HP8100 MOD SSQ: 11-4-99
;	LINK W/ LPON.CP, LPOFF.CP
;	10/09/02 ssq:	don't print prdcats "zz" or "ww"
;
;		PRINT OUT ITEM MASTER FILE
;		BASE DATA
;

RECORD ITMMAS		;
		.INCLUDE 'DEF:RD041A.def'
RECORD ,X		;
		.INCLUDE 'DEF:RD041B.def'

;;;RECORD ITMIDX		;
;;;		.INCLUDE 'DEF:RD042A.DEF'

RECORD TITLE
		,A38,	'ITEM MASTER FILE PRINT-OUT - BASE DATA'
RECORD LEGND1
		,A24,	'   BO (BACK ORDER) CODE:'
		,A31,	'   0 = ITEM MAY BE BACK ORDERED'
		,A37,	'     1 = ITEM MAY NOT BE BACK ORDERED'
RECORD LEGND2
		,A21,	'   TX (TAXABLE) CODE:'
		,A22,	'   Y = ITEM IS TAXABLE'
		,A26,	'   N = ITEM IS NOT TAXABLE'
RECORD HDR1
		,A50
		,A2,	'US'
		,A73
		,A2,	'UN'
		,A4
		,A1,	'T'
RECORD HDR2
		,A46
		,A3,	'PRD'
		,A1
		,A2,	'DF'
		,A5
		,A19,	'----QUANTITIES-----'
		,A1
		,A5,	'ORDER'
		,A1
		,A5,	'REORD'
		,A1
		,A3,	'BIN'
		,A1
		,A3,	'PRC'
		,A29
		,A2,	'OF'
		,A1
		,A2,	'BO'
		,A1
		,A1,	'A'
RECORD HDR3
		,A11,	'ITEM NUMBER'
		,A5
		,A11,	'DESCRIPTION'
		,A19
		,A3,	'CAT'
		,A1
		,A2,	'CD'
		,A1
		,A3,	'LOC'
		,A1
		,A4,	'ONHD'
		,A3
		,A5,	'ALLOC'
		,A2
		,A5,	'ONORD'
		,A1
		,A5,	'UP-TO'
		,A1
		,A5,	'LEVEL'
		,A1
		,A3,	'NO.'
		,A1
		,A3,	'CDE'
		,A3
		,A5,	'PRICE'
		,A4
		,A3,	'AVG'
		,A1
		,A4,	'COST'
		,A2
		,A6,	'WEIGHT'
		,A1
		,A2,	'MS'
		,A1
		,A2,	'CD'
		,A1
		,A1,	'X'
RECORD
	SPLFIL	,A14

record	dis
	ii	,d6

RECORD	VARS
	CFG_ITEM,D1	;1=INCLUDE CFG'D ITEMS
	vanila	,d1	;1=print vanilla only
	ZRO	,A5,	'00000'
	A15	,A15
	CNGCTL	,D1
	WHATNO	,D2
	ALLCAT	,D1
	RECNO	,D5
	ITMCNT	,D5
	MASK2	,A11,	'ZZZ,ZZZ.XX-'
	MSK	,A5,	'ZZZZX'
	MASK1	,A7,	'ZZZZZX-'
	KEY   	,A15
	BSEND	,D5
	BSMID	,D5
	SRCCTL	,D1
	LOCTNS	,D2
	LINCNT	,D2,	60
	PGCNT	,D3
	PLINE	,A132
	PRTCTL	,D3
	ENTRY	,A30
	INXCTL	,D1
	SELECT	,D1
	TOTONH	,D6
	TOTCOM	,D6
	STRTNO	,A15
	ENDNO	,A15
	STRCAT	,A2
	ENDCAT	,A2
	STRWGT	,D6
	ENDWGT	,D6
	STFLG	,D1
	STXCTL	,D1
	LOCCTR	,D2,	02
	BLANKS	,A15
	SWITCH	,D1,	1
	V	,D1
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	LPSW	,D2
	LPARG	,D1
	RPTNUM	,D3
	PRTTYP	,A1
	JUSTIF	,D1
	LPFLAG	,D1
	FILFLG	,D1
	ORG41	,D5
PROC 
	XCALL TERID (V)
MENU,
	XCALL OUTPT (1,1,0,'ITEM MASTER FILE MAINTENANCE',V)
	XCALL OUTPT (2,1,2,'PRINT OUT',1)


;***********************************************************
;	SECTION TO PRINT OUT BASE ITEM MASTER DATA
;***********************************************************

BEGIN,
	STRTNO =
	ENDNO =
	STRCAT =
	ENDCAT =
	STRWGT =
	ENDWGT =

	XCALL OUTPT (1,1,2,'ITEM MASTER FILE MAINTENANCE',V)
	XCALL OUTPT (2,1,0,'PRINT OUT - BASE ITEM MASTER DATA',1)

	IF (FILFLG.EQ.1) GO TO ITEMS
	SWITCH = 1
	XCALL FILES (12,'SI',041,SWITCH)
	IF (SWITCH.EQ.9) GO TO END

	read (12, itmmas, ^first)

	JUSTIF = JSTIFY
	XCALL FILES (12,'SI',041,4)
ITEMS,
	CLEAR CNGCTL
	XCALL OUTPT (06,4,0,'1. STARTING ITEM #',1)
	XCALL OUTPT (07,4,0,'2. ENDING   ITEM #',1)
	XCALL OUTPT (09,4,0,'3. STARTING PROD CAT',1)
	XCALL OUTPT (10,4,0,'4. ENDING   PROD CAT',1)
	XCALL OUTPT (12,4,0,'5. STARTING PROD WGT',1)
	XCALL OUTPT (13,4,0,'6. ENDING   PROD WGT',1)
	XCALL OUTPT (15,4,0,'7. VANILLA ONLY <N>?',1)
	XCALL OUTPT (17,4,0,'8. INCLUDE CFG ITEMS <N>?',1)

;                         123456789012345678901234567890
;                                  1         2         3
STRTNO,
	XCALL INPUT (6,25,15,00,'AE',ENTRY,INXCTL,1)
	GOTO (ITEMS,END),INXCTL
	STRTNO = ENTRY(1,15)
	IF (STRTNO .EQ. A15)
		BEGIN
		ENDNO = 'ZZZZZZZZZZZZZZZ'
		XCALL OUTPT (6,25,1,'ALL',1)
		XCALL OUTPT (7,25,1,' ',1)
		GOTO (ANYCNG),CNGCTL
		GOTO STRCAT
		END
	GOTO (ANYCNG), CNGCTL
ENDNO,
	XCALL INPUT (7,25,15,00,'AE',ENTRY,INXCTL,1)
	GOTO (ITEMS,END),INXCTL
	ENDNO = ENTRY(1,15)
	IF (ENDNO .EQ. A15)
		BEGIN
		ENDNO = STRTNO
		XCALL OUTPT (7,25,1,ENDNO,1)
		END
	GOTO (ANYCNG), CNGCTL
STRCAT,
	ALLCAT = %FALSE
	XCALL INPUT (9,25,02,00,'AE',ENTRY,INXCTL,1)
	GOTO (ITEMS,END),INXCTL
	STRCAT = ENTRY(1,2)
	IF (STRCAT .EQ. A15) 
		BEGIN
		ENDCAT = 'ZZ'
		XCALL OUTPT (9,25,1,'ALL',1)
		XCALL OUTPT (10,25,1,' ',1)
		ALLCAT = %TRUE
		GOTO (ANYCNG), CNGCTL
		GOTO STRWGT
		END
	GOTO (ANYCNG), CNGCTL
ENDCAT,
	XCALL INPUT (10,25,02,00,'AE',ENTRY,INXCTL,1)
	GOTO (ITEMS,END),INXCTL
	ENDCAT = ENTRY(1,2)
	IF (ENDCAT .EQ. A15)
		BEGIN
		ENDCAT = STRCAT
		XCALL OUTPT (10,25,1,ENDCAT,1)
		END
	GOTO (ANYCNG), CNGCTL
STRWGT,
	XCALL INPUT (12,25,06,00,'$ ',ENTRY,INXCTL,1)
	GOTO (ITEMS),INXCTL
	STRWGT = ENTRY(1,6)
	IF (ENTRY(1,6) .EQ. A15)
		BEGIN
		ENDWGT = 999999
		XCALL OUTPT (12,25,1,'ALL',1)
		XCALL OUTPT (13,25,1,' ',1)
		goto (anycng),cngctl
		goto vanila
		END
	GOTO (ANYCNG),CNGCTL

ENDWGT,
	XCALL INPUT (13,25,06,00,'$ ',ENTRY,INXCTL,1)
	GOTO (ITEMS),INXCTL
	ENDWGT = ENTRY(1,6)
	IF (ENDWGT .EQ. 0)
		BEGIN
		ENDWGT = STRWGT
		ENTRY(1,7) = ENDWGT, 'ZZZZ.XX' [LEFT]
		XCALL OUTPT (13,25,1,ENTRY(1,7),1)
		END
	GOTO (ANYCNG),CNGCTL
VANILA,
	XCALL INPUT (15,25,01,00,'YN',ENTRY,INXCTL,1)
	VANILA = INXCTL
	GOTO (ANYCNG),CNGCTL
CFG_ITEM,
	XCALL INPUT (17,31,01,00,'YN',ENTRY,INXCTL,1)
	CFG_ITEM = INXCTL
	

ANYCNG,
	XCALL ANYCN(CNGCTL, WHATNO)
	GOTO (PROCES,CNGBR),CNGCTL+1
CNGBR,
	GOTO (ITEMS,ENDNO,STRCAT,ENDCAT,STRWGT,ENDWGT,VANILA,CFG_ITEM),WHATNO
	GOTO ANYCNG
PROCES,

	IF (FILFLG.EQ.1) GO TO NOOPEN


	SWITCH = 1
	XCALL FILES (1,'SI',41,SWITCH)		;FILE # 41 -- ITMMAS FILE
	IF (SWITCH.EQ.9) GO TO CLOSE
	FILFLG = 1
NOOPEN,

	IF (STRTNO.EQ.ENDNO) GO TO BSERCH
	ITMCNT =
	RECNO =
	read (1, itmmas, ^first) [err=total]
READ,
	incr ii
	if (ii/500*500 .eq. ii) xcall outpt (1,70,1,dis,1)
	INCR RECNO
	LOKCTL = 1
	
	XCALL IOS (1, ITMMAS, READ, LOKCTL)
	if (lokctl .ne. 0) goto total
	IF (ITEMNO.GT.ENDNO) GO TO TOTAL
	IF (ITEMNO.LT.STRTNO) GO TO READ
	IF (PRDCAT.LT.STRCAT) GO TO READ
	IF (PRDCAT.GT.ENDCAT) GO TO READ
	IF (ALLCAT .AND. (PRDCAT.EQ.'ZZ'.OR.PRDCAT.EQ.'WW')) GOTO READ	;10-9-02
	IF (IP_WGT.LT.STRWGT) GOTO READ
	IF (IP_WGT.GT.ENDWGT) GOTO READ
	IF (CFG_ITEM.EQ.2 .AND. ICFG.EQ.1) GOTO READ	;don't want cfg'd items

	IF (VANILA .NE. 1) GOTO RNXT
	IF (IF1.NE.ZRO .OR. IF2.NE.ZRO .OR. IF3.NE.ZRO) GOTO READ

RNXT,
	IF (LINCNT.GE.58) LINCNT = 60
BUFF,
	IF (LPFLAG.EQ.1) GO TO CONTIN
	SPLFIL(5,6) = 'CA'
	LPSW = 1
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO MENU
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	LPFLAG = 1
CONTIN,
	PLINE (1,15) = ITEMNO
	IF (JUSTIF) XCALL LEFTJ (PLINE(1,15),15)
	PLINE (17,46) = DESCR
	PLINE (48,49) = PRDCAT
	PLINE (51,52) = USRDEF
	PLINE (53,63) = QTYONH(1), 'ZZ,ZZZ,ZZX-'
	PLINE (65,70) = QTYCOM(1), 'ZZZZZX'
	PLINE (71,77) = QTYONO(1), 'ZZZZZX-'
	PLINE (78,82) = ORDUPT(1), MSK
	PLINE (84,88) = REOLVL(1), MSK
	PLINE (90,92) = PIKSEQ(1)
	PLINE (94,95) = PRICCD(1)
	PLINE (97,105) = PRICE(1), 'ZZZZZ.XXX'
	PLINE (107,116) = AVGCST,'ZZZZZX.XXX'
	PLINE (118,124) = WEIGHT,'ZZZX.XX'
	PLINE (126,127) = SUOFM
	PLINE (129,129) = BOCODE
	PLINE (132,132) = TXFLAG
	TOTONH = QTYONH(1)
	TOTCOM = QTYCOM(1)
	CALL PRINT
BYLOC,

	IF (IF1.NE.ZRO .OR. IF2.NE.ZRO .OR. IF3.NE.ZRO)
		BEGIN
		PLINE(1,3) = IF1
		PLINE(5,7) = IF2
		PLINE(9,13) = IF3
		END

	PLINE (54,62) = 'SOLD MTD:'
	PLINE (64,71) = QTYMTD, MASK1
	PLINE (85,93) = 'SOLD YTD:'
	PLINE (95,102) = QTYYTD, MASK1
	
	PLINE (104,115) = 'PROD WEIGHT:'
	PLINE (116,122) = IP_WGT, 'ZZZZ.XX'
	PLINE (124,125) = IP_UM
	CALL PRINT
ONELOC,
	IF (LPFLAG.EQ.1) 	XCALL LINFD (1)
	TOTONH =
	TOTCOM =
	INCR LINCNT
	LOCCTR = 02
	IF (STRTNO.EQ.ENDNO) RETURN
	INCR ITMCNT
	GO TO READ
PRINT,
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,HDR3,LEGND1,LEGND2,
&		'NO LEGEND3',0,132,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
BSERCH,
	IF (ENDNO.EQ.BLANKS) GO TO NOFIND
	KEY = ENDNO
	FIND (1, ITMMAS, KEY) [ERR=FND_OK]
FND_OK,
;;;	KEY = ENDNO
;;;	XCALL SERCH (2,ITMIDX,KEY,1,15,BSEND,BSMID,SRCCTL,4,16,20,0,0,0,0)
;;;	GO TO (NOFIND), SRCCTL

	IF (PRDCAT.NE.STRCAT.AND.STRCAT.NE.BLANKS) GO TO NOFND2
	CALL BUFF
	GO TO CLOS
NOFIND,
	XCALL MESAG ('ITEM NOT ON FILE',2)
	GO TO CLOS
NOFND2,
	XCALL MESAG ('NO ITEMS ON FILE MATCHING CRITERIA',2)
	GO TO CLOS
TOTAL,
;;;	IF (IITMNO.EQ.']]]]]]'.AND.ITMCNT.EQ.0) GO TO NOFND2
	IF (ITMCNT.EQ.0) GO TO NOFND2
;;;	IF (ENDNO.NE.'[[[') GO TO CLOS
	IF (LPFLAG.EQ.1) 	XCALL LINFD (2)
;;;	XCALL LINFD (2)
	PLINE (2,7) = ITMCNT, 'ZZ,ZZX'
	PLINE (9,21) = 'ITEMS ON FILE'
	LINCNT = LINCNT + 1
	CALL PRINT
CLOS,
	IF (LPFLAG.EQ.1) XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	PGCNT =
	LINCNT = 61
	LPFLAG =
	IF (STRTNO.EQ.'   '.AND.ENDNO.EQ.'[[['
&		.AND.STRCAT.EQ.'  '.AND.ENDCAT.EQ.'[[') GO TO MENU
	GO TO BEGIN

;********************************************************

CLOSE,
;;;	XCALL FILES (2,'I',42,4)
END,
	IF (FILFLG.EQ.1) CALL CLOSE2
	IF (LPFLAG.EQ.1) XCALL LPOFF (LPSW,SPLFIL,PGCNT)

	STFLG = 1
	XCALL PGCHN ('CP:IMFMNT',STFLG)
;;;	XCALL PGCHN ('itm:IMFMNT',STFLG)

;********************************************************

CLOSE2,
	XCALL FILES (1,'SI',41,4)
;;;	XCALL FILES (2,'I',42,4)
	FILFLG =
	RETURN
END
