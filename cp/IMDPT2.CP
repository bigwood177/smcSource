;imdpt2.cp
; IMDEPT.CP
;  IMFPRT / COP	isam

;	LPON.CP, LPOFF.CP HP8100 MOD SSQ: 11-4-99
;	LINK W/ LPON.CP, LPOFF.CP
;	10/09/02 ssq:	don't print prdcats "zz" or "ww"
;
;		PRINT OUT ITEM MASTER FILE
;		BASE DATA
;

RECORD ITMMAS		;
		.INCLUDE 'DEF:RD041A.DEF'
RECORD ,X		;
		.INCLUDE 'DEF:RD041B.new'

RECORD TITLE
		,A*,	'ITEMS BY DEPARTMENT'

RECORD
	LEG	,A*,	'NO LEGEND'
	HDR	,A*,	'NO HDR'

RECORD	HD1
		,A*,	'PRDUCT CAT: '
	HCAT1	,A2
		,A*,	' THRU '
	HCAT2	,A2
		,A4
		,A*,	'DEPT: '
	HDPT1	,A2
		,A*,	' THRU '
	HDPT2	,A2

RECORD HD2
		,A11,	'ITEM NUMBER'
		,A5
		,A11,	'DESCRIPTION'
		,A19
		,A3,	'CAT'
		,A1
		,A3,	'DPT'
		,A1
		,A3,	'STK'
		,A1
		,A8,	'   PRICE'
		,A1
		,A9,	'  QTY MTD'
		,A1
		,A9,	'  QTY YTD'
		,A1
		,A12,	'   SALES YTD'

RECORD
	SPLFIL	,A14

record	dis
	ii	,d6

record	key
	k_cat	,a2
	k_item	,a15

RECORD	VARS
	A2	,A2
	A15	,A15
	STITM	,A15
	ENITM	,A15
	STCAT	,A2
	ENCAT	,A2
	STDPT	,A2
	ENDPT	,A2
	DEPT	,A2
	STK	,A1
	ZRO	,A5,	'00000'
	ALLCAT	,D1
	RECNO	,D5
	ITMCNT	,D5
	MASK2	,A11,	'ZZZ,ZZZ.XX-'
	MSK	,A5,	'ZZZZX'
	MASK1	,A7,	'ZZZZZX-'
	BSEND	,D5
	BSMID	,D5
	CNGCTL	,D1
	WHATNO	,D2
	SRCCTL	,D1
	LOCTNS	,D2
	LINCNT	,D2,	60
	PGCNT	,D3
	PLINE	,A132
	PRTCTL	,D3
	ENTRY	,A30
	INXCTL	,D1
	SELECT	,D1
	STRTNO	,A15
	ENDNO	,A15
	STRCAT	,A2
	ENDCAT	,A2
	STFLG	,D1
	STXCTL	,D1
	BLANKS	,A15
	SWITCH	,D1,	1
	V	,D1
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	LPSW	,D2
	LPARG	,D1
	RPTNUM	,D3
	PRTTYP	,A1
	JUSTIF	,D1
	LPFLAG	,D1
	FILFLG	,D1
	ORG41	,D5
;
PROC 
	XCALL TERID (V)
MENU,
	XCALL OUTPT (1,1,2,'PRINT ITEMS BY PROD CAT & DEPT',1)


BEGIN,
	XCALL OUTPT (1,1,2,'PRINT ITEMS BY DEPT',1)

	IF (FILFLG.EQ.1) GO TO DISPLA
	SWITCH = 1
	XCALL FILES (12,'SI',041,SWITCH)
	IF (SWITCH.EQ.9) GO TO END

	read (12, itmmas, ^first)

	JUSTIF = JSTIFY
	XCALL FILES (12,'SI',041,4)
DISPLA,
	CLEAR INXCTL
	XCALL OUTPT (4,4,0,'1. START CAT',1)
	XCALL OUTPT (5,4,0,'2. END   CAT',1)

	XCALL OUTPT (7,4,0,'3. START DEPT',1)
	XCALL OUTPT (8,4,0,'4. END   DEPT',1)

STCAT,
	XCALL INPUT (4,18,02,00,'AE',ENTRY,INXCTL,1)
	GOTO (DISPLA, ENDOFF),INXCTL
	STCAT = ENTRY(1,2)
	IF (STCAT .EQ. A2)
		BEGIN
		XCALL OUTPT (4,18,1,'ALL',1)
		XCALL OUTPT (5,18,1,' ',1)
		ENCAT = 'ZZ'
		GOTO (STDPT), CNGCTL + 1
		END
	GOTO (ANYCNG),CNGCTL
ENCAT,
	XCALL INPUT (5,18,02,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	ENCAT = ENTRY(1,2)
	IF (ENCAT .EQ. A2)
		BEGIN
		ENCAT = STCAT
		XCALL OUTPT (5,18,1,ENCAT,1)
		END
	GOTO (ANYCNG),CNGCTL
STDPT,
	XCALL INPUT (7,18,02,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	STDPT = ENTRY(1,2)
	IF (STDPT .EQ. A2)
		BEGIN
		XCALL OUTPT (7,18,1,'ALL',1)
		XCALL OUTPT (8,18,1,A2,1)
		ENDPT = 'ZZ'
		GOTO ANYCNG
		END
ENDPT,
	XCALL INPUT (8,18,02,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	ENDPT = ENTRY(1,2)
	IF (ENDPT .EQ. A2)
		BEGIN
		ENDPT = STDPT
		XCALL OUTPT (8,18,1,ENDPT,1)
		END

	GOTO (ANYCNG),CNGCTL

ANYCNG,
	XCALL ANYCN(CNGCTL, WHATNO)
	GOTO (PROCES, CNGBR), CNGCTL+1
CNGBR,
	GOTO (STCAT, ENCAT, STDPT, ENDPT), WHATNO
	GOTO ANYCNG
PROCES,
	HCAT1 = STCAT
	HCAT2 = ENCAT
	HDPT1 = STDPT
	HDPT2 = ENDPT

	IF (FILFLG.EQ.1) GO TO NOOPEN

	SWITCH = 1
	XCALL FILES (1,'SI',41,SWITCH)		;FILE # 41 -- ITMMAS FILE
	IF (SWITCH.EQ.9) GO TO CLOSE
	FILFLG = 1
NOOPEN,

	ITMCNT =
	RECNO =
;;;	find (1, itmmas, ^first, krf=2) [err=total]
	clear key
	k_cat = stcat
	find (1, itmmas, key, krf=2) [err=loop]
LOOP,
	incr ii
	if (ii/500*500 .eq. ii) xcall outpt (1,70,1,dis,1)
	INCR RECNO
	LOKCTL = 1
	
	XCALL IOS (1, ITMMAS, READ, LOKCTL)
	if (lokctl .ne. 0) goto total


	IF (PRDCAT .LT. STCAT) GOTO LOOP
	IF (PRDCAT .GT. ENCAT) GOTO TOTAL

	IF (USRDEF .LT. STDPT) GOTO LOOP
	IF (USRDEF .GT. ENDPT) GOTO LOOP
	if (qtyytd .le. 0) goto loop


	IF (LINCNT.GE.58) LINCNT = 60
BUFF,
	IF (LPFLAG.EQ.1) GO TO CONTIN
	SPLFIL(5,6) = 'CA'
	LPSW = 1
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO MENU
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	LPFLAG = 1
CONTIN,
	PLINE (1,15) = ITEMNO
	IF (JUSTIF) XCALL LEFTJ (PLINE(1,15),15)
	PLINE (17,46) = DESCR
	PLINE (48,49) = PRDCAT
	PLINE (52,53) = USRDEF
	PLINE (56,57) = STOCK
	PLINE (59,66) = PRICE,	'ZZZX.XXX'
	PLINE (68,76) = QTYMTD,	'Z,ZZZ,ZZX'
	PLINE (78,86) = QTYYTD,	'Z,ZZZ,ZZX'
	PLINE (88,99) = SLSYTD,	'Z,ZZZ,ZZX.XX'
	CALL PRINT

;                AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA AA  AA   A ZZZX.XXX Z,ZZZ,ZZX
;12345678901234567890123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5         6         7         8

	IF (IF1.NE.ZRO .OR. IF2.NE.ZRO .OR. IF3.NE.ZRO)
		BEGIN
		PLINE(1,3) = IF1
		PLINE(5,7) = IF2
		PLINE(9,13) = IF3
		END

	CALL PRINT


	IF (LPFLAG.EQ.1) 	XCALL LINFD (1)
	INCR LINCNT
	INCR ITMCNT
	GO TO LOOP

PRINT,
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HD1,HD2,HDR,LEG,LEG,
&		LEG,0,132,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
NOFND2,
	XCALL MESAG ('NO ITEMS ON FILE MATCHING CRITERIA',2)
	GO TO CLOS
TOTAL,
	IF (ITMCNT.EQ.0) GO TO NOFND2
	IF (LPFLAG.EQ.1) 	XCALL LINFD (2)
	PLINE (2,7) = ITMCNT, 'ZZ,ZZX'
	PLINE (9,21) = 'ITEMS ON FILE'
	LINCNT = LINCNT + 1
	CALL PRINT
CLOS,
	IF (LPFLAG.EQ.1) XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	PGCNT =
	LINCNT = 61
	LPFLAG =
	GOTO BEGIN

;********************************************************

CLOSE,
ENDOFF,
END,
	IF (FILFLG.EQ.1) CALL CLOSE2
	IF (LPFLAG.EQ.1) XCALL LPOFF (LPSW,SPLFIL,PGCNT)

	STFLG = 1
	XCALL PGCHN ('CP:IMMENU',STFLG)

;********************************************************

CLOSE2,
	XCALL FILES (1,'SI',41,4)
	FILFLG =
	RETURN
END

