;SITMNT.CP
;
;	STOCKED PART NUMBERS
;
RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'

RECORD	ITMMAS
	.INCLUDE 'DEF:RD041A.DEF'

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;RECORD,X
;;;	.INCLUDE 'DEF:RD041B.DEF'
;;;
;;;RECORD	ITMIDX
;;;	.INCLUDE 'DEF:RD042A.DEF'
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

RECORD	CHANNEL
	CHN041	,D2
	CHN042	,D2
	CHN182	,D2


RECORD	PRINT
	TITLE	,A*,	'STOCKED PART NUMBERS TABLE'
	HD	,A6,	'NO HDR'
	LG	,A9,	'NO LEGEND'
	PLINE	,A80
	PRNTON	,D1
	LINCNT	,D2,60
	PGCNT	,D6
	LPSW	,D2
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	PRTCTL	,D3,080
	LPARG	,D1
	PRNTSW	,D1
	PRTCTR	,D1

RECORD	HD1
		,A*,	'ITEM            DESCR                           '
		,A*,	'PG  ALUM  SS'

RECORD	VARS
	YESNO	,2A1,	'Y','N'
	OPNOK	,D1
	ST_ITEM	,A15
	EN_ITEM	,A15
	BLANKS	,A15
	PGM	,D1
	ROW	,D2
	ITEM	,A15
	TL	,D2
	NUMITM	,D2
	BSEND	,D5
	BSMID	,D5
	SRCCTL	,D1
	ENTRY	,A30
	INXCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	STORE	,D1,2
	DELETE	,D1,3
	LOKCTL	,D1
	WHATNO	,D2
	SELECT	,D1
	CNGCTL	,D1
	I	,D3
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'STOCKED PART NUMBERS TABLE',1)

	NUMITM = 4

	CALL OPENS
	IF (.NOT. OPNOK) GOTO CLOSE

MENU,
	CLEAR PRNTON
	XCALL OUTPT (1,1,2,'STOCKED PART NUMBERS TABLE',1)
	XCALL OUTPT (3,9,0,'PLEASE SELECT APPLICATION',1)
	XCALL OUTPT (5,15,0,'1. TABLE MAINTENANCE',1)
	XCALL OUTPT (6,15,0,'2. PRINT TABLE',1)
MINPUT,
	XCALL INPUT (3,36,1,1,'#E',ENTRY,INXCTL,1)
	GOTO (MINPUT,ENDOFF), INXCTL
	PGM = ENTRY(1,1)
	GOTO (DISPLA,PRINT_TABLE),PGM
	GOTO MINPUT

DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,'STOCKED PART NUMBERS TABLE',1)
	XCALL OUTPT (4,05,0,'1. ITEM #',1)
	XCALL OUTPT (6,05,0,'2. PG?',1)
	XCALL OUTPT (7,05,0,'3. ALUM?',1)
	XCALL OUTPT (8,05,0,'4. SS?',1)
ITEM,
	XCALL INPUT (04,17,15,00,'AE',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	ITEM = ENTRY(1,15)
;look up based on the first 3 char, eg: R26...
;;;	XCALL SERCH (CHN042,ITMIDX,ITEM(1,3),1,15,BSEND,BSMID,SRCCTL,4,16,20,0,0,0,0)
;;;	IF (SRCCTL.NE.0 .OR. IRC041.EQ.0)
	XCALL ISIO (CHN041, ITMMAS, ITEM, READ, LOKCTL)
	IF (LOKCTL .NE. 0)
	THEN	BEGIN
		XCALL MESAG ('ITEM NOT ON FILE',1)
		GOTO ITEM
		END
	ELSE	BEGIN
	;;;	XCALL IO (CHN041,ITMMAS,IRC041,READ,LOKCTL)
		XCALL OUTPT (04,33,1,DESCR,1)
		END
	CLEAR TBL_KEY
	TBLCOD= 'SI'
	SI_ITEM = ITEM
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .EQ. 0)
		BEGIN
		SELECT = 2
		CALL DSPREC
		GOTO ANYCNG
		END


	SELECT = 1			;ADD MODE
	CLEAR COPTBL
PG,
	XCALL INPUT(06,17,01,01,'YN',ENTRY,INXCTL,1)
	SI_PG = INXCTL
	GOTO (ANYCNG),CNGCTL
ALUM,
	XCALL INPUT(07,17,01,01,'YN',ENTRY,INXCTL,1)
	SI_ALUM = INXCTL
	GOTO (ANYCNG),CNGCTL
SS,
	XCALL INPUT (08,17,01,01,'YN',ENTRY,INXCTL,1)
	SI_SS = INXCTL
	GOTO (ANYCNG),CNGCTL


ANYCNG,
;;;	XCALL ANYCN(CNGCTL,WHATNO)
	XCALL OUTPT (24,1,1,'FIELD # TO CHANGE            <TAB> = DELETE',1)
	XCALL INPUT (24,20,02,00,'#T',ENTRY,INXCTL,1)
	IF (INXCTL .EQ. 3)
		BEGIN
		XCALL OUTPT (24,1,1,'DELETE, ARE YOU SURE ?',1)
		XCALL INPUT (24,24,01,01,'YN',ENTRY,INXCTL,1)
		GOTO (ANYCNG),INXCTL-1
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,DELETE,LOKCTL)
		GOTO DISPLA
		END

	WHATNO = ENTRY(1,2)
	IF (WHATNO .EQ. 0)
	THEN 	CLEAR CNGCTL
	ELSE 	CNGCTL = 1

	GOTO (PROCES,CNGBR),CNGCTL+1
CNGBR,
	GOTO (ITEM, PG, ALUM, SS), WHATNO
	GOTO ANYCNG
	
PROCES,
	CASE SELECT OF
	BEGINCASE
	1:	BEGIN
		TBLCOD = 'SI'
		TBLKEY = ITEM
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,STORE,LOKCTL)
		END
	2:	XCALL ISIO (CHN182,COPTBL,TBL_KEY,WRITE,LOKCTL)
	ENDCASE
	GOTO DISPLA

ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('CP:TBLMNU',1)
	STOP


DSPREC,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; DISPLAY RECORD
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	IF (SI_PG.LT.1 .OR. SI_PG.GT.2) SI_PG = 2
	IF (SI_ALUM.LT.1 .OR. SI_PG.GT.2) SI_ALUM = 2
	IF (SI_SS.LT.1 .OR. SI_PG.GT.2) SI_SS = 2


	XCALL OUTPT (6,17,0,YESNO(SI_PG), 1)
	XCALL OUTPT (7,17,0,YESNO(SI_ALUM), 1)
	XCALL OUTPT (8,17,0,YESNO(SI_SS), 1)

	RETURN
;-----------------------------------



;===================================================================
PRINT_TABLE,
;===================================================================
	LINCNT = 66
	PGCNT  = 0
	
PDISP,
	CNGCTL = 
	XCALL OUTPT (1,1,2,'PRINT INVENTORY PRICE TABLE',1)
	XCALL OUTPT (4,4,0,'1. STARTING ITEM #',1)
	XCALL OUTPT (6,4,0,'2. ENDING   ITEM #',1)
ST_ITEM,
	XCALL INPUT (4,25,15,00,'AE',ENTRY,INXCTL,1)
	GOTO (PDISP,PDONE),INXCTL
	ST_ITEM = ENTRY(1,15)
	IF (ST_ITEM .EQ. BLANKS)
		BEGIN
		EN_ITEM = 'ZZZZZZZZZZZZZZZ'
		XCALL OUTPT (4,25,1,'ALL',1)
		XCALL OUTPT (6,25,1,' ',1)
		GOTO P_ANY
		END
	GOTO (P_ANY),CNGCTL
EN_ITEM,
	XCALL INPUT (6,25,15,00,'A ',ENTRY,INXCTL,1)
	GOTO (PDISP),INXCTL
	EN_ITEM = ENTRY(1,15)
	IF (EN_ITEM .EQ. BLANKS)
		BEGIN
		EN_ITEM = ST_ITEM
		XCALL OUTPT (6,25,0,EN_ITEM,1)
		END
P_ANY,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (P_PRINT,P_CNGBR),CNGCTL + 1
P_CNGBR,
	GOTO (ST_ITEM,EN_ITEM),WHATNO
	GOTO P_ANY

P_PRINT,
	CLEAR TBL_KEY
	TBLCOD = 'SI'
	SI_ITEM = ST_ITEM
	FIND (CHN182,COPTBL,TBL_KEY)[ERR=PLOOP]

PLOOP,
	XCALL IOS (CHN182,COPTBL,READ,LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF
	IF (TBLCOD .NE. 'SI') GOTO EOF
	IF (SI_ITEM .LT. ST_ITEM) GOTO PLOOP
	IF (SI_ITEM .GT. EN_ITEM) GOTO EOF

;look up based on the first 3 char, eg: R26...
;;;	XCALL SERCH (CHN042,ITMIDX,SI_ITEM(1,3),1,15,BSEND,BSMID,SRCCTL,4,16,20,0,0,0,0)
;;;	IF (SRCCTL.NE.0 .OR. IRC041.EQ.0)
;;;	THEN	DESCR = '*** NOT ON FILE ***'
;;;	ELSE	XCALL IO (CHN041,ITMMAS,IRC041,READ,LOKCTL)

	XCALL ISIO (CHN041, ITMMAS, SI_ITEM, READ, LOKCTL)
	IF (LOKCTL .NE. 0) DESCR = '*** NOT ON FILE ***'

	PLINE (1,15) = SI_ITEM
	PLINE (17,46) = DESCR
	PLINE (50,50) = YESNO(SI_PG)
	PLINE (54,54) = YESNO(SI_ALUM)
	PLINE (60,60) = YESNO(SI_SS)

	CALL PRINT
	GOTO PLOOP
;ITEM            DESCR                           PG  ALUM  SS
;AAAAAAAAAAAAAAA AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA   X   X     X
;12345678901234567890123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5         6         7

EOF,
	IF (PRNTON.EQ.1)  XCALL LPOFF(LPSW,SPLFIL,PGCNT)
PDONE,
	GOTO MENU

PRINT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (PRNTON .EQ. 0) CALL PRNTON
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HD1,HD,HD
&		,LG,LG,LG,0,080,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;-------------------------------------------------------------
PRNTON,
	SPLFIL (5,6) = 'EF'
	LPSW = 1		;PRINT,SPOOL, OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTON = 1
	RETURN
;-------------------------------------------------------------
;===================================================================


OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (1,'SU',182,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN182 = 1
	
	XCALL FILES (2,'SI',041,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN041 = 2

;;;	XCALL FILES (3,'I',042,SWITCH)
;;;	IF (SWITCH .EQ. 9) RETURN
;;;	CHN042 = 3

;;;	XCALL IO (CHN041,ITMMAS,1,READ,LOKCTL)
;;;	BSEND = ORG041

	OPNOK = 1
	RETURN
;----------------------------------------------------

CLOSE,
	CLOSE CHN041
;;;	CLOSE CHN042
	CLOSE CHN182
	RETURN
;----------------------------------------------------

