;ORDHLD.CP
;
;	MAINTAIN CREDIT HOLD LIST
;

RECORD	CRHOLD
	.INCLUDE 'DEF:RD195A.DEF'

RECORD	ORDHDR
	.INCLUDE 'DEF:RD044A.DEF'

RECORD	CHANNEL
	CHN044	,D2
	CHN195	,D2

RECORD	VARS
	OPNOK	,D1
	XORD	,D6
	SNAM	,A12
	LNAM	,A25
	SINT	,A3
	XDATE	,D8
	ENTRY	,A30
	INXCTL	,D1
	WHATNO	,D2
	CNGCTL	,D1
	LOKCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	STORE	,D1,2
	DELETE	,D1,3
	SWITCH	,D1
	V	,D1
;
PROC
	XCALL TERID(V)
	XCALL OUTPT (1,1,2,'CREDIT HOLD',1)
	CALL OPENS
	IF(.NOT. OPNOK) GOTO ENDOFF

DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,'CREDIT HOLD',1)
	XCALL OUTPT (4,4,0,'ORDER #',1)
	XCALL OUTPT (4,36,0,'DATE:',1)
	XCALL OUTPT (6,5,0,'SREP:',1)
ORDNO,
	XCALL INPUT (4,13,06,00,'#E',ENTRY,INXCTL,1)
	GOTO(DISPLA,ENDOFF),INXCTL
	XORD = ENTRY(1,6)
	IF (XORD .LE. 0) GOTO ORDNO
	XCALL ISIO(CHN044,ORDHDR,XORD,READ,LOKCTL)
	IF(LOKCTL .NE. 0)
		BEGIN
		XCALL MESAG ('ORDER NOT ON FILE',1)
		GOTO ORDNO
		END

	XDATE(1,4) = OORDDT(5,8)
	XDATE(5,8) = OORDDT(1,4)
	ENTRY(1,10) = XDATE,	'XX/XX/XXXX'
	XCALL OUTPT (4,42,0,ENTRY(1,10),1)

	ENTRY(1,2) = OSLMAN,	'ZX' [LEFT]
	XCALL OUTPT(6,12,0,ENTRY(1,2),1)
	XCALL SREP(OSLMAN,LNAM,SNAM,SINT)
	XCALL OUTPT (6,15,0,SNAM,1)

	ENTRY(1,6) = OCUSNO,'ZZZZZX' [LEFT]
	XCALL OUTPT (8,4,0,ENTRY(1,6),1)
	XCALL OUTPT (8,12,0,OCUSNM,1)
	XCALL OUTPT (9,12,0,OSHPNM,1)
	XCALL OUTPT (10,12,0,OSHAD1,1)
	XCALL OUTPT (11,12,0,OSHAD2,1)
	XCALL OUTPT (12,12,0,OSHAD3,1)

	FIND (CHN195,CRHOLD,XORD) [ERR=NEW_OH]

	READS(CHN195,CRHOLD,ENDOFF)
	IF (CH_ORD .NE. XORD)
		BEGIN
		XCALL MESAG ('ORDER # MISSMATCH',1)
		GOTO DISPLA
		END
	XCALL OUTPT (24,1,1,'REMOVE THIS ORDER FROM CREDIT HOLD LIST?',1)
	XCALL INPUT (24,48,01,01,'YN',ENTRY,INXCTL,1)
	GOTO (REMOVE,DISPLA),INXCTL
REMOVE,
	XCALL ISIO (CHN195,CRHOLD,CH_ORD,DELETE,LOKCTL)
	IF (LOKCTL .NE. 0) XCALL MESAG ('ERROR DELETING RECORD',1)
	GOTO DISPLA
;---------------------------------------------

NEW_OH,
	XCALL OUTPT (24,1,1,'PLACE THIS ORDER ON CREDIT HOLD?',1)
	XCALL INPUT (24,38,01,01,'YN',ENTRY,INXCTL,1)
	GOTO (ADD,DISPLA),INXCTL
ADD,
	CLEAR CRHOLD
	CH_ORD = XORD
	XCALL ISIO (CHN195,CRHOLD,CH_ORD,STORE,LOKCTL)
	IF (LOKCTL.NE.0) XCALL MESAG ('ERROR STORING RECORD',1)
	GOTO DISPLA
;---------------------------------------------

ENDOFF,
	CALL CLOSE
	XCALL FLAGS (7000000)	;suppress program termination message

	STOP
;;;	XCALL PGCHN('AR:CUSMNU',1)
	STOP


OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (4,'SI',044,SWITCH)
	IF(SWITCH.EQ.9) RETURN
	CHN044 = 4

	XCALL FILES (5,'SU',195,SWITCH)
	IF(SWITCH.EQ.9) RETURN
	CHN195 = 5

	OPNOK = 1
	RETURN
;-----------------------------------

CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	IF(CHN044) CLOSE CHN044
	IF(CHN195) CLOSE CHN195

	RETURN
;-----------------------------------


