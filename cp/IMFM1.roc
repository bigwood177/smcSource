SUBROUTINE IMFM1
;
;		INVENTORY MANAGEMENT MASTER FILE MAINTENANCE
;
	ITMMAS	,A
	ITMIDX	,A
	SELECT	,D
	INXCTL	,D
	CNGCTL	,D
	V	,D
	BSEND	,D
	BSMID	,D
	JUSTIF	,D

RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'

RECORD TNVMAS		;
		.INCLUDE 'DEF:RD041A.DEF'
RECORD TNVIDX		;
		.INCLUDE 'DEF:RD042A.DEF'
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2

RECORD	VARS
	IN_TABLE	,D1	;SSQ 9-10-03
	RLOC	,A1		;"R" = ROCKFORD
	IDX	,D1	;1=SMC, 2=ROCKFORD
	MODE	,A14
	OPTION	,D1
	ENTRY	,A31
	WHATNO	,D2
	DECMAL	,D18
	KEY	,A15
	KITMNO	,A15
	SRCCTL	,D1
	SCRCNG	,D1
	READ	,D1,	0		;IO READ FLAG FOR I-O SUBROUTINE
	LOKCTL	,D1			;RECORD-LOCKED INDICATOR
	YESNO	,2A1,	'Y','N'
	JSTIFY	,D1
;
PROC (1)
	JSTIFY = JUSTIF
	INXCTL = 0
	TNVMAS = ITMMAS
	TNVIDX = ITMIDX
DISPLA,
	IF (INXCTL.EQ.1) RETURN			;CTRL/W TO ABORT RECORD
						;IN MIDDLE OF PROCESSING
	IF (INXCTL.EQ.2) RETURN			;'END' HAS BEEN TYPED
	CNGCTL =
	MODE = 'ADD'
	IF (SELECT.EQ.2) MODE = 'CHANGE/INQUIRE'
DISPL2,
	IF (SELECT.EQ.3) MODE = 'DELETE'
	XCALL OUTPT (1,1,2,'ITEM MASTER FILE MAINTENANCE',V)
	IF (V.EQ.2) XCALL OUTPT (2,1,1,MODE,1)
	XCALL OUTPT (3,2,0,'   LOC',1)
	XCALL OUTPT (2,2,0,'1. ITEM #',V)
	XCALL OUTPT (3,2,0,'2. DESCR',V)
	XCALL OUTPT (4,2,0,'3. PROD CAT',V)
	XCALL OUTPT (5,2,0,'4. ON-HAND',V)

	XCALL OUTPT (6,2,0,'5. ALLOCATD',V)
	XCALL OUTPT (7,2,0,'6. ON-ORDER',V)
	XCALL OUTPT (8,2,0,'7. ORD UPTO',V)
	XCALL OUTPT (9,2,0,'8. REO LVL',V)
	XCALL OUTPT (10,2,0,'9. AVG COST',V)
	XCALL OUTPT (11,1,0,'10. PRICE',V)

;;;	XCALL OUTPT (4,26,0,'11. USER CDE',V)
	XCALL OUTPT (4,26,0,'11. DEPT/LOC',V)
	XCALL OUTPT (5,26,0,'12. STOCKED',V)
	XCALL OUTPT (6,26,0,'13. PER BNDLE',V)
	XCALL OUTPT (7,26,0,'14. PER CASE',V)

	XCALL OUTPT (2,48,0,'15. WEIGHT',V)
	XCALL OUTPT (3,48,0,'16. UNIT MEASURE',V)
	XCALL OUTPT (4,48,0,'17. QTY SOLD MTD',V)
	XCALL OUTPT (5,48,0,'18. QTY SOLD YTD',V)
	XCALL OUTPT (6,48,0,'19. SALES $ MTD',V)
	XCALL OUTPT (7,48,0,'20. SALES $ YTD',V)
	XCALL OUTPT (8,48,0,'21. COST MTD',V)
	XCALL OUTPT (9,48,0,'22. COST YTD',V)
	XCALL OUTPT (10,48,0,'23. BACKORDERABLE ?',V)
	XCALL OUTPT (11,48,0,'24. TAXABLE ?',V)
	IF (SELECT.EQ.2.OR.SELECT.EQ.3) XCALL OUTPT (2,1,0,'*',V)

RLOC,
	XCALL INPUT(3,15,01,00,'AE',ENTRY,INXCTL,1)
	GOTO (DISPLA,DISPLA),INXCTL
	RLOC = ENTRY(1,1)
	IF (RLOC .EQ. 'R')
	THEN	IDX = 2		;ROCKFORD
	ELSE	IDX = 1		;SMC

ITMNUM,
	CTL = '02,15,15,01,AX'
	CALL INPUT
	GO TO (DISPLA,DISPLA,SCROLL), INXCTL
	IF (JSTIFY) XCALL FRMAT (ENTRY(1,15),15)
	XCALL OUTPT (2,15,0,ENTRY(1,15),V)
	KITMNO = ENTRY (1,15)
	KEY = KITMNO
	SRCCTL = 3
	XCALL SERCH (2,TNVIDX,KEY,1,15,BSEND,BSMID,SRCCTL,1,16,20,0,0,0,0)
	IF (SRCCTL.EQ.2) INXCTL = 2
	IF (SRCCTL.EQ.2) GO TO DISPLA
	GO TO (FNDREC,FNDREC), SELECT-1
	GO TO (NEWOK), SRCCTL
BADITM,
	XCALL MESAG ('ITEM # ALREADY ON FILE',1)
	GO TO DISPLA
NEWOK,
	ITEMNO = KITMNO
	GO TO (ANYCNG), CNGCTL
DSCRIP,
	IF (IDX .EQ. 1)		;SMC ONLY
		BEGIN
		CTL = '03,15,30,01,A '
		CALL INPUT
		GO TO (DISPLA), INXCTL
		DESCR = ENTRY (1,30)
		END
	GO TO (ANYCNG), CNGCTL
PRODCT,
	IF (IDX .EQ. 1)		;SMC ONLY
		BEGIN
		CTL = '04,15,02,00,A '
		CALL INPUT
		GO TO (DISPLA), INXCTL
		PRDCAT = ENTRY (1,2)
		IPRCAT = PRDCAT
		END
	GO TO (ANYCNG), CNGCTL
QONHND,
	CTL = '05,15,08,00,#-'
	CALL INPUT
	GO TO (DISPLA), INXCTL
	QTYONH(IDX) = ENTRY (1,8)
	GO TO (ANYCNG), CNGCTL
QALLOC,
;;;	IF (SELECT.NE.1.AND.STOCK.EQ.'S') GOTO BADCNG	;removed 1/18/95
	CTL = '06,15,08,00,# '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	QTYCOM (IDX) = ENTRY
	GO TO (ANYCNG), CNGCTL
QONODR,
	CTL = '07,15,08,00,#-'
	CALL INPUT
	GO TO (DISPLA), INXCTL
	QTYONO(IDX) = ENTRY (1,8)
	GO TO (ANYCNG), CNGCTL
ORDRUP,
	CTL = '08,15,08,00,# '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	ORDUPT(IDX) = ENTRY(1,8)
	GO TO (ANYCNG), CNGCTL
REORDR,
	CTL = '09,15,08,00,#N'
	CALL INPUT
	GO TO (DISPLA), INXCTL
	REOLVL(IDX) = ENTRY (1,8)
	GO TO (ANYCNG), CNGCTL
UCOST,
	IF (IDX .EQ. 1)		;SMC ONLY
		BEGIN
		CTL = '10,15,09,01,# '
		CALL INPUT
		GO TO (DISPLA),INXCTL
		AVGCST = ENTRY(1,9)
		CALL DSPCST
		END
	GO TO (ANYCNG), CNGCTL
PRCE1,
	IF (IN_TABLE .EQ. $TRUE)
		BEGIN
		XCALL MESAG('(See Table)',1)
		GOTO (USCODE,ANYCNG),CNGCTL+1
		END
		
	IF (IDX .EQ. 1)		;SMC ONLY
		BEGIN
		CTL = '11,15,08,00,# '
		CALL INPUT
		DECMAL = ENTRY
		CALL DSP3DP
		GO TO (DISPLA), INXCTL
		PRICE(1) = ENTRY(1,8)
		END
	GO TO (ANYCNG), CNGCTL

;-------------------------------------
USCODE,
	CTL = '04,40,02,00,A '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	USRDEF(IDX) = ENTRY(1,2)
	GO TO (ANYCNG), CNGCTL
STOCK,
	IF (IDX .EQ. 1)		;SMC ONLY
		BEGIN
		XCALL OUTPT (24,1,1,'Stock Flag: (S) STOCKED or (N) NON-STOCK',1)
		CTL = '05,40,01,00,A '
		CALL INPUT
		GO TO (DISPLA), INXCTL
		IF (ENTRY.EQ.' ')
			BEGIN
			  ENTRY = 'N'
			  XCALL OUTPT (ROW,COL,0,ENTRY(1,1),V)
			END
		STOCK = ENTRY
		IF (STOCK.NE.'S'.AND.STOCK.NE.'N') GOTO STOCK
		XCALL OUTPT (24,1,1,'\',1)
		END
	GO TO (ANYCNG), CNGCTL
FTBNDL,
	IF (IDX .EQ. 1)		;SMC ONLY
		BEGIN
		CTL = '06,40,03,00,# '
		CALL INPUT
		GOTO (DISPLA),INXCTL
		FTBNDL = ENTRY(1,3)
		END
	GOTO (ANYCNG),CNGCTL
FTCASE,
	IF (IDX .EQ. 1)		;SMC ONLY
		BEGIN
		CTL = '07,40,04,00,# '
		CALL INPUT
		GOTO (DISPLA),INXCTL
		FTCASE = ENTRY(1,4)
		END
	GOTO (ANYCNG),CNGCTL
;-------------------------------------

WAYT,
	IF (IDX .EQ. 1)		;SMC ONLY
		BEGIN
		CTL = '02,68,06,00,# '
		CALL INPUT
		GO TO (DISPLA), INXCTL
		WEIGHT = ENTRY (1,6)
		CALL DSPWGT
		END
	GO TO (ANYCNG), CNGCTL
UMEAS,
	IF (IDX .EQ. 1)		;SMC ONLY
		BEGIN
		CTL = '03,68,02,00,A '
		CALL INPUT
		GO TO (DISPLA), INXCTL
		IF (ENTRY.EQ.'  ') 
			BEGIN
			ENTRY = 'EA'
			XCALL OUTPT (3,68,0,ENTRY,V)
			END
		SUOFM = ENTRY (1,2)
		END
	GO TO (ANYCNG), CNGCTL
QMTD,
	CTL = '04,68,08,00,#-'
	CALL INPUT
	GO TO (DISPLA), INXCTL
	QTYMTD(IDX) = ENTRY
	GO TO (ANYCNG), CNGCTL
QYTD,
	CTL = '05,68,09,00,#-'
	CALL INPUT
	GO TO (DISPLA), INXCTL
	QTYYTD(IDX) = ENTRY
	GO TO (ANYCNG), CNGCTL
SMTD,
	IF (IDX .EQ. 1)		;SMC ONLY
		BEGIN
		CTL = '06,66,10,00,$-'
		CALL INPUT
		GO TO (DISPLA), INXCTL
		SLSMTD = ENTRY
		END
	GO TO (ANYCNG), CNGCTL
SYTD,
	IF (IDX .EQ. 1)		;SMC ONLY
		BEGIN
		CTL = '07,66,10,00,$-'
		CALL INPUT
		GO TO (DISPLA), INXCTL
		SLSYTD = ENTRY
		END
	GO TO (ANYCNG), CNGCTL
CMTD,
	IF (IDX .EQ. 1)		;SMC ONLY
		BEGIN
		CTL = '08,66,10,00,$-'
		CALL INPUT
		GO TO (DISPLA), INXCTL
		CSTMTD = ENTRY
		END
	GO TO (ANYCNG), CNGCTL
CYTD,
	IF (IDX .EQ. 1)		;SMC ONLY
		BEGIN
		CTL = '09,66,10,00,$-'
		CALL INPUT
		GO TO (DISPLA), INXCTL
		CSTYTD = ENTRY
		END
	GO TO (ANYCNG), CNGCTL
RCODE,
	IF (IDX .EQ. 1)		;SMC ONLY
		BEGIN
		CTL = '10,68,01,00,YY'
		CALL INPUT
		BOCODE = INXCTL - 1
		INXCTL =
		END
	GO TO (ANYCNG), CNGCTL
TFLAG,
	IF (IDX .EQ. 1)		;SMC ONLY
		BEGIN
		CTL = '11,68,01,00,YY'
		CALL INPUT
		TXFLAG = ENTRY (1,1)
		INXCTL =
		END
	GO TO ANYCNG
CNGBR,
	IF (SELECT.EQ.2.AND.WHATNO.EQ.1) GO TO BADCNG
	GO TO (ITMNUM,DSCRIP,PRODCT,QONHND,QALLOC,QONODR,ORDRUP,REORDR,
&		UCOST,PRCE1,USCODE,STOCK,FTBNDL,FTCASE,WAYT,UMEAS,QMTD,
&		QYTD,SMTD,SYTD,CMTD,CYTD,RCODE,TFLAG), WHATNO
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;	IF (SELECT.EQ.2.AND.WHATNO.GT.14.AND.WHATNO.LT.21) GO TO BADCNG
;;;	GO TO (ITMNUM,DSCRIP,PRODCT,USCODE,QONHND,QALLOC,QONODR,ORDRUP,REORDR,
;;;&		STOCK,UCOST,PRCE1,WAYT,UMEAS,QMTD,QYTD,SMTD,SYTD,CMTD,CYTD,
;;;&		RCODE,TFLAG), WHATNO
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	GO TO BADCNG
PROCES,
	INXCTL = 0			; IF DELETE, Y ANSWER MAKES INXCTL 1
	XCALL WATE (3,V)
	ITMMAS = TNVMAS
	ITMIDX = TNVIDX
	RETURN
FNDREC,
	GO TO (NOFIND), SRCCTL
	XCALL WATE (3,V)
READ5,
	LOKCTL = 0
	XCALL IO (1,TNVMAS,IRC041,READ,LOKCTL)
	IF (LOKCTL) INXCTL = 2
	IF (LOKCTL) GO TO DISPLA

	TBL_KEY(1,2) = 'I1'
	I1_KEY = KITMNO
	XCALL ISIO (5,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .EQ. 0)
	THEN	IN_TABLE = $TRUE
	ELSE	IN_TABLE = $FALSE

	XCALL OUTPT (12,1,1,'\',V)
	CTL =
	XCALL OUTPT (3,15,0,DESCR,V)
	XCALL OUTPT (4,15,0,PRDCAT,V)
	CTL = '05,15,08'
	DECMAL = QTYONH(IDX)
	CALL DSPNUM
	ROW = 6
	MAX = 8
	DECMAL = QTYCOM(IDX)
	CALL DSPNUM
	ROW = 7
	DECMAL = QTYONO(IDX)
	CALL DSPNUM
	ROW = 8
	MAX = 8
	DECMAL = ORDUPT(IDX)
	CALL DSPNUM
	ROW = 9
	DECMAL = REOLVL(IDX)
	OPTION = 6
	CALL CALDSP
	CTL = '10,15,09'
	CALL DSPCST
	CTL = '11,15,08'

	IF (IN_TABLE .EQ. $TRUE)
		BEGIN
		PRICE(1) = 0		;per SteveM 3-18-05
	;;;	PRICE(1) = I1_PRICE(1)
		XCALL OUTPT (ROW,COL+13,0,'(See Table)',V)
		END		
	DECMAL = PRICE(1)
	CALL DSP3DP
	CTL = '02,68,06'

;---------------------------------------
	XCALL OUTPT (4,40,0,USRDEF(IDX),V)
	XCALL OUTPT (5,40,0,STOCK,V)
	CTL = '06,40,03'
	DECMAL = FTBNDL
	CALL DSPNUM
	CTL = '07,40,04'
	DECMAL = FTCASE
	CALL DSPNUM
;---------------------------------------

	CALL DSPWGT
	XCALL OUTPT (3,68,0,SUOFM,V)
	CTL = '04,68,08'
	DECMAL = QTYMTD(IDX)
	CALL DSPNUM
	CTL = '05,68,09'
	DECMAL = QTYYTD(IDX)
	CALL DSPNUM
	CTL = '06,66,10'
	DECMAL = SLSMTD
	CALL DSPDLR
	ROW = 7
	MAX = 10
	DECMAL = SLSYTD
	CALL DSPDLR
	ROW = 8
	MAX = 10
	DECMAL = CSTMTD
	CALL DSPDLR
	ROW = 9
	MAX = 10
	DECMAL = CSTYTD
	CALL DSPDLR
	XCALL OUTPT (10,68,0,YESNO(BOCODE+1),V)
	XCALL OUTPT (11,68,0,TXFLAG,V)
	IF (SELECT.EQ.2) GO TO ANYCNG
	XCALL OUTPT (12,1,0,'RIGHT ITEM ?',V)
	CTL = '12,15,01,01,YN'
	CALL INPUT
	XCALL WATE (3,V)
	IF (INXCTL.EQ.1) GO TO PROCES
	GO TO DISPL2
DSPWGT,
	CTL = '02,68,06'
	DECMAL = WEIGHT
	CALL DSPDLR
	RETURN
NOFIND,
	XCALL MESAG ('ITEM NOT ON FILE',1)
	GO TO DISPLA
INPUT,
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,V)
	RETURN
BADCNG,
	XCALL MESAG ('INVALID SELECTION',1)
ANYCNG,
	CNGCTL = 6
	XCALL ANYCN (CNGCTL,WHATNO)
	GO TO (PROCES,PROCES,CNGBR), CNGCTL+2
DSPCST,
	DECMAL = AVGCST
DSP3DP,
	OPTION = 4
	GO TO CALDSP
DSPNUM,
	OPTION = 1
	GO TO CALDSP
DSPTNT,
	OPTION = 5
	GO TO CALDSP
DSPDLR,
	OPTION = 3
CALDSP,
	XCALL DSPLY (MAX,ROW,COL,DECMAL,OPTION,V)
	RETURN
SCROLL,
	GO TO (SCRADD,SCRCNG,SCRCNG),SELECT
SCRADD,
	XCALL MESAG ('SCROLLING NOT ALLOWED IN ADD MODE',1)
	GO TO ITMNUM
SCRCNG,
	INCR BSMID
	LOKCTL = 0
	XCALL IO (2,TNVIDX,BSMID,READ,LOKCTL)
	IF (LOKCTL) INXCTL = 2
	IF (LOKCTL) GO TO DISPLA
	UNLOCK 2
	IF (IPRCAT.EQ.']]') GO TO NOMORE
	IF (IRC041.EQ.0) GO TO SCRCNG
	XCALL OUTPT (2,15,0,IITMNO,V)
	GO TO READ5
NOMORE,
	XCALL MESAG ('END OF FILE',1)
	GO TO ITMNUM
END
