;ADVIC2.CP	isam
; ADVICE.IM - REORDER ADVICE REPORT
;
;	CUSTOM FOR SHEET METAL I/M SYSTEM
;
RECORD ITMMAS
	.INCLUDE 'DEF:RD041A.def'

RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'

RECORD	LEG
		,A*,	'PERCENT: '
	L_PCT	,D3
		,A4
	L_SCAT	,A2
		,A*,	' THRU: '
	L_ECAT	,A2
		,A4
	L_SITM	,A15
		,A*,	' THRU: '
	L_EITM	,A15


RECORD HDR1
		,A50,'---------------- ITEM -------------------------   '
		,A50,'    QTY      -QTY      +QTY   -12 MON      =NET   '
;;;		,A50,'    QTY      -QTY      +QTY   -REORDR      =NET   '
		,A32,'                                '
RECORD HDR2
		,A50,'NUMBER           DESCRIPTION                      '
		,A50,' ONHAND    COMMIT    ONORDR       AVG     AVAIL   '
;;;		,A50,' ONHAND    COMMIT    ONORDR     POINT     AVAIL   '
		,A09,'MNTH OH  '
;;;		,A09,'BLD QTY  '
		,A09,'    NEED '
;;;		,A09,'  WEIGHT '
		,A14

record
	test1	,a132
	
RECORD	PARAM
	STCAT	,A2
	ENCAT	,A2
	STITM	,A15
	ENITM	,A15
	PCT	,D3

RECORD	CHANNEL
	CHN041	,D2
	CHN182	,D2

RECORD	VARS
	REOPCT	,D10
	OPNOK	,D1
	ENTRY	,A30
	INXCTL	,D1
	CNGCTL	,D1
	WHATNO	,D2
	BLANKS	,A15
	ZRO	,A5,	'00000'
	LPARG	,D1
	V	,D1
	READ	,D1,0
	LOKCTL	,D1
	SWITCH	,D1
;;;	TITLE	,A*,	'OUT OF STOCK REPORT - DEPT I'
	TITLE	,A*,	'REORDER ADVICE REPORT'
	PLINE	,A132
	LINCNT	,D2,	60
	PGCNT	,D3
	RPTNUM	,D3
	PRTTYP	,A1
	LPSW	,D1
	SPLFIL	,A14
	RECNO	,D5
	MASK	,A8,	'ZZZ,ZZX-'
	XF1	,D3
	XF2	,D3
	XF3	,D5
	DEC	,D18
	DECMAL	,D18
;
PROC 
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'REORDER ADVICE REPORT',1)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO ENDOFF
DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,'REORDER ADVICE REPORT',1)
	XCALL OUTPT (4,4,0,'1. PRDCAT:      THRU:',1)
	XCALL OUTPT (6,4,0,'2. ITEM:                   THRU:',1)
;                           4567890123456789012345678901234567890
;                                 1         2         3
	XCALL OUTPT (8,4,0,'3. PERCENT:',1)

CAT,
	XCALL OUTPT (4,4,1,'1. PRDCAT:      THRU:',1)
	XCALL INPUT (4,15,02,00,'AE',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	STCAT = ENTRY(1,2)
	IF (STCAT .EQ. BLANKS)
	THEN	BEGIN
		ENCAT = 'ZZ'
		XCALL OUTPT (4,15,1,'ALL',1)
		END
	ELSE	BEGIN
		XCALL INPUT (4,26,02,00,'AE',ENTRY,INXCTL,1)
		GOTO (DISPLA),INXCTL
		ENCAT = ENTRY(1,2)
		IF(ENCAT.EQ.BLANKS)
			BEGIN
			ENCAT = STCAT
			XCALL OUTPT (4,26,0,ENCAT,1)
			END
		END
	GOTO (ANYCN),CNGCTL
ITEM,
	XCALL OUTPT (6,4,0,'2. ITEM:                   THRU:',1)
	XCALL INPUT (6,13,15,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	STITM = ENTRY(1,15)
	IF (STITM .EQ. BLANKS)
	THEN	BEGIN
		ENITM = 'ZZZZZZZZZZZZZZZ'
		XCALL OUTPT (6,13,1,'ALL',1)
		END
	ELSE	BEGIN
		XCALL INPUT (6,37,15,00,'A ',ENTRY,INXCTL,1)
		GOTO (DISPLA),INXCTL
		ENITM = ENTRY(1,15)
		IF(ENITM.EQ.BLANKS)
			BEGIN
			ENITM = STITM
			XCALL OUTPT (6,37,0,ENITM,1)
			END
		END
	GOTO (ANYCN),CNGCTL

PCT,
	XCALL INPUT (8,16,03,00,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	PCT = ENTRY(1,3)
	GOTO ANYCN
ANYCN,
	XCALL ANYCN (CNGCTL,WHATNO)
	GOTO (PROCES, CNGBR),CNGCTL+1
CNGBR,
	GOTO (CAT, ITEM, PCT), WHATNO
	GOTO ANYCN

PROCES,
	L_PCT = PCT
	L_SCAT = STCAT
	L_ECAT = ENCAT
	L_SITM = STITM
	L_EITM = ENITM

	SPLFIL (5,6) = 'EA'
	LPSW = 1				;PRINTER: PRINT SPOOL OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
RDHDR,
	read (1, itmmas, ^first, keynum:2) [err=eof]
RDLOOP,
	lokctl = 1
	xcall ios (1, itmmas, read, lokctl)
	if (lokctl .ne. 0) goto eof

	IF (STOCK .NE. 'S') GOTO RDLOOP
	IF (PRDCAT .LT. STCAT) GOTO RDLOOP
	IF (PRDCAT .GT. ENCAT) GOTO EOF

	IF (ITEMNO.LT.STITM .OR. ITEMNO.GT.ENITM) GOTO RDLOOP

	IF (INN .EQ. 1) GOTO SKIP_DKEY		;this item doesn't use notes

	CLEAR XF1, XF2, XF3
	ONERROR NOT_NUM
	XF1 = IF1
	XF2 = IF2
	XF3 = IF3
	OFFERROR
NOT_NUM,
	DEC = XF1 + XF2 + XF3
	IF (DEC .NE. 0) GOTO SKIP_DKEY		;has a key

; if no f_key's and there is a default key, then skip this item...
	CLEAR TBL_KEY
	TBLCOD = 'IK'
	IK_ITEM = ITEMNO
	XCALL ISIO (CHN182, COPTBL, TBL_KEY, READ, LOKCTL)
	IF (LOKCTL.EQ.0) 
		BEGIN
		DEC = IK_F1 + IK_F2 + IK_F3	;in case 000-000-00000 is in table
		IF (DEC .ne. 0)	GOTO RDLOOP	
		END
;;;	IF (LOKCTL.EQ.0) GOTO RDLOOP	

SKIP_DKEY,
;;;	IF (USRDEF .NE. 'I') GOTO RDLOOP

	REOPCT = REOLVL
	IF (PCT .GT. 0) REOPCT = (REOLVL * PCT)#2

	DECMAL = ( QTYONH - QTYCOM + QTYONO - REOPCT)

	IF (DECMAL .GE. 0) GOTO RDLOOP

	DECMAL = ( QTYONH - QTYCOM + QTYONO - REOLVL)

	PLINE (1,15) = ITEMNO
	PLINE (19,47) = DESCR
	PLINE (51,58) = QTYONH, MASK
	PLINE (61,68) = QTYCOM, MASK
	PLINE (71,78) = QTYONO, MASK
	PLINE (81,88) = REOLVL, MASK
	PLINE (91,98) = DECMAL, MASK
	PLINE (101,108) = ORDUPT, MASK
	PLINE (111,117) = DECMAL*ordupt,	'ZZZZ,ZZX'
;;;	PLINE (111,117) = WEIGHT, 'ZZZX.XX'
;;;	IF (FTBNDL.GT.0) PLINE(121,130) = FTBNDL, 'ZZX/BNDL'
;;;	IF (FTCASE.GT.0) PLINE(121,130) = FTCASE, 'ZZZX/CASE'
	IF (IBXQTY.GT.0) 
		BEGIN
		PLINE(121,130) = IBXQTY, 'ZZZX/box'
		PLINE (111,117) = ((DECMAL*ordupt*10)/IBXQTY)#1, 'ZZZZ,ZZX'
		END
	CALL PRINT
;;;	GOTO (RDLOOP),INN
	IF (IF1.NE.ZRO .OR. IF2.NE.ZRO .OR. IF3.NE.ZRO)
		BEGIN
		PLINE(1,3) = IF1
		PLINE(5,7) = IF2
		PLINE(9,13) = IF3
		CALL PRINT
		END

	GOTO RDLOOP
PRINT,
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,' ',LEG,
&		' ',' ',0,0,132,0,LPSW,RPTNUM,PRTTYP)


	RETURN
EOF,
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('CP:IMMENU',1)

OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (1,'SI',041,SWITCH)		;FILE # 41 - ITMMAS
	IF (SWITCH.EQ.9) RETURN
	CHN041 = 1

	SWITCH = 5
	XCALL FILES (2,'SI',182,SWITCH)		;182=COPTBL
	IF (SWITCH .EQ. 9) RETURN
	CHN182 = 2

	OPNOK = 1
	RETURN
;--------------------------------------------------------

CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	
	IF (CHN041) CLOSE CHN041
	IF (CHN182) CLOSE CHN182

	RETURN
;--------------------------------------------------------

END
;---------------- ITEM -------------------------       QTY       QTY       QTY       NET    REORDR     ORDER    NOTES
;NUMBER           DESCRIPTION                       ONHAND    COMMIT    ONORDR     AVAIL     POINT     UP TO    ---------------------
;XXXXXXXXXXXXXXX  XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   XXX,XXX-  XXX,XXX-  XXX,XXX-  XXX,XXX-  XXX,XXX-  XXX,XXX-   _____________________
;123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5         6

