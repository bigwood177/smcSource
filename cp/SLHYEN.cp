; SLHYEN.COP - SALE HISTORY YEAR END
;
;	PROGRAM TO ROLLOVER SALES HISTORY FILES TO LAST YEAR AND 
;	SETUP NEW YEAR FILES.
;
;	SLHHDR	SLHLIN	SLHDUC -> -> -> SLHHYY	SLHLYY	SLHDYY
;
RECORD
	.INCLUDE 'DEF:RD184S.DEF'
	.INCLUDE 'DEF:RD185S.DEF'
	.INCLUDE 'DEF:RD186S.DEF'
	.INCLUDE 'DEF:RD188S.DEF'
	.INCLUDE 'DEF:RD189S.DEF'
RECORD
	LSTYY	,D2
	NFILE	,A14
	OFILE	,A14
	TODAY	,D6
	ENTRY	,A5
	INXCTL	,D1	
	V	,D1
	SWITCH	,D1
	ROW	,D2
	I	,D3
RECORD	,X
	ALSTYY	,A2
		,A8
	NYY	,D2
PROC
	XCALL TERID (V)
	XCALL RDATE (TODAY)
	LSTYY = TODAY(5,6) - 1
	XCALL OUTPT (1,1,2,'SALES HISTORY YEAR END',1)
	XCALL OUTPT ( 7,10,0,'This program will roll the current sales history files ',1)
	XCALL OUTPT ( 8,10,0,'over to last year''s data and set up new current year files.',1)
	XCALL OUTPT ( 9,10,0,'According to the current system date, it appears the following',1)
	XCALL OUTPT (10,10,0,'files will be affected:',1)
	XCALL OUTPT (12,10,0,'SLHHDR ---> SLHH',1)
	XCALL OUTPT (0,0,0,ALSTYY,1)
	XCALL OUTPT (13,10,0,'SLHLIN ---> SLHL',1)
	XCALL OUTPT (0,0,0,ALSTYY,1)
	XCALL OUTPT (14,10,0,'SLHDUC ---> SLHD',1)
	XCALL OUTPT (0,0,0,ALSTYY,1)
	XCALL OUTPT (15,10,0,'SBLHDR ---> SBLH',1)
	XCALL OUTPT (0,0,0,ALSTYY,1)
	XCALL OUTPT (16,10,0,'SBLLIN ---> SBLL',1)
	XCALL OUTPT (0,0,0,ALSTYY,1)
	XCALL OUTPT (18,10,0,'IS THIS CORRECT <N> ?',1)
	XCALL INPUT (18,33,01,00,'YN',ENTRY,INXCTL,1)
	IF (INXCTL.NE.1) GOTO ABORT
	XCALL OUTPT (20,10,0,'ARE YOU ABSOLUTELY POSITIVE YOU WISH TO PROCEED <N> ?',1)
	XCALL INPUT (20,66,01,00,'YN',ENTRY,INXCTL,1)
	IF (INXCTL.NE.1) GOTO ABORT
	ROW = 11
	XCALL OUTPT (ROW,1,2,'\',1)
	FOR I FROM 184 THRU 189
	BEGIN
	  IF (I.EQ.187) NEXTLOOP	;187 DOES NOT ROLL OVER --- SKIP!!!!
	  INCR ROW
	  XCALL OUTPT (ROW,10,0,'\',1)
	  XCALL FFILE (I,OFILE,SWITCH)
	  OFILE(14,14) = 'M'
	  XCALL OUTPT (0,0,0,OFILE,1)
	  XCALL OUTPT (0,0,0,' ---> ',1)
	  NFILE = OFILE
	  NYY = LSTYY
	  XCALL OUTPT (0,0,0,NFILE,1)
	  XCALL RENAM (1,NFILE,OFILE)
	  XCALL OUTPT (0,0,0,' ... OK',1)
	  INCR ROW
	  XCALL OUTPT (ROW,10,0,'\',1)
	  OFILE (14,14) = '1'
	  XCALL OUTPT (0,0,0,OFILE,1)
	  XCALL OUTPT (0,0,0,' ---> ',1)
	  NFILE (14,14) = '1'
	  XCALL OUTPT (0,0,0,NFILE,1)
	  XCALL RENAM (1,NFILE,OFILE)
	  XCALL OUTPT (0,0,0,' ... OK',1)
	  NFILE (14,14) = 'M'
	  IF (I.EQ.184) XCALL ISAMC (NFILE,SIZ184,NKEY184,K184_1,K184_2,K184_3,K184_4,K184_5)
	  IF (I.EQ.185) XCALL ISAMC (NFILE,SIZ185,NKEY185,K185_1,K185_2,K185_3)
	  IF (I.EQ.186) XCALL ISAMC (NFILE,SIZ186,NKEY186,K186_1)
	  IF (I.EQ.188) XCALL ISAMC (NFILE,SIZ188,NKEY188,K188_1)
	  IF (I.EQ.189) XCALL ISAMC (NFILE,SIZ189,NKEY189,K189_1)
	END
	XCALL MESAG ('ROLLOVER SUCCESSFUL',2)
ABORT,
	XCALL PGCHN ('CP:SPCFUN',1)
END
