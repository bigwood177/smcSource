;SLHCA2.CP
;

RECORD	PRDACT
	.INCLUDE 'DEF:RD069A.DEF'

RECORD	SLSSUM
	SCAT	,A2
	SMM	,D2
	SYY	,D4	
		,A1
	SDOLR	,D10
;
RECORD,X
	SL_KEY	,A8



RECORD	TITLE
		,A*,	'SALES CATEGORY RECAP '
	TYEAR	,D4

RECORD	HDR1
		,A*,	'ACCT     DESCRIPTION                         SALES'

RECORD	PRINT
	PLINE	,A80
	LEG1	,A9,	'NO LEGEND'
	LPARG	,D1
	LINCNT	,D2,	62
	PGCNT	,D3
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	LPSW	,D1
	PRTCTL	,D3,	080

RECORD	CHANNEL
	CHN058	,D2
	CHN069	,D2

RECORD	VARS
	OPNOK	,D1
	ENTRY	,A30
	INXCTL	,D1
	CNGCTL	,D1
	WHATNO	,D2
	LOKCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	SAVACT	,D7
	SAVCAT	,A2
	SAVDES	,A30
	CATTOT	,D10
	TOTAMT	,D10
	YEAR	,D4
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID(V)
	XCALL OUTPT(1,1,2,'YEARLY SALES CATEGORY RECAP',1)
	CALL OPENS
	IF (.NOT. OPNOK) GOTO ENDOFF

	LPSW = 1
	SPLFIL =
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) RETURN
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)

DISPLA,
	XCALL OUTPT(1,1,2,'YEARLY SALES CATEGORY RECAP',1)
	XCALL OUTPT (4,4,0,'YEAR: ',1)
	XCALL INPUT(4,12,04,04,'#E',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	YEAR = ENTRY(1,4)
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (DISPLA),CNGCTL

	SAVCAT = '-1'
	CLEAR CATTOT, TOTAMT
	TYEAR = YEAR
LOOP,
	READS(CHN058, SLSSUM, EOF)
	IF (SCAT .NE. SAVCAT) CALL NEWCAT
	IF (SYY .NE. YEAR) GOTO LOOP
	CATTOT = CATTOT + SDOLR
	GOTO LOOP

NEWCAT,
	IF (SAVCAT .EQ. '-1') GOTO OUTCAT
	PLINE(1,8) = SAVACT,	'ZZXX-XXX'
	PLINE(10,39) = SAVDES
	PLINE(41,54) = CATTOT,	'ZZ,ZZZ,ZZX.XX-'
	IF (CATTOT .NE. 0) CALL PRINT
OUTCAT,
	SAVCAT = SCAT
	SAVDES = '*** NOT ON FILE ***'
	SAVACT = '0000000'
	READ(CHN069,PRDACT,SCAT)[ERR=BADCAT]
	SAVDES = PACTDS
	SAVACT = PACTNO
BADCAT,
	TOTAMT = TOTAMT + CATTOT
	CLEAR CATTOT

	RETURN

PRINT,	
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,'NO HDR',' ',
&		LEG1,'NO LEGEND',' ',0,80,PRTCTL,1,LPSW,RPTNUM,PRTTYP)
	RETURN

EOF,
	CALL NEWCAT
	XCALL FILL ('_',PLINE(41,54))
	CALL PRINT

	PLINE(10,39) = 'TOTAL SALES'
	PLINE(41,54) = TOTAMT,	'ZZ,ZZZ,ZZX.XX-'
	CALL PRINT
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)

ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('CP:COPRPT',1)
	STOP

OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES(9,'SI',069,SWITCH)	;PRDACT
	IF (SWITCH .EQ. 9) RETURN
	CHN069 = 9

	SWITCH = 5
	XCALL FILES (8,'SI',58,SWITCH)		;FILE 58 - SLSSUM
	IF (SWITCH.EQ.9) RETURN
	CHN058 = 8


	OPNOK = 1
	RETURN
;---------------------------------------

CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (CHN058) CLOSE CHN058
	IF (CHN069) CLOSE CHN069
	RETURN
;---------------------------------------

END


;ZZZX-XXX AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA ZZ,ZZZ,ZZX.XX-
;12345678901234567890123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5         6         7
