;LVRMNT.CP
;		LOUVER PRICING

RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'

RECORD	CHANNEL
	CHN182	,D2

RECORD	PRINT
	TITLE	,A*,	'LOUVER TABLE'
	HD	,A6,	'NO HDR'
	LG	,A9,	'NO LEGEND'
	PLINE	,A132
	PRNTON	,D1
	LINCNT	,D2,60
	PGCNT	,D6
	LPSW	,D2
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	PRTCTL	,D3,080
	LPARG	,D1
	PRNTSW	,D1
	PRTCTR	,D1

RECORD	HD1
	,A*,	
&	'-------GALV/PAINT GRIP--------  --------ALUM/COLOR CLAD--------'
RECORD	HD2
	,A*,	'   MIN  2" FRM  4" FRM  6" FRM      MIN  2" FRM  4" FRM  6" FRM'

RECORD	VARS
	OPNOK	,D1
	BF	,A1
	PGM	,D1
	ENTRY	,A30
	INXCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	STORE	,D1,2
	DELETE	,D1,3
	LOKCTL	,D1
	WHATNO	,D2
	SELECT	,D1
	CNGCTL	,D1
	I	,D3
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'LOUVER TABLE',1)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO CLOSE

MENU,
	XCALL OUTPT (1,1,2,'LOUVER TABLE',1)
	XCALL OUTPT (3,9,0,'PLEASE SELECT APPLICATION',1)
	XCALL OUTPT (5,15,0,'1. TABLE MAINTENANCE',1)
	XCALL OUTPT (6,15,0,'2. PRINT TABLE',1)
MINPUT,
	XCALL INPUT (3,36,1,1,'#E',ENTRY,INXCTL,1)
	GOTO (MINPUT,ENDOFF), INXCTL
	PGM = ENTRY(1,1)
	GOTO (DISPLA,PRINT_TABLE),PGM
	GOTO MINPUT

DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,'LOUVER TABLE',1)
	XCALL OUTPT ( 2,4,0,'   BOX or FLANGE (B/F)',1)
	XCALL OUTPT ( 4,4,0,'1. MINIMUM PRICE GALV/PG',1)
	XCALL OUTPT ( 6,4,0,'2. PRICE/SQFT 2"',1)
	XCALL OUTPT ( 8,4,0,'3. PRICE/SQFT 4"',1)
	XCALL OUTPT (10,4,0,'4. PRICE/SQFT 6"',1)
	XCALL OUTPT (12,4,0,'5. MINIMUM PRICE AL/UNACLAD',1)
	XCALL OUTPT (14,4,0,'6. PRICE/SQFT 2"',1)
	XCALL OUTPT (16,4,0,'7. PRICE/SQFT 4"',1)
	XCALL OUTPT (18,4,0,'8. PRICE/SQFT 6"',1)
;                            45678901234567890123456789012345678901234567890
;                                  1         2         3         4         5

	XCALL INPUT (2,33,01,01,'AE',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	BF = ENTRY(1,1)
	USING BF SELECT
	('B'),		XCALL OUTPT (2,33,1,'BOX',1)
	('F'),		XCALL OUTPT (2,33,1,'FLANGE',1)
	(),		BEGIN
			XCALL MESAG ('MUST BE "B" OR "F"',1)
			GOTO DISPLA
			END
	ENDUSING

	CLEAR TBL_KEY
	TBLCOD = 'LV'
	TBLKEY = BF

	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .EQ. 0)
		BEGIN
		SELECT = 2
		CALL DSPREC
		GOTO ANYCNG
		END


	SELECT = 1			;ADD MODE
	CLEAR COPTBL
GMIN,
	XCALL INPUT (4,33,05,00,'$E',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	LV_GMIN = ENTRY(1,5)
	GOTO (ANYCNG),CNGCTL
G02,
	XCALL INPUT (6,33,05,00,'$ ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	LV_G02 = ENTRY(1,5)
	GOTO (ANYCNG),CNGCTL
G04,
	XCALL INPUT (8,33,05,00,'$ ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	LV_G04 = ENTRY(1,5)
	GOTO (ANYCNG),CNGCTL
G06,
	XCALL INPUT (10,33,05,00,'$ ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	LV_G06 = ENTRY(1,5)
	GOTO (ANYCNG),CNGCTL

AMIN,
	XCALL INPUT (12,33,05,00,'$E',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	LV_AMIN = ENTRY(1,5)
	GOTO (ANYCNG),CNGCTL
A02,
	XCALL INPUT (14,33,05,00,'$ ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	LV_A02 = ENTRY(1,5)
	GOTO (ANYCNG),CNGCTL
A04,
	XCALL INPUT (16,33,05,00,'$ ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	LV_A04 = ENTRY(1,5)
	GOTO (ANYCNG),CNGCTL
A06,
	XCALL INPUT (18,33,05,00,'$ ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	LV_A06 = ENTRY(1,5)
	GOTO (ANYCNG),CNGCTL

ANYCNG,
	XCALL OUTPT (24,1,1,'FIELD # TO CHANGE            <TAB> = DELETE',1)
	XCALL INPUT (24,20,02,00,'#T',ENTRY,INXCTL,1)
	IF (INXCTL .EQ. 3)
		BEGIN
		XCALL OUTPT (24,1,1,'DELETE, ARE YOU SURE ?',1)
		XCALL INPUT (24,24,01,01,'YN',ENTRY,INXCTL,1)
		GOTO (ANYCNG),INXCTL-1
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,DELETE,LOKCTL)
		GOTO DISPLA
		END

	WHATNO = ENTRY(1,2)
	IF (WHATNO .EQ. 0)
	THEN 	CLEAR CNGCTL
	ELSE 	CNGCTL = 1

	GOTO (PROCES,CNGBR),CNGCTL+1
CNGBR,
	GOTO (GMIN,G02,G04,G06,AMIN,A02,A04,A06),WHATNO
	GOTO ANYCNG

PROCES,
	CASE SELECT OF
	BEGINCASE
	1:	BEGIN
		TBLCOD = 'LV'
		TBLKEY = BF
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,STORE,LOKCTL)
		END
	2:	XCALL ISIO (CHN182,COPTBL,TBL_KEY,WRITE,LOKCTL)
	ENDCASE
	GOTO MENU

ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('CP:TBLMNU',1)
	STOP


DSPREC,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; DISPLAY RECORD
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	ENTRY(1,6) = LV_GMIN,	'ZZX.XX' [LEFT]
	XCALL OUTPT (4,33,1,ENTRY(1,6),1)

	ENTRY(1,6) = LV_G02,	'ZZX.XX' [LEFT]
	XCALL OUTPT (6,33,1,ENTRY(1,6),1)

	ENTRY(1,6) = LV_G04,	'ZZX.XX' [LEFT]
	XCALL OUTPT (8,33,1,ENTRY(1,6),1)

	ENTRY(1,6) = LV_G06,	'ZZX.XX' [LEFT]
	XCALL OUTPT (10,33,1,ENTRY(1,6),1)

	ENTRY(1,6) = LV_AMIN,	'ZZX.XX' [LEFT]
	XCALL OUTPT (12,33,1,ENTRY(1,6),1)

	ENTRY(1,6) = LV_A02,	'ZZX.XX' [LEFT]
	XCALL OUTPT (14,33,1,ENTRY(1,6),1)

	ENTRY(1,6) = LV_A04,	'ZZX.XX' [LEFT]
	XCALL OUTPT (16,33,1,ENTRY(1,6),1)

	ENTRY(1,6) = LV_A06,	'ZZX.XX' [LEFT]
	XCALL OUTPT (18,33,1,ENTRY(1,6),1)

	RETURN
;-----------------------------------


;===================================================================
PRINT_TABLE,
;===================================================================

	LINCNT = 66
	PGCNT  = 0
	
PDISP,
	CLEAR TBL_KEY
	TBLCOD = 'LV'
	FIND (CHN182,COPTBL,TBL_KEY)[ERR=PT_LOOP]
PT_LOOP,
	XCALL IOS (CHN182,COPTBL,READ,LOKCTL)
	IF (LOKCTL .NE. 0) GOTO PT_EOF
	IF (TBLCOD .NE. 'LV') GOTO PT_EOF
	
	PLINE(1,6) = LV_GMIN,	'ZZX.XX'
	PLINE(9,14) = LV_G02,	'ZZX.XX'
	PLINE(17,22) = LV_G04,	'ZZX.XX'
	PLINE(25,30) = LV_G06,	'ZZX.XX'
	PLINE(34,39) = LV_AMIN,	'ZZX.XX'
	PLINE(42,47) = LV_A02,	'ZZX.XX'
	PLINE(50,55) = LV_A04,	'ZZX.XX'
	PLINE(58,63) = LV_A06,	'ZZX.XX'

	USING TBLKEY SELECT
	('B'),	PLINE (65,70) = 'BOX'
	('F'),	PLINE (65,70) = 'FLANGE'
	ENDUSING

	CALL PRINT
	GOTO PT_LOOP
PT_EOF,
EOF,
	IF (PRNTON.EQ.1)  XCALL LPOFF(LPSW,SPLFIL,PGCNT)
	PRNTON  = 0
	CLOSE 10
PDONE,
	GOTO MENU

PRINT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (PRNTON .EQ. 0) CALL PRNTON
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HD1,HD2,HD
&		,LG,LG,LG,0,080,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;-------------------------------------------------------------
PRNTON,
	SPLFIL (5,6) = 'EF'
	LPSW = 1		;PRINT,SPOOL, OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTON = 1
	RETURN
;-------------------------------------------------------------
;===================================================================


OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (1,'SU',182,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN182 = 1

	OPNOK = 1
	RETURN
;----------------------------------------------------

CLOSE,
	CLOSE CHN182
	RETURN
;----------------------------------------------------
	
	
;-------GALV/PAINT GRIP--------  --------ALUM/COLOR CLAD--------
;   MIN  2" FRM  4" FRM  6" FRM      MIN  2" FRM  4" FRM  6" FRM
;ZZZ.XX  ZZZ.XX  ZZZ.XX  ZZZ.XX   ZZZ.XX  ZZZ.XX  ZZZ.XX  ZZZ.XX 
;12345678901234567890123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5         6         7         8
