;COOMNT.CP
;	COP TABLE MAINTENANCE
;	COMPANY INFO
;
RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'

RECORD	COINFO
	.INCLUDE 'DEF:COINFO.DEF'

RECORD	CHANNEL
	CHN182	,D2

RECORD	PRINT
	TITLE	,A*,	'COMPANY INFO'
	HD	,A6,	'NO HDR'
	LG	,A9,	'NO LEGEND'
	PLINE	,A132
	PRNTON	,D1
	LINCNT	,D2,60
	PGCNT	,D6
	LPSW	,D2
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	PRTCTL	,D3,132
	LPARG	,D1
	PRNTSW	,D1
	PRTCTR	,D1


RECORD	HD1
		,A*,	'CODE NAME                                ADD1'
		,A*,	'                      ADD2'

RECORD	HD2
		,A*,	'      LOCAL PHONE      800-NUMBER             '
		,A*,	'FAX    WEB SITE'

RECORD	VARS
	ALPHA	,A12
	A3	,A3
	OPNOK	,D1
	BLANKS	,A15
	MBOX	,D1
	ST_CO	,A3
	EN_CO	,A3
	PGM	,D1
	ROW	,D2
	ENTRY	,A35
	INXCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	STORE	,D1,2
	DELETE	,D1,3
	LOKCTL	,D1
	WHATNO	,D2
	SELECT	,D1
	CNGCTL	,D1
	I	,D3
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'COMPANY INFO',1)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO CLOSE

MENU,
	XCALL OUTPT (1,1,2,'COMPANY INFO',1)
	XCALL OUTPT (3,9,0,'PLEASE SELECT APPLICATION',1)
	XCALL OUTPT (5,15,0,'1. TABLE MAINTENANCE',1)
	XCALL OUTPT (6,15,0,'2. PRINT TABLE',1)
MINPUT,
	XCALL INPUT (3,36,1,1,'#E',ENTRY,INXCTL,1)
	GOTO (MINPUT,ENDOFF), INXCTL
	PGM = ENTRY(1,1)
	GOTO (DISPLA,PRINT_TABLE),PGM
	GOTO MINPUT

DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,'COMPANY INFO',1)
	XCALL OUTPT ( 4,4,0,'1. COMPANY CODE',1)
	XCALL OUTPT ( 6,4,0,'2. NAME',1)
	XCALL OUTPT ( 8,4,0,'3. ADD1',1)
	XCALL OUTPT (10,4,0,'4. ADD2',1)
	XCALL OUTPT (12,4,0,'5. PHONE',1)
	XCALL OUTPT (14,4,0,'6. 800 #',1)
	XCALL OUTPT (16,4,0,'7. FAX',1)
	XCALL OUTPT (18,4,0,'8. WEB',1)
	
CODE,
	XCALL INPUT (4,21,03,00,'AE',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	CODE = ENTRY(1,3)
CHKTBL,
	CLEAR TBL_KEY
	TBL_KEY(1,2) = 'CO'
	C1_CODE = CODE
	C1_REC = 1
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .EQ. 0)
		BEGIN
		SELECT = 2
		CALL DSPREC
		GOTO ANYCNG
		END

	SELECT = 1			;ADD MODE
	CLEAR COPTBL

NAME,
	XCALL INPUT (6,14,35,00,'LE',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	C_NAME = ENTRY(1,35)
	GOTO (ANYCNG),CNGCTL
ADD1,
	XCALL INPUT(8,14,25,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	C_ADD1 = ENTRY(1,25)
	GOTO (ANYCNG),CNGCTL
ADD2,
	XCALL INPUT(10,14,25,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	C_ADD2 = ENTRY(1,25)
	GOTO (ANYCNG),CNGCTL
LOC,
	XCALL INPUT(12,14,10,00,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	C_LOC = ENTRY(1,10)
	ENTRY(1,12) = C_LOC,'ZZZ ZZZ-XXXX'
	XCALL OUTPT (12,14,1,ENTRY(1,12),1)
	GOTO (ANYCNG),CNGCTL
LD,
	XCALL INPUT(14,14,10,00,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	C_LD = ENTRY(1,10)
	ENTRY(1,12) = C_LD,'ZZZ ZZZ-XXXX'
	XCALL OUTPT (14,14,1,ENTRY(1,12),1)
	GOTO (ANYCNG),CNGCTL
FAX,
	XCALL INPUT(16,14,10,00,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	C_FAX = ENTRY(1,10)
	ENTRY(1,12) = C_FAX, 'ZZZ ZZZ-XXXX'
	XCALL OUTPT (16,14,1,ENTRY(1,12),1)
	GOTO (ANYCNG),CNGCTL
WEB,
	XCALL INPUT (18,14,30,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	C_WEB = ENTRY
	GOTO ANYCNG

ANYCNG,
	XCALL OUTPT (24,1,1,'FIELD # TO CHANGE            <TAB> = DELETE',1)
	XCALL INPUT (24,20,02,00,'#T',ENTRY,INXCTL,1)
	IF (INXCTL .EQ. 3)
		BEGIN
		XCALL OUTPT (24,1,1,'DELETE, ARE YOU SURE ?',1)
		XCALL INPUT (24,24,01,01,'YN',ENTRY,INXCTL,1)
		GOTO (ANYCNG),INXCTL-1
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,DELETE,LOKCTL)
		C2_REC = 2
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,DELETE,LOKCTL)
		GOTO DISPLA
		END

	WHATNO = ENTRY(1,2)
	IF (WHATNO .EQ. 0)
	THEN 	CLEAR CNGCTL
	ELSE 	CNGCTL = 1

	GOTO (PROCES,CNGBR),CNGCTL+1
CNGBR,
	GOTO (CODE,NAME,ADD1,ADD2,LOC,LD,FAX,WEB),WHATNO
	GOTO ANYCNG

PROCES,
	CASE SELECT OF
	BEGINCASE
	1:	BEGIN
		CALL SET_1
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,STORE,LOKCTL)
		
		CALL SET_2
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,STORE,LOKCTL)
		END
	2:	BEGIN
		C1_REC = 1
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
		IF (LOKCTL .EQ. 0)
			BEGIN
			CALL SET_1
			XCALL ISIO (CHN182,COPTBL,TBL_KEY,WRITE,LOKCTL)
			END
		C2_REC = 2
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
		IF (LOKCTL .EQ. 0)
			BEGIN
			CALL SET_2
			XCALL ISIO (CHN182,COPTBL,TBL_KEY,WRITE,LOKCTL)
			END
		END
	ENDCASE

	GOTO DISPLA


SET_1,	;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR COPTBL
	TBLCOD = 'CO'
	C1_CODE = CODE
	C1_REC = 1
	C1_NAME = C_NAME
	C1_ADD1 = C_ADD1
	C1_ADD2 = C_ADD2
	RETURN
SET_2,
	CLEAR COPTBL
	TBLCOD = 'CO'
	C2_CODE = CODE
	C2_REC = 2
	C2_LOC = C_LOC
	C2_LD = C_LD
	C2_FAX = C_FAX
	C2_WEB = C_WEB
	RETURN
;------------------------------------

ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('CP:TBLMNU',1)
	STOP


DSPREC,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; DISPLAY RECORD
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CALL GET_1
	CALL GET_2

	XCALL OUTPT (06,14,0,C_NAME,1)
	XCALL OUTPT (08,14,0,C_ADD1,1)
	XCALL OUTPT (10,14,0,C_ADD2,1)
	ENTRY(1,12) = C_LOC,	'ZZZ ZZX-XXXX'
	XCALL OUTPT (12,14,0,ENTRY(1,12),1)
	ENTRY(1,12) = C_LD,	'ZZZ ZZX-XXXX'
	XCALL OUTPT (14,14,0,ENTRY(1,12),1)
	ENTRY(1,12) = C_FAX,	'ZZZ ZZX-XXXX'
	XCALL OUTPT (16,14,0,ENTRY(1,12),1)
	XCALL OUTPT (18,14,0,C_WEB,1)
	RETURN
;-----------------------------------

GET_1,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR TBL_KEY
	TBL_KEY(1,2) = 'CO'
	C1_CODE = CODE
	C1_REC = 1
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)

	C_NAME = C1_NAME
	C_ADD1 = C1_ADD1
	C_ADD2 = C1_ADD2
	RETURN
;-----------------------------------

GET_2,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;	
	CLEAR TBL_KEY
	TBL_KEY(1,2) = 'CO'
	C2_CODE = CODE
	C2_REC = 2
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)

	C_LOC = C2_LOC
	C_LD =  C2_LD
	C_FAX = C2_FAX
	C_WEB = C2_WEB
	RETURN
;-----------------------------------

;===================================================================
PRINT_TABLE,
;===================================================================
	LINCNT = 66
	PGCNT  = 0
	
PDISP,
	CNGCTL = 
	XCALL OUTPT (1,1,2,'PRINT COMPANY INFO',1)
	XCALL OUTPT (4,4,0,'1. STARTING COMPANY',1)
	XCALL OUTPT (6,4,0,'2. ENDING   COMPANY',1)
ST_CO,
	XCALL INPUT (4,27,03,00,'AE',ENTRY,INXCTL,1)
	GOTO (PDISP,PDONE),INXCTL
	ST_CO = ENTRY(1,3)
	IF (ST_CO .EQ. A3)
		BEGIN
		EN_CO = 'ZZZ'
		XCALL OUTPT (4,25,1,'ALL',1)
		XCALL OUTPT (6,25,1,' ',1)
		GOTO P_ANY
		END
	GOTO (P_ANY),CNGCTL
EN_CO,
	XCALL INPUT (6,25,03,00,'A ',ENTRY,INXCTL,1)
	GOTO (PDISP),INXCTL
	EN_CO = ENTRY(1,3)
	IF (EN_CO .EQ. A3)
		BEGIN
		EN_CO = ST_CO
		XCALL OUTPT (6,25,0,EN_CO,1)
		END
P_ANY,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (P_PRINT,P_CNGBR),CNGCTL + 1
P_CNGBR,
	GOTO (ST_CO,EN_CO),WHATNO
	GOTO P_ANY

P_PRINT,
	PRNTON = 0
	CLEAR TBLKEY
	TBLCOD = 'CO'
	C1_CODE = ST_CO
	FIND (CHN182,COPTBL,TBL_KEY)[ERR=PLOOP]

PLOOP,
	XCALL IOS (CHN182,COPTBL,READ,LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF
	IF (TBLCOD .NE. 'CO') GOTO EOF
	IF (C1_CODE .LT. ST_CO) GOTO PLOOP
	IF (C1_CODE .GT. EN_CO) GOTO EOF

	CODE = C1_CODE
	PLINE(1,3) = C1_CODE
	PLINE(6,40) = C1_NAME
	PLINE(42,66) = C1_ADD1
	PLINE(68,92) = C1_ADD2
	CALL PRINT

	XCALL IOS (CHN182,COPTBL,READ,LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF
	IF (TBLCOD .NE. 'CO') GOTO EOF
	IF (C2_CODE .LT. CODE) GOTO PLOOP
		
	PLINE(6,17) = C2_LOC,	'ZZZ ZZX-XXXX'
	PLINE(22,33) = C2_LD,	'ZZZ ZZX-XXXX'
	PLINE(38,49) = C2_FAX,	'ZZZ ZZX-XXXX'
	PLINE(54,84) = C2_WEB
	CALL PRINT

;CODE NAME                           ADD1                      ADD2
;AAA  AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA AAAAAAAAAAAAAAAAAAAAAAAAA AAAAAAAAAAAAAAAAAAAAAAAAA
;      LOCAL PHONE      800-NUMBER             FAX    WEB SITE
;     ZZZ ZZX-XXXX    ZZZ ZZX-XXXX    ZZZ ZZX-XXXX    AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
;12345678901234567890123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5         6         7         8
	CALL PRINT
	GOTO PLOOP

EOF,
	IF (PRNTON.EQ.1)  XCALL LPOFF(LPSW,SPLFIL,PGCNT)
	PRNTON  = 0

PDONE,
	GOTO MENU

PRINT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (PRNTON .EQ. 0) CALL PRNTON
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HD1,HD2,HD
&		,LG,LG,LG,0,132,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;-------------------------------------------------------------
PRNTON,
	SPLFIL (5,6) = 'EF'
	LPSW = 1		;PRINT,SPOOL, OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTON = 1
	RETURN
;-------------------------------------------------------------
;===================================================================


OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (1,'SU',182,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN182 = 1

	OPNOK = 1
	RETURN
;----------------------------------------------------

CLOSE,
	CLOSE CHN182
	RETURN
;----------------------------------------------------
	
	



