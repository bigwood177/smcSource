
;PDTMNT.CP
;	COP TABLE MAINTENANCE
;	PRICE DUCT INFO
; 5-30-18 ssq: make cusmas isam
;
RECORD	D_STUFF
	D_IN	,D8		;DATE-IN, ANY FORMAT
	D_OUT	,D6		;RETURN VALUE MMDDYY
	D_OUTR	,D8		;RETURN VALUE CCYYMMDD
	D_FMT	,A10		;RETURN VALUE MM/DD/CCYY
	D_SW	,A2		;"99" = DATE CONVERSION ERROR

RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'

RECORD	CUSMAS
	.INCLUDE 'DEF:RD001A.DEF'
RECORD,X
	.INCLUDE 'DEF:RD001B.DEF'

;;;RECORD	CUSIDX
;;;	.INCLUDE 'DEF:RD002A.DEF'

RECORD	CHANNEL
	CHN182	,D2
	CHN001	,D2
	CHN002	,D2

RECORD	PRINT
	TITLE	,A*,	'PRICE DUCT INFO'
	HD	,A6,	'NO HDR'
	LG	,A9,	'NO LEGEND'
	PLINE	,A80
	PRNTON	,D1
	LINCNT	,D2,60
	PGCNT	,D6
	LPSW	,D2
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	PRTCTL	,D3,080
	LPARG	,D1
	PRNTSW	,D1
	PRTCTR	,D1


RECORD	HD1
		,A*,	' CUST # NAME                           ANNIV DATE'

RECORD	VARS
	CUST_FOUND	,D1
	KEY	,A6
	BSEND	,D5
	BSMID	,D5
	SRCCTL	,D1
	TDATE	,D6
	TODAY	,D8
	XDATE	,D8
	ALPHA	,A12
	OPNOK	,D1
	BLANKS	,A30
	CUST	,D6
	DESC	,A30
	LOC	,A20	
	ST_DPT	,D6
	EN_DPT	,D6
	PGM	,D1
	ROW	,D2
	ENTRY	,A30
	INXCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	STORE	,D1,2
	DELETE	,D1,3
	LOKCTL	,D1
	WHATNO	,D2
	SELECT	,D1
	CNGCTL	,D1
	I	,D3
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'PRICE DUCT INFO',1)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO CLOSE
	XCALL RDATE (TDATE)
	XCALL DATE8 (TDATE, D_OUT, TODAY, D_FMT, D_SW)

MENU,
	XCALL OUTPT (1,1,2,'PRICE DUCT INFO',1)
	XCALL OUTPT (3,9,0,'PLEASE SELECT APPLICATION',1)
	XCALL OUTPT (5,15,0,'1. TABLE MAINTENANCE',1)
	XCALL OUTPT (6,15,0,'2. PRINT TABLE',1)
MINPUT,
	XCALL INPUT (3,36,1,1,'#E',ENTRY,INXCTL,1)
	GOTO (MINPUT,ENDOFF), INXCTL
	PGM = ENTRY(1,1)
	GOTO (DISPLA,PRINT_TABLE),PGM
	GOTO MINPUT

DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,'PRICE DUCT INFO',1)
	XCALL OUTPT ( 4,4,0,'1. CUST #',1)
	XCALL OUTPT ( 6,4,0,'2. ANNIVERSARY DATE',1)
CUST,
	XCALL INPUT (4,24,06,00,'#E',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	CUST = ENTRY(1,6)
	CLEAR TBL_KEY
	TBL_KEY(1,2) = 'PD'
	PD_CUST = CUST
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .EQ. 0)
		BEGIN
		SELECT = 2
		CALL DSPREC
		GOTO ANYCNG
		END


	SELECT = 1			;ADD MODE
	CLEAR COPTBL
	PD_CUST = CUST

	CALL GETCUS
	IF (.NOT. CUST_FOUND)
		BEGIN
		XCALL MESAG ('CUSTOMER NOT ON FILE', 1)
		GOTO CUST
		END
	XCALL OUTPT (4,32,0,NAME,1)


DATE,
	XCALL INPUT (6,24,08,00,'DE',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	PD_DATE = ENTRY(1,8)
	IF (PD_DATE .EQ. 0) 
		BEGIN
		PD_DATE = TODAY
		XCALL OUTPT (6,24,0,D_FMT,1)
		END

	GOTO (ANYCNG),CNGCTL

ANYCNG,
	XCALL OUTPT (24,1,1,'FIELD # TO CHANGE            <TAB> = DELETE',1)
	XCALL INPUT (24,20,02,00,'#T',ENTRY,INXCTL,1)
	IF (INXCTL .EQ. 3)
		BEGIN
		XCALL OUTPT (24,1,1,'DELETE, ARE YOU SURE ?',1)
		XCALL INPUT (24,24,01,01,'YN',ENTRY,INXCTL,1)
		GOTO (ANYCNG),INXCTL-1
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,DELETE,LOKCTL)
		GOTO DISPLA
		END

	WHATNO = ENTRY(1,2)
	IF (WHATNO .EQ. 0)
	THEN 	CLEAR CNGCTL
	ELSE 	CNGCTL = 1

	GOTO (PROCES,CNGBR),CNGCTL+1
CNGBR,
	GOTO (CUST, DATE),WHATNO
	GOTO ANYCNG

PROCES,
	CASE SELECT OF
	BEGINCASE
	1:	BEGIN
		TBLCOD = 'PD'
		PD_CUST = CUST
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,STORE,LOKCTL)
		END
	2:	XCALL ISIO (CHN182,COPTBL,TBL_KEY,WRITE,LOKCTL)
	ENDCASE
	GOTO DISPLA

ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('CP:TBLMNU',1)
	STOP


DSPREC,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; DISPLAY RECORD
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CALL GETCUS

	ENTRY(1,6) = PD_CUST,	'ZZZZZX' [LEFT]
	XCALL OUTPT (4,24,1,ENTRY(1,6),1)
	XCALL OUTPT (4,32,0,NAME,1)

	XDATE(1,4) = PD_DATE(5,8)
	XDATE(5,8) = PD_DATE(1,4)
	ENTRY(1,10) = XDATE,	'ZX/XX/XXXX'
	XCALL OUTPT (6,24,1,ENTRY(1,10),1)
	RETURN
;-----------------------------------

GETCUS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	XCALL ISIO (CHN001, CUSMAS, PD_CUST, READ, LOKCTL)
	IF (LOKCTL .EQ. 0)
	THEN	CUST_FOUND = 1
	ELSE	CUST_FOUND = 0
	
;;;	KEY = PD_CUST, 'XXXXXX'
;;;	XCALL SERCH (CHN002,CUSIDX,KEY,1,6,BSEND,BSMID,SRCCTL,4,7,11,0,0,0,0)
;;;	IF (SRCCTL .NE. 0)
;;;	THEN	CUST_FOUND = 0
;;;	ELSE	CUST_FOUND = 1

;;;	IF (CUST_FOUND)
;;;		BEGIN
;;;		LOKCTL = 1
;;;		XCALL IO (CHN001,CUSMAS,IRC001,READ,LOKCTL)
;;;		END

	RETURN
		
;-----------------------------------------------------------------------------


;===================================================================
PRINT_TABLE,
;===================================================================
	LINCNT = 66
	PGCNT  = 0
	
PDISP,
	CNGCTL = 
	XCALL OUTPT (1,1,2,'PRINT WORK ORDER DEPARTMENTS TABLE',1)
	XCALL OUTPT (4,4,0,'1. STARTING CUST',1)
	XCALL OUTPT (6,4,0,'2. ENDING   CUST',1)
ST_DPT,
	XCALL INPUT (4,27,06,00,'#E',ENTRY,INXCTL,1)
	GOTO (PDISP,PDONE),INXCTL
	ST_DPT = ENTRY(1,6)
	IF (ST_DPT .EQ. 0)
		BEGIN
		EN_DPT = 999999
		XCALL OUTPT (4,25,1,'ALL',1)
		XCALL OUTPT (6,25,1,' ',1)
		GOTO P_ANY
		END
	GOTO (P_ANY),CNGCTL
EN_DPT,
	XCALL INPUT (6,25,06,00,'# ',ENTRY,INXCTL,1)
	GOTO (PDISP),INXCTL
	EN_DPT = ENTRY(1,6)
	IF (EN_DPT .EQ. 0)
		BEGIN
		EN_DPT = ST_DPT
		ENTRY(1,6) = EN_DPT, 'ZZZZZX' [LEFT]
		XCALL OUTPT (6,25,0,ENTRY(1,6),1)
		END
P_ANY,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (P_PRINT,P_CNGBR),CNGCTL + 1
P_CNGBR,
	GOTO (ST_DPT,EN_DPT),WHATNO
	GOTO P_ANY

P_PRINT,
	CLEAR TBL_KEY
	TBLCOD = 'PD'
	FC_PC = ST_DPT
	FIND (CHN182,COPTBL,TBL_KEY)[ERR=PLOOP]

PLOOP,
	XCALL IOS (CHN182,COPTBL,READ,LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF
	IF (TBLCOD .NE. 'PD') GOTO EOF
	IF (PD_CUST .LT. ST_DPT) GOTO PLOOP
	IF (PD_CUST .GT. EN_DPT) GOTO EOF

	CALL GETCUS
	IF (.NOT. CUST_FOUND) NAME = '*** NOT ON FILE ***'

	PLINE (2,7) = PD_CUST,	'ZZZZZX'
	PLINE (9,38) = NAME

	XDATE (1,4) = PD_DATE(5,8)
	XDATE (5,8) = PD_DATE(1,4)

	PLINE (40,49) = XDATE,	'ZX/XX/XXXX'


; CUST # NAME                           ANNIV DATE
; ZZZZZX AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA XX/XX/XXXX
;123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5
	CALL PRINT
	GOTO PLOOP

EOF,
	IF (PRNTON.EQ.1)  XCALL LPOFF(LPSW,SPLFIL,PGCNT)
	PRNTON  = 0

PDONE,
	GOTO MENU

PRINT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (PRNTON .EQ. 0) CALL PRNTON
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HD1,HD,HD
&		,LG,LG,LG,0,080,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;-------------------------------------------------------------
PRNTON,
	SPLFIL (5,6) = 'EF'
	LPSW = 1		;PRINT,SPOOL, OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTON = 1
	RETURN
;-------------------------------------------------------------
;===================================================================


OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (5,'SU',182,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN182 = 5


	SWITCH = 5
	XCALL FILES (1, 'SI', 001, SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN001 = 1

;;;	SWITCH = 5
;;;	XCALL FILES (2, 'I', 002, SWITCH)
;;;	IF (SWITCH .EQ. 9) RETURN
;;;	CHN002 = 2

;;;	LOKCTL = 1
;;;	XCALL IO (CHN001, CUSMAS, 1, READ, LOKCTL)
;;;	BSEND = ORG001

	OPNOK = 1
	RETURN
;----------------------------------------------------

CLOSE,
	IF (CHN182) CLOSE CHN182
	IF (CHN001) CLOSE CHN001
;;;	IF (CHN002) CLOSE CHN002
	RETURN
;----------------------------------------------------
	


