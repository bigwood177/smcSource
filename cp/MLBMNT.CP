;MLBMNT.CP
;
;	COP TABLE MAINTENANCE
;	MATERIAL PRICE BY F1 NOTE & GAUGE
;
RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'


RECORD	CHANNEL
	CHN182	,D2


RECORD	PRINT
	TITLE	,A*,	'COST/LB TABLE'
	HD	,A6,	'NO HDR'
	LG	,A9,	'NO LEGEND'
	PLINE	,A132
	PRNTON	,D1
	LINCNT	,D2,60
	PGCNT	,D6
	LPSW	,D2
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	PRTCTL	,D3,132
	LPARG	,D1
	PRNTSW	,D1
	PRTCTR	,D1

RECORD	HD1
		,A*,	' F1   GA   COST/LB'

RECORD	VARS
	GA	,D2
	ALPHA	,A12
	OPNOK	,D1
	BLANKS	,A30

	ST_MEMO	,D5
	EN_MEMO	,D5
	MEMO	,D5
	DESC	,A30
	PGM	,D1
	ROW	,D2
	ENTRY	,A30
	INXCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	STORE	,D1,2
	DELETE	,D1,3
	LOKCTL	,D1
	WHATNO	,D2
	SELECT	,D1
	CNGCTL	,D1
	I	,D3
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'COST/LB TABLE',1)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO CLOSE

MENU,
	XCALL OUTPT (1,1,2,'COST/LB TABLE',1)
	XCALL OUTPT (3,9,0,'PLEASE SELECT APPLICATION',1)
	XCALL OUTPT (5,15,0,'1. TABLE MAINTENANCE',1)
	XCALL OUTPT (6,15,0,'2. PRINT TABLE',1)
MINPUT,
	XCALL INPUT (3,36,1,1,'#E',ENTRY,INXCTL,1)
	GOTO (MINPUT,ENDOFF), INXCTL
	PGM = ENTRY(1,1)
	GOTO (DISPLA,PRINT_TABLE),PGM
	GOTO MINPUT

DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,'COST/LB TABLE',1)
	XCALL OUTPT ( 4,4,0,'1. F1',1)
	XCALL OUTPT ( 6,4,0,'2. GA',1)
	XCALL OUTPT ( 8,4,0,'3. COST',1)

MM_KEY,
	XCALL INPUT (4,24,03,00,'#E',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	MEMO = ENTRY(1,3)

	CLEAR TBL_KEY
	TBL_KEY(1,2) = 'M1'
	MM_KEY = MEMO
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .NE. 0) 
		BEGIN
		XCALL MESAG ('MEMO NOT FOUND',1)
		GOTO MM_KEY
		END

	XCALL OUTPT (4,30,0,M1_SHORT,1)
GA,
	XCALL INPUT (06,24,02,00,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	GA = ENTRY(1,2)
	
	CLEAR TBL_KEY
	TBLCOD = 'LB'
	LB_F1 = MEMO
	LB_GA = GA

	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .EQ. 0)
		BEGIN
		SELECT = 2
		CALL DSPREC
		GOTO ANYCNG
		END


	SELECT = 1			;ADD MODE
	CLEAR COPTBL

COST,
	XCALL INPUT (8,26,08,00,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	LB_COST = ENTRY(1,8)
	ENTRY(1,9) = LB_COST, 	'ZZZZ.XXXX' [LEFT]
	XCALL OUTPT (8,26,0,ENTRY(1,9),1)

	GOTO (ANYCNG), CNGCTL

ANYCNG,
	XCALL OUTPT (24,1,1,'FIELD # TO CHANGE            <TAB> = DELETE',1)
	XCALL INPUT (24,20,02,00,'#T',ENTRY,INXCTL,1)
	IF (INXCTL .EQ. 3)
		BEGIN
		XCALL OUTPT (24,1,1,'DELETE, ARE YOU SURE ?',1)
		XCALL INPUT (24,24,01,01,'YN',ENTRY,INXCTL,1)
		GOTO (ANYCNG),INXCTL-1
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,DELETE,LOKCTL)
		GOTO DISPLA
		END

	WHATNO = ENTRY(1,2)
	IF (WHATNO .EQ. 0)
	THEN 	CLEAR CNGCTL
	ELSE 	CNGCTL = 1

	GOTO (PROCES,CNGBR),CNGCTL+1
CNGBR,
	GOTO (MM_KEY, GA, COST),WHATNO
	GOTO ANYCNG

PROCES,
	CASE SELECT OF
	BEGINCASE
	1:	BEGIN
		TBLCOD = 'LB'
		LB_F1 = MEMO
		LB_GA = GA
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,STORE,LOKCTL)
		END
	2:	XCALL ISIO (CHN182,COPTBL,TBL_KEY,WRITE,LOKCTL)
	ENDCASE
	GOTO DISPLA

ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('CP:TBLMNU',1)
	STOP


DSPREC,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; DISPLAY RECORD
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	ENTRY(1,3) = LB_F1	,'ZZX' [LEFT]
	XCALL OUTPT (4,24,1,ENTRY(1,3),1)

	ENTRY(1,2) = LB_GA,	'ZZ'
	XCALL OUTPT ( 6,24,1,ENTRY(1,2),1)

	ENTRY(1,9) = LB_COST,	'ZZZZ.XXXX'
	XCALL OUTPT ( 8,26,1,ENTRY(1,9),1)

	RETURN
;-----------------------------------


;===================================================================
PRINT_TABLE,
;===================================================================
	LINCNT = 66
	PGCNT  = 0
	
PDISP,
	CNGCTL = 
	XCALL OUTPT (1,1,2,'PRINT MEMO TABLE',1)
	XCALL OUTPT (4,4,0,'1. STARTING CODE',1)
	XCALL OUTPT (6,4,0,'2. ENDING   CODE',1)
ST_MEMO,
	XCALL INPUT (4,25,03,00,'#E',ENTRY,INXCTL,1)
	GOTO (PDISP,PDONE),INXCTL
	ST_MEMO = ENTRY(1,3)
	IF (ST_MEMO .EQ. 0)
		BEGIN
		EN_MEMO = 99999
		XCALL OUTPT (4,25,1,'ALL',1)
		XCALL OUTPT (6,25,1,' ',1)
		GOTO P_ANY
		END
	GOTO (P_ANY),CNGCTL
EN_MEMO,
	XCALL INPUT (6,25,03,00,'# ',ENTRY,INXCTL,1)
	GOTO (PDISP),INXCTL
	EN_MEMO = ENTRY(1,3)
	IF (EN_MEMO .EQ. 0)
		BEGIN
		EN_MEMO = ST_MEMO
		ENTRY(1,3) = EN_MEMO	,'ZZX' [LEFT]
		XCALL OUTPT (6,25,0,ENTRY(1,3),1)
		END
P_ANY,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (P_PRINT,P_CNGBR),CNGCTL + 1
P_CNGBR,
	GOTO (ST_MEMO,EN_MEMO),WHATNO
	GOTO P_ANY

P_PRINT,
	switch = 5
	xcall files (18, 'SI', 182, switch)

	CLEAR TBL_KEY
	TBLCOD = 'LB'
	LB_F1 = ST_MEMO
	FIND (CHN182,COPTBL,TBL_KEY)[ERR=PLOOP]

PLOOP,
	XCALL IOS (CHN182,COPTBL,READ,LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF
	IF (TBLCOD .NE. 'LB') GOTO EOF
	IF (LB_F1 .LT. ST_MEMO) GOTO PLOOP
	IF (LB_F1 .GT. EN_MEMO) GOTO EOF

	PLINE(1,3) = LB_F1	,'ZZX' 
	PLINE (7,8) = LB_GA,	'ZX'
	PLINE (10,18) = LB_COST,	'ZZZZ.XXXX'

	MEMO = LB_F1
	CLEAR TBL_KEY
	TBL_KEY(1,2) = 'M1'
	MM_KEY = MEMO
	XCALL ISIO (18,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .EQ. 0) PLINE (20,40) = M1_SHORT

	CALL PRINT
	GOTO PLOOP

EOF,
	IF (PRNTON.EQ.1)  XCALL LPOFF(LPSW,SPLFIL,PGCNT)
	PRNTON  = 0
	xcall files (18, 'SI', 182, 4)
PDONE,
	GOTO MENU

PRINT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (PRNTON .EQ. 0) CALL PRNTON
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HD1,HD,HD
&		,LG,LG,LG,0,132,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;-------------------------------------------------------------
PRNTON,
	SPLFIL (5,6) = 'EF'
	LPSW = 1		;PRINT,SPOOL, OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTON = 1
	RETURN
;-------------------------------------------------------------
;===================================================================


OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (1,'SU',182,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN182 = 1

	OPNOK = 1
	RETURN
;----------------------------------------------------

CLOSE,
	CLOSE CHN182
	RETURN
;----------------------------------------------------
	
	
;  F1  GA   COST/LB
; ZXX  AA ZZZZ.XXXX
;12345678901234567890123456789012345678901234567890123
;         1         2         3         4         5

;
