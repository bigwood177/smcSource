;DSZMNT.CP
;	COP TABLE MAINTENANCE
;	DAMPER SIZE TABLE
;
RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'


RECORD	CHANNEL
	CHN182	,D2


RECORD	PRINT
	TITLE	,A*,	'DAMPER SIZES'
	HD	,A6,	'NO HDR'
	LG	,A9,	'NO LEGEND'
	PLINE	,A80
	PRNTON	,D1
	LINCNT	,D2,60
	PGCNT	,D6
	LPSW	,D2
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	PRTCTL	,D3,080
	LPARG	,D1
	PRNTSW	,D1
	PRTCTR	,D1


RECORD	HD1
		,A*,	'DIMENSION  SIZE-1  SIZE-2  SIZE-3  SIZE-4'
;;;		,A*,	'DIMENSION  SIZE-1  SIZE-2  SIZE-3'

RECORD	VARS
	ALPHA	,A12
	OPNOK	,D1
	BLANKS	,A15
	MBOX	,D1
	DIMEN	,D3
	ST_DIM	,D3
	EN_DIM	,D3
	PGM	,D1
	ROW	,D2
	ENTRY	,A30
	INXCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	STORE	,D1,2
	DELETE	,D1,3
	LOKCTL	,D1
	WHATNO	,D2
	SELECT	,D1
	CNGCTL	,D1
	I	,D3
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'DAMPER SIZES',1)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO CLOSE

MENU,
	XCALL OUTPT (1,1,2,'DAMPER SIZES',1)
	XCALL OUTPT (3,9,0,'PLEASE SELECT APPLICATION',1)
	XCALL OUTPT (5,15,0,'1. TABLE MAINTENANCE',1)
	XCALL OUTPT (6,15,0,'2. PRINT TABLE',1)
MINPUT,
	XCALL INPUT (3,36,1,1,'#E',ENTRY,INXCTL,1)
	GOTO (MINPUT,ENDOFF), INXCTL
	PGM = ENTRY(1,1)
	GOTO (DISPLA,PRINT_TABLE),PGM
	GOTO MINPUT

DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,'PRINTER MAILBOXES FOR A SALESMAN TABLE',1)
	XCALL OUTPT ( 4,4,0,'1. DIMENSION',1)
	XCALL OUTPT ( 6,4,0,'2. SIZE-1',1)
	XCALL OUTPT ( 7,4,0,'3. SIZE-2',1)
	XCALL OUTPT ( 8,4,0,'4. SIZE-3',1)
	XCALL OUTPT ( 9,4,0,'5. SIZE-4',1)
DIMEN,
	XCALL INPUT (4,24,03,00,'#E',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	DIMEN = ENTRY(1,3)
CHKTBL,
	CLEAR TBL_KEY
	TBL_KEY(1,2) = 'SZ'
	SZ_KEY = DIMEN
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .EQ. 0)
		BEGIN
		SELECT = 2
		CALL DSPREC
		GOTO ANYCNG
		END

	SELECT = 1			;ADD MODE
	CLEAR COPTBL
S1,
	XCALL INPUT (6,24,02,00,'#E',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	SZ_S1 = ENTRY(1,2)
	GOTO (ANYCNG),CNGCTL
S2,
	XCALL INPUT (7,24,02,00,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	SZ_S2 = ENTRY(1,2)
	GOTO (ANYCNG),CNGCTL
S3,
	XCALL INPUT (8,24,02,00,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	SZ_S3 = ENTRY(1,2)
	GOTO (ANYCNG),CNGCTL
S4,
	XCALL INPUT (9,24,02,00,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	SZ_S4 = ENTRY(1,2)
	GOTO (ANYCNG),CNGCTL

ANYCNG,
	XCALL OUTPT (24,1,1,'FIELD # TO CHANGE            <TAB> = DELETE',1)
	XCALL INPUT (24,20,02,00,'#T',ENTRY,INXCTL,1)
	IF (INXCTL .EQ. 3)
		BEGIN
		XCALL OUTPT (24,1,1,'DELETE, ARE YOU SURE ?',1)
		XCALL INPUT (24,24,01,01,'YN',ENTRY,INXCTL,1)
		GOTO (ANYCNG),INXCTL-1
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,DELETE,LOKCTL)
		GOTO DISPLA
		END

	WHATNO = ENTRY(1,2)
	IF (WHATNO .EQ. 0)
	THEN 	CLEAR CNGCTL
	ELSE 	CNGCTL = 1

	GOTO (PROCES,CNGBR),CNGCTL+1
CNGBR,
	GOTO (DIMEN,S1,S2,S3,S4),WHATNO
	GOTO ANYCNG

PROCES,
	CASE SELECT OF
	BEGINCASE
	1:	BEGIN
		TBLCOD = 'SZ'
		SZ_KEY = DIMEN
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,STORE,LOKCTL)
		END
	2:	XCALL ISIO (CHN182,COPTBL,TBL_KEY,WRITE,LOKCTL)
	ENDCASE
	GOTO DISPLA

ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('CP:TBLMNU',1)
	STOP


DSPREC,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; DISPLAY RECORD
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	ENTRY(1,3) = SZ_KEY,	'ZZX' [LEFT]
	XCALL OUTPT (4,24,1,ENTRY(1,3),1)
	ENTRY(1,2) = SZ_S1,	'ZX'
	XCALL OUTPT (6,24,1,ENTRY(1,2),1)

	ENTRY(1,2) = SZ_S2,	'ZX'
	XCALL OUTPT (7,24,1,ENTRY(1,2),1)

	ENTRY(1,2) = SZ_S3,	'ZZ'
	XCALL OUTPT (8,24,1,ENTRY(1,2),1)

	ENTRY(1,2) = SZ_S4,	'ZZ'
	XCALL OUTPT (9,24,1,ENTRY(1,2),1)

	RETURN
;-----------------------------------



;===================================================================
PRINT_TABLE,
;===================================================================
	LINCNT = 66
	PGCNT  = 0
	
PDISP,
	CNGCTL = 
	XCALL OUTPT (1,1,2,'PRINT DAMPER SIZES',1)
	XCALL OUTPT (4,4,0,'1. STARTING DIMENSION',1)
	XCALL OUTPT (6,4,0,'2. ENDING   DIMENSION',1)
ST_DIM,
	XCALL INPUT (4,27,03,00,'#E',ENTRY,INXCTL,1)
	GOTO (PDISP,PDONE),INXCTL
	ST_DIM = ENTRY(1,3)
	IF (ST_DIM .EQ. 0)
		BEGIN
		EN_DIM = 999
		XCALL OUTPT (4,27,1,'ALL',1)
		XCALL OUTPT (6,27,1,' ',1)
		GOTO P_ANY
		END
	GOTO (P_ANY),CNGCTL
EN_DIM,
	XCALL INPUT (6,27,03,00,'# ',ENTRY,INXCTL,1)
	GOTO (PDISP),INXCTL
	EN_DIM = ENTRY(1,3)
	IF (EN_DIM .EQ. 0)
		BEGIN
		EN_DIM = ST_DIM
		ENTRY(1,3) = EN_DIM, 'ZZX'
		XCALL OUTPT (6,27,0,ENTRY(1,3),1)
		END
P_ANY,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (P_PRINT,P_CNGBR),CNGCTL + 1
P_CNGBR,
	GOTO (ST_DIM,EN_DIM),WHATNO
	GOTO P_ANY

P_PRINT,
	CLEAR TBL_KEY
	TBLCOD = 'SZ'
	SZ_KEY = ST_DIM
	FIND (CHN182,COPTBL,TBL_KEY)[ERR=PLOOP]

PLOOP,
	XCALL IOS (CHN182,COPTBL,READ,LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF
	IF (TBLCOD .NE. 'SZ') GOTO EOF
	IF (SZ_KEY .LT. ST_DIM) GOTO PLOOP
	IF (SZ_KEY .GT. EN_DIM) GOTO EOF


	PLINE (4,6) = SZ_KEY,	'ZZX'	
	PLINE (16,17) = SZ_S1,	'ZX'
	PLINE (24,25) = SZ_S2,	'ZX'
	PLINE (32,33) = SZ_S3,	'ZZ'
	PLINE (40,41) = SZ_S4,	'ZX'
;DIMENSION  SIZE-1  SIZE-2  SIZE-3  SIZE-4
;   XXX         ZZ      ZZ      ZZ      ZZ
;12345678901234567890123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5         6         7         8
	CALL PRINT
	GOTO PLOOP

EOF,
	IF (PRNTON.EQ.1)  XCALL LPOFF(LPSW,SPLFIL,PGCNT)
	PRNTON  = 0

PDONE,
	GOTO MENU

PRINT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (PRNTON .EQ. 0) CALL PRNTON
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HD1,HD,HD
&		,LG,LG,LG,0,080,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;-------------------------------------------------------------
PRNTON,
	SPLFIL (5,6) = 'EF'
	LPSW = 1		;PRINT,SPOOL, OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTON = 1
	RETURN
;-------------------------------------------------------------
;===================================================================


OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (1,'SU',182,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN182 = 1

	OPNOK = 1
	RETURN
;----------------------------------------------------

CLOSE,
	CLOSE CHN182
	RETURN
;----------------------------------------------------
END
