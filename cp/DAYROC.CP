;DAYROC.CP
;DYROCK.DBL
;DY.DBL

RECORD	ORDHDR
	.INCLUDE 'DEF:RD044A.DEF'
;
RECORD	ORDLIN
	.INCLUDE 'DEF:RD045A.DEF'


RECORD TITLE
		,A*,	'DAILY ROCKFORD-SMC REPORT'
RECORD HD1
	,A*,	' ORDER#    ENTERED CUSTOMER                    SHIP-TO'

RECORD	PRINT
	HD	,A6,	'NO HDR'
	LEG	,A9,	'NO LEGEND'
	LINCNT	,D2,	60
	SPLFIL	,A14
	LPONSW	,D1
	LPARG	,D1
	LPSW	,D2
	RPTNUM	,D3
	PRTTYP	,A1
	PGCNT	,D6,	000000
	PLINE	,A132
	PRTCTL	,D3

RECORD	CHANNEL
	CHN001	,D2
	CHN002	,D2
	CHN044	,D2
	CHN045	,D2
	CHN175	,D2

RECORD	VARS
	ROCK	,D1
	LNAM	,A25
	SNAM	,A12
	SINT	,A3
	TODAY	,D8
	FIL045	,A14,	'SMC:ORDLIN.SMM'
	EXEPRC	,D10
	OPNOK	,D1
	ENTRY	,A30
	INXCTL	,D1
	INDATE	,D8
	XDATE	,D8
	SRCCTL	,D1
	BSEND	,D5
	BSMID	,D5
	RUNTOT	,D10
	DECMAL	,D18
	BLANKS	,A4
	CNGCTL	,D1
	WHATNO	,D1
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	SWITCH	,D1
	V	,D1
;
PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,TITLE,1)
	XCALL RDAT8(TODAY)
	
	CALL OPENS
	IF (.NOT. OPNOK) GOTO ENDOFF
DISPLA,
	XCALL OUTPT (1,1,2,TITLE,1)
	XCALL OUTPT (4,4,0,'FOR DATE:',1)
	XCALL INPUT (4,16,08,00,'DE',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL

	INDATE = ENTRY(1,8)
	IF (INDATE .EQ. 0)
		BEGIN
		INDATE(1,4) = TODAY(5,8)
		INDATE(5,8) = TODAY(1,4)
		ENTRY(1,10) = INDATE,'XX/XX/XXXX'
		XCALL OUTPT (4,16,1,ENTRY(1,10),1)
		END
	TODAY = INDATE

	CNGCTL = 2
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (DISPLA),CNGCTL
		
	FIND (CHN044,ORDHDR,^FIRST)
LOOP,
	READS (CHN044,ORDHDR,EOF)
	IF (OORDDT .NE. TODAY) GOTO LOOP
	CALL CHKLIN
	IF (.NOT. ROCK) GOTO LOOP


	PLINE(2,7) = OORDNO,	'ZZZZZX'
	XDATE(1,4) = OORDDT(5,8)
	XDATE(5,8) = OORDDT(1,4)
	PLINE(09,18) = XDATE,	'ZX/XX/XXXX'
	PLINE(20,45) = OCUSNM
	PLINE(48,77) = OSHPNM

	CALL PRINT
	GOTO LOOP

CHKLIN,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	ROCK = 0
	FIND (CHN045, ORDLIN, OORDNO) [ERR=BADLIN]
CK_LOOP,
	READS (CHN045,ORDLIN,BADLIN)
	IF (LORDNO .NE. OORDNO) GOTO BADLIN
	IF (LROCPO .NE. 9) GOTO CK_LOOP
	ROCK = 1
	RETURN
BADLIN,
	RETURN
;--------------------------------------------------

PRINT,
	IF (LPONSW.EQ.0) CALL PRNTON
	PRTCTL = 80
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HD1,HD,HD,
&			LEG,LEG,LEG,1,132,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
PRNTON,
;;;	SPLFIL (5,6) = 'SPL:DYROCK.SPL'
	LPSW = 1	; PRINT, SPOOL OR DISPLAY
;;;	open(14,o,splfil)

	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) CALL CLOSE
	IF (LPSW.EQ.0) XCALL PGCHN ('CP:CPMENU',1)
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)

	LPONSW = 1
	RETURN
;************************************************************************
EOF,
	CALL CLOSE
;;;	close 14
;;;	IF (LPONSW .EQ. 1) lpque(splfil)

	IF (LPONSW.EQ.1) XCALL LPOFF (LPSW,SPLFIL,PGCNT)
ENDOFF,
	XCALL FLAGS (7000000)
	STOP

CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLOSE CHN044
	CLOSE CHN045

	RETURN
;-----------------------------------------------------

OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	OPEN (1,SI,'ROC:ORDHDR.ROM')
	CHN044 = 1

	OPEN (2,SI,'ROC:ORDLIN.ROM')
	CHN045 = 2

	OPNOK = 1
	RETURN
;-----------------------------------------------------

; ORDER#    ENTERED CUSTOMER                    SHIP-TO
; XXXXXX XX/XX/XXXX AAAAAAAAAAAAAAAAAAAAAAAAA   AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
;12345678901234567890123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5         6         7         8
	END



