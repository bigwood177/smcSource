;TSCMNT.COP
;	COP TABLE MAINTENANCE
;	SCAC CODES
;
RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'

RECORD	INDEX
	I_CODE	,A4
	I_NAME	,A15
	I_SCAC	,A4
RECORD	CHANNEL
	CHN182	,D2


RECORD	PRINT
	TITLE	,A*,	'CARRIER TABLE'
	HD	,A6,	'NO HDR'
	LG	,A9,	'NO LEGEND'
	PLINE	,A132
	PRNTON	,D1
	LINCNT	,D2,60
	PGCNT	,D6
	LPSW	,D2
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	PRTCTL	,D3,132
	LPARG	,D1
	PRNTSW	,D1
	PRTCTR	,D1

RECORD	HD1
		,A*,	'SCAC  CARRIER         TRAFFIC      '
		,A*,	'CONT-1                     TRACING      CON-2'
		,A*,	'                     PPD  PRINT QUE     C/S'
RECORD	VARS
	WEBSIT	,A50	;FROM COPTBL OVERLAY RECORD (TYPE 'W')
	FILNAM	,A14
	ALPHA	,A12
	OPNOK	,D1
	BLANKS	,A15
	SCAC	,A4
	NAME	,A15
	ST_SCAC	,A4
	EN_SCAC	,A4
	NUM	,D10
	PGM	,D1
	ROW	,D2
	ENTRY	,A50
	INXCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	STORE	,D1,2
	DELETE	,D1,3
	LOKCTL	,D1
	WHATNO	,D2
	SELECT	,D1
	CNGCTL	,D1
	I	,D3
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'CARRIER TABLE',1)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO CLOSE

MENU,
	XCALL OUTPT (1,1,2,'CARRIER TABLE',1)
	XCALL OUTPT (3,9,0,'PLEASE SELECT APPLICATION',1)
	XCALL OUTPT (5,15,0,'1. TABLE MAINTENANCE',1)
	XCALL OUTPT (6,15,0,'2. PRINT TABLE',1)
MINPUT,
	XCALL INPUT (3,36,1,1,'#E',ENTRY,INXCTL,1)
	GOTO (MINPUT,ENDOFF), INXCTL
	PGM = ENTRY(1,1)
	GOTO (DISPLA,PRINT_TABLE),PGM
	GOTO MINPUT

DISPLA,
	CLEAR CNGCTL
	clear websit
	XCALL OUTPT (1,1,2,'CARRIER TABLE',1)
	XCALL OUTPT ( 4,4,0,'1. SCAC CODE',1)
	XCALL OUTPT ( 6,4,0,'2. CARRIER',1)
	XCALL OUTPT ( 8,4,0,'3. PHONE (TRAFFIC)',1)
	XCALL OUTPT (10,4,0,'4. CONTACT',1)
	XCALL OUTPT (12,4,0,'5. PHONE (TRACING)',1)
	XCALL OUTPT (14,4,0,'6. CONTACT',1)
	XCALL OUTPT (16,4,0,'7. FRT PPD ?',1)
	XCALL OUTPT (18,4,0,'8. PRINT COVER SHEET?',1)
	XCALL OUTPT (20,4,0,'9. PRINTER',1)
	XCALL OUTPT (22,3,0,'10. WEB SITE',1)

SCAC,
	XCALL INPUT (4,26,04,00,'AE',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	SCAC = ENTRY(1,4)

	CLEAR TBL_KEY
	TBL_KEY(1,2) = 'SC'
	I1_KEY = SCAC
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .EQ. 0)
		BEGIN
		SELECT = 2
		CALL GET_WEBSITE
		CALL DSPREC
		GOTO ANYCNG
		END


	SELECT = 1			;ADD MODE
	CLEAR COPTBL

NAME,
	XCALL INPUT (6,26,15,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	SC_NAME = ENTRY(1,15)
	GOTO (ANYCNG),CNGCTL
TEL1,
	XCALL INPUT (8,26,10,00,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	SC_TEL1 = ENTRY(1,10)
	ALPHA = SC_TEL1,	'ZZZ ZZZ ZZZZ' [LEFT]
	XCALL OUTPT (8,26,1,ALPHA,1)
	GOTO (ANYCNG),CNGCTL
CON1,
	XCALL INPUT (10,26,25,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	SC_CON1 = ENTRY(1,25)
	GOTO (ANYCNG),CNGCTL
TEL2,
	XCALL INPUT (12,26,10,00,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	SC_TEL2 = ENTRY(1,10)
	ALPHA = SC_TEL2,	'ZZZ ZZZ ZZZZ' [LEFT]
	XCALL OUTPT (12,26,1,ALPHA,1)
	GOTO (ANYCNG),CNGCTL
CON2,
	XCALL INPUT (14,26,25,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	SC_CON2 = ENTRY(1,25)
	GOTO (ANYCNG),CNGCTL
PPD,
	XCALL INPUT (16,26,01,00,'YN',ENTRY,INXCTL,1)
	SC_PPD = ENTRY(1,1)
	GOTO (ANYCNG),CNGCTL
CVRS,
	XCALL INPUT (18,26,01,00,'YN',ENTRY,INXCTL,1)
	SC_CVRS = ENTRY(1,1)
	GOTO (ANYCNG), CNGCTL

QUE,
	USING SC_CVRS SELECT
	('Y'),	XCALL OUTPT (20,4,0,'9. C/S PRINTER',1)
	('N'),	XCALL OUTPT (20,4,0,'9. BLUE PRINTER',1)
	ENDUSING

	XCALL INPUT (20,26,12,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	SC_QUE = ENTRY(1,12)
	GOTO (ANYCNG),CNGCTL

WEB,
	XCALL INPUT (22,26,50,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	WEBSIT = ENTRY(1,50)

	GOTO ANYCNG

ANYCNG,
	XCALL OUTPT (24,1,1,'FIELD # TO CHANGE            <TAB> = DELETE',1)
	XCALL INPUT (24,20,02,00,'#T',ENTRY,INXCTL,1)
	IF (INXCTL .EQ. 3)
		BEGIN
		XCALL OUTPT (24,1,1,'DELETE, ARE YOU SURE ?',1)
		XCALL INPUT (24,24,01,01,'YN',ENTRY,INXCTL,1)
		GOTO (ANYCNG),INXCTL-1
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,DELETE,LOKCTL)
		GOTO DISPLA
		END

	WHATNO = ENTRY(1,2)
	IF (WHATNO .EQ. 0)
	THEN 	CLEAR CNGCTL
	ELSE 	CNGCTL = 1

	GOTO (PROCES,CNGBR),CNGCTL+1
CNGBR,
	GOTO (SCAC,NAME,TEL1,CON1,TEL2,CON2,PPD,CVRS,QUE,WEB),WHATNO
	GOTO ANYCNG

PROCES,
	CASE SELECT OF
	BEGINCASE
	1:	BEGIN
		TBLCOD = 'SC'
		TBLKEY = SCAC
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,STORE,LOKCTL)
		CLEAR COPTBL
		TBLCOD = 'SC'
		SW_SCAC = SCAC
		SW_W = 'W'
		SW_WEB = WEBSIT
		XCALL ISIO (CHN182, COPTBL, TBL_KEY, STORE, LOKCTL)
		END
	2:	BEGIN
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,WRITE,LOKCTL)
		CLEAR TBL_KEY
		TBL_KEY(1,2) = 'SC'
		SC_SCAC = SCAC
		SW_W = 'W'	;WEBSITE RECORD
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
		IF (LOKCTL .EQ. 0)
			BEGIN
			SW_WEB = WEBSIT
			XCALL ISIO (CHN182, COPTBL, TBL_KEY, WRITE, LOKCTL)
			END
		END
	ENDCASE
	GOTO DISPLA


GET_WEBSITE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CLEAR TBL_KEY
	TBL_KEY(1,2) = 'SC'
	SC_SCAC = SCAC
	SW_W = 'W'	;WEBSITE RECORD
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .NE. 0)	;if no record exists, create one...
	THEN	BEGIN
		CLEAR COPTBL
		TBLCOD = 'SC'
		SW_SCAC = SCAC
		SW_W = 'W'
		XCALL ISIO (CHN182, COPTBL, TBL_KEY, STORE, LOKCTL)
		IF (LOKCTL .NE. 0)
			BEGIN
			XCALL MESAG ('ERROR CREATING WEBSITE RECORD',1)
			END
		END
	ELSE	WEBSIT = SW_WEB

; read back the original record...
	CLEAR TBL_KEY
	TBL_KEY(1,2) = 'SC'
	I1_KEY = SCAC
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)

	RETURN
;-----------------------------------------------------

ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('CP:TBLMNU',1)
	STOP


DSPREC,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; DISPLAY RECORD
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL OUTPT (4,26,1,SC_SCAC,1)
	XCALL OUTPT (6,26,1,SC_NAME,1)
	ALPHA = SC_TEL1,	'ZZZ ZZZ ZZZZ'[LEFT]
	XCALL OUTPT (8,26,1,ALPHA,1)
	XCALL OUTPT (10,26,1,SC_CON1,1)
	ALPHA = SC_TEL2,	'ZZZ ZZZ ZZZZ'[LEFT]
	XCALL OUTPT (12,26,1,ALPHA,1)
	XCALL OUTPT (14,26,1,SC_CON2,1)
	XCALL OUTPT (16,26,1,SC_PPD,1)
	XCALL OUTPT (18,26,1,SC_CVRS,1)
	XCALL OUTPT (20,26,1,SC_QUE,1)
	XCALL OUTPT (22,26,1,WEBSIT,1)
	RETURN
;-----------------------------------



;===================================================================
PRINT_TABLE,
;===================================================================

	LINCNT = 66
	PGCNT  = 0
	
PDISP,
	CNGCTL = 
	XCALL OUTPT (1,1,2,'PRINT CARRIER TABLE',1)
	XCALL OUTPT (4,4,0,'1. STARTING SCAC',1)
	XCALL OUTPT (6,4,0,'2. ENDING   SCAC',1)
ST_SCAC,
	XCALL INPUT (4,25,04,00,'AE',ENTRY,INXCTL,1)
	GOTO (PDISP,PDONE),INXCTL
	ST_SCAC = ENTRY(1,4)
	IF (ST_SCAC .EQ. BLANKS)
		BEGIN
		EN_SCAC = 'ZZZZ'
		XCALL OUTPT (4,25,1,'ALL',1)
		XCALL OUTPT (6,25,1,' ',1)
		GOTO P_ANY
		END
	GOTO (P_ANY),CNGCTL
EN_SCAC,
	XCALL INPUT (6,25,04,00,'A ',ENTRY,INXCTL,1)
	GOTO (PDISP),INXCTL
	EN_SCAC = ENTRY(1,4)
	IF (EN_SCAC .EQ. BLANKS)
		BEGIN
		EN_SCAC = ST_SCAC
		XCALL OUTPT (6,25,0,EN_SCAC,1)
		END
P_ANY,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (PT_PROC,P_CNGBR),CNGCTL + 1
P_CNGBR,
	GOTO (ST_SCAC,EN_SCAC),WHATNO
	GOTO P_ANY

PT_PROC,
	FILNAM = 'SPL:SCAC.IDX'
	OPEN(10,O,FILNAM)
	CLEAR TBL_KEY
	TBLCOD = 'SC'
	SC_SCAC = ST_SCAC
	FIND (CHN182,COPTBL,TBL_KEY)[ERR=PT_LOOP]
PT_LOOP,
	XCALL IOS (CHN182,COPTBL,READ,LOKCTL)
	IF (LOKCTL .NE. 0) GOTO PT_EOF
	IF (TBLCOD .NE. 'SC') GOTO PT_EOF
	IF (SC_SCAC .LT. ST_SCAC) GOTO PT_LOOP
	IF (SC_SCAC .GT. EN_SCAC) GOTO PT_EOF
	IF (SW_W .EQ. 'W') GOTO PT_LOOP		;SKIP WEBSITE RECORDS
	ONERROR NOT_NUM
	NUM = SC_SCAC
	I_CODE = NUM,	'XXXX'
	OFFERROR
	GOTO PT_CONT
NOT_NUM,
	OFFERROR
	I_CODE = '9999'
PT_CONT,
	I_NAME = SC_NAME
	I_SCAC = SC_SCAC
	WRITES(10,INDEX)
	GOTO PT_LOOP
PT_EOF,
	CLOSE 10
	SORT (INPUT=FILNAM, RECORD=INDEX, KEY=(I_CODE,I_NAME))
	OPEN(10,I,FILNAM)

P_PRINT,
;;;	CLEAR TBL_KEY
;;;	TBLCOD = 'SC'
;;;	SC_SCAC = ST_SCAC
;;;	FIND (CHN182,COPTBL,TBL_KEY)[ERR=PLOOP]

PLOOP,
	READS(10,INDEX,EOF)
	CLEAR TBL_KEY
	TBLCOD = 'SC'
	SC_SCAC = I_SCAC
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
;;;	XCALL IOS (CHN182,COPTBL,READ,LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF
	IF (TBLCOD .NE. 'SC') GOTO EOF
;;;	IF (SC_SCAC .LT. ST_SCAC) GOTO PLOOP
;;;	IF (SC_SCAC .GT. EN_SCAC) GOTO EOF


	PLINE (1,4) = SC_SCAC
	PLINE (7,21) = SC_NAME
	PLINE (23,34) = SC_TEL1,	'ZZZ ZZZ ZZZZ'
	PLINE (36,60) = SC_CON1
	PLINE (63,74) = SC_TEL2,	'ZZZ ZZZ ZZZZ'
	PLINE (76,100) = SC_CON2
	PLINE (103,103) = SC_PPD
	PLINE (106,119) = SC_QUE
	PLINE (122,122) = SC_CVRS	;5-6-19

;SCAC  NAME            TRAFFIC      CONT-1                     TRACING      CON-2
;AAAA  AAAAAAAAAAAAAAA ZZZ ZZZ ZZZZ AAAAAAAAAAAAAAAAAAAAAAAAA  ZZZ ZZZ ZZZZ 
;12345678901234567890123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5         6         7         8
	CALL PRINT


	XCALL IOS (CHN182,COPTBL,READ,LOKCTL)
	IF (SW_W.EQ.'W' .AND. SC_SCAC.EQ.I_SCAC)
		BEGIN
		PLINE = 'WEB SITE: ' + SW_WEB
		CALL PRINT
		END
	GOTO PLOOP

EOF,
	IF (PRNTON.EQ.1)  XCALL LPOFF(LPSW,SPLFIL,PGCNT)
	PRNTON  = 0
	CLOSE 10
PDONE,
	GOTO MENU

PRINT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (PRNTON .EQ. 0) CALL PRNTON
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HD1,HD,HD
&		,LG,LG,LG,0,132,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;-------------------------------------------------------------
PRNTON,
	SPLFIL (5,6) = 'EF'
	LPSW = 1		;PRINT,SPOOL, OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTON = 1
	RETURN
;-------------------------------------------------------------
;===================================================================


OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (1,'SU',182,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN182 = 1

	OPNOK = 1
	RETURN
;----------------------------------------------------

CLOSE,
	CLOSE CHN182
	RETURN
;----------------------------------------------------
	
	
