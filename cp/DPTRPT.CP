;DPTRPT.CP
;
RECORD	DPTSTS
	.INCLUDE 'DEF:RD193A.DEF'
;
RECORD	DUCACC
	.INCLUDE 'DEF:RD175A.DEF'

RECORD TITLE
		,A*,	'DUCT PRODUCTION REPORT'
RECORD HDR1
		,A*,	'FROM '
	H1_SD	,A10
		,A*,	' THRU '
	H1_ED	,A10

RECORD HDR2
		,A*,	'ORDER# DATE CMPLT DP     POUNDS  LINER SQFT'

RECORD	PRINT
	LINCNT	,D2,	60
	SPLFIL	,A14
	LPONSW	,D1
	LPARG	,D1
	RPTNUM	,D3
	PRTTYP	,A1
	PGCNT	,D6,	000000
	PLINE	,A132
	PRTCTL	,D3
	LPSW	,D2

RECORD
		,A9,	'1" - 1.5#'
		,A9,	'.5" - 2# '
		,A9,	'1" - 3#  '
		,A9,	'None     '
		,A9,	'.5" - 3# '
		,A9,	'2" - 1.5#'
		,A9,	'1" - 2#  '
		,A9,	'2" - 3#  '
RECORD,X
	QLINER	,8A9

RECORD	DIS
	II	,D6

RECORD	CHANNEL
	CHN175	,D2
	CHN193	,D2
;
RECORD	TOTALS
	LBS	,D10
	O_LBS	,D10
	P_LBS	,D10
	T_LBS	,D10

	TSF	,D10
	OSF	,D10
	PSF	,D10
	SF	,9D10
	O_SF	,9D10
	P_SF	,9D10
	T_SF	,9D10

RECORD	VARS
	OPNOK	,D1
	I	,D6
	TODAY	,D8
	XDATE	,D8
	WRKDAT	,D8
	STDAT	,D8
	ENDAT	,D8
	ENTRY	,A30
	INXCTL	,D1
	CNGCTL	,D1
	WHATNO	,D2
	LOKCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	SWITCH	,D1
	V	,D1
;
PROC
	XCALL TERID (V)
	V = 1
	XCALL OUTPT (1,1,2,'DAILY PRODUCTION REPORT',1)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO ENDOFF
	XCALL RDAT8 (TODAY)
	XDATE(1,4) = TODAY(5,8)
	XDATE(5,8) = TODAY(1,4)
DISPLA,
	CLEAR INXCTL
	XCALL OUTPT (1,1,2,'DAILY PRODUCTION REPORT',1)
	XCALL OUTPT (4,4,0,'1. START DATE',1)
	XCALL OUTPT (6,4,0,'2. END   DATE',1)
STDAT,
	XCALL INPUT (4,20,08,00,'DE',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	STDAT = ENTRY(1,8)
	IF (STDAT .EQ. 0)
		BEGIN
		STDAT = TODAY
		ENTRY(1,10) = XDATE, 'ZX/XX/XXXX'
		XCALL OUTPT (4,20,1,ENTRY(1,10),1)
		END
	GOTO (ANYCNG),CNGCTL
ENDAT,
	XCALL INPUT (6,20,08,00,'DE',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	ENDAT = ENTRY(1,8)
	IF (ENDAT .EQ. 0)
		BEGIN
		ENDAT = STDAT
		WRKDAT(1,4) = ENDAT(5,8)
		WRKDAT(5,8) = ENDAT(1,4)
		ENTRY(1,10) = WRKDAT,	'ZX/XX/XXXX'
		XCALL OUTPT (6,20,0,ENTRY(1,10),1)
		END
ANYCNG,
	XCALL ANYCN (CNGCTL,WHATNO)
	GOTO (PROCES, CNGBR),CNGCTL+1
CNGBR,
	GOTO (STDAT, ENDAT),WHATNO
	GOTO ANYCNG
PROCES,

	WRKDAT(1,4) = STDAT(5,8)
	WRKDAT(5,8) = STDAT(1,4)
	H1_SD = WRKDAT,	'ZX/XX/XXXX'

	WRKDAT(1,4) = ENDAT(5,8)
	WRKDAT(5,8) = ENDAT(1,4)
	H1_ED = WRKDAT,	'ZX/XX/XXXX'
	
	CLEAR II, TOTALS
	FIND (CHN193, DPTSTS, ^FIRST) [ERR=EOF]
LOOP,
	INCR II
	IF (II/500*500 .EQ. II) XCALL OUTPT (1,70,1,DIS,1)
	LOKCTL = 1
	XCALL IOS (CHN193, DPTSTS, READ, LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF

	IF (S_DEPT .EQ. '**') GOTO LOOP		;6-21-17 HEADER
	IF (S_DATE .LT. STDAT) GOTO LOOP
	IF (S_DATE .GT. ENDAT) GOTO LOOP

	USING S_DEPT SELECT
	('O','P'),	CALL DUCT
	ENDUSING

	GOTO LOOP
EOF,
	PLINE (13,20) = 'DEPT "O"'
	PLINE (22,31) = O_LBS,		'ZZ,ZZZ,ZZX'
	PLINE (33,43) = OSF,		'ZZZ,ZZZ,ZZX'
	CALL PRINT

	PLINE (13,20) = 'DEPT "P"'
	PLINE (22,31) = P_LBS,		'ZZ,ZZZ,ZZX'
	PLINE (33,43) = PSF,		'ZZZ,ZZZ,ZZX'
	CALL PRINT

	PLINE (16,20) = 'TOTAL'
	PLINE (22,31) = T_LBS,		'ZZ,ZZZ,ZZX'
	PLINE (33,43) = TSF,		'ZZZ,ZZZ,ZZX'
	CALL PRINT
	CALL PRINT

	PLINE = '               DEPT O      DEPT P       TOTAL'
	CALL PRINT
	FOR I FROM 1 THRU 8
		BEGIN
		PLINE (1,9) = QLINER(I)
		PLINE (11,21) = O_SF(I),	'ZZZ,ZZZ,ZZX'
		PLINE (23,33) = P_SF(I),	'ZZZ,ZZZ,ZZX'
		PLINE (35,45) = T_SF(I),	'ZZZ,ZZZ,ZZX'
		CALL PRINT
		END

;               DEPT O      DEPT P       TOTAL
;AAAAAAAAA ZZZ,ZZZ,ZZZ ZZZ,ZZZ,ZZZ ZZZ,ZZZ,ZZZ
;123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5

	IF (LPONSW.EQ.1) XCALL LPOFF (LPSW,SPLFIL,PGCNT)
ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('CP:IMMENU',1)
	STOP

DUCT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CLEAR LBS
	FOR I FROM 1 THRU 8	CLEAR SF(I)

	FIND (CHN175, DUCACC, S_ORDN) [ERR=D_LOOP]
D_LOOP,

	LOKCTL = 1
	XCALL IOS (CHN175, DUCACC, READ, LOKCTL)
	IF (LOKCTL .NE. 0) GOTO D_EOF
	IF (DUCTOR .NE. S_ORDN) GOTO D_EOF

	USING STY SELECT
	(2,3,6),	IF (S_DEPT .NE. 'O') GOTO D_LOOP
	(1,4,5),	IF (S_DEPT .NE. 'P') GOTO D_LOOP
	ENDUSING

	IF(LINER.LT.1 .OR. LINER.GT.8) LINER = 1
	SF(LINER) = SF(LINER) + SQFLIN
	TSF = TSF + SQFLIN

	LBS = LBS + POUNDS
	GOTO D_LOOP

D_EOF,
	PLINE (1,6) = S_ORDN
	PLINE (8,17) = S_DATE,	'XXXX/XX/XX'
	PLINE (19,20) = S_DEPT
	PLINE (25,31) = LBS,	'ZZZ,ZZX'

	CLEAR SQFLIN
	FOR I FROM 1 THRU 8 SQFLIN = SQFLIN + SF(I)

	PLINE (37,43) = SQFLIN,	'ZZZ,ZZX'
	CALL PRINT

	T_LBS = T_LBS + LBS
	IF (S_DEPT .EQ. 'O') 
		BEGIN
		O_LBS = O_LBS + LBS
		OSF = OSF + SQFLIN
		END

	IF (S_DEPT .EQ. 'P') 
		BEGIN
		P_LBS = P_LBS + LBS
		PSF = PSF + SQFLIN
		END

	FOR I FROM 1 THRU 8 
		BEGIN
		T_SF(I) = T_SF(I) + SF(I)
		IF (S_DEPT .EQ. 'O') O_SF(I) = O_SF(I) + SF(I)
		IF (S_DEPT .EQ. 'P') P_SF(I) = P_SF(I) + SF(I)
		END

;ORDER# DATE CMPLT DP     POUNDS  LINER SQFT
;XXXXXX XXXX/XX/XX AA ZZ,ZZZ,ZZX ZZZ,ZZZ,ZZX  AAAAAAAAA
;12345678901234567890123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5         6         7	
	RETURN
;-----------------------------------------------
PRINT,
	IF (LPONSW.EQ.0) CALL PRNTON
	PRTCTL = 70
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,'NO HDR3',
&			'NO LEGEND',' ',' ',1,132,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
PRNTON,
	SPLFIL (5,6) = 'EL'
	LPSW = 1	; PRINT, SPOOL OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) CALL CLOSE
	IF (LPSW.EQ.0) XCALL PGCHN ('CP:CPMENU',1)
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	LPONSW = 1
	RETURN

OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK
	SWITCH = 5
	XCALL FILES (1,'SI',193,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN193 = 1

	SWITCH = 5
	XCALL FILES (2,'SI',175,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN175 = 2

	OPNOK = 1

	RETURN
;----------------------------------------------

CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	IF (CHN175) CLOSE CHN175
	IF (CHN193) CLOSE CHN193
	RETURN
;----------------------------------------------
END

