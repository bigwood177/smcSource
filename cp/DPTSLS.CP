;DPTSLS.CP
;
; YTD SALES BY DEPT
;
RECORD	ITMMAS
	.INCLUDE 'DEF:RD041A.DEF'
;
RECORD	WORK
	W_DEPT	,A2
	W_CAT	,A2
	W_SLS	,D10
	W_ITEM	,A15
	W_F1	,A3
	W_F2	,A3
	W_F3	,A5
	W_DESCR	,A30

RECORD	PRINT
	TITLE	,A*,	'YTD SALES BY DEPARTMENT'
	HD1, A*, 'ITEM            F1  F2  F3    DESCRIPTION                   Y-T-D SALES  CAT'
;;;	HD1	,A*,	'ITEM            DESCRIPTION                      Y-T-D SALES  CAT'
	HD	,A6,	'NO HDR'
	LPSW	,D1
	LINCNT	,D2,	60
	SPLFIL	,A14
	LPONSW	,D1
	LPARG	,D1
	RPTNUM	,D3
	PRTTYP	,A1
	PGCNT	,D6,	000000
	PLINE	,A132
	PRTCTL	,D3

RECORD	PARAM
	DEPT	,A2
	STCAT	,A2
	ENCAT	,A2

RECORD	CHANNELS
	CHN041	,D2
	CHNWRK	,D2

RECORD	FFILE
	FILNUM	,D3
	CLCTL	,D1
	REDFIL	,A14

RECORD	DIS
	II	,D6

RECORD	VARS
	XFIL	,D3,	171
	I	,D6
	OPNOK	,D1
	SRT	,A1	;S/C
	ZERO	,D1	;1= INCLUDE, 2=DON'T INCLUDE
	INXCTL	,D1
	LOKCTL	,D1
	CNGCTL	,D1
	WHATNO	,D2
	ENTRY	,A30
	READ	,D1,0
	SWITCH	,D1
	V	,D1
;
PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'YTD SALES BY BY DEPARTMENT',1)
;
	CALL OPENS 
	IF (.NOT. OPNOK) GOTO ENDOFF

DISPLA,
	XCALL OUTPT (1,1,2,'YTD SALES BY BY DEPARTMENT',1)
	XCALL OUTPT (2,1,1,'FOR DEPT "I"',1)
	
	CLEAR CNGCTL

	XCALL OUTPT (4,4,0,'1. SORT BY SALES OR CAT (S/C)?',1)
	XCALL OUTPT (6,4,0,'2. INCLUDE ZERO SALES ITEMS?',1)

SORT,
	XCALL INPUT (4,37,01,01,'AE',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	SRT = ENTRY(1,1)
	GOTO (ANYCN),CNGCTL
ZERO,
	XCALL INPUT (6,37,01,01,'YN',ENTRY,INXCTL,1)
	ZERO = INXCTL
ANYCN,
	CNGCTL = 
	XCALL ANYCN (CNGCTL, WHATNO)
	GOTO (PROCES, CNGBR),CNGCTL+1
CNGBR,
	GOTO (SORT,ZERO),WHATNO
	GOTO ANYCN

PROCES,
	II = 0
	FIND (CHN041, ITMMAS, ^FIRST, KRF:1) [ERR=LOOP]
LOOP,
	INCR II
	IF (II/500*500 .EQ. II) XCALL OUTPT (1,70,0,DIS,1)
	LOKCTL = 1
	XCALL IOS (CHN041, ITMMAS, READ, LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF
	IF (ZERO.EQ.2 .AND. SLSYTD.EQ.0) GOTO LOOP

	IF (USRDEF .NE. 'I') GOTO LOOP
	W_DEPT = 'I'
	W_CAT = PRDCAT
	W_SLS = QTYYTD
	W_ITEM = ITEMNO
	W_F1 = IF1
	W_F2 = IF2
	W_F3 = IF3
	W_DESCR = DESCR

	WRITES (CHNWRK, WORK)
	GOTO LOOP
EOF,
	CALL CLOSE 

 
	USING SRT SELECT
	('S'),	SORT (IN=REDFIL,RECORD=WORK,KEY = (W_DEPT,W_SLS/R))
	('C'),	SORT (IN=REDFIL,RECORD=WORK,KEY = (W_DEPT,W_CAT,W_SLS/R))
	ENDUSING

	OPEN (CHNWRK, 'I', REDFIL)

	FIND (CHNWRK, WORK, ^FIRST) [ERR=R_LOOP]
R_LOOP,
	READS (CHNWRK, WORK, R_EOF)

	PLINE (1,15) = W_ITEM
	PLINE (17,19) = W_F1
	PLINE (21,23) = W_F2
	PLINE (25,29) = W_F3
	PLINE (31,60) = W_DESCR
	PLINE (61,71) = W_SLS,	'ZZZ,ZZZ,ZZX'
	PLINE (74,75) = W_CAT
	CALL PRINT
	GOTO R_LOOP

;ITEM            F1  F2  F3    DESCRIPTION                   Y-T-D SALES  CAT
;AAAAAAAAAAAAAAA AAA AAA AAAAA AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA ZZ,ZZZ,ZZX  AA 
;12345678901234567890123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5         6         7
R_EOF,
	CLOSE CHNWRK

	IF (LPONSW.EQ.1) XCALL LPOFF (LPSW,SPLFIL,PGCNT)

ENDOFF,
	XCALL PGCHN ('CP:COPRPT',1)

PRINT,
	IF (LPONSW.EQ.0) CALL PRNTON
	PRTCTL = 80
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HD1,HD,HD,
&			'NO LEGEND',' ',' ',1,132,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
PRNTON,
	SPLFIL (5,6) = 'EL'
	LPSW = 1	; PRINT, SPOOL OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) CLOSE CHNWRK
	IF (LPSW.EQ.0) XCALL PGCHN ('CP:CPMENU',1)
	LPONSW = 1
	RETURN

OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CLEAR OPNOK
	SWITCH = 5
	XCALL FILES (1,'SI',41,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN041 = 1

	REDFIL = 'SPL:DPTSLS.DAT'
	OPEN (2,O,REDFIL)
	CHNWRK = 2

	OPNOK = 1
	RETURN
;-------------------------------------------------------

CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	IF (CHN041) CLOSE CHN041
	IF (CHNWRK) CLOSE CHNWRK
	RETURN
;-------------------------------------------------------
