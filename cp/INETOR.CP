;INETOR.CP
;	INTERNET Orders

RECORD	ORDHDR
	.INCLUDE 'DEF:RD044A.DEF'
;
RECORD	ORDLIN
	.INCLUDE 'DEF:RD045A.DEF'

RECORD	DUCACC
	.INCLUDE 'DEF:RD175A.DEF'


RECORD TITLE
		,A*,	'INTERNET ACTIVITY REPORT'
RECORD HD1
	,A*,	'ORDER#    ENTERED CUSTOMER                  '
	,A*,	'SALES REP      AMOUNT  REQUIRED'


RECORD	HD2
		,A*,	'FROM '
	H2SD	,A10
		,A*,	' THRU '
	H2ED	,A10

RECORD	PRINT
	HD3	,A30
	HD	,A6,	'NO HDR'
	LEG	,A9,	'NO LEGEND'
	LINCNT	,D2,	60
	SPLFIL	,A14
	LPONSW	,D1
	LPARG	,D1
	LPSW	,D2
	RPTNUM	,D3
	PRTTYP	,A1
	PGCNT	,D6,	000000
	PLINE	,A132
	PRTCTL	,D3

RECORD	CHANNEL
	CHN044	,D2
	CHN045	,D2
	CHN175	,D2


RECORD	VARS
	redfil	,a14
	PASS	,D1
	ROCK	,D1
	LNAM	,A25
	SNAM	,A12
	SINT	,A3
	RPTTOT	,D12
	GRDTOT	,D12
	TODAY	,D8
	EXEPRC	,D10
	OPNOK	,D1
	ENTRY	,A30
	INXCTL	,D1
	INDATE	,D8
	XDATE	,D8
	STDAT	,D8
	ENDAT	,D8
	SRCCTL	,D1
	BSEND	,D5
	BSMID	,D5
	RUNTOT	,D10
	DUCTOT	,D10
	DECMAL	,D18
	BLANKS	,A4
	CNGCTL	,D1
	WHATNO	,D1
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	SWITCH	,D1
	V	,D1
;
PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,TITLE,1)
	XCALL RDAT8(TODAY)
	
	CALL OPENS
	IF (.NOT. OPNOK) GOTO ENDOFF
DISPLA,
	XCALL OUTPT (1,1,2,TITLE,1)
	XCALL OUTPT (4,4,0,'FOR DATE: XX/XX/XXXX   THRU: XX/XX/XXXX',1)
;                           45678901234567890123456789012345678901234
;                                 1         2         3         4
	XCALL INPUT (4,14,08,00,'DE',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL

	INDATE = ENTRY(1,8)
	IF (INDATE .EQ. 0)
		BEGIN
		INDATE(1,4) = TODAY(5,8)
		INDATE(5,8) = TODAY(1,4)
		ENTRY(1,10) = INDATE,'XX/XX/XXXX'
		XCALL OUTPT (4,14,0,ENTRY(1,10),1)
		INDATE = TODAY
		END
	TODAY = INDATE
	STDAT = INDATE
	XCALL INPUT (4,33,08,00,'DE',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	ENDAT = ENTRY(1,8)
	IF (ENDAT .EQ. 0)
		BEGIN
		ENDAT = STDAT
		INDATE(1,4) = ENDAT(5,8)
		INDATE(5,8) = ENDAT(1,4)
		ENTRY(1,10) = INDATE,'XX/XX/XXXX'
		XCALL OUTPT (4,33,0,ENTRY(1,10),1)
		END

	CNGCTL = 2
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (DISPLA),CNGCTL
		
	XDATE(1,4) = STDAT(5,8)
	XDATE(5,8) = STDAT(1,4)
	H2SD = XDATE,	'ZX/XX/XXXX'


	XDATE(1,4) = ENDAT(5,8)
	XDATE(5,8) = ENDAT(1,4)
	H2ED = XDATE,	'ZX/XX/XXXX'

	HD3 = 'OPEN ORDERS'
	CLEAR PASS
	CLEAR GRDTOT
RE_LOOP,
	CLEAR RPTTOT
;;;	READ (CHN044,ORDHDR,^FIRST)
	READ (CHN044,ORDHDR,^FIRST) [err=eof]	;ssq 1-10-06
LOOP,
	READS (CHN044,ORDHDR,EOF)
	IF (ORDTYP .NE. 'I') GOTO LOOP		;INTERNET ONLY
	IF (OORDDT.LT.STDAT .OR. OORDDT.GT.ENDAT) GOTO LOOP

	CALL RUNTOT
	CALL DUCTOT
	OSALE = RUNTOT + DUCTOT
	RPTTOT = RPTTOT + OSALE

	XCALL SREP(OSLMAN, LNAM, SNAM, SINT)

	PLINE (1,6) = OORDNO,	'ZZZZZX'
	XDATE(1,4) = OORDDT(5,8)
	XDATE(5,8) = OORDDT(1,4)
	PLINE (8,17) = XDATE,	'ZX/XX/XXXX'
	PLINE (19,43) = OCUSNM
	PLINE (45,54) = SNAM
	PLINE (56,65) = OSALE,	'ZZZ,ZZX.XX'
;;;	PLINE (68,75) = OPROMD,	'ZZZX'
	IF (OLOC.EQ.'O') PLINE(68,72) = OPROMD(5,8),'ZX/XX'

;ORDER#    ENTERED CUSTOMER                  SALES REP      AMOUNT  REQUIRED
;XXXXXX XX/XX/XXXX AAAAAAAAAAAAAAAAAAAAAAAAA AAAAAAAAAA ZZZ,ZZX.XX  XX/XX/XX
;12345678901234567890123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5         6         7         8
	CALL PRINT
	GOTO LOOP


RUNTOT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR RUNTOT
	FIND (CHN045, ORDLIN, OORDNO) [ERR=N_ORD]
N_ORD,
	XCALL IOS (CHN045, ORDLIN, READ, LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF_LIN
	IF (LORDNO .NE. OORDNO) GOTO EOF_LIN
	IF (LTYPE .EQ. 'M') GOTO N_ORD		;SKIP MEMOS

	DECMAL = ((LQTYOR)*LPRICE)#1
&		-(((LQTYOR*LPRICE)#1*LDISC)#2)
	EXEPRC = DECMAL - ((DECMAL*LODISC)#2)
	
	RUNTOT = RUNTOT + EXEPRC
	GOTO N_ORD
EOF_LIN,
	RETURN
;------------------------------------

DUCTOT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR DUCTOT
	DUCTOR = OORDNO
	FIND(CHN175,DUCACC,DUCTOR)[ERR=EOF_DUC]
NXTDUC,
	LOKCTL = 1
	XCALL IOS (CHN175,DUCACC,READ,LOKCTL)
	IF (LOKCTL.NE.0) GOTO EOF_DUC
	IF (DUCTOR.NE.OORDNO) GOTO EOF_DUC

SEL_DUC,
	DECMAL = (POUNDS*GPRICE)#1
	IF (LINER.GT.0.AND.LINER.LE.8) DECMAL = DECMAL + (SQFLIN*LINPRC)#1
	IF (ACC.EQ.1)
	BEGIN
	  IF (SLIPS)  DECMAL = DECMAL + (SLIPS*SLPPRC)#1
	  IF (DRIVES) DECMAL = DECMAL + (DRIVES*DRVPRC)#1
	  IF (TCORN)  DECMAL = DECMAL + (TCORN*CORPRC)#1
	  IF (TNBQTY) DECMAL = DECMAL + (TNBQTY*TNBPRC)#1
	  IF (TGAQTY) DECMAL = DECMAL + (TGAQTY*TGAPRC)#1
	  IF (TCLQTY) DECMAL = DECMAL + (TCLQTY*TCLPRC)#1
	  IF (TBAQTY) DECMAL = DECMAL + (TBAQTY*TBAPRC)#1
	  IF (TBNQTY) DECMAL = DECMAL + (TBNQTY*TBNPRC)#1
	END
	DUCTOT = DUCTOT + DECMAL
	GOTO NXTDUC

EOF_DUC,
	RETURN
;-----------------------------------------------------

PRINT,
	IF (LPONSW.EQ.0) CALL PRNTON
	PRTCTL = 80
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HD1,HD2,HD3,
&			LEG,LEG,LEG,1,132,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
PRNTON,
	LPSW = 1	; PRINT, SPOOL OR DISPLAY

	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) CALL CLOSE
	IF (LPSW.EQ.0) XCALL PGCHN ('CP:COPRPT',1)
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)

	LPONSW = 1
	RETURN
;************************************************************************
EOF,
	CALL PRINT
	USING PASS SELECT
	(0),	PLINE (36,54) = 'TOTAL OPEN ORDERS:'
	(1),	PLINE (33,54) = 'TOTAL INVOICED ORDERS:'
	ENDUSING

	PLINE (56,68) = RPTTOT,	'ZZ,ZZZ,ZZX.XX'
	CALL PRINT

	GRDTOT = GRDTOT + RPTTOT
	CALL CLOSE
	IF (PASS .EQ. 0)
		BEGIN
		LINCNT = 66
		INCR PASS
		CALL OPEN2
		HD3 = 'INVOICED ORDERS'
		GOTO RE_LOOP
		END

	CALL PRINT
	PLINE (33,54) = 'REPORT TOTAL:'
	PLINE (56,68) = GRDTOT,	'ZZ,ZZZ,ZZX.XX'
	CALL PRINT

	IF (LPONSW.EQ.1) XCALL LPOFF (LPSW,SPLFIL,PGCNT)
ENDOFF,
	XCALL PGCHN ('CP:COPRPT',1)

CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (CHN044) CLOSE CHN044
	IF (CHN045) CLOSE CHN045
	IF (CHN175) CLOSE CHN175

	RETURN
;-----------------------------------------------------

OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (1,'SI',044,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN044 = 1

	XCALL FILES (2,'SI',045,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN045 = 2

	XCALL FILES (3,'SI',175,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN175 = 3


	OPNOK = 1
	RETURN
;-----------------------------------------------------

OPEN2,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	; if run befor settin up new files can use SLHHST,
	; else must hardcode last years file number...

	SWITCH = 5
	xcall files (1, 'SI', 184, switch)
;;;	xcall ffile (184, redfil, switch)
;;;	redfil(14,14) = 'M'
;;;	redfil(9,10) = '10'			;year 2010
;;;	open (1, si, redfil)
	CHN044 = 1

	xcall files (2, 'SI', 185, switch)
;;;	xcall ffile (185,redfil, switch)
;;;	redfil(9,10) = '10'			;year 2010
;;;	redfil(14,14) = 'M'
;;;	open (2, si, redfil)
	CHN045 = 2

	xcall files (3, 'SI', 186, switch)
;;;	xcall ffile (186,redfil, switch)
;;;	redfil(9,10) = '10'			;year 2010
;;;	redfil(14,14) = 'M'
;;;	open (3, si, redfil)
	CHN175 = 3

	RETURN
;-----------------------------------------------------

	END


