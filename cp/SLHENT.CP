; SLHENT.COP - PROGRAM TO ALLOW ENTRY OF SALES HISTORY INFO TO A DATA FILE
; 5-18-01 SSQ: MODIFIED TO USE ISAM VERSION OF SLSSUM
;

RECORD	SLSSUM
	.INCLUDE 'DEF:SLSSUM.DEF'

RECORD	PRDACT
	.INCLUDE 'DEF:RD069A.DEF'

RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2

RECORD	CHANNEL
	CHN058	,D2
	CHN069	,D2

RECORD	VARS
	OPNOK	,D1
	CATOK	,D1
	XCAT	,A2
	XDAT	,D4	;YYMM
	CEN	,D2,	20
	YY	,D2
	MM	,D2
	I	,D2
	LOKCTL	,D1
	READ	,D1,	0
	WRITE	,D1,	1
	STORE	,D1,	2
	INXCTL	,D1
	ENTRY	,A10
	DECMAL	,D15
	SRCCTL	,D1
	BSMID	,D5
	BSEND	,D5
	KEY1	,D4
	KEY2	,A2
	SKEY	,A8
	SELECT	,D1
	OPTION	,D1
	CNGCTL	,D1
	WHATNO	,D2
	FILNAM	,A14
	SWITCH	,D1
	V	,D1
PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'SALES SUMMARY ENTRY AND EDITING',1)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO ENDOFF

DISPLA,
	SLSSUM =
	UNLOCK 8
	XCALL OUTPT ( 6,10,2,'1. SALES PERIOD (YYMM)',1)
	XCALL OUTPT ( 8,10,0,'2. CATEGORY',1)
	XCALL OUTPT (10,10,0,'3. SALES DOLLARS',1)

SYYMM,
	CTL = '06,35,04,00,#E'
	CALL INPUT
	GOTO (DISPLA,ENDOFF), INXCTL
	IF (ENTRY.EQ.'    '.AND.KEY1.NE.0)
	BEGIN
	  ENTRY(1,4) = KEY1+1,'XXXX'
	  XCALL OUTPT (ROW,COL,0,ENTRY(1,4),1)
	END
	KEY1 = ENTRY
	YY = KEY1(1,2)
	MM = KEY1(3,4)

	IF (MM.LT.01 .OR. MM.GT.12) GOTO SYYMM
SCAT,
	CTL = '08,35,02,00,A '
	CALL INPUT
	GOTO (DISPLA), INXCTL
	IF (ENTRY.EQ.'  ') 
	BEGIN
	  ENTRY(1,2) = XCAT
	  XCALL OUTPT (ROW,COL,0,ENTRY(1,2),1)
	END
	XCAT = ENTRY
	SCAT = XCAT
	CALL GETCAT
	IF (.NOT. CATOK)
		BEGIN
		XCALL MESAG('INVALID CATEGORY',1)
		GOTO SCAT
		END

	XCALL OUTPT (ROW,40,0,PACTDS,1)

	SCAT = XCAT
	SMM = MM
	SYY(1,2) = CEN
	SYY(3,4) = YY
	XCALL ISIO (CHN058, SLSSUM, SL_KEY, READ, LOKCTL)
	IF (LOKCTL .EQ. 0) GOTO DSPREC
	
	SLSSUM =
	SYY(1,2) = CEN
	SYY(3,4) = YY
	SMM = MM
	SCAT = XCAT
	SELECT = 1			;ADD MODE

SDOLR,
	CTL = '10,35,10,00,$-'
	CALL INPUT
	GOTO (DISPLA), INXCTL
	SDOLR = ENTRY
ANYCNG,
	XCALL ANYCN (CNGCTL,WHATNO)
	IF (CNGCTL)
	BEGIN
	  GOTO (SYYMM,SCAT,SDOLR), WHATNO
	  CNGCTL = 3
	  GOTO ANYCNG
	END
	GOTO (ADD,CHANGE), SELECT
DSPREC,
	SELECT = 2				;CHANGE MODE
	CTL = '06,35,04,04,# '
	XDAT(1,2) = SYY(3,4)		;YEAR
	XDAT(3,4) = SMM			;MONTH
	DECMAL = XDAT			;YYMM
	CALL DSPNUM

	XCALL OUTPT (08,35,0,SCAT,1)
	CALL GETCAT
	XCALL OUTPT (08,40,0,PACTDS,1)

	CTL = '10,35,08,00,$ '
	DECMAL = SDOLR
	CALL DSPDLR
	GOTO ANYCNG

ADD,
	LOKCTL = 1
	XCALL ISIO (CHN058, SLSSUM, SL_KEY, STORE, LOKCTL)
	IF(LOKCTL)
		BEGIN
		XCALL MESAG('ERROR STORING SLSSUM',1)
		END
	GOTO DISPLA

CHANGE,
	LOKCTL = 1
	XCALL ISIO (CHN058, SLSSUM, SL_KEY, WRITE, LOKCTL)
	GOTO DISPLA

;==========================================
DSPNUM,
	OPTION = 1
	GO TO CALDSP
DSPDTE,
	OPTION = 2
	GO TO CALDSP
DSPDLR,
	OPTION = 3
CALDSP,
	XCALL DSPLY (MAX,ROW,COL,DECMAL,OPTION,1)
	RETURN
ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('CP:SPCFUN',1)
	STOP
INPUT,
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,1)
	RETURN
SORT,
	IF (INXCTL.EQ.2) GOTO ENDOFF
	GOTO DISPLA

GETCAT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL ISIO (CHN069, PRDACT, SCAT, READ, LOKCTL)
	
	IF(LOKCTL)
	THEN	CLEAR CATOK
	ELSE	CATOK = 1
	RETURN
;----------------------------------------------------------

OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (8,'SU',58,SWITCH)		;FILE 58 - SLSSUM
	IF (SWITCH.EQ.9) RETURN
	CHN058 = 8

	XCALL FILES (7,'SI',69,SWITCH)		;PRDACT
	IF (SWITCH .EQ. 9) RETURN
	CHN069 = 7

	OPNOK = 1
	RETURN
;-----------------------------------------

CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (CHN058) CLOSE CHN058
	IF (CHN069) CLOSE CHN069

	RETURN
;-----------------------------------------

END
