;XTWGHT.CP
;
;	Weight matrix for configured parts.
;
;
;
RECORD	FILPRC
	F_DEV	,A3
		,A1,	':'
	F_NAME	,A6
		,A1,	'.'
	F_EXT	,A3

RECORD TWEGHT
	.INCLUDE 'DEF:TWEGHT.DEF'

RECORD
	GTYPE	,4A14,	'FITTINGS      ',
&			'SWEDGE SADDLE ',
&			'CONICAL SADDLE',
&			'SHOE TAP      '

	GTYP2	,4A2,	'FT','SS','CS','ST'
	GMAT	,6A5,	'GALV ','PVS  ','ALUM ','SST  ','PGRIP','PRINT'
	GMAT2	,5A1,	'G','P','A','S','R'
	GCOM	,5A7,	'MAIN   ','90 DEG ','45 DEG ','REDUCER','WELD-RD'
	GCOM2	,5A2,	'MN','S9','S4','RD','RW'
	DGA	,D2

RECORD	HLINE
	HMAT	,A5
		,A1
	HGA	,D2
		,A2,	'GA'
		,A1
	HDES1	,A7
		,A1
	HDES2	,A14



RECORD HDR1
		,A40,'SZ"   WEIGHT        SZ"   WEIGHT        '
		,A40,'SZ"   WEIGHT        SZ"   WEIGHT        '
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2

RECORD
	I	,D2
		,A1,'"'
RECORD	,X
	AI	,A3


RECORD	VARS
	STTYP	,A2
	ENTYP	,A2
	STMAT	,D1
	ENMAT	,D1
	STCOM	,A2
	ENCOM	,A2
	STGA	,D2
	ENGA	,D2
	SAVTYP	,D1
	SAVMAT	,D1
	SAVCON	,D1
	SAVGA	,D2
	DTYPE	,D2
	MTYPE	,D2
	MST	,D2
	MEND	,D2
	INXCTL	,D2
	ENTRY	,A6
	CNGCTL	,D2
	WHATNO	,D2
	READ	,D1,0
	WRITE	,D1,1
	LOKCTL	,D1
	DLMASK	,A6,'ZZZ.XX'
	V	,D1
	ENTALL	,D1
	LINCNT	,D2
	PGCNT	,D3
	TITLE	,A*,	'WEIGHT ARRAY REPORT'
	PLINE	,A80
	HD	,A6,	'NO HDR'
	RPTNUM	,D3
	PRTTYP	,A1
	SPLFIL	,A14
	LPARG	,D1
	LPSW	,D1
	SWITCH	,D1
;
PROC
	XCALL TERID (V)

	XCALL FFILE(1,FILPRC,SWITCH)	;SSQ 9-9-03
	F_NAME = 'XTWGHT'		;SSQ 9-9-03
	F_EXT = 'ISM'			;SSQ 9-9-03
	OPEN(1,SU,FILPRC)		;SSQ 9-9-03


DISPLM,
	XCALL OUTPT (1,1,2,'WEIGHT MATRIX FOR SADDLES & TEES',1)
	XCALL OUTPT (4,10,0,'1. TYPE',1)
	XCALL OUTPT (6,10,0,'2. MATERIAL',1)
	XCALL OUTPT (8,10,0,'3. COMPONENT',1)
	XCALL OUTPT (10,10,0,'4. GAUGE',1)

GTYPE,
	FOR I FROM 1 THRU 4 
		BEGIN
		PLINE(1,1) = I,	'X'
		PLINE(2,2) = '.'
		PLINE(4,17) = GTYPE(I)
		XCALL OUTPT (I+3,40,0,PLINE(1,17),1)
		END
	XCALL INPUT (4,24,01,00,'#E',ENTRY,INXCTL,1)
	GOTO (DISPLM,ENDOFF),INXCTL

	I = ENTRY(1,1)
	IF (I.EQ.0)I = SAVTYP
	IF (I.EQ.0)I = 1
	ENTRY(1,1) = I,'X'
	XCALL OUTPT (4,24,0,ENTRY(1,1),1)
	SAVTYP = I
	
	IF (I.LT.1 .OR. I.GT.4) GOTO GTYPE
	HDES2 = GTYPE(I)
	WTYP = GTYP2(I)	
	FOR I FROM 1 THRU 4 XCALL OUTPT (I+3,40,1,'\',1)
	XCALL OUTPT (4,40,1,HDES2,1)
	GOTO (ANYCN),CNGCTL
GMAT,
	FOR I FROM 1 THRU 6
		BEGIN
		PLINE(1,1) = I,	'X'
		PLINE(2,2) = '.'
		PLINE(4,9) = GMAT(I)
		XCALL OUTPT (I+5,40,0,PLINE(1,9),1)
		END
	XCALL INPUT (6,24,01,00,'#E',ENTRY,INXCTL,1)
	GOTO (DISPLM,ENDOFF),INXCTL
	I = ENTRY(1,1)
	IF (I.EQ.0)I = SAVMAT
	IF (I.EQ.0)I = 1
	ENTRY(1,1) = I,'X'
	XCALL OUTPT (6,24,0,ENTRY(1,1),1)
	SAVMAT = I

	IF (I.LT.1 .OR. I.GT.6) GOTO GMAT
	IF (I .EQ. 6) GOTO PRTFIL
	HMAT = GMAT(I)
	WMAT = GMAT2(I)
	FOR I FROM 1 THRU 6 XCALL OUTPT (I+5,40,1,'\',1)
	XCALL OUTPT (6,40,1,HMAT,1)
	GOTO (ANYCN),CNGCTL
GCOM,
	FOR I FROM 1 THRU 5
		BEGIN
		PLINE(1,1) = I,	'X'
		PLINE(2,2) = '.'
		PLINE(4,11) = GCOM(I)
		XCALL OUTPT (I+7,40,0,PLINE(1,11),1)
		END
	XCALL INPUT(8,24,01,00,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLM),INXCTL
	I = ENTRY(1,1)

	IF (I.EQ.0)I = SAVCON
	IF (I.EQ.0)I = 1
	ENTRY(1,1) = I,'X'
	XCALL OUTPT (8,24,0,ENTRY(1,1),1)
	SAVCON = I

	IF (I.LT.1 .OR. I.GT.5) GOTO GCOM
	HDES1 = GCOM(I)
	WCON = GCOM2(I)
	FOR I FROM 1 THRU 5 XCALL OUTPT (I+7,40,1,'\',1)
	XCALL OUTPT (8,40,1,HDES1,1)
	GOTO (ANYCN),CNGCTL
	
GAUGE,
	XCALL INPUT (10,24,02,00,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLM),INXCTL
	DGA = ENTRY(1,2)

	IF (DGA.EQ.0)DGA = SAVGA
	IF (DGA.EQ.0)DGA = 26
	ENTRY(1,2) = DGA,'XX'
	XCALL OUTPT (10,24,0,ENTRY(1,2),1)
	SAVGA = DGA
	IF (DGA.LT.16 .OR. DGA.GT.26) GOTO GAUGE

	HGA = DGA
	WGA = DGA
ANYCN,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (PROCES,CNGBR),CNGCTL + 1
CNGBR,
	GOTO (GTYPE,GMAT,GCOM,GAUGE),WHATNO
	GOTO ANYCN

ENDOFF,
	CLOSE 1
	XCALL PGCHN ('CP:w_menu',1)

PROCES,

	LOKCTL = 1
	XCALL ISIO (1,TWEGHT,WT_KEY,READ,LOKCTL)
	IF (LOKCTL .NE. 0)
		BEGIN
		XCALL MESAG('NO WEIGHT TABLE FOR THAT COMBINATION',1)
		GOTO DISPLM
		END

	INXCTL =
	CNGCTL =
DISPLA,
	XCALL OUTPT (1,40,2,HLINE,1)
	XCALL OUTPT (3,1,0,HDR1,1)

	FOR I FROM 1 THRU 80
	BEGIN
	  CALL SETCUP
	  XCALL OUTPT (ROW,COL,1,AI,1)
	  XCALL OUTPT (0,0,0,'   ',1)
	  ENTRY(1,6) = W_ARRAY(I),DLMASK
	  XCALL OUTPT (0,0,0,ENTRY(1,6),1)
	END
	I =
	GOTO ANYCNG
INPLP,
	INCR I
	IF (I.GT.80) GOTO ANYCNG
GETPRC,
	CTL = '00,00,05,00,$X'
	CALL SETCUP
	COL = COL + 6
	CALL INPUT
	GOTO (DISPLA,ANYCNG,CLRALL), INXCTL
	W_ARRAY (I) = ENTRY
	ENTRY(1,6) = W_ARRAY(I),DLMASK
	XCALL OUTPT (ROW,COL,0,ENTRY(1,6),1)
	IF (ENTALL) GOTO INPLP
	GOTO ANYCNG
ANYCNG,
	ENTALL =
	XCALL ANYCN (CNGCTL,WHATNO)
	IF (CNGCTL.EQ.1)
	BEGIN
	  IF (WHATNO.GT.80) GOTO ANYCNG
	  I = WHATNO
	  GOTO GETPRC
	END
	LOKCTL = 1
	XCALL ISIO (1,TWEGHT,WT_KEY,WRITE,LOKCTL)
;;;	XCALL IO (1,TWEGHT,DTYPE,WRITE,LOKCTL)
	GOTO DISPLM
CLRALL,
	ENTALL =
	XCALL OUTPT (24,1,1,'CLEAR AND REENTER ALL WEIGHT IN THIS CATEGORY <N> ? ',1)
	CTL = '24,53,01,00,YN'
	CALL INPUT
	IF (INXCTL.NE.1) GOTO ANYCNG
	FOR I FROM 1 THRU 80
	BEGIN
	  CALL SETCUP
	  W_ARRAY(I) =
	  ENTRY(1,6) = W_ARRAY(I),DLMASK
	  XCALL OUTPT (ROW,COL,1,AI,1)
	  XCALL OUTPT (0,0,0,'   ',1)
	  XCALL OUTPT (0,0,0,ENTRY(1,6),1)
	END
	I=
	ENTALL = 1
	XCALL OUTPT (24,1,1,'\',1)
	GOTO INPLP
SETCUP,
	IF (I.GE.1.AND.I.LE.20)
	BEGIN
	  ROW = 4+((I-1)*1)
	  COL = 01
	END
	IF (I.GE.21.AND.I.LE.40)
	BEGIN
	  ROW = 4+((I-20-1)*1)
	  COL = 21
	END
	IF (I.GE.41.AND.I.LE.60)
	BEGIN
	  ROW = 4+((I-40-1)*1)
	  COL = 41
	END
	IF (I.GE.61.AND.I.LE.80)
	BEGIN
	  ROW = 4+((I-60-1)*1)
	  COL = 61
	END
	RETURN
INPUT,
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,1)
	RETURN

PRTFIL,
	XCALL OUTPT (1,1,2,'WEIGHT MATRIX FOR SADDLES & TEES',1)
	XCALL OUTPT ( 4,10,0,'1.      TYPE X    THRU X',1)
	XCALL OUTPT ( 6,10,0,'2.  MATERIAL X    THRU X',1)
	XCALL OUTPT ( 8,10,0,'3. COMPONENT X    THRU X',1)
	XCALL OUTPT (10,10,0,'4.     GAUGE X    THRU X',1)
;			      0123456789012345678901234567890123456
;			      1         2         3         4
P_GTYPE,
	FOR I FROM 1 THRU 4 
		BEGIN
		PLINE(1,1) = I,	'X'
		PLINE(2,2) = '.'
		PLINE(4,17) = GTYPE(I)
		XCALL OUTPT (I+3,40,0,PLINE(1,17),1)
		END
	XCALL INPUT (4,23,01,00,'#E',ENTRY,INXCTL,1)
	GOTO (DISPLM,ENDOFF),INXCTL

	I = ENTRY(1,1)
	IF (I.EQ.0)I = SAVTYP
	IF (I.EQ.0)I = 1
	ENTRY(1,1) = I,'X'
	XCALL OUTPT (4,23,0,ENTRY(1,1),1)
	SAVTYP = I
	
	IF (I.LT.1 .OR. I.GT.4) GOTO P_GTYPE
	STTYP = GTYP2(I)

	XCALL INPUT (4,33,01,00,'#E',ENTRY,INXCTL,1)
	GOTO (DISPLM,ENDOFF),INXCTL
	I = ENTRY(1,1)
	IF (I.EQ.0)I = SAVTYP
	IF (I.EQ.0)I = 1
	IF (I.LT.1 .OR. I.GT.4) GOTO P_GTYPE
	ENTRY(1,1) = I,'X'
	XCALL OUTPT (4,33,0,ENTRY(1,1),1)
	ENTYP = GTYP2(I)

	FOR I FROM 1 THRU 4 XCALL OUTPT (I+3,40,1,'\',1)
	GOTO (P_ANYCN),CNGCTL

P_GMAT,
	FOR I FROM 1 THRU 5
		BEGIN
		PLINE(1,1) = I,	'X'
		PLINE(2,2) = '.'
		PLINE(4,9) = GMAT(I)
		XCALL OUTPT (I+5,40,0,PLINE(1,9),1)
		END
	XCALL INPUT (6,23,01,00,'#E',ENTRY,INXCTL,1)
	GOTO (DISPLM,ENDOFF),INXCTL
	I = ENTRY(1,1)
	IF (I.EQ.0)I = SAVMAT
	IF (I.EQ.0)I = 1
	ENTRY(1,1) = I,'X'
	IF (I.LT.1 .OR. I.GT.5) GOTO P_GMAT
	XCALL OUTPT (6,23,0,ENTRY(1,1),1)
	SAVMAT = I

	STMAT = I
;;;	STMAT = GMAT2(I)
	GOTO (P_ANYCN),CNGCTL


	XCALL INPUT (6,33,01,00,'#E',ENTRY,INXCTL,1)
	GOTO (DISPLM,ENDOFF),INXCTL
	I = ENTRY(1,1)
	IF (I.EQ.0)I = SAVMAT
	IF (I.EQ.0)I = 1
	ENTRY(1,1) = I,'X'
	IF (I.LT.1 .OR. I.GT.5) GOTO P_GMAT
		
	ENMAT = I
;;;	ENMAT = GMAT2(I)
	XCALL OUTPT (6,33,0,ENTRY(1,1),1)
	FOR I FROM 1 THRU 5 XCALL OUTPT (I+5,40,1,'\',1)
P_GCOM,
;;;	FOR I FROM 1 THRU 4 
	FOR I FROM 1 THRU 5
		BEGIN
		PLINE(1,1) = I,	'X'
		PLINE(2,2) = '.'
		PLINE(4,11) = GCOM(I)
		XCALL OUTPT (I+7,40,0,PLINE(1,11),1)
		END
	XCALL INPUT(8,23,01,00,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLM),INXCTL
	I = ENTRY(1,1)

	IF (I.EQ.0)I = SAVCON
	IF (I.EQ.0)I = 1
	ENTRY(1,1) = I,'X'
	XCALL OUTPT (8,23,0,ENTRY(1,1),1)
	SAVCON = I

;;;	IF (I.LT.1 .OR. I.GT.4) GOTO GCOM
	IF (I.LT.1 .OR. I.GT.5) GOTO GCOM
	STCOM = GCOM2(I)

	XCALL INPUT(8,33,01,00,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLM),INXCTL
	I = ENTRY(1,1)

	IF (I.EQ.0)I = SAVCON
	IF (I.EQ.0)I = 1
	ENTRY(1,1) = I,'X'
	XCALL OUTPT (8,33,0,ENTRY(1,1),1)
;;;	IF (I.LT.1 .OR. I.GT.4) GOTO GCOM
	IF (I.LT.1 .OR. I.GT.5) GOTO GCOM

	ENCOM = GCOM2(I)

	FOR I FROM 1 THRU 5 XCALL OUTPT (I+7,40,1,'\',1)
	GOTO (ANYCN),CNGCTL
	
P_GAUGE,
	XCALL INPUT (10,23,02,00,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLM),INXCTL
	STGA = ENTRY(1,2)

	IF (STGA.EQ.0)STGA = SAVGA
	IF (STGA.EQ.0)STGA = 26
	ENTRY(1,2) = STGA,'XX'
	XCALL OUTPT (10,23,0,ENTRY(1,2),1)
	SAVGA = STGA
	IF (STGA.LT.16 .OR. STGA.GT.26) GOTO P_GAUGE

	XCALL INPUT (10,33,02,00,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLM),INXCTL
	ENGA = ENTRY(1,2)

	IF (ENGA.EQ.0)ENGA = SAVGA
	IF (ENGA.EQ.0)ENGA = 26
	ENTRY(1,2) = ENGA,'XX'
	XCALL OUTPT (10,33,0,ENTRY(1,2),1)
	SAVGA = ENGA
	IF (ENGA.LT.16 .OR. ENGA.GT.26) GOTO P_GAUGE


P_ANYCN,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (P_PROCES,P_CNGBR),CNGCTL + 1
P_CNGBR,
	GOTO (P_GTYPE,P_GMAT,P_GCOM,P_GAUGE),WHATNO
	GOTO ANYCN
P_PROCES,
;-------------------
	LPSW = 1
	SPLFIL (5,6) = 
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO ENDPFI
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	LINCNT = 62
	PGCNT =

	CLEAR WT_KEY
	WTYP = STTYP
	WMAT = STMAT
	WCON = STCOM
	WGA = STGA

	FIND(1,TWEGHT,WT_KEY) [ERR=PRLOOP]
PRLOOP,
	XCALL IOS (1,TWEGHT,READ,LOKCTL)
	if (lokctl .ne. 0) goto pr_eof
	IF (WTYP .GT. ENTYP) GOTO PR_EOF
	FOR i FROM STMAT THRU ENMAT 
		BEGIN
		IF (WMAT.EQ.GMAT2(i) ) GOTO PR_MAT_OK
		END
	GOTO PRLOOP
PR_MAT_OK,
;;	IF (WMAT.LT.STMAT .OR. WMAT.GT.ENMAT) GOTO PRLOOP
	IF (WCON.LT.STCOM .OR. WCON.GT.ENCOM) GOTO PRLOOP
	IF (WGA.LT.STGA .OR. WGA.GT.ENGA) GOTO PRLOOP

	IF (LINCNT+23.GT.60) LINCNT = 62

	USING WMAT SELECT
	('G'),	PLINE = 'GALVANIZED'
	('P'),	PLINE = 'POLYVINYL'
	('A'),	PLINE = 'ALUMINUM'
	('S'),	PLINE = 'STAINLESS STEEL'
	('R'),	PLINE = 'PAINT GRIP'
	ENDUSING

	USING WTYP SELECT
	('FT'),	PLINE(%TRIM(PLINE)+2,30) = 'FITTINGS'
	('SS'),	PLINE(%TRIM(PLINE)+2,30) = 'SWEDGE SADDLES'
	('CS'),	PLINE(%TRIM(PLINE)+2,30) = 'CONICAL SADDLES'
	('ST'),	PLINE(%TRIM(PLINE)+2,30) = 'SHOE TAP'
	ENDUSING
	CALL PRINT

	PLINE (1,2) = WGA,	'XX'
	PLINE (4,5) = 'GA'
	USING WCON SELECT
	('MN'),	PLINE(7,30) = 'MAIN'
	('S4'),	PLINE(7,30) = '45 DEG SADDLE'
	('S9'),	PLINE(7,30) = '90 DEG SADDLE'
	('RD'),	PLINE(7,30) = 'REDUCER'	
	('RW'),	PLINE(7,30) = 'WELDED REDUCER'	
	ENDUSING

	PLINE(%TRIM(PLINE)+2,60) = 'PRICE'
	CALL PRINT

	PLINE = HDR1
	CALL PRINT

	FOR I FROM 1 THRU 20
	  BEGIN
	    PLINE ( 1, 3) = AI
	    PLINE ( 7,12) = W_ARRAY(I), DLMASK
	    I = I + 20
	    PLINE (21,23) = AI
	    PLINE (27,32) = W_ARRAY(I), DLMASK
	    I = I + 20
	    PLINE (41,43) = AI
	    PLINE (47,52) = W_ARRAY(I), DLMASK
	    I = I + 20
	    PLINE (61,63) = AI
	    PLINE (67,72) = W_ARRAY(I), DLMASK
	    I = I - 60
	    CALL PRINT
	END
	XCALL LINFD (2)
	LINCNT = LINCNT + 2
	GOTO PRLOOP
PR_EOF,
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
ENDPFI,
	GOTO DISPLM

PRINT,
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HD,' ',' ',
&		'NO LEG',' ',' ',0,80,80,0,LPSW,RPTNUM,PRTTYP)
	RETURN
END


