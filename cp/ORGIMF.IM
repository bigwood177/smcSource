;  ORGIMF / IM 
;
;
;		::PCPYIM.DEF::
;*****************************************************************************
;		INVENTORY MANAGEMENT  
;
;		RELEASED: AUGUST 1, 1984 (d70s10)
;*****************************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;
;		REMOVES RECORDS MARKED FOR DELETION FROM ITMMAS
;		AND CREATES NEW ITMIDX
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD ITMMAS		;
		.INCLUDE 'DEF:RD041A.DEF'
RECORD DUMINV		;
		.INCLUDE 'DEF:RD041B.DEF'
RECORD ITMIDX		;
		.INCLUDE 'DEF:RD042A.DEF'
RECORD BRACKS		;
		.INCLUDE 'DEF:RD041C.DEF'
RECORD SNDMSG
	FILNAM	,A9, 'IM:IMFCNT'
	RCNT	,D5
	OCNT	,D5
	FILNUM	,D3, 042
RECORD
	MSGPRG	,A9
	RRECNO	,D5		; Record# last read
	WRECNO	,D5		; Record# last written
	RECCNT	,D5		; # of total records from control record
	COL2	,D2
	SWITCH	,D1
	V	,D1
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
PROC (1)
	XCALL TERID (V)
	XCALL OUTPT (2,01,1,'REORGANIZE ITMMAS FILE',V)
	XCALL WATE (4,V)
	XCALL FILES (2,'U',042,5)		;FILE # 042 -- ITMIDX FILE
	XCALL FILES (1,'U',041,5)		;FILE # 041 -- ITMMAS FILE
	LOKCTL = 1
	XCALL IO (1,DUMINV,1,READ,LOKCTL)
	RECCNT = REC041
	RRECNO = 1
	WRECNO = 1
RD041,
	INCR RRECNO
	IF (RRECNO.GT.RECCNT) GO TO EOF041
	LOKCTL = 1
	XCALL IO (1,ITMMAS,RRECNO,READ,LOKCTL)
	IF (DESCR(1,6).EQ.']]]]]]')  GO TO EOF041
	IF (DESCR(1,6).EQ.']]]DEL') GO TO RD041
	INCR WRECNO
	LOKCTL = 1
	XCALL IO (1,ITMMAS,WRECNO,WRITE,LOKCTL)
	IITMNO = ITEMNO
	IRC041 = WRECNO
	IPRCAT = PRDCAT
	LOKCTL = 1
	XCALL IO (2,ITMIDX,WRECNO,WRITE,LOKCTL)
	GO TO RD041
EOF041,
	SPCFLG = 1
	ORG041 = 1
	REC041 = WRECNO
	DEL041 = 0
	LOKCTL = 1
	XCALL IO (1,DUMINV,1,WRITE,LOKCTL)
	ITMMAS = BRACKS
	ITMIDX = BRACKS
BRK041,
	IF (WRECNO.GE.RECCNT) GO TO END
	INCR WRECNO
	LOKCTL = 1
	XCALL IO (1,ITMMAS,WRECNO,WRITE,LOKCTL)
	LOKCTL = 1
	XCALL IO (2,ITMIDX,WRECNO,WRITE,LOKCTL)
	GO TO BRK041
END,
	CLOSE 1
	CLOSE 2
	RCNT = REC041
	OCNT = 1
	SWITCH = 5
	XCALL SNMSG (SNDMSG,SWITCH)
	IF (TYPSYS.LE.2) MSGPRG = 'CP:CPMENU'
	IF (TYPSYS.GT.2) MSGPRG = 'IM:PRSSRT'
	SWITCH = 2
	XCALL SNMSG (MSGPRG,SWITCH)
	XCALL PGCHN ('CP:SRTIID',1)
END
