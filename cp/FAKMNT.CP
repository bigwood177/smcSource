;FAKMNT.CP
;
;	fake parts
;PPXMNT.CP	
;
;	COP TABLE MAINTENANCE
;	FAKE PARTS X-REF
;
RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'

RECORD	ITMMAS
	.INCLUDE 'DEF:RD041A.DEF'

RECORD	CHANNEL
	CHN041	,D2
	CHN182	,D2


RECORD	PRINT
	TITLE	,A*,	'FAKE PARTS X-REF'
	HD	,A6,	'NO HDR'
	LG	,A9,	'NO LEGEND'
	PLINE	,A132
	PRNTON	,D1
	LINCNT	,D2,60
	PGCNT	,D6
	LPSW	,D2
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	PRTCTL	,D3,132
	LPARG	,D1
	PRNTSW	,D1
	PRTCTR	,D1

RECORD	HD1
		,A*,	'FAKE ITEM       REAL ITEM     FAKE GA'

RECORD	VARS
	XGA	,D2
	LF1	,D3
	LF2	,D3
	LF3	,D5
	ITMRFA	,A6
	IVFLAG	,D1
	VANFLG	,D1
	VANRFA	,A6
	ALPHA	,A12
	OPNOK	,D1
	BLANKS	,A30
	A15	,A15
	ST_ITEM	,A15
	EN_ITEM	,A15
	ITEM	,A15
	DESC	,A30
	PGM	,D1
	ROW	,D2
	ENTRY	,A30
	INXCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	STORE	,D1,2
	DELETE	,D1,3
	LOKCTL	,D1
	WHATNO	,D2
	SELECT	,D1
	CNGCTL	,D1
	I	,D3
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'FAKE PARTS X-REF',1)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO CLOSE

MENU,
	XCALL OUTPT (1,1,2,'FAKE PARTS X-REF',1)
	XCALL OUTPT (3,9,0,'PLEASE SELECT APPLICATION',1)
	XCALL OUTPT (5,15,0,'1. TABLE MAINTENANCE',1)
	XCALL OUTPT (6,15,0,'2. PRINT TABLE',1)
MINPUT,
	XCALL INPUT (3,36,1,1,'#E',ENTRY,INXCTL,1)
	GOTO (MINPUT,ENDOFF), INXCTL
	PGM = ENTRY(1,1)
	GOTO (DISPLA,PRINT_TABLE),PGM
	GOTO MINPUT

DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,'FAKE PARTS X-REF',1)
	XCALL OUTPT ( 4,4,0,'1. FAKE ITEM #',1)
	XCALL OUTPT ( 6,4,0,'2. REAL ITEM #',1)
	XCALL OUTPT ( 8,4,0,'3. FAKE GA',1)
ITEM,
	XCALL INPUT (4,24,15,00,'AE',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	ITEM = ENTRY(1,15)

	CLEAR TBL_KEY
	TBL_KEY(1,2) = 'FP'
	IK_ITEM = ITEM
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .EQ. 0)
		BEGIN
		SELECT = 2
		CALL DSPREC
		GOTO ANYCNG
		END

	SELECT = 1			;ADD MODE
	CLEAR COPTBL

	XCALL GTITM (ITEM, LF1, LF2, LF3, ITMRFA, IVFLAG, VANFLG, VANRFA)
	IF (VANFLG .EQ. 0) GOTO I_ERR
	READ (CHN041, ITMMAS, RFA:VANRFA) [ERR=I_ERR]
	FP_DESC = DESCR				;DESC OF FAKE ITEM	
	xcall outpt (4,41,0,fp_desc,1)
	GOTO PART
I_ERR,
	XCALL MESAG ('VANILLA ITEM NOT FOUND',1)
	GOTO ITEM

PART,
	XCALL INPUT (6,24,15,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	FP_PART = ENTRY(1,15)

	XCALL GTITM (FP_PART, LF1, LF2, LF3, ITMRFA, IVFLAG, VANFLG, VANRFA)
	IF (IVFLAG .EQ. 0) 
		BEGIN
		XCALL MESAG ('REAL ITEM NOT FOUND',1)
		GOTO PART
		END
GA,
	XCALL INPUT (8,24,02,00,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	XGA = ENTRY(1,2)
	GOTO (ANYCNG), CNGCTL

ANYCNG,
	XCALL OUTPT (24,1,1,'FIELD # TO CHANGE            <TAB> = DELETE',1)
	XCALL INPUT (24,20,02,00,'#T',ENTRY,INXCTL,1)
	IF (INXCTL .EQ. 3)
		BEGIN
		XCALL OUTPT (24,1,1,'DELETE, ARE YOU SURE ?',1)
		XCALL INPUT (24,24,01,01,'YN',ENTRY,INXCTL,1)
		GOTO (ANYCNG),INXCTL-1
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,DELETE,LOKCTL)
		GOTO DISPLA
		END

	WHATNO = ENTRY(1,2)
	IF (WHATNO .EQ. 0)
	THEN 	CLEAR CNGCTL
	ELSE 	CNGCTL = 1

	GOTO (PROCES,CNGBR),CNGCTL+1
CNGBR,
	GOTO (ITEM, PART, GA), WHATNO
	GOTO ANYCNG

PROCES,
	FP_GA = XGA
	CASE SELECT OF
	BEGINCASE
	1:	BEGIN
		TBLCOD = 'FP'
		FP_ITEM = ITEM
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,STORE,LOKCTL)
		END
	2:	XCALL ISIO (CHN182,COPTBL,TBL_KEY,WRITE,LOKCTL)
	ENDCASE
	GOTO DISPLA

ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('CP:TBLMNU',1)
	STOP


DSPREC,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; DISPLAY RECORD
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL OUTPT (4,24,1,FP_ITEM,1)
	xcall outpt (4,41,0,fp_desc,1)
	XCALL OUTPT (6,24,1,FP_PART,1)

	ENTRY(1,2) = FP_GA, 'XX'
	XCALL OUTPT (8,24,1,ENTRY(1,2),1)

	RETURN
;-----------------------------------


;===================================================================
PRINT_TABLE,
;===================================================================
	LINCNT = 66
	PGCNT  = 0
	
PDISP,
	CNGCTL = 
	XCALL OUTPT (1,1,2,'PRINT FAKE ITEM TABLE',1)
	XCALL OUTPT (4,4,0,'1. STARTING ITEM',1)
	XCALL OUTPT (6,4,0,'2. ENDING   ITEM',1)
ST_ITEM,
	XCALL INPUT (4,25,15,00,'AE',ENTRY,INXCTL,1)
	GOTO (PDISP,PDONE),INXCTL
	ST_ITEM = ENTRY(1,15)
	IF (ST_ITEM .EQ. A15)
		BEGIN
		EN_ITEM = 'ZZZZZZZZZZZZZZZ'

		XCALL OUTPT (4,25,1,'ALL',1)
		XCALL OUTPT (6,25,1,' ',1)
		GOTO P_ANY
		END
	GOTO (P_ANY),CNGCTL
EN_ITEM,
	XCALL INPUT (6,25,15,00,'A ',ENTRY,INXCTL,1)
	GOTO (PDISP),INXCTL
	EN_ITEM = ENTRY(1,15)
	IF (EN_ITEM .EQ. A15)
		BEGIN
		EN_ITEM = ST_ITEM
		XCALL OUTPT (6,25,0,EN_ITEM,1)
		END
P_ANY,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (P_PRINT,P_CNGBR),CNGCTL + 1
P_CNGBR,
	GOTO (ST_ITEM,EN_ITEM),WHATNO
	GOTO P_ANY

P_PRINT,
	CLEAR TBL_KEY
	TBLCOD = 'FP'
	IK_ITEM = ST_ITEM
	FIND (CHN182,COPTBL,TBL_KEY)[ERR=PLOOP]

PLOOP,
	XCALL IOS (CHN182,COPTBL,READ,LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF
	IF (TBLCOD .NE. 'FP') GOTO EOF
	IF (FP_ITEM .LT. ST_ITEM) GOTO PLOOP
	IF (FP_ITEM .GT. EN_ITEM) GOTO EOF

	PLINE(1,15) = FP_ITEM	
	PLINE(17,32) = FP_PART
	PLINE(34,35) = FP_GA, 'ZZ'

	CALL PRINT
	GOTO PLOOP

;FAKE ITEM       REAL ITEM        GA
;AAAAAAAAAAAAAAA AAAAAAAAAAAAAAA  XX
;123456789012345678901234567890123456789
;         1         2         3
EOF,
	IF (PRNTON.EQ.1)  XCALL LPOFF(LPSW,SPLFIL,PGCNT)
	PRNTON  = 0

PDONE,
	GOTO MENU

PRINT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (PRNTON .EQ. 0) CALL PRNTON
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HD1,HD,HD
&		,LG,LG,LG,0,132,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;-------------------------------------------------------------
PRNTON,
	SPLFIL (5,6) = 'EF'
	LPSW = 1		;PRINT,SPOOL, OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTON = 1
	RETURN
;-------------------------------------------------------------
;===================================================================


OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (1,'SU',182,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN182 = 1


	SWITCH = 5
	XCALL FILES (2,'SI',041,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN041 = 2

	OPNOK = 1
	RETURN
;----------------------------------------------------

CLOSE,
	IF (CHN182) CLOSE CHN182
	IF (CHN041) CLOSE CHN041
	RETURN
;----------------------------------------------------
	
	


