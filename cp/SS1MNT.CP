;SS1MNT.CP
;
;	COP TABLE MAINTENANCE
;	SAFETY SEAL PARAMETERS
;
;
RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'


RECORD	CHANNEL
	CHN182	,D2


RECORD	PRINT
	TITLE	,A*,	'COMPLETE SEAL PARAMETERS'
	HD	,A6,	'NO HDR'
	LG	,A9,	'NO LEGEND'
	PLINE	,A80
	PRNTON	,D1
	LINCNT	,D2,60
	PGCNT	,D6
	LPSW	,D2
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	PRTCTL	,D3,080
	LPARG	,D1
	PRNTSW	,D1
	PRTCTR	,D1


RECORD	HD1
		,A*,	'DEPT  DESCRIPTION                    DEFAULT LOCATION'
		,A*,	'    W/O PRINTER'

RECORD	VARS
	KEY	,D2
	SAVKEY	,D2
	SAVTYP	,A1
	A1	,A1
	A2	,A2
	A7	,A7
	ALPHA	,A12
	OPNOK	,D1
	BLANKS	,A30
	TYPE	,A1
	DIM	,D2
	DESC	,A30
	LOC	,A20	
	QUE	,A12
	ST_DIM	,D2
	EN_DIM	,D2
	PGM	,D1
	ROW	,D2
	ENTRY	,A30
	INXCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	STORE	,D1,2
	DELETE	,D1,3
	LOKCTL	,D1
	WHATNO	,D2
	SELECT	,D1
	CNGCTL	,D1
	I	,D3
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'COMPLETE SEAL PARAMETERS TABLE',1)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO CLOSE

MENU,
	SAVTYP = '*'
	XCALL OUTPT (1,1,2,'COMPLETE SEAL PARAMETERS TABLE',1)
	XCALL OUTPT (3,9,0,'PLEASE SELECT APPLICATION',1)
	XCALL OUTPT (5,15,0,'1. TABLE MAINTENANCE',1)
	XCALL OUTPT (6,15,0,'2. PRINT TABLE',1)
MINPUT,
	XCALL INPUT (3,36,1,1,'#E',ENTRY,INXCTL,1)
	GOTO (MINPUT,ENDOFF), INXCTL
	PGM = ENTRY(1,1)
	GOTO (DISPLA,PRINT_TABLE),PGM
	GOTO MINPUT

DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,'COMPLETE SEAL PARAMETERS TABLE',1)
	XCALL OUTPT ( 4,4,0,'1. KEY',1)
	XCALL OUTPT ( 6,4,0,'2. COMPONENT (BODY, REDUCER ,TAP)      (B,R,T)',1)
	XCALL OUTPT ( 8,4,0,'3. DIM',1)

KEY,	
	XCALL INPUT (4,38,02,00,'#E',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	KEY = ENTRY(1,2)
	IF (KEY .EQ. 0) KEY = SAVKEY
	IF (KEY .EQ. 0) GOTO KEY
	A2 = KEY,	'ZX' [LEFT]
	XCALL OUTPT (4,38,1,A2,1)
	SAVKEY = KEY

	CLEAR TBL_KEY
	TBLCOD = 'SK'
	SK_KEY = KEY
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .EQ. 0)
	THEN	XCALL OUTPT (4,44,1,SK_DESC,1)
	ELSE	GOTO KEY
	GOTO (ANYCNG), CNGCTL

TYPE,
	XCALL INPUT (6,38,01,00,'AE',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	TYPE = ENTRY(1,1)
	IF (TYPE .EQ. A1) 
		BEGIN
		TYPE = SAVTYP
		XCALL OUTPT (6,38,1,TYPE,1)
		END

	SAVTYP = TYPE

	USING TYPE SELECT
	('B'),	BEGIN
		XCALL OUTPT (10,4,0,'4. W',1)
		XCALL OUTPT (12,4,0,'5. L',1)
		END
	('R'),	BEGIN
			XCALL OUTPT (10,4,0,'4. WB',1)
			XCALL OUTPT (12,4,0,'5. WT',1)
			END
	('T'),	BEGIN
		XCALL OUTPT (10,4,0,'4. WL',1)
		XCALL OUTPT (12,4,0,'5. DL',1)
		XCALL OUTPT (14,4,0,'6. WR',1)
		XCALL OUTPT (16,4,0,'7. LT',1)
		XCALL OUTPT (18,4,0,'8. LB',1)
		END
	(),	BEGIN
		XCALL MESAG ('TYPE MUST BE B,R, or T',1)
		GOTO TYPE
		END
	ENDUSING

DIM,
	XCALL INPUT (8,12,02,01,'#E',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	DIM = ENTRY(1,2)

	CLEAR TBL_KEY
	TBLCOD = 'S1'
	S1_KEY = KEY
	S1_TYPE = TYPE
	S1_DIM = DIM
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .EQ. 0)
		BEGIN
		SELECT = 2
		CALL DSPREC
		GOTO ANYCNG
		END
	SELECT = 1			;ADD MODE
	CLEAR COPTBL
	S1_DIM = DIM

SS1,
	XCALL INPUT (10,12,06,00,'#',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	S1_D1 = ENTRY(1,6)
	A7 = S1_D1,	'ZZX.XXX' [LEFT]
	XCALL OUTPT (10,12,1,A7,1)
	GOTO (ANYCNG),CNGCTL
SS2,
	XCALL INPUT (12,12,06,00,'#',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	S1_D2 = ENTRY(1,6)
	A7 = S1_D2,	'ZZX.XXX' [LEFT]
	XCALL OUTPT (12,12,1,A7,1)
	GOTO (ANYCNG),CNGCTL
SS3,
	IF (TYPE .NE. 'T') GOTO ANYCNG

	XCALL INPUT (14,12,06,00,'#',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	S1_D3 = ENTRY(1,6)
	A7 = S1_D3,	'ZZX.XXX' [LEFT]
	XCALL OUTPT (14,12,1,A7,1)
	GOTO (ANYCNG),CNGCTL
SS4,
	XCALL INPUT (16,12,06,00,'#',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	S1_D4 = ENTRY(1,6)
	A7 = S1_D4,	'ZZX.XXX' [LEFT]
	XCALL OUTPT (16,12,1,A7,1)
	GOTO (ANYCNG),CNGCTL
SS5,
	XCALL INPUT (18,12,06,00,'#',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	S1_D5 = ENTRY(1,6)
	A7 = S1_D5,	'ZZX.XXX' [LEFT]
	XCALL OUTPT (18,12,1,A7,1)
	GOTO (ANYCNG),CNGCTL

ANYCNG,
	XCALL OUTPT (24,1,1,'FIELD # TO CHANGE            <TAB> = DELETE',1)
	XCALL INPUT (24,20,02,00,'#T',ENTRY,INXCTL,1)
	IF (INXCTL .EQ. 3)
		BEGIN
		XCALL OUTPT (24,1,1,'DELETE, ARE YOU SURE ?',1)
		XCALL INPUT (24,24,01,01,'YN',ENTRY,INXCTL,1)
		GOTO (ANYCNG),INXCTL-1
		XCALL ISIO (CHN182,COPTBL,TBL_KEY,DELETE,LOKCTL)
		GOTO DISPLA
		END

	WHATNO = ENTRY(1,2)
	IF (WHATNO .EQ. 0)
	THEN 	CLEAR CNGCTL
	ELSE 	CNGCTL = 1

	GOTO (PROCES,CNGBR),CNGCTL+1
CNGBR,
	GOTO (KEY,TYPE, DIM, SS1, SS2, SS3, SS4, SS5),WHATNO
	GOTO ANYCNG

PROCES,

	CASE SELECT OF
	BEGINCASE
	1:	BEGIN
		TBLCOD = 'S1'
		S1_KEY = KEY
		S1_TYPE = TYPE
		S1_DIM = DIM

		XCALL ISIO (CHN182,COPTBL,TBL_KEY,STORE,LOKCTL)
		END
	2:	XCALL ISIO (CHN182,COPTBL,TBL_KEY,WRITE,LOKCTL)
	ENDCASE
	GOTO DISPLA

ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('CP:TBLMNU',1)
	STOP


DSPREC,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; DISPLAY RECORD
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	A7 = S1_D1, 'ZZX.XXX' [LEFT]
	XCALL OUTPT (10,12,1,A7,1)

	A7 = S1_D2, 'ZZX.XXX' [LEFT]
	XCALL OUTPT (12,12,1,A7,1)

	IF (S1_TYPE .NE. 'T') RETURN

	A7 = S1_D3, 'ZZX.XXX' [LEFT]
	XCALL OUTPT (14,12,1,A7,1)

	A7 = S1_D4, 'ZZX.XXX' [LEFT]
	XCALL OUTPT (16,12,1,A7,1)

	A7 = S1_D5, 'ZZX.XXX' [LEFT]
	XCALL OUTPT (18,12,1,A7,1)

	RETURN
;-----------------------------------



;===================================================================
PRINT_TABLE,
;===================================================================
	PGCNT  = 0
	
PDISP,
	CNGCTL = 
	XCALL OUTPT (1,1,2,'PRINT COMPLETE SEAL PARAMETERS TABLE',1)
	XCALL OUTPT (4,4,0,'1. STARTING DIM',1)
	XCALL OUTPT (6,4,0,'2. ENDING   DIM',1)
ST_DIM,
	XCALL INPUT (4,27,02,00,'#E',ENTRY,INXCTL,1)
	GOTO (PDISP,PDONE),INXCTL
	ST_DIM = ENTRY(1,2)
	IF (ST_DIM .EQ. 0)
		BEGIN
		EN_DIM = 99
		XCALL OUTPT (4,25,1,'ALL',1)
		XCALL OUTPT (6,25,1,' ',1)
		GOTO P_ANY
		END
	GOTO (P_ANY),CNGCTL
EN_DIM,
	XCALL INPUT (6,25,02,00,'# ',ENTRY,INXCTL,1)
	GOTO (PDISP),INXCTL
	EN_DIM = ENTRY(1,2)
	IF (EN_DIM .EQ. 0)
		BEGIN
		EN_DIM = ST_DIM
		A2 = EN_DIM, 'ZX' [LEFT]
		XCALL OUTPT (6,25,0,A2,1)
		END
P_ANY,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (P_PRINT,P_CNGBR),CNGCTL + 1
P_CNGBR,
	GOTO (ST_DIM,EN_DIM),WHATNO
	GOTO P_ANY

P_PRINT,
	SAVTYP = '*'
	CLEAR TBL_KEY
	TBLCOD = 'S1'
	S1_DIM = ST_DIM
	FIND (CHN182,COPTBL,TBL_KEY)[ERR=PLOOP]

PLOOP,
	XCALL IOS (CHN182,COPTBL,READ,LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF
	IF (TBLCOD .NE. 'S1') GOTO EOF
	IF (S1_DIM .LT. ST_DIM) GOTO PLOOP
	IF (S1_DIM .GT. EN_DIM) GOTO EOF

	IF (S1_TYPE .NE. SAVTYP) CALL NEWTYP

	PLINE (11,12) = S1_DIM,	'ZX'
	PLINE (14,20) = S1_D1,	'ZZX.XXX'
	PLINE (22,28) = S1_D2,	'ZZX.XXX'


	USING S1_TYPE SELECT
	('B'),	PLINE (1,9) = 'BODY'
	('F'),	PLINE (1,9) = 'F REDUCER'
	('H'),	PLINE (1,9) = 'H REDUCER'

	('T'),	BEGIN
		PLINE (1,9) = 'TAP'
		PLINE (30,36) = S1_D3,	'ZZX.XXX'
		PLINE (38,44) = S1_D4,	'ZZX.XXX'
		PLINE (46,52) = S1_D5,	'ZZX.XXX'
		END

	ENDUSING


;TYPE           W       L
;TYPE          WB      WT
;TYPE     DM   WL      DL      WR      LT      LB
;AAAAAAAA ZX ZZX.XXX ZXX.XXX ZXX.XXX ZXX.XXX ZXX.LLL
;123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5
	CALL PRINT
	GOTO PLOOP

NEWTYP,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	USING S1_TYPE SELECT
	('B'),	HD1 = 'TYPE            W       L'
	('F','H'),	HD1 = 'TYPE           WB      WT'
	('T'),	HD1 = 'TYPE      DM   WL      DL      WR      LT      LB'
	ENDUSING
	IF (SAVTYP .EQ. '*') GOTO ENDTYP

	LINCNT = 66
ENDTYP,
	SAVTYP = S1_TYPE
	RETURN
;------------------------------------------
EOF,
	IF (PRNTON.EQ.1)  XCALL LPOFF(LPSW,SPLFIL,PGCNT)
	PRNTON  = 0

PDONE,
	GOTO MENU

PRINT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (PRNTON .EQ. 0) CALL PRNTON
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HD1,HD,HD
&		,LG,LG,LG,0,080,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;-------------------------------------------------------------
PRNTON,
	SPLFIL (5,6) = 'EF'
	LPSW = 1		;PRINT,SPOOL, OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTON = 1
	RETURN
;-------------------------------------------------------------
;===================================================================


OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (1,'SU',182,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN182 = 1

	OPNOK = 1
	RETURN
;----------------------------------------------------

CLOSE,
	CLOSE CHN182
	RETURN
;----------------------------------------------------
	

