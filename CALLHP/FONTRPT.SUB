SUBROUTINE FONTRPT

;	returns description of a D18 font_parm 
;
;syntax	XCALL FONTRPT (D18,DESCRIP)
;		where	D18	is a D18 number representing a font
;			DESCRIP	is a minimum A70 alpha to hold description

				D18	,D	;A D18 value of a font
				DESCRIP	,A	;must be minimum A70

.INCLUDE DEF:HPSUB.DEF

.DEFINE UNKNOWN ,'unknown'
.DEFINE ERROR   ,'-ERR-'

RECORD ALPHA
	A_ORIENTATION	,A10	;either LANDSCAPE or PORTRAIT	; 1 10
	A_SYMBOL_CODE	,A3	;eg: "10Q"			;11 13
	EQ1		,A1	;Holds an equals sign		;14 14
	A_SYMBOL_DESCR	,A8	;eg: "ECMA7"			;15 22
	A_SPACING	,A7	;either "PROPOR" or "FIXED"	;23 29
	A_PITCH		,A5					;29 34
	LBLCPI		,A3					;35 37
			,A1					;38 38
	A_POINTS	,A4					;39 42
	LBLPTS		,A2					;43 44
			,A1					;45 45
	A_STYLE		,A8	;either "UPRIGHT" or "ITALIC"	;46 53
	A_WEIGHTDESCR	,A7	;"LIGHT","MEDIUM","BOLD","CUSTOM;54 60
	A_TYPEFACE	,A10	;				;61 70
RECORD
	FONT_PARMS	,D18
RECORD ,X
	ORIENT	,D1	; 1 - 1
	SYMVAL	,D2	; 2 - 3	;SYMVAL and SYMTERM define SYMBOL SET. SYMTERM
	SYMTERM	,D2	; 4 - 5	;("termination character") is ASCII "A"-"V"
	SPACING	,D1	; 6 - 6
	PITCH	,D3	; 7 - 9 (note that 16.7 is converted to 16.67)
	POINTS	,D3	;10 -12
	STYLE	,D1	;13 -13
	WEIGHT	,D1	;14 -14
	TYPE	,D4	;15 -18
PROC
	ALPHA =
	EQ1 = '='
	LBLCPI = 'cpi'
	LBLPTS = 'pt'
	FONT_PARMS = D18
	CASE ORIENT OF BEGINCASE
		A_ORIENTATION = 'PORTRAIT'
		A_ORIENTATION = 'LANDSCAPE'
		ENDCASE ELSE A_ORIENTATION = ERROR

	A_SYMBOL_CODE(1,2) = SYMVAL ,'ZX'
	IF (SYMTERM.LT.65.OR.SYMTERM.GT.90)
	   THEN	BEGIN	;-------------------------------;
		A_SYMBOL_CODE(3,3) = '?'		;
		A_SYMBOL_DESCR = ERROR			;
		END	;-------------------------------;
	   ELSE	BEGIN	;-------------------------------;
		XCALL ASCII (SYMTERM,A_SYMBOL_CODE(3,3));
		CASE A_SYMBOL_CODE OF BEGINCASE		;
			' 0A': A_SYMBOL_DESCR = 'MATH7'	;
			' 0B': A_SYMBOL_DESCR = 'TAX'	;
			' 0D': A_SYMBOL_DESCR = 'DANISH';
			' 1E': A_SYMBOL_DESCR = 'UK'	;
			' 0F': A_SYMBOL_DESCR = 'FRENCH';
			' 0G': A_SYMBOL_DESCR = 'GERMAN';
			' 0I': A_SYMBOL_DESCR = 'ITALIAN';
			' 8M': A_SYMBOL_DESCR = 'MATH8'	;
			' 0N': A_SYMBOL_DESCR = 'ECMA94';
			' 0Q': A_SYMBOL_DESCR = 'MATH8A';
			' 1Q': A_SYMBOL_DESCR = 'MATH8B';
			' 2Q': A_SYMBOL_DESCR = 'PIA'	;
			' 4Q': A_SYMBOL_DESCR = 'PCLINE';
			'10Q': A_SYMBOL_DESCR = 'ECMA7'	;
			'11Q': A_SYMBOL_DESCR = 'ECMA8'	;
			' 0S': A_SYMBOL_DESCR = 'SWEDISH';
			' 1S': A_SYMBOL_DESCR = 'SPANISH';
			' 0U': A_SYMBOL_DESCR = 'ASCII'	;
			' 1U': A_SYMBOL_DESCR = 'LEGAL'	;
			' 8U': A_SYMBOL_DESCR = 'ROMAN8';
			'10U': A_SYMBOL_DESCR = 'PC8'	;
			'11U': A_SYMBOL_DESCR = 'PC8DN'	;
			'12U': A_SYMBOL_DESCR = 'PC850'	;
			'15U': A_SYMBOL_DESCR = 'PIFONT';
			ENDCASE 			;
			ELSE A_SYMBOL_DESCR = UNKNOWN	;
			END	;-----------------------;
	CASE SPACING OF BEGINCASE
		A_SPACING = 'PROPOR'
		A_SPACING = 'FIXED'
		ENDCASE ELSE A_SPACING = ERROR

	IF (PITCH.EQ.167)
	   THEN	A_PITCH = '16.67'
	   ELSE	A_PITCH = PITCH*10 ,'ZX.XX'

	A_POINTS = POINTS ,'ZX.X'

	CASE STYLE OF BEGINCASE
		A_STYLE = 'UPRIGHT'
		A_STYLE = 'ITALIC'
		ENDCASE ELSE A_STYLE = ERROR

;;	A_WEIGHTNUM = WEIGHT-5 ,'-X'
	CASE WEIGHT OF BEGINCASE
		2: A_WEIGHTDESCR = 'LIGHT'
		5: A_WEIGHTDESCR = 'MEDIUM'
		8: A_WEIGHTDESCR = 'BOLD'
		ENDCASE	ELSE
			BEGIN	;-----------------------;
			IF (WEIGHT.GT.0)		;
			   THEN	A_WEIGHTDESCR = 'CUSTOM';
			   ELSE A_WEIGHTDESCR = ERROR	;
			END	;-----------------------;

	IF (.NOT.TYPE)
	   THEN	A_TYPEFACE = ERROR
	   ELSE	CASE TYPE OF BEGINCASE
			   1: A_TYPEFACE = 'LINEPRINT'
			   3: A_TYPEFACE = 'COURIER'
			   4: A_TYPEFACE = 'HELV'
			   5: A_TYPEFACE = 'TIMES'
			   6: A_TYPEFACE = 'GOTHIC'
			   8: A_TYPEFACE = 'PRESTIGE'
			  11: A_TYPEFACE = 'PRESENT'
			  17: A_TYPEFACE = 'OPTIMA'
			  18: A_TYPEFACE = 'GARAMOND'
			  19: A_TYPEFACE = 'COOPER'
			  20: A_TYPEFACE = 'CORONET'
			  21: A_TYPEFACE = 'BROADWAY'
			  22: A_TYPEFACE = 'BAUER'
			  23: A_TYPEFACE = 'CENTURY'
			  24: A_TYPEFACE = 'UNIVERSITY'
			4101: A_TYPEFACE = 'CGTIMES'
			4148: A_TYPEFACE = 'UNIVERS'
			ENDCASE ELSE A_TYPEFACE = UNKNOWN
	DESCRIP = ALPHA
	RETURN
