; VCHRG2 / AP 
;
;
;			::PCPYAP.DEF::
;******************************************************************************
;		ACCOUNTS PAYABLE
;
;		RELEASED: AUGUST 1, 1984 (d70s10)
;******************************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;		PRINTS VOUCHER REGISTER AND EDIT LIST (JC-SPC-PO VERSION)
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD NEWAP	
		.INCLUDE 'DEF:RD014G.DEF'
RECORD,X	
		.INCLUDE 'DEF:RD014C.DEF'
RECORD	,X
		,A825
	REGNUM	,A3

RECORD ITMCTL	,X
		.INCLUDE 'DEF:RD041B.DEF'
RECORD NEWAPD	
		.INCLUDE 'DEF:RD015B.DEF'
RECORD	,X
		,A5
	PRGNAM	,A6
		,A24
RECORD TITLE
		,A30,'NEW PAYABLES VOUCHER REGISTER '
RECORD HDR1
		,A9,'VOUCHER  '
		,A31,'----------- VENDOR ------------'
		,A20,'  INVOICE   INV/VCH '
		,A33,'  INV AMOUNT  DISCOUNT   DISC/DUE'
		,A17,'  A/P OR    CHECK'
RECORD HDR2
		,A19,'NUMBER    NO   NAME'
		,A23
		,A30,'NUMBER     DATES    (NON-DISC)'
		,A31,'   PCT/AMT     DATES  CASH/DISC'
		,A7,' NUMBER'
RECORD HDR3
		,A8	,'DST-ACCT'
		,A6
		,A6	,'AMOUNT'
		,A3
		,A8	,'VAR-ACCT'
		,A6
		,A6	,'AMOUNT'
		,A3
		,A11	,'ITEM-NUMBER'
		,A6
		,A8	,'QUANTITY'
		,A2
		,A3	,'UOM'
		,A1
		,A6	,'JOB-NO'
		,A1
		,A7	,'CST-TYP'
		,A2
		,A8	,'P.O.-#  '
		,A4
		,A9	,'P.O.-LINE'
RECORD LEGND
		,A55,'"A/P OR CASH/DISC" ARE ACCTS CREDITED FOR THE VOUCHER. '
		,A36,'REGULAR VOUCHERS SHOW THE A/P ACCT, '
		,A41,'PREPAIDS & MANUALS SHOW CASH & DISC ACCT.'
RECORD SNDMSG
		,A9,'AP:APDSUM'
	RCNT	,D5
	OCNT	,D5
RECORD
	ACTMSK	,A8,'ZXXX-XXX'
	ADJENT	,D3
	AFLD	,A9
	BLANKS	,A8
	CSHDIS	,D10
	DASHES	,A40,'----------------------------------------'
	DELENT	,D4
	DTMASK	,A8,'ZX/XX/XX'
	EXPFLG	,D1
	JUSTIF	,D1
	I	,D2
	LINCNT	,D2,60
	LNFEED	,D1
	LPSW	,D1
	MANDIS	,D10
	MANENT	,D3
	MANTOT	,D10
	MASK	,A14,'ZZ,ZZZ,ZZX.XX-'
	MASK1	,A5,'Z,ZZX'
	MASK2	,A11,'ZZZ,ZZZ.ZZ-'
	MASK3	,A3,'ZZZ'
	MSGCTL	,D1
	N	,D2
	PGCNT	,D3
	PLINE	,A132
	PRPENT	,D4
	PRPTOT	,D10
	PRDTOT	,D10
	PRTTYP	,A1
	REGENT	,D4
	REGTOT	,D10
	REGDIS	,D10
	RGDTOT	,D10
	RGFLG	,A1
	RPTNUM	,D3
	SPLFIL	,A14
	SWITCH	,D1,1
	SYSTEM	,D1
	LOKCTL	,D1
	READ	,D1,	0
	WRITE	,D1,	1
	V	,D1,1
	VOIDNT	,D3
	WTARG	,D1,4
PROC
	XCALL TERID (V)
	V = 1
	XCALL ENVRN(SYSTEM)
	LOKCTL = 5
	XCALL FILES (4,'I',41,LOKCTL)	;GETS VALUE OF JSTIFY FOR ITEM NUMBER
	LOKCTL = 1
	XCALL IO (4,ITMCTL,1,READ,LOKCTL)
	JUSTIF = JSTIFY
	CLOSE 4
	XCALL FILES(4,'I',14,5)
	LOKCTL = 1
	XCALL IO (4,NEWAP,1,READ,LOKCTL)
	CLOSE 4
	IF (REGFLG.EQ.'R') GO TO REG1
	SPLFIL (5,6) = 'FD'
	LPSW = 1
	TITLE (22,30) = 'EDIT LIST'
	GO TO OPENLP
REG1,
	SPLFIL (5,6) = 'FE'
	LPSW = 4
OPENLP,
	LPSW = 1
	IF (REGFLG.EQ.'R') LPSW = 4
	XCALL OUTPT (2,1,2,TITLE(22,30),1)
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.NE.0) GO TO BEGIN
	IF (REGFLG.NE.'R') GO TO EXIT
	XCALL MESAG
&	('New Payables Voucher Register must be either SPOOLED or PRINTED.',2)
	GO TO OPENLP
BEGIN,
	XCALL OUTPT (2,1,2,TITLE(22,30),1)
	IF (REGFLG.EQ.'R') GO TO SKIP1
	XCALL FILES(4,'I',14,SWITCH)		;FILE # 14 -- NEWAP FILE
	IF (SWITCH.EQ.9) GO TO EXIT
SKIP1,
	IF (LPSW.NE.2) WTARG = 2
	IF (REGFLG.NE.'R') GO TO SKIP2
	XCALL FILES(4,'I',14,5)
	RGFLG = 'R'
SKIP2,
	XCALL WATE(WTARG,V)
	LOKCTL = 1
	XCALL IOS (4,NEWAP,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOF
READ,
	LOKCTL = 1
	XCALL IOS (4,NEWAP,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOF
	IF (NEWAP.EQ.']]]]]]') GO TO EOF
BUFF,
	PLINE (1,6) = WVCHNO
	PLINE (10,13) = WVENNO
	XCALL LEFTJ (PLINE(10,13),4)
	PLINE (16,40) = WNAME
	PLINE (43,50) = WINVNO
	PLINE (53,60) = WINVDT,DTMASK
	PLINE (63,73) = WINVAM,MASK
	IF (WDPCT.NE.0.AND.WNDISC.NE.-1) PLINE (77,82) = WDPCT,'ZX.X%'
	IF (WDISDT.NE.0) PLINE (86,93) = WDISDT,DTMASK
	IF (WCHKNO.EQ.0.AND.WINVAM.NE.0) PLINE (96,103) = WAPACT, ACTMSK
	IF (WCHKNO.GT.0.AND.WINVAM.NE.0) PLINE (96,103) = WCSACT, ACTMSK
	CALL VEREXP
	N =
	CALL PRINT
	PLINE (11,14) = 'REF:'
	PLINE (16,40) = WDESC
	IF (WCHKNO.GT.0.AND.WNDISC.NE.-1) PLINE (1,9) = '*PREPAID*'
	IF (WNDISC.EQ.-1) PLINE (1,9) = '*MANUAL*'
	IF (WINVAM.EQ.0.AND.EXPFLG.EQ.0) PLINE (1,9) = '**VOID**'
	IF (WINVAM.EQ.0.AND.EXPFLG.EQ.1) PLINE (1,9) = '*ADJ EXP*'
	IF (WNAME.EQ.'000000') PLINE (1,9) = '*DELETED*'
	IF (PLINE(1,8).EQ.BLANKS) PLINE (1,9) = '*REGULAR*'
	PLINE (53,60) = WVCHDT,DTMASK
	IF (WDAMT.NE.0) PLINE (75,83) = WDAMT, MASK
	PLINE (86,93) = WDUEDT,DTMASK
	IF (WNDISC.EQ.-1) GO TO BUFF3
	AFLD = WNDISC,'$$,$$X.XX'
	I =
DSCLP,
	INCR I
	IF (I.GT.9) GO TO EXTDCS
	IF (AFLD(I,I).NE.'$') GO TO DSCLP
EXTDCS,
	AFLD (I,I) = '('
	PLINE (64,72) = AFLD
	PLINE (73,73) = ')'
	IF (WNDISC.EQ.0) PLINE (64,73) =
BUFF3,
	IF (WCHKNO.GT.0.AND.WDAMT.NE.0) PLINE (96,103) = WDSACT, ACTMSK
	IF (WCHKNO.NE.0) PLINE (105,110) = WCHKNO,'ZZZXXX'
	CALL PRINT
	IF (WNAME.EQ.'000000') INCR DELENT
	IF (WNAME.EQ.'000000') GO TO DISTRB
	IF (WNDISC.EQ.-1) GO TO MANUAL
	IF (WCHKNO.NE.0) GO TO PRPAID
REGULR,
	REGTOT = REGTOT + WINVAM - WDAMT
	REGDIS = REGDIS + WDAMT
	IF (WINVAM.EQ.0) GO TO ADJVD
	INCR REGENT
	GO TO DISTRB
PRPAID,
	PRPTOT = PRPTOT + WINVAM - WDAMT
	CSHDIS = CSHDIS + WDAMT
	IF (WINVAM.EQ.0) GO TO ADJVD
	INCR PRPENT
DISTRB,
	IF (EXPFLG.EQ.0) GO TO ENDPR
	LNFEED = 1
	CALL LINFD
	PLINE = HDR3
	CALL PRINT
DISTR2,
	INCR N
	IF (N.GT.9) GO TO ENDPR
	IF (WDACTS(N*2-1).EQ.0.AND.WDACTS(N*2).EQ.0.AND.WDJOBN(N).EQ.BLANKS)
&			 GO TO DISTR2
	PLINE(1,8) = WDACTS(N*2-1),ACTMSK
	PLINE(11,21) = WDAMTS(N*2-1), MASK2
	IF (WDACTS(N*2).NE.0) PLINE(24,31) = WDACTS(N*2),ACTMSK
	PLINE(34,44) = WDAMTS(N*2), MASK2
	PLINE(47,61) = WDPRTN(N)
	IF (JUSTIF.EQ.1) XCALL LEFTJ (PLINE(47,61),15)
	PLINE(64,71) = WDQTY(N),'ZZZ,ZZZ-'
	PLINE(74,75) = WCUOM(N)
	PLINE(78,83) = WDJOBN(N)
	PLINE(87,88) = WDCTYP(N)
	PLINE(93,101) = WPONUM(N),'ZZZZZX-XX'
	IF (WPONUM(N).EQ.0) PLINE (93,101) =
	PLINE(107,109) = WPOLIN(N), MASK3
	CALL PRINT
	IF (WNAME.EQ.'000000') GO TO DISTR2
	IF (WCHKNO.EQ.0) RGDTOT = RGDTOT + WDAMTS(N*2-1)
	IF (WCHKNO.EQ.0) RGDTOT = RGDTOT + WDAMTS(N*2)
	IF (WCHKNO.NE.0) PRDTOT = PRDTOT + WDAMTS(N*2-1)
	IF (WCHKNO.NE.0) PRDTOT = PRDTOT + WDAMTS(N*2)
	GO TO DISTR2
ADJVD,
	IF (EXPFLG.EQ.0) INCR VOIDNT
	IF (EXPFLG.NE.0) INCR ADJENT
	GO TO DISTRB
MANUAL,
	MANTOT = MANTOT + WINVAM - WDAMT
	MANDIS = MANDIS + WDAMT
	INCR MANENT
	GO TO ENDPR
VEREXP,
	EXPFLG =
	N =
VEREX2,
	INCR N
	IF (N.GT.9) RETURN
	IF (WDAMTS(N).EQ.0.OR.WDACTS(N).EQ.0) GO TO VEREX2
	EXPFLG = 1
	RETURN
ENDPR,
	IF (EXPFLG) GO TO DASHES
	LNFEED = 1
	CALL LINFD
	GO TO READ
DASHES,
	PLINE (1,40) = DASHES
	PLINE (41,80) = DASHES
	PLINE (81,120) = DASHES
	PLINE (121,132) = DASHES
	CALL PRINT
	GO TO READ
EOF,
	LNFEED = 2
	CALL LINFD
	HDR1 =
	HDR2 =
	PLINE (24,40) = 'NET-CHANGE-TO-A/P'
	PLINE (44,61) = 'NET-CHANGE-TO-CASH'
	PLINE (65,82) = 'NET-DISCOUNT-TAKEN'
	PLINE (86,104) = 'DISTRIBUTION-TOTALS'
	CALL PRINT
	LNFEED = 1
	CALL LINFD
	PLINE (1,5) = REGENT,MASK1
	PLINE (7,21) = 'REGULAR ENTRIES'
	PLINE (24,37) = REGTOT + REGDIS, MASK
	PLINE (88,101) = RGDTOT,MASK
	CALL PRINT
	IF (PRPENT.EQ.0) GO TO MANTOT
	PLINE (1,5) = PRPENT, MASK1
	PLINE (7,21) = 'PREPAID ENTRIES'
	PLINE (45,58) = PRPTOT, MASK
	PLINE (66,79) = CSHDIS, MASK
	PLINE (88,101) = PRDTOT,MASK
	CALL PRINT
MANTOT,
	IF (MANENT.EQ.0) GO TO VOIDS
	PLINE (1,5) = MANENT, MASK1
	PLINE (7,21) = 'MANUAL PAYMENTS'
	PLINE (45,58) = MANTOT, MASK
	PLINE (66,79) = MANDIS, MASK
	PLINE (24,37) = -MANTOT - MANDIS, MASK
	CALL PRINT
VOIDS,
	IF (VOIDNT.EQ.0) GO TO ADJEXP
	PLINE (1,5) = VOIDNT, MASK1
	PLINE (7,17) = 'VOID CHECKS'
	CALL PRINT
ADJEXP,
	IF (ADJENT.EQ.0) GO TO DELETS
	PLINE (1,5) = ADJENT, MASK1
	PLINE (7,25) = 'EXPENSE ADJUSTMENTS'
	CALL PRINT
DELETS,
	IF (DELENT.EQ.0) GO TO TOTENT
	PLINE (1,5) = DELENT, MASK1
	PLINE (7,22) = 'DELETED VOUCHERS'
	CALL PRINT
TOTENT,
	LNFEED = 1
	CALL LINFD
	PLINE (1,5) = REGENT + PRPENT + MANENT + VOIDNT +
&			ADJENT + DELENT, MASK1
	PLINE (7,19) = 'TOTAL ENTRIES'
	CALL PRINT
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
EOF2,
	IF (RGFLG.NE.'R') GO TO END2
	CLOSE 4
	XCALL FILES (4,'U',14,5)
	LOKCTL = 1
	XCALL IO (4,NEWAP,1,READ,LOKCTL)
	REGNUM = PGCNT (1,3)
	LOKCTL = 1
	XCALL IO (4,NEWAP,1,WRITE,LOKCTL)
	CLOSE 4
	XCALL FILES (3,'U',15,5)
	LOKCTL = 1
	XCALL IO (3,NEWAPD,1,READ,LOKCTL)
	PRGNAM = 'PSTAP2'
	LOKCTL = 1
	XCALL IO (3,NEWAPD,1,WRITE,LOKCTL)
	RCNT = REC015
	OCNT = 1
	CLOSE 3
	XCALL WATE(4,V)
	MSGCTL = 5
	XCALL SNMSG (SNDMSG,MSGCTL)
	XCALL PGCHN('AP:SRTAPD',0)
PRINT,
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,'NO HDR',
&			LEGND,'NO LEGEND',' ',0,0,0,0,LPSW,RPTNUM,PRTTYP)
	RETURN
EXITR,
	XCALL WATE(3,V)
	XCALL FILES(3,'O',15,4)
	XCALL FILES(6,'U',16,4)
	XCALL FILES(7,'U',17,4)
END2,
	XCALL FILES(4,'I',14,4)
EXIT,
	XCALL WATE(3,V)
	XCALL PGCHN('AP:VCHENT',1)
LINFD,
	XCALL LINFD (LNFEED)
	LINCNT = LINCNT + LNFEED
	RETURN
END
