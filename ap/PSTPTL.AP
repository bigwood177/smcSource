; PSTPTL / AP 
;
;
;			::PCPYAP.DEF::
;******************************************************************************
;		ACCOUNTS PAYABLE
;
;		RELEASED: AUGUST 1, 1984 (d70s10)
;******************************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;		POSTS PTLPAY TO APOPEN FILE USING MERGE-X ROUTINE
;		MODIFIED TO DEFER ALL VOUCHERS ASSOCIATED WITH PARTIAL PAYMENTS
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD APOPEN	
		.INCLUDE 'DEF:RD017A.DEF'
RECORD PRTIAL	
		.INCLUDE 'DEF:RD019A.DEF'
RECORD APOTST	
		.INCLUDE 'DEF:RD017B.DEF'
RECORD	,X
	TVENNO	,A4
	TVCHNO	,D6
		,A108
RECORD
	APCNT	,D5
	APOKEY	,A10
	ARCNT	,D2,00
	DAPCNT	,D5,00000
	KEYMSK	,A6,'XXXXXX'
	LSTKEY	,A10
	N	,D2,01
	NAPCNT	,D5
	NEWCNT	,D5
	PRTKEY	,A10
	RECARR	,50A118
	LOKCTL	,D1
	READ	,D1,	0
	WRITE	,D1,	1
	V	,D1,1
PROC
	XCALL TERID (V)
	V = 1
	XCALL WATE(4,V)
	XCALL OUTPT(2,1,1,'ADD TO AP OPEN FILE',V)
	XCALL FILES (4,'I',19,5)		;FILE # 19 -- PTLPAY FILE
	LOKCTL = 1
	XCALL IOS (4,PRTIAL,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOFNAP
	NAPCNT = 1
CNTNAP,
	LOKCTL = 1
	XCALL IOS (4,PRTIAL,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOFNAP
	IF (PRTIAL.EQ.']]]]]]') GOTO EOFNAP
	INCR NAPCNT
	IF (PINVDT.EQ.0) GO TO CNTNAP
	INCR DAPCNT
	GO TO CNTNAP
EOFNAP,
	IF (DAPCNT.EQ.0) GO TO END
	XCALL FILES(7,'U',17,5)		;FILE # 17 -- APOPEN FILE
	LOKCTL = 1
	XCALL IO (7,APOTST,1,READ,LOKCTL)
	APCNT = REC017
	NEWCNT = APCNT+DAPCNT
	REC017 = NEWCNT
	ORG017 = REC017
	LOKCTL = 1
	XCALL IO (7,APOTST,1,WRITE,LOKCTL)
	CALL RDAPOT
	CALL RDPART
COMPAR,
	IF (APOKEY.GT.PRTKEY) GO TO WRTEST
	LSTKEY = PRTKEY
	CALL WRPART
	CALL RDPART
	GO TO COMPAR
WRTEST,
	CALL WRAPOT
	CALL RDAPOT
	GO TO COMPAR
RDAPOT,
	IF (APCNT.EQ.0) RETURN
	LOKCTL = 1
	XCALL IO (7,APOTST,APCNT,READ,LOKCTL)
	APCNT = APCNT-1
	APOKEY(1,4) = TVENNO
	APOKEY(5,10) = TVCHNO,KEYMSK
	RETURN
RDPART,
	IF (NAPCNT.EQ.0) GOTO ENDOFF
	LOKCTL = 1
	XCALL IO (4,PRTIAL,NAPCNT,READ,LOKCTL)
	NAPCNT = NAPCNT-1
	IF (PINVDT.EQ.0) GO TO RDPART
	PRTKEY(1,4) = PVENNO
	PRTKEY(5,10) = PVCHNO,KEYMSK
	RETURN
WRAPOT,
	APOPEN = APOTST
	IF (APOKEY.EQ.LSTKEY.AND.AFLAG.EQ.8) AFLAG = 9
	IF (APOKEY.EQ.LSTKEY.AND.
&(AFLAG.EQ.0.OR.AFLAG.EQ.3.OR.AFLAG.EQ.4.OR.AFLAG.EQ.5)) AFLAG = 1
	IF (AFLAG.EQ.-1.OR.AFLAG.EQ.7) AFLAG = 6
	GO TO WRAPO
WRPART,
	AVCHNO = PVCHNO
	AVENNO = PVENNO
	AINVNO = PINVNO
	AINVDT = PINVDT
	AINVAM = PPYAMT
	ADSCAM = PPYDSC
	ADUEDT = PDUDTE
	ADESC (1,17) = 'PARTIAL ON INV # '
	ADESC (18,25) = PINVNO
	ADISDT =
	AFLAG = 6
	ACHKNO =
	APAID =
	ADSTKN =
	AAPACT = PAPACT
WRAPO,
	INCR ARCNT
	RECARR(ARCNT) = APOPEN
	IF (ARCNT.EQ.50) CALL WRTARR
	RETURN
WRTARR,
	LOKCTL = 1
	XCALL IO (7,RECARR(N),NEWCNT,WRITE,LOKCTL)
	NEWCNT = NEWCNT-1
	IF (NEWCNT.EQ.1) GO TO END
	IF (N.EQ.ARCNT) GOTO OUT
	INCR N
	GOTO WRTARR
OUT,
	ARCNT = 0
	N = 1
	RETURN
ENDOFF,
	IF (APOKEY.EQ.LSTKEY) CALL WRAPOT
	IF (APOKEY.EQ.LSTKEY) CALL RDAPOT
	IF (APOKEY.EQ.LSTKEY) GOTO ENDOFF
	IF (ARCNT.NE.0) CALL WRTARR
END,
	CALL CLOSE1
	XCALL PGCHN('AP:CLRPTL',1)
CLOSE1,
	XCALL FILES(7,'U',17,4)
	RETURN
END
