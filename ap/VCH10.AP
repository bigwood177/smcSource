SUBROUTINE VCH10; (WORK1)
;
; VCH10 / AP 
;
;		INITIALIZES FOR NEW PAYABLES VOUCHERING
;
;
	WORK1	,A
;
RECORD DUMNAP	
		.INCLUDE 'DEF:RD014C.DEF'
RECORD,X
		,A834
	NEWDEF	,A24
RECORD DUMVEN	
		.INCLUDE 'DEF:RD011B.DEF'
RECORD EXPACT	
		.INCLUDE 'DEF:RD013A.DEF'
RECORD DUMEXP,X	
		.INCLUDE 'DEF:RD013B.DEF'
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD WORK
	ACTITL	,A6
	ACTNUM	,D7
	ALPHA	,A8
	APOREC	,D5
	APOEND	,D5
	BLANKS	,A19
	BSEND	,D5
	BSMID	,D5
	CNGCTL	,D1
	CNT	,D2
	DECMAL	,D18
	DUPFLG	,D1
	DUEDT2	,D6
	ENTRY	,A38
	EXPEND	,D5
	IDX	,D2
	INXCTL	,D1
	INVDT2	,D6
	KEY	,A12
	MASK	,A7,	'XXXXXXX'
	MASK2	,A8,	'XXXX-XXX'
	MSGCTL	,D1
	NAPREC	,D5
	NEWCNT	,D3,	000
	NUM	,D10
	OPTION	,D1
	SAVVCH	,D6
	SELECT	,D1
	SRCCTL	,D1
	SWITCH	,D1,	1
	SYSTEM	,D1
	TCHAR	,D3
	TOTEXP	,D8
	V	,D1,	1
	VENREC	,D5
	VENEND	,D5
	WHATNO	,D2
RECORD
	READ	,D1,	0
	WRITE	,D1,	1
	LOKCTL	,D1
PROC
	WORK = WORK1
DEFALT,
	CNGCTL =
	XCALL OUTPT (6,8,2,'NEW PAYABLES ENTRY DEFAULTS',1)
	XCALL OUTPT (8,13,0,'1. MULTIPLE A/P ACCTS ?',1)
	XCALL OUTPT (10,13,0,'2. DEFAULT A/P ACCT',1)
	XCALL OUTPT (12,13,0,'3. MULTIPLE CASH ACCTS ?',1)
	XCALL OUTPT (14,13,0,'4. DEFAULT CASH ACCT',1)
	XCALL OUTPT (16,13,0,'5. MULTIPLE DISCNT ACCTS ?',1)
	XCALL OUTPT (18,13,0,'6. DEFAULT DISCNT ACCT',1)
	XCALL OUTPT (20,13,0,'7. VOUCHERING DATE',1)
RDCTL,
	LOKCTL = 1
	XCALL IO (4,DUMNAP,1,READ,LOKCTL)
	UNLOCK 4
	IF (MLTAP.NE.BLANKS) GO TO DSPDEF
GETDEF,
	LOKCTL = 1
	XCALL IO (1,DUMVEN,1,READ,LOKCTL)
	UNLOCK 1
	NEWDEF = VENDEF
	IF (MLTAP.EQ.BLANKS) GO TO MULAP
DSPDEF,
	XCALL OUTPT (8,41,0,MLTAP,1)
	ROW = 10
	ACTNUM = DFLAP
	CALL SERCHD
	XCALL OUTPT (12,41,0,MLTCSH,1)
	ROW = 14
	ACTNUM = DFLCSH
	CALL SERCHD
	XCALL OUTPT (16,41,0,MLTDSC,1)
 	ROW = 18
	ACTNUM = DFLDSC
	CALL SERCHD
	IF (DVCHDT.EQ.0) GO TO DEFVDT
	CTL = '20,41'
	DECMAL(1,6) = DVCHDT
	CALL DSPDTE
	GO TO ANYCND
MULAP,
	CTL = '08,41,01,00,AE'
	CALL INPUT
	MLTAP = ENTRY
	GO TO (MULAP,BEGIN), INXCTL
	IF (MLTAP.EQ.BLANKS) MLTAP = 'N'
	IF (MLTAP.NE.'Y'.AND.MLTAP.NE.'N') GO TO MULAP
	XCALL OUTPT (8,41,0,MLTAP,1)
	GO TO (ANYCND), CNGCTL
DEFAP,
	XCALL OUTPT (10,41,1,'\',1)
	CTL = '10,41,04,03,# '
	CALL INPUT
	GO TO (DEFALT), INXCTL
	IF (ENTRY(1,3).EQ.'000') GO TO DEFAP
	DFLAP (1,4) = ENTRY (1,4)
	XCALL OUTPT (10,45,1,'-',1)
	CTL = '10,46,03,00,# '
	CALL INPUT
	GO TO (DEFAP), INXCTL
	DFLAP (5,7) = ENTRY (1,3)
	ROW = 10
	ACTNUM = DFLAP
	CALL SERCHD
	GO TO (ANYCND), CNGCTL
MULCSH,
	CTL = '12,41,01,00,YN'
	CALL INPUT
	MLTCSH = ENTRY
	GO TO (ANYCND), CNGCTL
DEFCSH,
	XCALL OUTPT (14,41,1,'\',1)
	CTL = '14,41,04,03,# '
	CALL INPUT
	GO TO (DEFALT), INXCTL
	IF (ENTRY(1,3).EQ.'000') GO TO DEFCSH
	DFLCSH (1,4) = ENTRY (1,4)
	XCALL OUTPT (14,45,1,'-',1)
	CTL = '14,46,03,00,# '
	CALL INPUT
	GO TO (DEFCSH), INXCTL
	DFLCSH (5,7) = ENTRY (1,3)
	ROW = 14
	ACTNUM = DFLCSH
	CALL SERCHD
	GO TO (ANYCND), CNGCTL
MULDSC,
	CTL = '16,41,01,00,YN'
	CALL INPUT
	MLTDSC = ENTRY
	GO TO (ANYCND), CNGCTL
DEFDSC,
	XCALL OUTPT (18,41,1,'\',1)
	CTL = '18,41,04,03,# '
	CALL INPUT
	GO TO (DEFALT), INXCTL
	IF (ENTRY(1,3).EQ.'000') GO TO DEFDSC
	DFLDSC (1,4) = ENTRY (1,4)
	XCALL OUTPT (18,45,1,'-',1)
	CTL = '18,46,03,00,# '
	CALL INPUT
	GO TO (DEFDSC), INXCTL
	DFLDSC (5,7) = ENTRY (1,3)
	ROW = 18
	ACTNUM = DFLDSC
	CALL SERCHD
	GO TO (ANYCND), CNGCTL
DEFVDT,
	XCALL OUTPT (20,41,1,'\',1)
	CTL = '20,41,06,00,D '
	CALL INPUT
	GO TO (DEFALT), INXCTL
	DVCHDT = ENTRY(1,6)
	IF (DVCHDT.EQ.0) XCALL RDATE (DVCHDT)
	CTL = '20,41'
	DECMAL(1,6) = DVCHDT
	CALL DSPDTE
ANYCND,
	XCALL ANYCN (CNGCTL,WHATNO)
	GO TO (SETDEF), CNGCTL+1
	GO TO (MULAP,DEFAP,MULCSH,DEFCSH,MULDSC,DEFDSC,DEFVDT), WHATNO
	GO TO ANYCND
SETDEF,
	LOKCTL = 1
	XCALL IO (4,DUMNAP,1,WRITE,LOKCTL)
	XCALL OUTPT (24,53,1,'UPDATE MASTER DEFAULTS ?',1)
	CTL = '24,79,01,00,YN'
	CALL INPUT
	GO TO (ENDDEF), INXCTL - 1
SETDF1,
	LOKCTL = 1
	XCALL IO (1,DUMVEN,1,READ,LOKCTL)
	VENDEF = NEWDEF
	LOKCTL = 1
	XCALL IO (1,DUMVEN,1,WRITE,LOKCTL)
	UNLOCK 1
ENDDEF,
	XCALL OUTPT (24,1,1,'\',1)
	WHATNO = 0
RETURN,
	INXCTL = 0
	WORK1 = WORK
	RETURN
BEGIN,
	WHATNO = 4
	GO TO RETURN
SERCHD,
	IF (ACTNUM.EQ.0) RETURN
	ALPHA = ACTNUM, MASK2
	XCALL OUTPT (ROW,41,0,ALPHA,1)
	KEY(1,7) = ACTNUM, MASK
	BSEND = EXPEND

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL ACCT(KEY(1,7), ACDESC, SRCCTL)

;;;	XCALL SERCH (3,EXPACT,KEY(1,7),1,7,BSEND,BSMID,SRCCTL,4,32,37,0,0,0,0)
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (SRCCTL) ACDESC = '** ACCOUNT NOT ON FILE **'
	XCALL OUTPT (ROW,51,0,ACDESC,1)
	RETURN
INPUT,
	XCALL INPUT(ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,1)
	RETURN
DSPDTE,
	OPTION = 2
	XCALL DSPLY(MAX,ROW,COL,DECMAL,OPTION,1)
	RETURN
END
