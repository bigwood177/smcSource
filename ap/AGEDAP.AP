; AGEDAP / AP 
;
;
;
;		ACCTS PAYABLE AGING REPORT
;
RECORD	D_STUFF
	D_IN	,D8		;DATE-IN, ANY FORMAT
	D_OUT	,D6		;RETURN VALUE MMDDYY
	D_OUTR	,D8		;RETURN VALUE CCYYMMDD
	D_FMT	,A10		;RETURN VALUE MM/DD/CCYY
	D_SW	,A2		;"99" = DATE CONVERSION ERROR



RECORD APOPEN	
		.INCLUDE 'DEF:RD017A.DEF'
RECORD ,X	
		.INCLUDE 'DEF:RD017B.DEF'

;;;RECORD VENIDX	
;;;		.INCLUDE 'DEF:RD012A.DEF'

RECORD VENMAS	
		.INCLUDE 'DEF:RD011A.DEF'
RECORD	,X	
		.INCLUDE 'DEF:RD011B.DEF'
RECORD TITLE
		,A37,'ACCOUNTS PAYABLE AGING REPORT SUMMARY'
RECORD HDR1
		,A12,'------------'
		,A6,'VENDOR'
		,A13,'-------------'
		,A2
		,A7,'VOUCHER'
		,A1
		,A5,'-----'
		,A7,'INVOICE'
		,A6,'------'
		,A2
		,A15,'---------------'
		,A4,'AGED'
		,A1
		,A7,'INVOICE'
		,A1
		,A6,'AMOUNT'
		,A16,'----------------'
		,A3
		,A8,'DISCOUNT'
		,A4
		,A3,'DUE'
RECORD HDR2
		,A1
		,A2,'NO'
		,A3
		,A4,'NAME'
		,A25
		,A2,'NO'
		,A7
		,A2,'NO'
		,A7
		,A4,'DATE'
		,A7
		,A7,'CURRENT'
		,A3
		,A10,'31-60 DAYS'
		,A3
		,A10,'61-90 DAYS'
		,A2
		,A12,'OVER-90 DAYS'
		,A4
		,A6,'AMOUNT'
		,A5
		,A4,'DATE'
RECORD HDR3
		,A12,'------------'
		,A6,'VENDOR'
		,A13,'-------------'
		,A2
		,A19,'-------------------'
		,A15,'AGED-SUB-TOTALS'
		,A19,'-------------------'
		,A6
		,A6,'VENDOR'
		,A7
		,A5,'VALID'
		,A10
		,A3,'NET'
RECORD HDR4
		,A1
		,A2,'NO'
		,A3
		,A4,'NAME'
		,A26
		,A7,'CURRENT'
		,A4
		,A10,'31-60 DAYS'
		,A4
		,A10,'61-90 DAYS'
		,A3
		,A12,'OVER 90 DAYS'
		,A6
		,A5,'TOTAL'
		,A6
		,A9,'DISCOUNTS'
		,A7
		,A5,'TOTAL'
RECORD LEGND
		,A5,'AS OF'
		,A1
	ASOFDT	,A10
		,A2,', '
		,A8,'AGED BY '
	TYPAGE	,A3
		,A5,' DATE'

RECORD	VARS
	TDATE	,D6
	TODAY	,D8
	ADIDT2	,D8
	AGEDT2	,D8
	AGEDTE	,D8
	AGEMTH	,A1
	AMTREM	,D8
	APCNT	,D5
	BSEND	,D5
	BSMID	,D5
	CNGCTL	,D1
	DASHES	,A40,'----------------------------------------'
	DETAIL	,D1
	DSCREM	,D6
	ENTRY	,A7
	ENDNO	,A10
	FSTFLG	,D1,1
	GRDTOT	,4D9
	INXCTL	,D1
	KEY	,A4
	LINCNT	,D2,61
	LINES	,D5
	LNFEED	,D1
	LPSW	,D1
	LSTVND	,A4
	MASK1	,A8,'XX/XX/XX'
	MASK2	,A12,'ZZZZ,ZZZ.XX-'
	MASK3	,A13,'Z,ZZZ,ZZZ.XX-'
	MASK4	,A14,'Z,ZZZ,ZZZ.XXCR'
	MNTSUB	,D2
	NODAYS	,D5
	NOMNTH	,D3
	NOFRMF	,D1
	ORGVEN	,D5
	OSUB	,D1
	PGCNT	,D3
	PLINE	,A132
	PRTCTL	,D3
	PRTCNT	,D5
	PRTTYP	,A1
	RPTNUM	,D3
	SAVLPS	,D1
	SPLFIL	,A14
	SRCCTL	,D1
	STRTNO	,A10
	STXCTL	,D1
	SUBDSC	,D7
	SUBTOT	,4D9
	SWITCH	,D1,1
	TOTDSC	,D7
	LOKCTL	,D1
	READ	,D1,	0
	WRITE	,D1,	1
	V	,D1,1
	VCHCNT	,D3,000
	VENCNT	,D4
	WHATNO	,D1
	WRKDTE	,D8
	WRKDT2	,D8
	WTARG	,D1,2
PROC
	XCALL TERID (V)
	V = 1

;;;	XCALL FILES(2,'I',12,SWITCH)		;FILE # 12 -- VENIDX FILE
;;;	IF (SWITCH.EQ.9) GO TO EXIT

	XCALL FILES(7,'I',17,SWITCH)		;FILE # 17 -- APOPEN FILE
	IF (SWITCH.NE.9) GO TO OPEN1
	CALL CLOSE1
	GO TO EXIT
OPEN1,
	SWITCH = 5
	XCALL FILES(1,'SI',11,SWITCH)		;FILE # 11 -- VENMAS FILE
	IF (SWITCH.EQ.9) GO TO END1
BEGIN,
	XCALL RDATE(TDATE)
	XCALL DATE8(TDATE, D_OUT, TODAY, D_FMT, D_SW)

	SPLFIL (5,6) = 'FF'
	LPSW = 1
	LINCNT = 61
	LSTVND =
	XCALL OUTPT(1,1,2,'PRINT PAYABLES AGING REPORT',V)
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.NE.0) GO TO GETDAT
	CALL CLOSE3
	GO TO EXIT
GETDAT,
	WTARG = 2
	IF (LPSW.EQ.2) WTARG = 4
	IF (FSTFLG.NE.1) GO TO BEGIN2
	XCALL OUTPT(12,20,2,'PLEASE ENTER AGING DATE',V)

	XCALL INPUT(12,50,8,0,'DE',ENTRY,INXCTL,V)
	GO TO (GETDAT,END), INXCTL
	AGEDTE = ENTRY(1,8)
;;;	IF (AGEDTE.EQ.0) XCALL RDATE(AGEDTE)
;;;	IF (AGEDTE.EQ.0) GO TO GETDAT
	IF (AGEDTE .EQ. 0) AGEDTE = TODAY
	XCALL DATE8(AGEDTE, D_OUT, D_OUTR, D_FMT, D_SW)
	ASOFDT = D_FMT
	AGEDT2 = D_OUTR
;;;	ASOFDT = AGEDTE, MASK1
;;;	AGEDT2 (1,2) = AGEDTE (5,6)
;;;	AGEDT2 (3,6) = AGEDTE (1,4)

	XCALL OUTPT(12,50,0,ASOFDT,V)
	XCALL OUTPT (14,20,0,'SHOW OPEN ITEM DETAIL ?',V)
	XCALL INPUT (14,50,1,0,'YY',ENTRY,INXCTL,V)
	DETAIL = INXCTL
AGEBY,
	XCALL OUTPT(16,20,0,'AGE BY INVOICE OR DUE DATE ?',V)
	XCALL INPUT(16,50,1,0,'A ',ENTRY,INXCTL,V)
	GO TO (GETDAT), INXCTL
	AGEMTH = ENTRY
	IF (AGEMTH.EQ.' ') AGEMTH = 'I'
	IF (AGEMTH.NE.'I'.AND.AGEMTH.NE.'D') GO TO AGEBY
	TYPAGE = 'INV'
	IF (AGEMTH.EQ.'D') TYPAGE = 'DUE'
	XCALL OUTPT(16,50,0,AGEMTH,V)
	CNGCTL = 2
	XCALL ANYCN(CNGCTL,WHATNO)
	GO TO (GETDAT), CNGCTL
	IF (DETAIL.EQ.2) HDR1 = HDR3
	IF (DETAIL.EQ.2) HDR2 = HDR4
	IF (DETAIL.EQ.1) TITLE (31,38) =
	XCALL OUTPT(2,1,0,LEGND,V)

;;;	LOKCTL = 1
;;;	XCALL IO (1,VENMAS,1,READ,LOKCTL)
;;;	ORGVEN = ORG011

BEGIN2,
	FSTFLG =
	VENCNT =
	GRDTOT (1,36) =
	TOTDSC =
	STXCTL = 3
	LINES =
	XCALL STENO(STRTNO,ENDNO,'VENDOR #',4,STXCTL,2)
	GO TO (END), STXCTL
	APCNT = 1
	LOKCTL = 1
	XCALL IO (7,APOPEN,APCNT,READ,LOKCTL)
	IF (ENDNO.EQ.'[[[ ') GO TO GOON
	XCALL WATE(4,V)
CONT05,
	INCR APCNT
	LOKCTL = 1
	XCALL IO (7,APOPEN,APCNT,READ,LOKCTL)
	IF (REDVEN.LT.STRTNO) GO TO CONT05
	APCNT = APCNT - 1
	LOKCTL = 1
	XCALL IO (7,APOPEN,APCNT,READ,LOKCTL)
GOON,
	XCALL WATE(WTARG,V)
NXTREC,
	LOKCTL = 1
	XCALL IOS (7,APOPEN,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO LSTREC
	IF (AINVNO.EQ.']]]]]]') GO TO LSTREC
	IF (REDVEN.LT.STRTNO) GO TO NXTREC
	IF (REDVEN.GT.ENDNO) GO TO LSTREC
	IF (AINVNO.EQ.']]]DEL') GO TO NXTREC
	IF (AFLAG.LT.0) GO TO NXTREC
	IF (AFLAG.EQ.2) GO TO NXTREC
	IF (AFLAG.GT.5) GO TO NXTREC
	IF (AVENNO.NE.LSTVND) CALL PRTSUB
	IF (DETAIL.EQ.2) GO TO NODET1
	PLINE(34,39) = AVCHNO
	PLINE(42,49) = AINVNO
	XCALL DATE8(AINVDT, D_OUT, D_OUTR, D_FMT, D_SW)
	PLINE(52,59) = D_OUT,MASK1
NODET1,
	AMTREM = AINVAM - APAID
	DSCREM = ADSCAM - ADSTKN

	WRKDTE = AINVDT
	ADIDT2 (1,2) = ADISDT (5,6)
	ADIDT2 (3,6) = ADISDT (1,4)
	WRKDT2 (1,2) = WRKDTE (5,6)
	WRKDT2 (3,6) = WRKDTE (1,4)
	IF (AGEMTH.EQ.'D') WRKDTE = ADUEDT
	
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;	NODAYS = (AGEDTE(5,6)-WRKDTE(5,6))*360 +
;;;&		 (AGEDTE(1,2)-WRKDTE(1,2))*30 + (AGEDTE(3,4)-WRKDTE(3,4))
	NODAYS = (AGEDTE(1,4)-WRKDTE(1,4))*360 +
&		 (AGEDTE(5,6)-WRKDTE(5,6))*30 + (AGEDTE(7,8)-WRKDTE(7,8))
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	IF (WRKDTE.EQ.0) NODAYS = 0

	IF (NODAYS.GT.90) GO TO AGE90
	IF (NODAYS.GT.60) GO TO AGE60
	IF (NODAYS.GT.30) GO TO AGE30
	IF (WRKDT2.GT.AGEDT2) GO TO CHKDSC
ANYWAY,
	IF (DETAIL.EQ.1) PLINE(62,72) = AMTREM,MASK2
	SUBTOT(1) = SUBTOT(1) + AMTREM
	GO TO ENDBUF
CHKDSC,
	IF (AGEMTH.NE.'D') GO TO NXTREC
	IF (ADIDT2.LT.AGEDT2) GO TO ANYWAY
	GO TO NXTREC
AGE30,
	IF (DETAIL.EQ.1) PLINE(75,85) = AMTREM,MASK2
	SUBTOT(2) = SUBTOT(2) + AMTREM
	GO TO ENDBUF
AGE60,
	IF (DETAIL.EQ.1) PLINE(88,98) = AMTREM,MASK2
	SUBTOT(3) = SUBTOT(3) + AMTREM
	GO TO ENDBUF
AGE90,
	IF (DETAIL.EQ.1) PLINE(101,111) = AMTREM,MASK2
	SUBTOT(4) = SUBTOT(4) + AMTREM
ENDBUF,
	IF (AINVAM.GT.0.AND.DSCREM.LT.0) DSCREM = 0
	IF (DETAIL.EQ.1) PLINE(114,122) = DSCREM,MASK2
	IF (DETAIL.EQ.1.AND.DSCREM.EQ.0) PLINE(114,122) =
	SUBDSC = SUBDSC+DSCREM
	IF (DETAIL.EQ.2.AND.ADIDT2.LT.AGEDT2.AND.DSCREM.GT.0)
&		SUBDSC = SUBDSC - DSCREM
	INCR VCHCNT
	IF (DETAIL.EQ.2) GO TO NXTREC
	XCALL DATE8(ADUEDT, D_OUT, D_OUTR, D_FMT, D_SW)
	PLINE(125,132) = D_OUT ,MASK1
	PRTCTL = 132
	CALL PRINT
	INCR PRTCNT
	GO TO NXTREC
PRINT,
	IF (LINCNT.GE.57) LINCNT = 61
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,'NO HDR3',
&		LEGND,'NO LEGEND',' ',0,132,PRTCTL,1,LPSW,RPTNUM,PRTTYP)
	INCR LINES
	RETURN
PRTSUB,
	IF (LSTVND.EQ.'    ') GO TO ENDSUB
	IF (DETAIL.EQ.2) GO TO NODET2
	IF (PRTCNT.LT.1) GO TO ENDSB2
	IF (PRTCNT.EQ.1) GO TO ONLY1
	LNFEED = 1
	CALL LINFD
	PLINE(42,60) = 'VENDOR  SUBTOTALS:'
	PLINE(61,72) = SUBTOT(1),MASK2
	PLINE(74,85) = SUBTOT(2),MASK2
	PLINE(87,98) = SUBTOT(3),MASK2
	PLINE(100,111) = SUBTOT(4),MASK2
	PLINE(113,122) = SUBDSC,MASK2
	PLINE (7,19) = 'VENDOR TOTAL:'
	PLINE(21,32) = SUBTOT(1)+SUBTOT(2)+SUBTOT(3)+SUBTOT(4),MASK3
	CALL PRINT
	GO TO ONLY1
NODET2,
	IF (VCHCNT.EQ.0) GO TO ENDSB2
	PLINE (34,44) = SUBTOT(1) ,MASK2
	PLINE (48,58) = SUBTOT(2) ,MASK2
	PLINE (62,72) = SUBTOT(3) ,MASK2
	PLINE (76,86) = SUBTOT(4) ,MASK2
	PLINE (90,100) = SUBTOT(1)+SUBTOT(2)+SUBTOT(3)+SUBTOT(4) ,MASK2
	PLINE (104,113) = SUBDSC, MASK2
	PLINE (117,127) = SUBTOT(1)+SUBTOT(2)+SUBTOT(3)+SUBTOT(4)-SUBDSC ,MASK2
	CALL PRINT
	INCR VENCNT
ONLY1,
	GRDTOT(1) = GRDTOT(1)+SUBTOT(1)
	GRDTOT(2) = GRDTOT(2)+SUBTOT(2)
	GRDTOT(3) = GRDTOT(3)+SUBTOT(3)
	GRDTOT(4) = GRDTOT(4)+SUBTOT(4)
	TOTDSC = TOTDSC+SUBDSC
	SUBTOT(1) =
	SUBTOT(2) =
	SUBTOT(3) =
	SUBTOT(4) =
	SUBDSC =
ENDSUB,
	LNFEED = 1
	CALL LINFD
	IF (DETAIL.EQ.1) CALL DASHES
ENDSB2,
	LSTVND = AVENNO
	CALL MCHVND
	PRTCNT =
	VCHCNT =
	RETURN
DASHES,
	PLINE (1,40) = DASHES
	PLINE (41,80) = DASHES
	PLINE (81,120) = DASHES
	PLINE (121,132) = DASHES
	CALL PRINT
	RETURN
RDVMAS,
;;;	LOKCTL = 1
;;;	XCALL IO (1,VENMAS,IRC011,READ,LOKCTL)
	GO TO MACH
MCHVND,
	LOKCTL = 1
	XCALL ISIO (1, VENMAS, AVENNO, READ, LOKCTL)
	IF (LOKCTL .EQ. 0)
	THEN	SRCCTL = 0
	ELSE	BEGIN
		CLEAR VENMAS
		SRCCTL = 1
		END

;;;	BSEND = ORGVEN
;;;	KEY = AVENNO
;;;	XCALL SERCH(2,VENIDX,KEY,1,4,BSEND,BSMID,SRCCTL,4,6,10,0,0,0,0)

	GO TO (RDVMAS), SRCCTL+1
	NAME = '** NOT ON VENDOR FILE **'
MACH,
	PLINE(1,4) = AVENNO
	XCALL LEFTJ (PLINE(1,4),4)
	PLINE(7,31) = NAME
	RETURN
CLEAR,
	GRDTOT (1,36) =
	TOTDSC =
	VENCNT =
	GO TO BEGIN
LSTREC,
	CALL PRTSUB
	PLINE =
	LNFEED = 1
	CALL LINFD
	IF (ENDNO.EQ.'[[[ ') GO TO LSTRC2
	GO TO END
LSTRC2,
	PLINE(1,42) =
	IF (DETAIL.EQ.2) GO TO NODET3
	PLINE(8,19) = 'GRAND TOTAL:'
	PLINE(42,60) = 'TOTAL ALL VENDORS:'
	PLINE(61,72) = GRDTOT(1),MASK2
	PLINE(74,85) = GRDTOT(2),MASK2
	PLINE(87,98) = GRDTOT(3),MASK2
	PLINE(100,111) = GRDTOT(4),MASK2
	PLINE(113,122) = TOTDSC,MASK2
	PLINE(21,32) = GRDTOT(1)+GRDTOT(2)+GRDTOT(3)+GRDTOT(4),MASK2
	CALL PRINT
	GO TO END
NODET3,
	PLINE (1,5) = VENCNT,'Z,ZZX'
	PLINE (7,29) = 'VENDORS   GRAND TOTALS:'
	PLINE (32,44) = GRDTOT(1), MASK3
	PLINE (46,58) = GRDTOT(2), MASK3
	PLINE (60,72) = GRDTOT(3), MASK3
	PLINE (74,86) = GRDTOT(4), MASK3
	PLINE (88,100) = GRDTOT(1)+GRDTOT(2)+GRDTOT(3)+GRDTOT(4), MASK3
	PLINE (103,113) = TOTDSC, MASK2
	PLINE (115,127) = GRDTOT(1)+GRDTOT(2)+GRDTOT(3)+GRDTOT(4)-TOTDSC, MASK3
	CALL PRINT
END,
	SAVLPS = LPSW
	IF (LINES.EQ.0.AND.LPSW.NE.2) CLOSE 14
	IF (LINES.EQ.0.AND.LPSW.NE.2) GO TO CLEAR
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	IF (SAVLPS.EQ.2) GO TO END1
	IF (SAVLPS.EQ.-4) PGCNT =
	IF (ENDNO.EQ.'[[[ ') GO TO END1
	GO TO CLEAR
END1,
	CALL CLOSE3
EXIT,
	XCALL PGCHN('AP:APMENU',1)
CLOSE3,
	XCALL WATE(3,V)
	CLOSE 1
	XCALL FILES(7,'I',17,4)
CLOSE1,
	XCALL FILES(2,'I',12,4)
	RETURN
LINFD,
	XCALL LINFD (LNFEED)
	LINCNT = LINCNT + LNFEED
	RETURN
END
