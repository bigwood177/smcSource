; DEFSEL / AP 
;
;
;
;		FLAGS AP OPEN ITEMS FOR PAYMENT DEFERRAL
;
;
RECORD	D_STUFF
	D_IN	,D8		;DATE-IN, ANY FORMAT
	D_OUT	,D6		;RETURN VALUE MMDDYY
	D_OUTR	,D8		;RETURN VALUE CCYYMMDD
	D_FMT	,A10		;RETURN VALUE MM/DD/CCYY
	D_SW	,A2		;"99" = DATE CONVERSION ERROR


RECORD VENMAS	
		.INCLUDE 'DEF:RD011A.DEF'
RECORD	,X	
		.INCLUDE 'DEF:RD011B.DEF'
RECORD APOPEN	,X	
		.INCLUDE 'DEF:RD017A.DEF'
RECORD DUMAPO	
		.INCLUDE 'DEF:RD017B.DEF'
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
RECORD,X
		.INCLUDE 'DEF:RD017D.DEF'	;1-27-99 SSQ: created to 
						;replace record below...
;;;RECORD	,X
;;;		,A32
;;;	FSTFLG	,A1
;;;		,A4
;;;	CHEKNO	,D6
;;;		,A24
;;;	CUTDT2	,D6
;;;	CHKDTE	,D6
;;;		,A39
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2

RECORD	VARS
	TDATE	,D6
	TODAY	,D8
;;; 12-14-15	TODAY	,D6
	ALL	,A3,	'ALL'
	APACT	,D7
	ALPHA	,A8
	ALLFLG	,D1
	BLANKS	,A8
	BSEND	,D5
	BSMID	,D5
	CLRALL	,D1
	CLRFLG	,D1
	CNGCTL	,D1
	CUTDTE	,D8
	DATE	,D8
	DECMAL	,D4
	DFRFLG	,D1
	DFRMAP	,11D1,	6,1,1,2,1,1,1,6,6,9,9
	DUEDT1	,D8
	DUEDT2	,D8
	ENTRY	,A8
	ENDNO	,D5
	FLAG	,D1
	FOUND	,D1
	FSTSRC	,D1
	INXCTL	,D1
	KVCHNO	,D6
	KVENNO	,A4
	LOKCTL	,D1
	LSTFLG	,D1
	LSTVCH	,D6
	MODE	,2A12,	'BY SELECTION','BY DEFERRAL '
	MODE2	,4A9,	'SELECTED ','CANCELLED','DEFERRED ','CANCELLED'
	N	,D2,	10
	PRTFLG	,D1
	PROCTL	,D1
	PRTCNT	,D5
	READ	,D1,	0
	SAVAP	,D7
	SAVVEN	,A4
	SELECT	,D1
	SELCT2	,D1
	SRCKEY	,A10
	SRCCTL	,D1
	STRTNO	,D5
	SWITCH	,D1,	1
	TRXKEY	,A10
	UNDFLG	,D1
	UNDMAP	,11D1,	6,0,0,2,3,0,0,6,6,8,8
	V	,D1,	1
	WHATNO	,D2
	WRITE	,D1,	1
PROC
	XCALL TERID (V)
	V = 1

	XCALL FILES(1,'SI',11,SWITCH)			;VENMAS FILE
	IF (SWITCH.EQ.9) XCALL PGCHN ('AP:APMENU',1)

	LOKCTL = 1
	CLEAR VENMAS
	VENNO = '   #'				;HEADER
	XCALL ISIO (1, VENMAS, VENNO, READ, LOKCTL)

;;;	XCALL IO (1,VENMAS,1,READ,LOKCTL)
	XCALL FILES (1,'SI',11,4)
	SAVAP = VENDEF(2,8)
	XCALL FILES(4,'U',17,SWITCH)		;FILE # 17 -- APOPEN FILE
	IF (SWITCH.EQ.9) GO TO EXIT
	LOKCTL = 0
	XCALL IO (4,DUMAPO,1,READ,LOKCTL)
	IF (LOKCTL) GO TO CLOSE1
	UNLOCK 4

	XCALL RDATE(TDATE)
	XCALL DATE8(TDATE, D_OUT, TODAY, D_FMT, D_SW)

	BSEND = REC017
	XCALL OUTPT(1,1,2,'PAYMENT PREPARATION',V)
	IF (CUTDT2.NE.0) CALL DSPCUT
MENU,
	XCALL OUTPT(2,1,2,'\',V)
	XCALL OUTPT(10,20,0,'PLEASE SELECT APPLICATION',V)
	XCALL OUTPT(12,25,0,'1. PAYMENT BY SELECTION',V)
	XCALL OUTPT(14,25,0,'2. PAYMENT BY DEFERRAL',V)
	XCALL OUTPT(16,25,0,'3. PRINT PRE-CHECK-WRITING REPORT',V)
	CTL = '10,47,01,01,#E'
	CALL INPUT
	GOTO (MENU,ENDOFF), INXCTL
	SELECT = ENTRY
	IF (SELECT.LT.1.OR.SELECT.GT.3) GOTO MENU
	IF (SELECT.EQ.3) GO TO CHAIN2
	XCALL OUTPT(2,1,0,MODE(SELECT),V)
	GOTO (SELEC1,DEFER1), SELECT
SELEC1,
	XCALL OUTPT(8,1,2,'\',V)
	CALL GETACT
	GOTO (MENU), INXCTL-1
	XCALL OUTPT(12,20,0,'CLEAR ALL SELECTION FLAGS ?',V)
	CTL = '12,55,01,01,YN'
	CALL INPUT
	IF (INXCTL.EQ.2) GOTO MENU2
	XCALL OUTPT(14,20,0,'ARE YOU SURE ?',V)
	CTL (1,2) = '14'
	CALL INPUT
	IF (INXCTL.EQ.2) GO TO MENU
	CUTDT2 =
	BSMID = 1
	FSTFLG = 1
	XCALL WATE (3,V)
READ2,
	INCR BSMID
	IF (BSMID.GT.BSEND) GOTO MENU2
	CALL READ
	IF (AFLAG.LT.-1) GOTO READ2
	IF (AAPACT.NE.APACT.AND.APACT.GT.0) GO TO READ2
	CALL SETFLG
	GOTO READ2
READ2D,
	INCR BSMID
	IF (BSMID.GT.REC017) GO TO DISPL1
	CALL READ
	IF (AFLAG.LT.-1) GO TO READ2D
	IF (AAPACT.NE.APACT.AND.APACT.GT.0) GO TO READ2D
	IF (AINVNO.EQ.']]]DEL') GO TO READ2D
	GO TO (RITE1,READ2D,CHKPRT,READ2D,READ2D,READ2D,READ2D,READ2D,RITE1,
&			RITE1,READ2D), AFLAG + 2
RITE1,
	CALL UNDEFR
	GO TO READ2D
CHKPRT,
	PRTFLG =
	STRTNO = BSMID
	SRCKEY = APOPEN (1,10)
	DFRFLG =
	UNDFLG =
	CALL READIV
	IF (PRTFLG) DFRFLG = 1
	IF (PRTFLG.EQ.0) UNDFLG = 1
	BSMID = STRTNO - 1
	CALL READIV
	BSMID = ENDNO
	GO TO READ2D
DEFER1,
	FSTFLG = 1
	CLRALL =
	CLRFLG =
	BSMID = 1
	XCALL OUTPT(8,1,2,'\',V)
	CALL GETACT
	GOTO (MENU), INXCTL-1
	XCALL OUTPT(12,20,0,'CLEAR ALL DEFERRAL FLAGS ?',V)
	CTL = '12,55,01,01,YN'
	CALL INPUT
	IF (INXCTL.EQ.2) GOTO TESDTE
	XCALL OUTPT(14,20,0,'ARE YOU SURE ?',V)
	CTL (1,2) = '14'
	CALL INPUT
	GO TO (MENU), INXCTL - 1
	CUTDT2 =
	XCALL WATE(3,V)
	GO TO READ2D
DSPCUT,
	XCALL OUTPT (1,52,0,'DUE DATE CUT-OFF: ',V)
	XCALL DATE8(CUTDT2, D_OUT, D_OUTR, D_FMT, D_SW)
	XCALL OUTPT (1, 70, 0, D_FMT, V)
;;;	DATE(1,4) = CUTDT2(3,6)
;;;	DATE(5,6) = CUTDT2(1,2)
;;;	XCALL DSPLY (6,1,72,DATE,2,V)
	RETURN
TESDTE,
	IF (CUTDT2.EQ.0) GOTO DISPL1
	XCALL OUTPT(10,20,2,'CHANGE THE DUE DATE CUT-OFF ?',V)
	CTL = '10,55,01,01,YN'
	CALL INPUT
	IF (INXCTL.EQ.2) GOTO PROC1
DISPL1,
	CUTDT2 =
	CLRFLG = 1
	XCALL OUTPT(8,1,2,'\',V)
	XCALL OUTPT(12,20,0,'PLEASE ENTER THE DUE DATE CUT-OFF',V)
	XCALL OUTPT(14,20,0,'(ALL ITEMS WITH DUE & DISCOUNT DATES AFTER',V)
	XCALL OUTPT(16,20,0,'THIS DATE WILL AUTOMATICALLY BE DEFERRED.)',V)
CUTOFF,
	CTL = '12,55,08,00,DE'
	CALL INPUT
	GOTO (CUTOFF,MENU), INXCTL
	CUTDTE = ENTRY(1,8)
	IF (CUTDTE.EQ.0) CUTDTE = TODAY
	IF (CUTDTE.EQ.0) GO TO CUTOFF
	XCALL DATE8(CUTDTE, D_OUT, D_OUTR, D_FMT, D_SW)
	XCALL OUTPT (ROW, COL, 0, D_FMT, V)	
	CUTDT2 = CUTDTE
;;;	XCALL DSPLY (6,12,55,CUTDTE,2,V)
;;;	CUTDT2(1,2) = CUTDTE(5,6)
;;;	CUTDT2(3,6) = CUTDTE(1,4)
DEFER3,
	PROCTL = 1
	CNGCTL = 2
	GOTO ANYCNG
GETACT,
	APACT =
	XCALL OUTPT(10,20,2,'PLEASE ENTER A/P ACCOUNT NUMBER',V)
	CTL = '10,55,04,00,#X'
	CALL INPUT
	GOTO (GETACT,ENDACT,DEFAP), INXCTL
	IF (ENTRY.EQ.BLANKS) GOTO SETALL
	APACT(1,4) = ENTRY(1,4)
	XCALL OUTPT(10,59,0,'-',V)
	CTL = '10,60,03,00,# '
	CALL INPUT
	GOTO (GETACT), INXCTL
	APACT(5,7) = ENTRY(1,3)
DSPACT,
	ALPHA = APACT,'ZXXX-XXX'
	IF (APACT .NE. 0) XCALL OUTPT (2,14,1,'- FOR ACCT# ',V)
	IF (APACT .NE. 0) XCALL OUTPT (0,0,0,ALPHA,V)
	IF (APACT.NE.0) XCALL OUTPT(10,55,1,ALPHA,V)
	IF (APACT.EQ.0) XCALL OUTPT(2,14,1,'FOR ALL A/P ACCTS',V)
ENDACT,
	RETURN
SETALL,
	ENTRY =
	XCALL OUTPT (10,55,1,'ALL',V)
	GO TO DSPACT
DEFAP,
	APACT = SAVAP
	GO TO DSPACT
PROCES,
	CALL DSPCUT
	GOTO (PROC1,PROC2), PROCTL
PROC1,
	PROCTL = 1
	XCALL WATE (3,V)
	BSMID = 1
	IF (CLRFLG.NE.1) GOTO MENU2
READ3,
	INCR BSMID
	IF (BSMID.GT.REC017) GO TO MENU2
	CALL READ
	IF (AFLAG.LT.-1) GO TO READ3
	IF (AAPACT.NE.APACT.AND.APACT.GT.0) GO TO READ3
	IF (AINVNO.EQ.']]]DEL') GO TO READ3
	GO TO (RITE2,RITE3,READ3,READ3,READ3,RITE3,RITE3,READ3,RITE2,
&			PDPART,READ3), AFLAG + 2
RITE2,
	CALL SETFLG
	GO TO READ3
RITE3,
	CALL DEFER2
	LSTFLG = AFLAG
	GO TO READ3
PDPART,
	IF (LSTFLG.EQ.1) CALL SETFLG
	GO TO READ3
MENU2,
	XCALL OUTPT(8,1,2,'\',V)
	XCALL OUTPT(10,20,0,'PLEASE SELECT APPLICATION',V)
	IF (SELECT.NE.1) GOTO DFRMEN
	XCALL OUTPT(12,25,0,'1. SELECT ITEM(S)',V)
	XCALL OUTPT(14,25,0,'2. CANCEL SELECTED ITEM(S)',V)
	GOTO ENDMEN
DFRMEN,
	XCALL OUTPT(12,25,0,'1. DEFER ITEM(S)',V)
	XCALL OUTPT(14,25,0,'2. CANCEL DEFERRED ITEM(S)',V)
ENDMEN,
	SELCT2 =
	CTL = '10,47,01,01,#E'
	CALL INPUT
	GOTO (ENDMEN,MENU), INXCTL
	SELCT2 = ENTRY(1,1)
	GOTO (MENOK,MENOK), SELCT2
	XCALL MESAG (' ',3)
	GOTO ENDMEN
MENOK,
	SELCT2 = 2*(SELECT-1) + SELCT2
	GOTO DISPL2
DISPLA,
	GOTO (DISPL1,DISPL2) , PROCTL
DISPL2,
	ALLFLG =
	KVENNO =
	KVCHNO =
GETNEX,
	UNLOCK 4
	XCALL OUTPT(8,1,2,'\',V)
	XCALL OUTPT (12,20,2,'PLEASE ENTER FOR ITEMS TO BE ',V)
	XCALL OUTPT(0,0,0,MODE2(SELCT2),V)
	XCALL OUTPT(14,25,0,'VENDOR #',V)
	XCALL OUTPT(16,25,0,'VOUCHER #',V)
VENDNO,
	CTL = '14,47,04,00,AE'
	CALL  INPUT
	GO TO (DISPLA,MENU2), INXCTL
	XCALL FRMAT (ENTRY(1,4),4)
	KVENNO = ENTRY(1,4)
	IF (KVENNO.EQ.BLANKS) KVENNO = SAVVEN
	IF (KVENNO.EQ.BLANKS) GO TO VENDNO
	XCALL FRMAT (ENTRY(1,4),4)
	SAVVEN = KVENNO
	XCALL OUTPT(14,47,0,KVENNO,V)
CONT,
	SRCKEY(1,4) = KVENNO
	CTL = '16,47,06,00,# '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	IF (ENTRY.EQ.BLANKS) GO TO ALL
	KVCHNO = ENTRY (1,6)
	SRCKEY (5,10) = KVCHNO,'XXXXXX'
	GOTO CONTIN
ALL,
	ALLFLG = 1
	XCALL OUTPT(16,47,1,ALL,V)
	ENTRY = ALL
CONTIN,
	CNGCTL = 2
	PROCTL =2
	GOTO ANYCNG
PROC2,
	XCALL WATE(3,V)
	GOTO (SELCT,DEFER,DEFER,SELCT), SELCT2
SELCT,
	IF (ALLFLG) GOTO ALL1
	DFRFLG =
	UNDFLG =
	N = 10
	FOUND = 1
	CALL READVV
	IF (SRCCTL) GOTO NOFIND
	IF (PRTFLG) GO TO SELERR
	IF (FOUND.EQ.0) GO TO NOAP
	UNDFLG = 1
	BSMID = STRTNO - 1
	CALL READIV
	CALL MESAGE
	GOTO DISPLA
ALL1,
	FSTSRC = 1
	N = 4
	CALL READVV
	FSTSRC =
	N = 10
	IF (SRCCTL) GOTO NOFND2
READ6,
	LOKCTL = 1
	XCALL IO (4,APOPEN,STRTNO,READ,LOKCTL)
	IF (APOPEN.EQ.']]]]]]') GOTO ENDOF1
	IF (APOPEN(1,4).NE.SRCKEY(1,4)) GOTO ENDOF1
	SRCKEY (1,N) = APOPEN(1,N)
	DFRFLG =
	UNDFLG =
	CALL READVV
	IF (PRTFLG) GOTO ENDAL1
	UNDFLG = 1
	BSMID = STRTNO - 1
	CALL READIV
ENDAL1,
	STRTNO = ENDNO + 1
	GOTO READ6
ENDOF1,
	CALL MESAGE
	GOTO DISPLA
DEFER,
	IF (ALLFLG) GOTO ALL2
	DFRFLG = 1
	UNDFLG =
	FOUND = 1
	CALL READVV
	IF (SRCCTL) GOTO NOFIND
	IF (FOUND.EQ.0) GO TO NOAP
	CALL MESAGE
	GOTO DISPLA
ALL2,
	FSTSRC = 1
	N = 4
	CALL READVV
	FSTSRC =
	N = 10
	IF (SRCCTL) GOTO NOFND2
READ7,
	LOKCTL = 1
	XCALL IO (4,APOPEN,STRTNO,READ,LOKCTL)
	IF (APOPEN.EQ.']]]]]]') GOTO ENDOF2
	IF (APOPEN(1,4).NE.SRCKEY(1,4)) GOTO ENDOF2
	SRCKEY(1,N) = APOPEN(1,N)
	UNDFLG =
	DFRFLG = 1
	CALL READVV
	STRTNO = ENDNO + 1
	GOTO READ7
ENDOF2,
	CALL MESAGE
	GOTO DISPLA
NOAP,
	XCALL MESAG ('NOT IN THIS A/P ACCOUNT',1)
	GO TO DISPLA
MESAGE,
	XCALL MESAG (MODE2(SELCT2),2)
	RETURN
READVV,
	PRTFLG =
	STRTNO =
	ENDNO =
	TRXKEY =
	TRXKEY (1,N) = SRCKEY (1,N)
	LOKCTL = 1
	XCALL IO (4,APOPEN,MAX017,READ,LOKCTL)
SERCH,
	BSEND = ORG017
	SRCCTL = 2
	XCALL SERCH (4,APOPEN,TRXKEY(1,N),1,N,BSEND,BSMID,SRCCTL,4,32,37,0,0,0,0)
	IF (SRCCTL) RETURN
	IF (AAPACT.NE.APACT.AND.APACT.GT.0) FOUND =
FIRST,
	BSMID = BSMID - 1
	IF (BSMID.EQ.1) GOTO FIRST2
	CALL READ
	IF (APOPEN(1,N).EQ.SRCKEY(1,N)) GOTO FIRST
FIRST2,
	STRTNO = BSMID + 1
	ENDNO = BSMID + 1
	IF (FSTSRC.EQ.1) RETURN
READIV,
	INCR BSMID
	IF (BSMID.GT.BSEND) GOTO LSTRD1
	CALL READ
	IF (APOPEN(1,N).NE.SRCKEY(1,N)) GOTO LSTRD1
	ENDNO = BSMID
	IF (AFLAG.LT.-1) GO TO BYPASS
	IF (APACT.GT.0.AND.AAPACT.NE.APACT) GO TO BYPASS
	IF (AINVNO.EQ.']]]DEL') GOTO BYPASS
	IF (DFRFLG.EQ.1) CALL SETFLG
	IF (UNDFLG.EQ.1) CALL UNDEFR
	IF (AFLAG.EQ.-1.OR.AFLAG.EQ.6.OR.AFLAG.EQ.7) PRTFLG = 1
BYPASS,
	GOTO READIV
LSTRD1,
	RETURN
DEFER2,
;;;	DUEDT1(1,2) = ADISDT(5,6)
;;;	DUEDT1(3,6) = ADISDT(1,4)
;;;	DUEDT2(1,2) = ADUEDT(5,6)
;;;	DUEDT2(3,6) = ADUEDT(1,4)
;;;	IF (DUEDT1.GT.CUTDT2 .AND. DUEDT2.GT.CUTDT2)  GOTO SETFLG	; DEFER
	IF (ADISDT.GT.CUTDT2 .AND. ADUEDT.GT.CUTDT2) GOTO SETFLG
	RETURN
SETFLG,
	AFLAG = DFRMAP (AFLAG+2)	;DFRMAP	,11D1,	6,1,1,2,1,1,1,6,6,9,9
	LOKCTL = 1
	XCALL IO (4,APOPEN,BSMID,WRITE,LOKCTL)
	RETURN
UNDEFR,
	AFLAG = UNDMAP (AFLAG+2)
	LOKCTL = 1
	XCALL IO (4,APOPEN,BSMID,WRITE,LOKCTL)
	RETURN
NOFND2,
	XCALL MESAG('VENDOR NOT FOUND',1)
	GO TO DISPLA
NOFIND,
	XCALL MESAG('VENDOR/VOUCHER NOT FOUND',1)
	GOTO DISPLA
SELERR,
	XCALL MESAG('CANNOT SELECT VOUCHER WITH UNPAID PARTIALS',1)
	GO TO DISPLA
CHAIN2,
	XCALL WATE(3,V)
	LOKCTL = 1
	XCALL IO (4,DUMAPO,1,WRITE,LOKCTL)
	XCALL FILES(4,'U',17,4)
	XCALL PGCHN('AP:PRECHK',1)
ENDOFF,
	XCALL WATE(3,V)
	LOKCTL = 1
	XCALL IO (4,DUMAPO,1,WRITE,LOKCTL)
CLOSE1,
	XCALL FILES(4,'U',17,4)
EXIT,
	XCALL PGCHN('AP:APMENU',1)
READ,
	LOKCTL = 1
	XCALL IO (4,APOPEN,BSMID,READ,LOKCTL)
	UNLOCK 4
	RETURN
INPUT,
	XCALL INPUT(ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,V)
	RETURN
ANYCNG,
	CNGCTL = 2
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (PROCES,DISPLA,DISPLA), CNGCTL+1
END
