;  APALST / AP 
;
;
;			::PCPYAP.DEF::
;******************************************************************************
;		ACCOUNTS PAYABLE
;
;		RELEASED: AUGUST 1, 1984 (d70s10)
;******************************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;		PRINTS LIST OF VALID A/P ACCOUNTS
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD EXPACT	
		.INCLUDE 'DEF:RD013A.DEF'
RECORD ,X	
		.INCLUDE 'DEF:RD013B.DEF'
RECORD		,X
	REDACT	,A7
		,A30
RECORD TITLE
		,A22,'VALID A/P ACCOUNT LIST'
RECORD HDR1
		,A17,'-----------------'
		,A7,'ACCOUNT'
		,A17,'-----------------'
RECORD HDR2
		,A3
		,A2,'NO'
		,A6
		,A11,'DESCRIPTION'
RECORD
	ACTCNT	,D4
	ALL	,A4,'ALL '
	BLANKS	,A4
	BSEND	,D5
	BSMID	,D5
	CNGCTL	,D1
	DECMAL	,D4
	ENDNO	,A7
	ENTRY	,A5
	INXCTL	,D1
	KEY	,A7
	LNFEED	,D1
	LINCNT	,D2,60
	LPSW	,D1
	MASK	,A7,'XXXXXXX'
	PGCNT	,D3
	PLINE	,A132
	PRTCNT	,D5
	PRTCTL	,D3
	PRTFIL	,A14
	PRTTYP	,A1
	RPTNUM	,D3
	SAVLPS	,D1
	SPLFIL	,A14
	SRCCTL	,D1
	STRTNO	,A7
	SWITCH	,D1
	LOKCTL	,D1
	READ	,D1,	0
	WRITE	,D1,	1
	V	,D1,1
	WHATNO	,D1
	WTARG	,D1
PROC
	XCALL TERID (V)
BEGIN,
	LPSW = 1
	SPLFIL (5,6) = 'FA'
	XCALL OUTPT(1,1,2,'VALID A/P ACCT FILE MAINTENANCE',1)
	XCALL OUTPT(2,1,0,'PRINT OUT',1)
	XCALL LPON (LPSW,SPLFIL)
	GO TO (END2), LPSW + 1
OPEN,
	SWITCH = 1
	XCALL FILES(3,'I',13,SWITCH)		;FILE # 13 -- EXPACT FILE
	IF (SWITCH.EQ.9) GO TO END2
	LOKCTL = 1
	XCALL IO (3,EXPACT,1,READ,LOKCTL)
	BSEND = ORG013
	ACTCNT = 0
	LINCNT = 60
GETNOS,
	PRTCNT =
	XCALL OUTPT(6,1,2,'\',V)
	XCALL OUTPT(6,25,0,'STARTING ACCOUNT #',V)
	XCALL OUTPT(7,25,0,'ENDING ACCOUNT #',V)
	XCALL INPUT(6,45,4,0,'AE',ENTRY,INXCTL,V)
	GO TO (GETNOS,END1), INXCTL
	IF (ENTRY.EQ.ALL.OR.ENTRY.EQ.BLANKS) GO TO SETALL
	ONERROR NOTNUM
	DECMAL = ENTRY(1,4)
	OFFERROR
	STRTNO(1,4) = DECMAL,MASK
	XCALL OUTPT(6,49,0,'-',V)
	XCALL INPUT(6,50,3,0,'# ',ENTRY,INXCTL,V)
	GO TO (GETNOS), INXCTL
	IF (ENTRY.EQ.BLANKS) XCALL OUTPT(6,49,1,'\',V)
	DECMAL = ENTRY(1,3)
	STRTNO(5,7) = DECMAL,MASK
	XCALL INPUT(7,45,4,0,'# ',ENTRY,INXCTL,V)
	GO TO (GETNOS), INXCTL
	IF (ENTRY.EQ.BLANKS) GO TO SAMNUM
	DECMAL = ENTRY(1,4)
	ENDNO(1,4) = DECMAL,MASK
	XCALL OUTPT(7,49,0,'-',V)
	XCALL INPUT(7,50,3,0,'# ',ENTRY,INXCTL,V)
	GO TO (GETNOS), INXCTL
	IF (ENTRY.EQ.BLANKS) XCALL OUTPT(7,49,1,'\',V)
	DECMAL = ENTRY(1,3)
	ENDNO(5,7) = DECMAL,MASK
ANYCNG,
	CNGCTL = 2
	XCALL ANYCN(CNGCTL,WHATNO)
	GO TO (GETNOS), CNGCTL
	WTARG = 2
	IF (LPSW.EQ.2) WTARG = 4
	XCALL WATE(WTARG,V)
	IF (STRTNO.EQ.ENDNO) GO TO BSERCH
	LOKCTL = 1
	XCALL IO (3,EXPACT,1,READ,LOKCTL)
READ,
	LOKCTL = 1
	XCALL IOS (3,EXPACT,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOF
	IF (ACDESC.EQ.']]]]]]]') GO TO EOF
	IF (REDACT.LT.STRTNO) GO TO READ
	IF (REDACT.GT.ENDNO) GO TO EOF
	IF (ACDESC(25,30).EQ.'000000') GO TO READ
PRNTIT,
	INCR PRTCNT
	PLINE(1,4) = ACTNO(1,4)
	PLINE(5,5) = '-'
	PLINE(6,8) = ACTNO(5,7),MASK
	PLINE(12,41) = ACDESC
	CALL PRINT
	IF (STRTNO.EQ.ENDNO) RETURN
	INCR ACTCNT
	GO TO READ
NOTNUM,
	XCALL MESAG(' ',3)
	GO TO GETNOS
SAMNUM,
	ENDNO = STRTNO
	GO TO ANYCNG
SETALL,
	XCALL OUTPT(6,45,0,ALL,V)
	STRTNO =
	ENDNO = '[[['
	GO TO ANYCNG
PRINT,
	PRTCTL = 70
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,
&	HDR1,HDR2,'NO HDR3','NO LEGEND',' ',' ',0,80,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
BSERCH,
	KEY(1,7) = ENDNO
;	ABOVE LINE CHANGED AS PER MCBA PATCH AP4-DIBOL-7
;
	XCALL SERCH(3,EXPACT,KEY,1,7,BSEND,BSMID,SRCCTL,4,32,37,0,0,0,0)
	GO TO (NOFIND), SRCCTL
	CALL PRNTIT
	CALL CLOSE
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	GO TO BEGIN
;*********************************************************
NOFIND,
	XCALL MESAG('ACCOUNT # NOT ON FILE',1)
	GO TO GETNOS
EOF,
	IF (ENDNO.EQ.'[[[ ') CALL TOTAL
	CALL CLOSE
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	IF (ENDNO.EQ.'[[[ ') GO TO END2
	GO TO BEGIN
TOTAL,
	PLINE(3,6) = ACTCNT
	PLINE(8,23) = 'ACCOUNTS ON FILE'
	LNFEED = 2
	CALL LINFD
	CALL PRINT
	RETURN
;*******************************************************
END1,
	CALL CLOSE
END2,
	XCALL WATE(3,V)
	XCALL PGCHN('AP:APAMNT',1)
CLOSE,
	XCALL FILES(3,'I',13,4)
	RETURN
LINFD,
	XCALL LINFD (LNFEED)
	LINCNT = LINCNT + LNFEED
	RETURN
END
