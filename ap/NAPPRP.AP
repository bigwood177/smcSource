;  NAPPRP / AP 
;
;
;		NEW PAYABLES POSTING PREPARATION
;
; 26-Oct-2015 ssq: separate voucher and check batches.
; 11-Nov-2015 ssq: use invoice date instead of voucher date

RECORD	FFILE
	FILNUM	,D3
	CLCTL	,D1
	REDFIL	,A14

 
RECORD	HEADER
		,A7,	'DBSORT '
	HFILE	,A14

RECORD NEWAP	
		.INCLUDE 'DEF:RD014A.DEF'
RECORD DUMNAP	
		.INCLUDE 'DEF:RD014B.DEF'
RECORD APDIST	
		.INCLUDE 'DEF:RD016B.DEF'
RECORD NEWAPD	
		.INCLUDE 'DEF:RD015B.DEF'
RECORD	,X	
		.INCLUDE 'DEF:RD015A.DEF'
RECORD	,X
		,A5
	PROGNM	,A6
		,A24
RECORD
		.INCLUDE 'DEF:RD015S.DEF'
RECORD APOPEN	
		.INCLUDE 'DEF:RD017B.DEF'
;;;RECORD SNDMSG
;;;		,A9,	'AP:VCHREG'

RECORD	CHANNEL
	CHN012	,D2
	CHN014	,D2
	CHN015	,D2
	CHN016	,D2
	CHN017	,D2
	
RECORD	VARS
	XFIL	,D3,	014
	OPNOK	,D1
	APDFIL	,D1
	APDOVF	,D5
	APOFIL	,D1
	APOOVF	,D5
	ALPHA	,A5
	BLKSIZ	,D4
	CPNTR	,D2
	DSCAMT	,D6
	DSTCNT	,D5
	DSTIDX	,D2
	MSGCTL	,D1
	NAPCNT	,D5
	NAPREC	,D5
	NO	,D1,	0
	NPDFIL	,D1
	NPDOVF	,D5
	ROW	,D2
	ROW2	,D2
	SWITCH	,D1,3
	TRXCNT,	D5
	LOKCTL	,D1
	READ	,D1,	0
	WRITE	,D1,	1
	V	,D1
	YES	,D1,	1
	ERROR   ,D1
PROC
	XCALL TERID (V)
	V = 1
	XCALL WATE (4,V)
	XCALL OUTPT(2,1,0,'POSTING PREPARATION',V)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO EXIT


PROCES,

	NEWAPD =
	LOKCTL = 1
	XCALL IOS (5,NEWAPD,WRITE,LOKCTL)
	NAPCNT = 1
	LOKCTL = 1
	XCALL IO (4,NEWAP,1,READ,LOKCTL)
MAKDST,
	LOKCTL = 1
	XCALL IOS (4,NEWAP,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO ENDPRP
	IF (WNAME.EQ.']]]]]]') GO TO ENDPRP
	IF (WNAME.EQ.'00000') GO TO MAKDST
	CALL MOVE
	IF (WNDISC.EQ.-1) GO TO MAKDS3
	IF (WCHKNO.NE.0) GO TO MAKDS2
	NACCT = WAPACT
	NTRXAM = WINVAM
	NTYPE = 1
	CALL WRITED
	GO TO MAKEXP
MAKDS2,
	CALL CLCDSC
	NTYPE = 2
	NACCT = WCSACT
	NTRXAM = WINVAM - DSCAMT
	CALL WRITED
	NTYPE = 3
	NACCT = WDSACT
	NTRXAM = DSCAMT
	CALL WRITED
	GO TO MAKEXP
MAKDS3,
	NTYPE = 4
	NACCT = WAPACT
	NTRXAM = WINVAM
	CALL WRITED
	NTYPE = 2
	NACCT = WCSACT
	NTRXAM = WINVAM - WDAMT
	CALL WRITED
	NTYPE = 3
	NACCT = WDSACT
	NTRXAM = WDAMT
	CALL WRITED
	GO TO MAKDST
MAKEXP,
	DSTIDX =
	NTYPE = 6
MAKEX2,
	INCR DSTIDX
	IF (DSTIDX.GT.9) GO TO MAKDST
	NACCT = WDACTS (DSTIDX)
	NTRXAM = WDAMTS (DSTIDX)
	CALL WRITED
	GO TO MAKEX2
CLCDSC,
	DSCAMT = ((WINVAM - WNDISC) * WDPCT) #3
	IF (WDAMT.NE.0.OR.WNDISC.EQ.-1) DSCAMT = WDAMT
	RETURN
MOVE,
	NDATE = WINVDT		;11-11-15 ssq Invoice date
;;;	NDATE = WVCHDT		;Voucher date

	IF (WCHKNO.GT.0 .AND. WNDISC.NE.-1) NDATE = WDUEDT	;Prepaid
	IF (WNDISC.EQ.-1) NDATE = WDUEDT			;Manual Payment
	NVENNO = WVENNO
	NVCHNO = WVCHNO
	RETURN
WRITED,
	IF (NTRXAM.EQ.0) RETURN
	LOKCTL = 1
	XCALL IOS (5,NEWAPD,WRITE,LOKCTL)
	INCR NAPCNT
	RETURN

ENDPRP,
	LOKCTL = 1
	XCALL FILL (']', NEWAPD)
	XCALL IOS (5,NEWAPD,WRITE,LOKCTL)
	CLOSE 5
	XCALL FILES(5,'U',15,5)
	LOKCTL = 1
	XCALL IO (5,NEWAPD,1,READ,LOKCTL)
	REC015 = NAPCNT
	ORG015 = NAPCNT		;10-26-15
	PROGNM = 'PSTAPO'
	LOKCTL = 1
	XCALL IO (5,NEWAPD,1,WRITE,LOKCTL)
	CLOSE 5

;-------------------------------------------------------------
;;; 10-26-15 ssq: replace SRTVCH 
;SRTVCH.AP
	close 4
 
	FILNUM = XFIL			;HARD CODE IF NO MESSAGE
	XCALL FFILE (FILNUM,REDFIL,CLCTL)	;GET FILE SPEC
	HFILE = REDFIL
 
	XCALL OUTPT (2,1,2,HEADER,1)
 
	SORT (IN=REDFIL,
&		RECORD=NEWAP,
&		KEY = (WVCHNO,WNDISC)
&		)
 

	XCALL PGCHN('AP:VCHREG',0)

;;;	MSGCTL = 5
;;;	XCALL SNMSG (SNDMSG,MSGCTL)
;;;	XCALL PGCHN('AP:SRTVCH',0)
;-------------------------------------------------------------

EXIT,
	CALL CLOSE
	XCALL PGCHN('AP:VCHENT',1)

OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES(5,'O',15,SWITCH)		;NEWAPD FILE
	IF (SWITCH.EQ.9) RETURN
	CHN015 = 5

	SWITCH = 3

	XCALL FILES(4,'U',14,SWITCH)		;NEWAP0 FILE
	IF (SWITCH.EQ.9) RETURN
	CHN014 = 4

	XCALL FILES(6,'U',16,SWITCH)		;APDIST FILE
	IF (SWITCH.EQ.9) RETURN
	CHN016 = 6

	XCALL FILES(7,'U',17,SWITCH)		;APOPEN FILE
	IF (SWITCH.EQ.9) RETURN
	CHN017 = 7

	SWITCH = 1
	XCALL FILES(2,'U',12,SWITCH)		;VENIDX FILE
	IF (SWITCH.EQ.9) RETURN
	CHN012 = 2

	CLOSE 2
	XCALL FILES (4,'I',14,5)
	XCALL FILES (6,'I',16,5)
	XCALL FILES (7,'I',17,5)
	LOKCTL = 1
	XCALL IO (4,DUMNAP,1,READ,LOKCTL)

	NAPREC = REC014
	NAPCNT =
	DSTCNT =
	TRXCNT =

	OPNOK = 1
	RETURN
;--------------------------------------------------------------

CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (CHN014) XCALL FILES (CHN014, 'U', 014, 4)
	IF (CHN015) XCALL FILES (CHN015, 'U', 015, 4)
	IF (CHN016) XCALL FILES (CHN016, 'U', 016, 4)
	IF (CHN017) XCALL FILES (CHN017, 'U', 017, 4)
	RETURN
;--------------------------------------------------------------

END
