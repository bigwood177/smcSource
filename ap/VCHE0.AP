SUBROUTINE VCHE0; (WORK1)
;
; VCHE0 / AP 
;
;
;		INITIALIZES FOR NEW PAYABLES VOUCHERING
;
;
	WORK1	,A
;

RECORD	D_STUFF
	D_IN	,D8		;DATE-IN, ANY FORMAT
	D_OUT	,D6		;RETURN VALUE MMDDYY
	D_OUTR	,D8		;RETURN VALUE CCYYMMDD
	D_FMT	,A10		;RETURN VALUE MM/DD/CCYY
	D_SW	,A2		;"99" = DATE CONVERSION ERROR



RECORD DUMNAP	
		.INCLUDE 'DEF:RD014B.DEF'
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; This overlay info in RD014B.DEF
;;;RECORD,X
;;;		,A231
;;;		,A6	;DAF FILLER
;;;	NEWDEF	,A24
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

RECORD VENMAS	
		.INCLUDE 'DEF:RD011A.DEF'
RECORD DUMVEN,X	
		.INCLUDE 'DEF:RD011B.DEF'

;;;RECORD VENIDX	
;;;		.INCLUDE 'DEF:RD012A.DEF'

RECORD EXPACT	
		.INCLUDE 'DEF:RD013A.DEF'
RECORD DUMEXP,X	
		.INCLUDE 'DEF:RD013B.DEF'
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD WORK
	WND_ON	,D1
	ACTITL	,A6
	ACTNUM	,D7
	ALPHA	,A8
	APOREC	,D5
	APOEND	,D5
	BLANKS	,A23
	BSEND	,D5
	BSMID	,D5
	CNGCTL	,D1
	CNT	,D2
	DECMAL	,D18
	DUPFLG	,D1
	DUEDT2	,D8
	ENTRY	,A38
	EXPEND	,D5
	IDX	,D2
	INXCTL	,D1
	INVDT2	,D8
	KEY	,A12
	MASK	,A7,'XXXXXXX'
	MASK2	,A8,'XXXX-XXX'
	MSGCTL	,D1
	NAPREC	,D5
	NEWCNT	,D3,000
	NUM	,D10
	OPTION	,D1
	SAVVCH	,D6
	SELECT	,D1
	SRCCTL	,D1
	SWITCH	,D1,1
	SYSTEM	,D1
	TCHAR	,D3
	TOTEXP	,D8
	V	,D1,1
	VENREC	,D5
	VENEND	,D5
	WHATNO	,D2

RECORD	VARS
	TDATE	,D6
	READ	,D1,	0
	WRITE	,D1,	1
	LOKCTL	,D1
PROC
	WORK = WORK1
DEFALT,
	CNGCTL =
	XCALL OUTPT (6,8,2,'NEW PAYABLES ENTRY DEFAULTS',V)
	XCALL OUTPT (8,13,0,'1. MULTIPLE A/P ACCTS ?',V)
	XCALL OUTPT (10,13,0,'2. DEFAULT A/P ACCT',V)
	XCALL OUTPT (12,13,0,'3. MULTIPLE CASH ACCTS ?',V)
	XCALL OUTPT (14,13,0,'4. DEFAULT CASH ACCT',V)
	XCALL OUTPT (16,13,0,'5. MULTIPLE DISCNT ACCTS ?',V)
	XCALL OUTPT (18,13,0,'6. DEFAULT DISCNT ACCT',V)
	XCALL OUTPT (20,13,0,'7. VOUCHERING DATE',V)
	XCALL OUTPT (22,13,0,'8. ACCOUNT WINDOW ON (Y/N) ?',V)
RDCTL,
	LOKCTL = 1
	XCALL IO (4,DUMNAP,1,READ,LOKCTL)
	UNLOCK 4
;;;	WND_ON = 2		;11-13-02 SSQ default to "N"
;;;	XCALL OUTPT (22,41,1,'N',V)
	IF (MLTAP .NE. ' ')  GOTO DSPDEF
GETDEF,
	LOKCTL = 1
	CLEAR VENMAS
	VENNO = '   #'
	XCALL ISIO (1, VENMAS, VENNO, READ, LOKCTL)

;;;	XCALL IO (1,DUMVEN,1,READ,LOKCTL)

	UNLOCK 1
;;;	NEWDEF = VENDEF
	DEFLTS = VENDEF		;1-22-99 SSQ
	ACWND = DFWND		;11/18/02 SSQ
	IF (MLTAP .EQ. ' ')  GOTO MULAP
DSPDEF,
	XCALL OUTPT (8,41,0,MLTAP,V)
	ROW = 10
	ACTNUM = DFLAP
	CALL SERCHD
	XCALL OUTPT (12,41,0,MLTCSH,V)
	ROW = 14
	ACTNUM = DFLCSH
	CALL SERCHD
	XCALL OUTPT (16,41,0,MLTDSC,V)
 	ROW = 18
	ACTNUM = DFLDSC
	CALL SERCHD
	IF (DVCHDT .EQ. 0)  GOTO DEFVDT
	CTL = '20,41'
	DECMAL(1,8) = DVCHDT
	CALL DSPDTE
	USING ACWND SELECT
	('Y'),	BEGIN
		WND_ON = 1	
		XCALL OUTPT (22,41,1,'Y',V)
		END
	('N'),	BEGIN
		WND_ON = 2		;11-13-02 SSQ default to "N"
		XCALL OUTPT (22,41,1,'N',V)
		END
	ENDUSING

	GO TO ANYCND
MULAP,
	CTL = '08,41,01,00,YN'
	CALL INPUT
	MLTAP = ENTRY (1,1)
	GO TO (ANYCND), CNGCTL
DEFAP,
	XCALL OUTPT (10,41,1,'\',V)
	CTL = '10,41,04,03,#E'
	CALL INPUT
	GO TO (DEFALT,BEGIN), INXCTL
	DFLAP (1,4) = ENTRY (1,4)
	IF (ENTRY(1,3) .EQ. '000') GOTO DEFAP
	XCALL OUTPT (10,45,1,'-',V)
	CTL = '10,46,03,00,# '
	CALL INPUT
	GO TO (DEFAP), INXCTL
	DFLAP (5,7) = ENTRY (1,3)
	ROW = 10
	ACTNUM = DFLAP
	CALL SERCHD
	GO TO (ANYCND), CNGCTL
MULCSH,
	CTL = '12,41,01,00,YN'
	CALL INPUT
	MLTCSH = ENTRY (1,1)
	GO TO (ANYCND), CNGCTL
DEFCSH,
	XCALL OUTPT (14,41,1,'\',V)
	CTL = '14,41,04,03,# '
	CALL INPUT
	GO TO (DEFALT), INXCTL
	DFLCSH (1,4) = ENTRY (1,4)
	IF (ENTRY (1,3) .EQ. '000') GO TO DEFCSH
	XCALL OUTPT (14,45,1,'-',V)
	CTL = '14,46,03,00,# '
	CALL INPUT
	GO TO (DEFCSH), INXCTL
	DFLCSH (5,7) = ENTRY (1,3)
	ROW = 14
	ACTNUM = DFLCSH
	CALL SERCHD
	GO TO (ANYCND), CNGCTL
MULDSC,
	CTL = '16,41,01,00,YN'
	CALL INPUT
	MLTDSC = ENTRY (1,1)
	GO TO (ANYCND), CNGCTL
DEFDSC,
	XCALL OUTPT (18,41,1,'\',V)
	CTL = '18,41,04,03,# '
	CALL INPUT
	GO TO (DEFALT), INXCTL
	DFLDSC (1,4) = ENTRY (1,4)
	IF (ENTRY(1,3) .EQ. '000') GO TO DEFDSC
	XCALL OUTPT (18,45,1,'-',V)
	CTL = '18,46,03,00,# '
	CALL INPUT
	GO TO (DEFDSC), INXCTL
	DFLDSC (5,7) = ENTRY (1,3)
	ROW = 18
	ACTNUM = DFLDSC
	CALL SERCHD
	GO TO (ANYCND), CNGCTL
DEFVDT,
	XCALL OUTPT (20,41,1,'\',V)
	CTL = '20,41,08,00,D '
	CALL INPUT
	GO TO (DEFALT), INXCTL
	DVCHDT = ENTRY(1,8)
	IF (DVCHDT .EQ. 0)  
		BEGIN
		XCALL RDATE (TDATE)
		XCALL DATE8(TDATE, D_OUT, DVCHDT, D_FMT, D_SW)
		END

	CTL = '20,41'
	DECMAL(1,8) = DVCHDT
	CALL DSPDTE
	GO TO (ANYCND), CNGCTL
WND_ON,
	CTL = '22,41,01,00,YN'
	CALL INPUT
	WND_ON = INXCTL
	ACWND = ENTRY(1,1)
ANYCND,
	XCALL ANYCN (CNGCTL,WHATNO)
	GOTO (SETDEF), CNGCTL+1
	GOTO (MULAP,DEFAP,MULCSH,DEFCSH,MULDSC,DEFDSC,DEFVDT), WHATNO
	GOTO (WND_ON),WHATNO-7
	GOTO ANYCND
SETDEF,
	LOKCTL = 1
	XCALL IO (4,DUMNAP,1,WRITE,LOKCTL)
	XCALL OUTPT (24,53,1,'UPDATE MASTER DEFAULTS ?',V)
	CTL = '24,79,01,00,YN'
	CALL INPUT
	GO TO (ENDDEF), INXCTL - 1
SETDF1,
	LOKCTL = 1
	CLEAR VENMAS
	VENNO = '   #'
	XCALL ISIO (1, VENMAS, VENNO, READ, LOKCTL)

;;;	XCALL IO (1,DUMVEN,1,READ,LOKCTL)
;;;	VENDEF = NEWDEF
	VENDEF = DEFLTS			;1-22-99 SSQ
	DFWND = ACWND			;11/18/02 SSQ
	LOKCTL = 1
	XCALL ISIO (1, VENMAS, VENNO, WRITE, LOKCTL)

;;;	XCALL IO (1,DUMVEN,1,WRITE,LOKCTL)
ENDDEF,
	XCALL OUTPT (24,1,1,'\',V)
	WHATNO = 0
RETURN,
	INXCTL = 0
	WORK1 = WORK
	RETURN
BEGIN,
	WHATNO = 4
	GOTO RETURN
SERCHD,
	IF (ACTNUM .EQ. 0)  RETURN
	ALPHA = ACTNUM, MASK2
	XCALL OUTPT (ROW,41,0,ALPHA,V)
	KEY(1,7) = ACTNUM, MASK
	BSEND = EXPEND

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL ACCT(KEY(1,7), ACDESC, SRCCTL)

;;;	XCALL SERCH (3,EXPACT,KEY(1,7),1,7,BSEND,BSMID,SRCCTL,4,32,37,0,0,0,0)
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (SRCCTL)  ACDESC = '** ACCOUNT NOT ON FILE **'
	XCALL OUTPT (ROW,51,0,ACDESC,V)
	RETURN
INPUT,
	XCALL INPUT(ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,V)
	RETURN
DSPDTE,
	XCALL DATE8(DECMAL(1,8) , D_OUT, D_OUTR, D_FMT, D_SW)
	XCALL OUTPT (ROW, COL, 0, D_FMT, V)
	RETURN
;;;	OPTION = 2
;;;	XCALL DSPLY(MAX,ROW,COL,DECMAL,OPTION,V)
	RETURN
END
