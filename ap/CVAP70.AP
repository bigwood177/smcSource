;  CVAP70 / AP - D11
;
;		::CRELAP.DEF::
;**********************************************************************
;		ACCOUNTS PAYABLE - RELEASE 6.1
;		RELEASED: 1-MAY-82
;**********************************************************************
;:
;
;		::PCPYEMS.DEF::
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of Mini-Computer Business Applications, Inc. (MCBA) of
;		Glendale, California, USA and Effective Management Systems, Inc.
;		of Menomonee Falls, Wisconsin, USA embodying substantial
;		creative effort and confidential information, ideas and
;		expressions, no part of which may be reproduced or transmitted
;		in any form or by any means, electronic, mechanical or
;		otherwise, including photocopying and recording or in
;		connection with any information storage or retrieval system
;		without the permission in writing from MCBA.
;
;		COPYRIGHT NOTICES:  Copyright <C> 1980, 1981, 1982
;		by Mini-Computer Business Applications, Inc.
;		AN UNPUBLISHED WORK.  Copyright <C> 1980, 1981, 1982
;		by Effective Management Systems, Inc.  AN UNPUBLISHED WORK.
;
;
;		COMPILE & LINK PER DOCUMENTATION INSTALLATION NOTES.
;
;:
;
;		PROGRAM TO CONVERT RELEASE 1.0 AP FILES
;		INTO RELEASE 1.1 FILES.
;
RECORD NEWAP1		; ::CRD014G.DEF::
			;Source:	"RD014G.DEF"
			;AP Voucher Transaction Record Definition
			;Record Length = 894
			;Device File # = 014
			;
	WVCHNO	,D6	;Voucher Number
	WVCHDT	,D6	;Vouchering Date
	WAPACT	,D7	;Voucher A/P Account
	WCSACT 	,D7	;Voucher Cssh Account (if pre-paid)
	WDSACT	,D7	;Voucher Discount Account (if pre-paid)
	WVENNO	,A4	;Vendor Number
	WNAME	,A30	;Vendor Name
	WINVNO	,A8	;Invoice Number
	WDESC	,A25	;Voucher Reference
	WPCHNO	,A8	;Purchase Order Number
	WINVDT	,D6	;Invoice date (MM/DD/YY)
	WINVAM	,D8	;Invoice amount (XXX,XXX.XX-)
	WNDISC	,D7	;Invoice Non-Discount Amount (XX,XXX.XX-)
	WDPCT	,D3	;Invoice Discount Percent (XX.X)
	WDAMT	,D6	;Invoice Discount Amount (X,XXX.XX)
	WDISDT	,D6	;Discount Date (MM/DD/YY)
	WDUEDT	,D6	;Due Date (MM/DD/YY)
	WCHKNO	,D6	;Check Number
	WDACTS	,18D7	;Distribution Accounts (XXXX-XXX)
	WDAMTS	,18D8	;Distribution Amounts (XXX,XXX.XX-)
	WDJOBN	,9A6	;JC Distribution Job Numbers
	WDPRTN	,9A15	;JC-SPC Distribution Part Numbers
	WDCTYP	,9A2	;SPC Distribution Cost Types
	WDQTY	,9D6	;JC Distribution Quantities (XXX,XXX-)
	WCMCOD	,9A4	;SPC Distribution Commodity Codes
	WCUOM	,9A2	;JC Distribution Units of Measure
	WPONUM	,9D8	;Purchasing P/O Numbers (XXXXXX-XX)
	WPOLIN	,9D3	;Purchasing P/O Line Numbers
		,A54	;Filler
;:
RECORD AP1CTL,X		; ::CRD014C.DEF::
			;Source:	"RD014C.DEF"
			;AP Voucher Transaction Control Fields Definition
			;Record length 894
			;Device File # = 014
			;
		,A828	;Filler
	DVCHNO	,D6	;Next Voucher Number to Be Used
	MLTAP	,A1	;Multiple A/P Accounts ?
	DFLAP	,D7	;Default A/P Account
	MLTCSH	,A1	;Multiple Cash Accounts ?
	DFLCSH	,D7	;Default Cash Account
	MLTDSC	,A1	;Multiple Discount Accounts ?
	DFLDSC	,D7	;Default Discount Account
		,A6
	DVCHDT	,D6	;Default Vouchering Date
	REGFLG	,A1	;Register/Edit List Flag
		,A1
	INTRPO	,D1	;Purchasing Interface Switch (1=YES)
	INTRSP	,D1	;Standard Product Costing Interface Switch (1=YES)
	INTRJC	,D1	;Job Cost Interface Switch (1=YES)
	INTRGL	,D1	;General Ledger Interface Switch (1=YES)
	ORG014	,D5	;Organized Record Count
	REC014	,D5	;Active Record Count
	MAX014	,D5	;Maximum Record Count
	DEL014	,D3	;Deleted Record Count
;:


RECORD NEWAP0,X		; ::CRD014E.DEF::
			;"RD014E.DEF"
			;AP VOUCHER TRANSACTION RECORD DEFINITION
			;RECORD LENGTH = 291
			;DEVICE FILE # = 014
			;CONVERSION VERSION
		,A33	;UNUSED
	CVENNO	,A4	;VENDOR NUMBER
		,A236	;UNUSED
	CRG014	,D5	;ORGANIZED COUNT
	CRC014	,D5	;RECORD COUNT
	CMX014	,D5	;MAXIMUM COUNT
	CDL014	,D3	;DELETE COUNT
;:
RECORD APDIST,X		; ::CRD016A.DEF::
			;SOURCE:	"RD016A.DEF"
			;A/P DISTRIBUTION FILE
			;RECORD LENGTH = 35
			;
	GTYPE	,D1	;DISTRIBUTION TYPE
	GACCT	,D7	;G/L ACCOUNT NUMBER
	GDATE	,D6	;TRANSACTION DATE
		,A2	;
	GVENNO	,A4	;VENDOR NUMBER
	GVCHNO	,D6	;VOUCHER NUMBER
	GTRXAM	,D8	;TRANSACTION AMOUNT
	GFLAG	,D1	;INTERFACE FLAG (1 IF A/P INTERFACED WITH G/L)
;:
RECORD APDCTL,X		; ::CRD016B.DEF::
			;SOURCE:	"RD016B.DEF"
			;A/P DISTRIBUTION FILE CONTROL RECORD
			;RECORD LENGTH = 35 BYTES
			;
		,A16	;UNUSED
	INTFLG	,D1	;G/L INTERFACE FLAG
	ORG016	,D5	;ORGANIZED RECORD COUNT
	REC016	,D5	;ACTIVE RECORD COUNT
	MAX016	,D5	;MAXIMUM RECORD COUNT
	DEL016	,D3	;DELETED RECORD COUNT
;:
RECORD APOPEN,X		; ::CRD017A.DEF::
			;SOURCE:	"RD017A.DEF"
			;ACCOUNTS PAYABLE OPEN ITEMS FILE
			;RECORD LENGTH = 118 BYTES
			;DEVICE TABLE RECORD # 17
			;
	AVENNO	,A4
	AVCHNO	,D6
	AAPACT	,D7
	ACSACT	,D7
	ADSACT	,D7
	AINVNO	,A8
	ADESC	,A25
	AINVDT	,D6
	AINVAM	,D8
	ADSCAM	,D6
	ADISDT	,D6
	ADUEDT	,D6
	ACHKNO	,D6
	APAID	,D8
	ADSTKN	,D7
	AFLAG	,D1
;:
RECORD APOCTL,X		; ::CRD017B.DEF::
			;SOURCE:	"RD017B.DEF"
			;A/P OPEN ITEM FILE CONTROL RECORD
			;RECORD LENGTH: 118	DEVICE - #17
	REDVEN	,A4
		,A96
	ORG017	,D5
	REC017	,D5
	MAX017	,D5
	DEL017	,D3
;:
RECORD VENMAS,X		; ::CRD011A.DEF::
			; SOURCE:	"RD011A.DEF"
			; VENDOR MASTER FILE
			; RECORD LENGTH = 283 BYTES
			; KEY LENGTH = 4 BYTES
			; DEVICE TABLE # 11
			; SORT KEY: VENDOR NUMBER
			;
	VENNO	,A4	; VENDOR NUMBER
	VTYPE	,D1	; VENDOR TYPE (0=PERMANENT, 1=TEMPORARY)
	NAME	,A30	; VENDOR NAME   (']]]DEL' IN FIRST SIX CHARS)
	ADD1	,A25	; VENDOR ADDRESS LINE 1
	ADD2	,A25	; VENDOR ADDRESS LINE 2
	CITY	,A15	; CITY
	STATE	,A2	; STATE
		,A8	; (FILLER)
	ZIP	,A10	; ZIP CODE                   XXXXX-XXXX
	TELNO	,A12	; TELEPHONE NUMBER           XXX XXX-XXXX
	VIDNO	,D9	; FED ID OR SOCIAL SECURITY NO.
			;	F:   XX-XXXXXXX
			;	S:   XXX-XX-XXXX
	VIDTYP	,A1	; TYPE OF ID NO. (F=FEDERAL,S=SOC.SEC.)
	TP1099	,A1	; TYPE OF 1099
	CUTDAY	,D2	; PAYMENT CUTOFF DAY
	DUEDAY	,D2	; NUMBER OF DUE DAYS
	DISDAY	,D2	; NUMBER OF DISCOUNT DAYS
	DISPCT	,D3	; DISCOUNT PERCENT
	BILMTD	,D8	; TOTAL DOLLARS BILLED MTD   XXX,XXX.XX-
	NOVMTD	,D3	; NUMBER OF VOUCHERS MTD
	BILYTD	,D9	; TOTAL DOLLARS BILLED YTD   X,XXX,XXX.XX-
	NOVYTD	,D4	; TOTAL NUMBER OF VOUCHERS YTD
	PAYMTD	,D8	; PAYMENTS MADE MTD   XXX,XXX.XX-
	PAYYTD	,D9	; PAYMENTS MADE YTD   X,XXX,XXX.XX-
	POSYTD	,D3	; TOTAL NUMBER OF PO'S YTD
	LNSYTD	,D4	; TOTAL NUMBER OF LINE ITEMS YTD
	LSLYTD	,D4	; TOTAL NUMBER OF LINES LATE YTD
	LSLPYR	,D3	; PERCENT LINES LATE PRIOR YEAR
	AVCNTR	,D2	; EVENT COUNTER
	AVPVAR	,D3	; AVERAGE PERCENT COST VARIANCE
	AVPREJ	,D3	; AVERAGE PERCENT ITEMS REJECTED
	AVLDTM	,D3	; AVERAGE LEAD TIME IN DAYS
	AVDSLA	,D3	; AVERAGE NUMBER OF DAYS LATE
	CONTCT	,A20	; CONTACT NAME
	FOB	,A1	; FOB CODE
	SHPVIA	,A1	; SHIP-VIA CODE
	TERMS	,A1	; TERMS CODE
	USERCD	,A2	; USER CODE
	PRTPRC	,A1	; PRINT PRICE CODE
	ACKNWL	,A1	; ACKNOWLEDGEMENT CODE
	CONFRM	,A1	; CONFIRMATION CODE
	ACTIVE	,A1	; ACTIVE/INACTIVE CODE     = NO PURCHASING DATA IN RECORD
						;1 = ACTIVE
						;2 = INACTIVE
	COMMCD	,5A4	; COMMODITY CODES
		,A13	; (UNUSED)
;:
RECORD VENCTL,X		; ::CRD011B.DEF::
			; SOURCE:	"RD011B.DEF"
			; VENDOR MASTER FILE CONTROL RECORD
			; RECORD LENGTH = 283 BYTES
			; DEVICE TABLE # 11
			;
		,A219	; (UNUSED)
	DFSVIA	,A1	; DEFAULT FOR THE SHIP-VIA CODE
	DFTRMS	,A1	; DEFAULT FOR THE TERMS CODE
	DFTFOB	,A1	; DEFAULT FOR THE FOB CODE
	DFUSRC	,A2	; DEFAULT FOR THE USER CODE
	VENDEF	,A24	; ACCOUNT NO DEFAULTS FOR VOUCHER ENTRY
	ORG012	,D5	; ORG COUNT FOR IDX FILE
	REC012	,D5	; REC COUNT FOR IDX FILE
	MAX012	,D5	; MAX COUNT FOR IDX FILE
	DFL011	,D1	; DELETE FLAG
	SFL011	,D1	; SORT FLAG
	ORG011	,D5	; ORGANIZED RECORD COUNT
	REC011	,D5	; ACTIVE RECORD COUNT
	MAX011	,D5	; MAXIMUM RECORD COUNT
	DEL011	,D3	; DELETED RECORD COUNT
;:
RECORD VENIDX,X		; ::CRD012A.DEF::
			; SOURCE:	"RD012A.DEF"
			; VENDOR INDEX FILE
			; RECORD LENGTH = 10
			; DEVICE TABLE # 12
			;
	IVENNO	,A4	; VENDOR NUMBER
		,A1	; (FILLER)
	IRC011	,D5	; RECORD NUMBER OF VENDOR RECORD
;:
RECORD DEVICE		; ::CDEVICB.DEF::
		;DEVICE RECORD - DEVICB.DEF
	FILNAM	,A6
	DEVNAM	,8A3		;DEVICE NUMBER IN ORDER BY COMPANY LOC
	STATUS	,8D2
	;:
RECORD COMPNY
	CMPCOD	,8A3
RECORD FILBL
	CODEV	,A3
		,A1,	':'
	FILENM	,A6
		,A1,	'.'
	COEXT	,A3
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD
	BLANKS	,A3
	CNGCTL	,D1
	CNT	,D5
	CMPCNT	,D5
	ENTRY	,A5
	INXCTL	,D1
	LOKCTL	,D1
	READ	,D1,	0
	RECCNT	,D5
	SIZE	,D3
	V	,D1
	WHATNO	,D1
	WRITE	,D1,	1
PROC (2)
	V = -1
	XCALL TERID (V)
	V = 1
	ON ERROR NOCMP
	OPEN (1,I,'UT:COMPNY.DDF')
	OFF ERROR
	ON ERROR NODEV
	OPEN (2,I,'UT:DEVICE.DDF')
	OFF ERROR
	XCALL OUTPT (1,1,2,'CONVERT ACCOUNTS PAYABLE FILES',1)
BEGIN,
	CNGCTL =
	XCALL OUTPT (2,1,2,'\',1)
	XCALL OUTPT (12,20,0,'PLEASE ENTER COMPANY EXTENSION',1)
COEXT,
	CTL = '12,53,03,03,AE'
	CALL INPUT
	GO TO (BEGIN,END), INXCTL
	COEXT = ENTRY (1,3)
	CALL CHKEXT
	CMPCNT = CNT
	IF (COEXT.EQ.'   ') GO TO COEXT
	CALL CHKUSR
	IF (STATUS(CMPCNT).NE.0) GO TO COEXT
ANYCNG,
	CNGCTL = 2
	XCALL ANYCN (CNGCTL,WHATNO)
	GO TO (PROCES,COEXT), CNGCTL + 1
CHKEXT,
	LOKCTL = 1
	XCALL IOS (1,COMPNY,READ,LOKCTL)
	CNT = 0
LOOP,
	INCR CNT
	IF (CNT.GT.8) GO TO NOEXT
	IF (CMPCOD(CNT).EQ.COEXT) RETURN
	GO TO LOOP
NOEXT,
	XCALL MESAG ('INVALID COMPANY EXTENSION',1)
	COEXT =
	RETURN
CHKUSR,
	RECCNT = 14
	CALL RDDEV
	IF (STATUS(CMPCNT).NE.0) GO TO INUSE
	RECCNT = 16
	CALL RDDEV
	IF (STATUS(CMPCNT).NE.0) GO TO INUSE
	RECCNT = 17
	CALL RDDEV
	IF (STATUS(CMPCNT).NE.0) GO TO INUSE
	RECCNT = 11
	CALL RDDEV
	IF (STATUS(CMPCNT).NE.0) GO TO INUSE
	RECCNT = 12
	CALL RDDEV
	IF (STATUS(CMPCNT).NE.0) GO TO INUSE
	RETURN
RDDEV,
	LOKCTL = 1
	XCALL IO (2,DEVICE,RECCNT,READ,LOKCTL)
	RETURN
INUSE,
	XCALL MESAG
&	('CANNOT PROCESS THIS COMPANY - ONE OR MORE FILES ARE IN USE',2)
	RETURN
PROCES,
	XCALL WATE (4,1)
	CLOSE 1
	LOKCTL = 1
	XCALL IO (2,DEVICE,14,READ,LOKCTL)
	IF (DEVNAM(CMPCNT).EQ.BLANKS) GO TO BLKDEV
	FILENM = 'NEWAP0'
	CODEV = DEVNAM (CMPCNT)
	ON ERROR NOAP0
	OPEN (3,U,FILBL)
	OFF ERROR
	LOKCTL = 1
	XCALL IO (3,AP1CTL,1,READ,LOKCTL)
	XCALL RSTAT (AP1CTL,SIZE)
	XCALL OUTPT (24,1,1,'PROCESSING NEWAP0 FILE - VENDOR # ',1)
	IF (SIZE.EQ.291) GO TO SMLAP0
	CNT = 1
	RECCNT = REC014
AP1LUP,
	INCR CNT
	IF (CNT.GT.RECCNT) GO TO EOFAP1
	LOKCTL = 1
	XCALL IO (3,NEWAP1,CNT,READ,LOKCTL)
	XCALL FRMAT (WVENNO,4)
	XCALL OUTPT (24,35,1,WVENNO,1)
	LOKCTL = 1
	XCALL IO (3,NEWAP1,CNT,WRITE,LOKCTL)
	GO TO AP1LUP
SMLAP0,
	NEWAP1 =
	LOKCTL = 1
	XCALL IO (3,NEWAP0,1,READ,LOKCTL)
	RECCNT = CRC014
	CNT = 1
SMLLUP,
	INCR CNT
	IF (CNT.GT.RECCNT) GO TO EOFAP1
	LOKCTL = 1
	XCALL IO (3,NEWAP0,CNT,READ,LOKCTL)
	XCALL FRMAT (CVENNO,4)
	XCALL OUTPT (24,35,1,CVENNO,1)
	LOKCTL = 1
	XCALL IO (3,NEWAP0,CNT,WRITE,LOKCTL)
	GO TO SMLLUP
EOFAP1,
	CLOSE 3
	NEWAP1 =
	LOKCTL = 1
	XCALL IO (2,DEVICE,16,READ,LOKCTL)
	IF (DEVNAM(CMPCNT).EQ.BLANKS) GO TO BLKDEV
	XCALL OUTPT (24,1,1,'PROCESSING APDIST FILE - VENDOR # ',1)
	CODEV = DEVNAM (CMPCNT)
	FILENM = 'APDIST'
	ON ERROR NOAPD
	OPEN (3,U,FILBL)
	OFF ERROR
	LOKCTL = 1
	XCALL IO (3,APDCTL,1,READ,LOKCTL)
	RECCNT = REC016
	ORG016 = 1
	LOKCTL = 1
	XCALL IO (3,APDCTL,1,WRITE,LOKCTL)
	CNT = 1
APDLUP,
	INCR CNT
	IF (CNT.GT.RECCNT) GO TO EOFAPD
	LOKCTL = 1
	XCALL IO (3,APDIST,CNT,READ,LOKCTL)
	XCALL FRMAT (GVENNO,4)
	XCALL OUTPT (24,35,1,GVENNO,1)
	LOKCTL = 1
	XCALL IO (3,APDIST,CNT,WRITE,LOKCTL)
	GO TO APDLUP
EOFAPD,
	CLOSE 3
	NEWAP1 =
	LOKCTL = 1
	XCALL IO (2,DEVICE,17,READ,LOKCTL)
	IF (DEVNAM(CMPCNT).EQ.BLANKS) GO TO BLKDEV
	XCALL OUTPT (24,1,1,'PROCESSING APOPEN FILE - VENDOR # ',1)
	CODEV = DEVNAM (CMPCNT)
	FILENM = 'APOPEN'
	ON ERROR NOAPO
	OPEN (3,U,FILBL)
	OFF ERROR
	LOKCTL = 1
	XCALL IO (3,APOCTL,1,READ,LOKCTL)
	RECCNT = REC017
	CNT = 1
APOLUP,
	INCR CNT
	IF (CNT.GT.RECCNT) GO TO EOFAPO
	LOKCTL = 1
	XCALL IO (3,APOPEN,CNT,READ,LOKCTL)
	XCALL FRMAT (AVENNO,4)
	XCALL OUTPT (24,35,1,AVENNO,1)
	LOKCTL = 1
	XCALL IO (3,APOPEN,CNT,WRITE,LOKCTL)
	GO TO APOLUP
EOFAPO,
	CLOSE 3
	NEWAP1 =
	LOKCTL = 1
	XCALL IO (2,DEVICE,11,READ,LOKCTL)
	IF (DEVNAM(CMPCNT).EQ.BLANKS) GO TO BLKDEV
	XCALL OUTPT (24,1,1,'PROCESSING VENMAS FILE - VENDOR # ',1)
	CODEV = DEVNAM (CMPCNT)
	FILENM = 'VENMAS'
	ON ERROR NOVEN
	OPEN (3,U,FILBL)
	OFF ERROR
	LOKCTL = 1
	XCALL IO (3,VENCTL,1,READ,LOKCTL)
	RECCNT = REC011
	ORG011 = 1
	VENDEF =
	LOKCTL = 1
	XCALL IO (3,VENCTL,1,WRITE,LOKCTL)
	CNT = 1
VENLUP,
	INCR CNT
	IF (CNT.GT.RECCNT) GO TO EOFVEN
	LOKCTL = 1
	XCALL IO (3,VENMAS,CNT,READ,LOKCTL)
	XCALL FRMAT (VENNO,4)
	XCALL OUTPT (24,35,1,VENNO,1)
	LOKCTL = 1
	XCALL IO (3,VENMAS,CNT,WRITE,LOKCTL)
	GO TO VENLUP
EOFVEN,
	CLOSE 3
	NEWAP1 =
	LOKCTL = 1
	XCALL IO (2,DEVICE,12,READ,LOKCTL)
	IF (DEVNAM(CMPCNT).EQ.BLANKS) GO TO BLKDEV
	XCALL OUTPT (24,1,1,'PROCESSING VENIDX FILE - VENDOR # ',1)
	CODEV = DEVNAM (CMPCNT)
	FILENM = 'VENIDX'
	ON ERROR NOIDX
	OPEN (3,U,FILBL)
	OFF ERROR
	LOKCTL = 1
	XCALL IO (3,VENIDX,1,READ,LOKCTL)
	CNT = 1
IDXLUP,
	INCR CNT
	IF (CNT.GT.RECCNT) GO TO EOFIDX
	LOKCTL = 1
	XCALL IO (3,VENIDX,CNT,READ,LOKCTL)
	XCALL FRMAT (IVENNO,4)
	XCALL OUTPT (24,35,1,IVENNO,1)
	LOKCTL = 1
	XCALL IO (3,VENIDX,CNT,WRITE,LOKCTL)
	GO TO IDXLUP
EOFIDX,
	CLOSE 3
	XCALL MESAG ('CONVERSION OF ACCOUNTS PAYABLE FILES SUCCESSFUL',2)
	CLOSE 2
END,
	XCALL PGMND
INPUT,
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,1)
	RETURN
NOCMP,
	XCALL MESAG ('FATAL ERROR - COMPNY.DDF FILE NOT FOUND ON DEVICE UT',2)
	STOP
NODEV,
	XCALL MESAG ('FATAL ERROR - DEVICE.DDF FILE NOT FOUND ON DEVICE UT',2)
	CLOSE 1
	STOP
NOAP0,
	XCALL MESAG ('FATAL ERROR - NEWAP0 FILE NOT FOUND',2)
	CLOSE 2
	STOP
NOAPD,
	XCALL MESAG ('FATAL ERROR - APDIST FILE NOT FOUND',2)
	CLOSE 2
	STOP
NOAPO,
	XCALL MESAG ('FATAL ERROR - APOPEN FILE NOT FOUND',2)
	CLOSE 2
	STOP
NOVEN,
	XCALL MESAG ('FATAL ERROR - VENMAS FILE NOT FOUND',2)
	CLOSE 2
	STOP
NOIDX,
	XCALL MESAG ('FATAL ERROR - VENIDX FILE NOT FOUND',2)
	CLOSE 2
	STOP
BLKDEV,
	XCALL MESAG
&	('FATAL ERROR - NO DEVICE FOUND IN DEVICE.DDF FILE',2)
	CLOSE 2
	STOP
END
