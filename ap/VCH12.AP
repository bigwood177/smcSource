SUBROUTINE VCH12; (NEWAP1,WORK1,WORKB)
;
; VCH12 / AP 
;
;
;			::PCPYAP.DEF::
;******************************************************************************
;		ACCOUNTS PAYABLE
;
;		RELEASED: AUGUST 1, 1984 (d70s10)
;		MODIFIED: 22-JUL-85 DPT 1018
;******************************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;	PROCESSES SECOND SCREEN OF PAYABLES VOUCHERING
;
	NEWAP1	,A
	WORK1	,A
	WORKB	,A
;
RECORD NEWAP	
		.INCLUDE 'DEF:RD014G.DEF'
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD WORKA
	CSSEND	,D5
	ITMEND	,D5
	LOKCTL	,D1
	NAPEND	,D5
	NUMBER	,D2
	PRECSN	,D1
	SCREEN	,D1
	TBLEND	,D5
RECORD WORK
	ACTITL	,A6
	ACTNUM	,D7
	ALPHA	,A8
	APOREC	,D5
	APOEND	,D5
	BLANKS	,A19
	BSEND	,D5
	BSMID	,D5
	CNGCTL	,D1
	CNT	,D2
	DECMAL	,D18
	DUPFLG	,D1
	DUEDT2	,D6
	ENTRY	,A38
	EXPEND	,D5
	IDX	,D2
	INXCTL	,D1
	INVDT2	,D6
	KEY	,A12
	MASK	,A7,	'XXXXXXX'
	MASK2	,A8,	'XXXX-XXX'
	MSGCTL	,D1
	NAPREC	,D5
	NEWCNT	,D3,	000
	N	,D10
	OPTION	,D1
	SAVVCH	,D6
	SELECT	,D1
	SRCCTL	,D1
	SWITCH	,D1,	1
	SYSTEM	,D1
	TCHAR	,D3
	TOTEXP	,D8
	V	,D1,	1
	VENREC	,D5
	VENEND	,D5
	WHATNO	,D2
RECORD
	READ	,D1,	0
	WRITE	,D1,	1
PROC
	NEWAP = NEWAP1
	WORK = WORK1
	WORKA = WORKB
	IF (WHATNO.EQ.34) GO TO FNDVCH		; 34 = FIND, IN CHANGE MODE OR
						;      DELETE MODE
	GO TO (SCRN1,SCRN2), SCREEN
NOTFND,
	XCALL MESAG('VOUCHER NOT FOUND',1)
DISPLA,
	WHATNO = 35				; 35 = NOT FOUND OR NOT RIGHT
						;  OR NOT OK TO DELETE
	GO TO RETURN
FNDVCH,
	DUPFLG = 0
	KEY(1,6) = WVCHNO,MASK
	BSMID = 1
	BSEND = NAPEND
	XCALL WATE (3,1)
	XCALL SERCH(4,NEWAP,KEY(1,6),1,6,BSEND,BSMID,SRCCTL,5,38,43,0,0,0,0)
	GO TO (NOTFND), SRCCTL
FNDVC2,
	CTL =
	NAPREC = BSMID
	LOKCTL = 0
	XCALL IO (4,NEWAP,NAPREC,READ,LOKCTL)
	GO TO (DISPLA), LOKCTL
SCRN1,
	XCALL OUTPT (4,25,0,WVCHNO,1)
	CTL = '06,25'
	DECMAL(1,6) = WVCHDT
	CALL DSPDTE
	XCALL OUTPT (8,25,0,WVENNO,1)
	XCALL OUTPT (8,30,0,WNAME,1)
	XCALL OUTPT (10,25,0,WINVNO,1)
	CTL = '12,25'
	DECMAL (1,6) = WINVDT
	CALL DSPDTE
	CTL = '14,25,08'
	DECMAL = WINVAM
	CALL DSPDLR
	IF (WNDISC.EQ.-1) XCALL OUTPT(14,13,0,'AMT PAID',1)
	IF (WNDISC.EQ.-1) XCALL OUTPT(12,42,0,'CHK',1)
	IF (WNDISC.EQ.-1) GO TO AMT
	IF (WNDISC.EQ.0) GO TO DISDAT
	CTL = '16,25,07'
	DECMAL = WNDISC
	CALL DSPDLR
DISDAT,
	IF (WDISDT.EQ.0) GO TO DISPCT
	CTL = '18,25'
	DECMAL (1,6) = WDISDT
	CALL DSPDTE
DISPCT,
	CTL = '20,25,03'
	DECMAL = WDPCT
	CALL DSPPCT
AMT,
	CTL = '22,25,06'
	DECMAL = WDAMT
	CALL DSPDLR
	IF (WNDISC.EQ.-1) GO TO APACT		; MANUAL PYT
	CTL = '12,53'
	DECMAL (1,6) = WDUEDT
	CALL DSPDTE
	CTL = '14,53,06'
	DECMAL = WCHKNO
	IF (DECMAL.NE.0) CALL DSPNUM
APACT,
	IF (WCHKNO.NE.0.AND.WNDISC.NE.-1) GO TO NOAP
	ALPHA = WAPACT, MASK2
	XCALL OUTPT (16,53,0,ALPHA,1)
NOAP,
	IF (WCHKNO.EQ.0.AND.WNDISC.NE.-1) GO TO NOCASH
	ALPHA = WCSACT, MASK2
	XCALL OUTPT (18,53,0,ALPHA,1)
	ALPHA = WDSACT, MASK2
	XCALL OUTPT (20,53,0,ALPHA,1)
NOCASH,
	XCALL OUTPT (22,53,0,WDESC,1)
	IF (WHATNO.NE.34) GO TO ANYCNG
	XCALL OUTPT (24,1,1,'RIGHT VOUCHER ?',1)
	CTL = '24,18,01,00,YN'
	CALL INPUT
	GO TO (DISPLA), INXCTL - 1
	IF (SELECT.NE.3) GO TO ANYCNG
	XCALL OUTPT(24,1,1,'OK TO DELETE ?',1)
	CTL = '24,17,01,01,YN'
	CALL INPUT
	GO TO (ANYCNG,DISPLA), INXCTL
SCRN2,
	N =
	TOTEXP =
DSPLIN,
	INCR N
	IF (N.GT.9) GOTO ANYCNG
	ROW = (N * 2) + 4
	IF (WDACTS(N*2-1).EQ.0) GOTO DSPLIN
	ALPHA = WDACTS(N*2-1),MASK2
	XCALL OUTPT(ROW,5,0,ALPHA,1)
	CTL(4,8) = '14,08'
	DECMAL = WDAMTS(N*2-1)
	TOTEXP = TOTEXP + DECMAL
	CALL DSPDLR
	XCALL OUTPT(ROW,26,0,WDPRTN(N),1)
	CTL(4,8) = '42,06'
	DECMAL = WDQTY(N)
	CALL DSPNUM
	XCALL OUTPT(ROW,51,0,WCUOM(N),1)
	XCALL OUTPT(ROW,55,0,WDJOBN(N),1)
	ENTRY(1,9) = WPONUM(N),'ZZZZZX-XX'
	IF (WPONUM(N).EQ.0) ENTRY =
	XCALL OUTPT(ROW,63,0,ENTRY(1,9),1)
	ALPHA(1,3) = WPOLIN(N),'ZZZ'
	XCALL OUTPT(ROW,74,0,ALPHA(1,3),1)
	IF (WDACTS(N*2).EQ.0) GO TO DSPLIN
	INCR ROW
	ALPHA = WDACTS(N*2),MASK2
	XCALL OUTPT(ROW,5,0,ALPHA,1)
	CTL(4,8) = '14,08'
	DECMAL = WDAMTS(N*2)
	TOTEXP = TOTEXP + DECMAL
	CALL DSPDLR
	XCALL OUTPT(ROW,29,0,WDCTYP(N),1)
	GOTO DSPLIN
ANYCNG,
	IF (WHATNO.EQ.34.AND.WNDISC.EQ.-1) WHATNO = 23	; 23 = TRYING TO CHANGE
							;  A MANUAL PAYMENT
	IF (WHATNO.EQ.21.AND.WNDISC.EQ.-1) WHATNO = 23
	IF (WHATNO.NE.23) WHATNO = 0
RETURN,
	WORK1 = WORK
	NEWAP1 = NEWAP
	RETURN
INPUT,
	XCALL INPUT(ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,1)
	RETURN
DSPNUM,
	OPTION = 1
	GO TO CALDSP
DSPDTE,
	OPTION = 2
	GO TO CALDSP
DSPDLR,
	OPTION = 3
	GO TO CALDSP
DSPPCT,
	OPTION = 5
CALDSP,
	XCALL DSPLY(MAX,ROW,COL,DECMAL,OPTION,1)
	RETURN
END
