; VCHREG / AP 
;
;

RECORD	D_STUFF
	D_IN	,D8		;DATE-IN, ANY FORMAT
	D_OUT	,D6		;RETURN VALUE MMDDYY
	D_OUTR	,D8		;RETURN VALUE CCYYMMDD
	D_FMT	,A10		;RETURN VALUE MM/DD/CCYY
	D_SW	,A2		;"99" = DATE CONVERSION ERROR

RECORD	COMP14
	ESC_11	,A1		;<ESC>
		,a4,	"&l1O"	;landscape mode
	ESC_12	,A1		;<ESC>
		,A4,	"&l8D"	;vertical spacing, 8 lines/inch
	ESC_13	,A1		;<ESC>
	C14V	,A8,	"(s13.00H"	;pitch 14 CPI


RECORD	SRTREC
		.INCLUDE 'DEF:RD015A.DEF'

RECORD NEWAP	
		.INCLUDE 'DEF:RD014A.DEF'
RECORD,X	
		.INCLUDE 'DEF:RD014B.DEF'
RECORD NEWAPD	
		.INCLUDE 'DEF:RD015B.DEF'
RECORD	,X
		,A5
	PRGNAM	,A6
		,A24
RECORD TITLE
		,A30,'NEW PAYABLES VOUCHER REGISTER '
RECORD HDR1
		,A9,'VOUCHER  '
		,A31,'----------- VENDOR ------------'
		,A20,'   INV/PO   INV/VCH '
		,A33,'  INV AMOUNT  DISCOUNT   DISC/DUE'
		,A38,'  A/P OR    CHECK  --- DISTRIBUTION --'
RECORD HDR2
		,A19,'NUMBER    NO   NAME'
		,A23
		,A30,'NUMBERS    DATES    (NON-DISC)'
		,A31,'   PCT/AMT     DATES  CASH/DISC'
		,A28,' NUMBER  EXP-ACCT     AMOUNT'
RECORD LEGND
		,A55,'"A/P OR CASH/DISC" ARE ACCTS CREDITED FOR THE VOUCHER. '
		,A36,'REGULAR VOUCHERS SHOW THE A/P ACCT, '
		,A41,'PREPAIDS & MANUALS SHOW CASH & DISC ACCT.'

;;;RECORD SNDMSG
;;;		,A9,'AP:APDSUM'
;;;	RCNT	,D5
;;;	OCNT	,D5

RECORD	FFILE
	FILNUM	,D3
	CLCTL	,D1
	REDFIL	,A14

 
RECORD	HEADER
		,A7,	'DBSORT '
	HFILE	,A14
 
 

RECORD	VARS
	OPNOK	,D1
	CHN014	,D2

	pdffil	,a*,	'c:\smc\spool\vchreg.spl'

	XFIL	,D3,	015
	ACTMSK	,A8,'ZXXX-XXX'
	ADJENT	,D3
	AFLD	,A9
	BLANKS	,A8
	CSHDIS	,D10
	DELENT	,D4
	DTMASK	,A8,'ZX/XX/XX'
	EXPFLG	,D1
	I	,D2
	LINCNT	,D2,60
	LNFEED	,D1
	LPSW	,D1
	MANDIS	,D10
	MANENT	,D3
	MANTOT	,D10
	MASK	,A14,'ZZ,ZZZ,ZZX.XX-'
	MASK1	,A5,'Z,ZZX'
	MSGCTL	,D1
	N	,D2
	PGCNT	,D3
	PLINE	,A132
	PRPENT	,D4
	PRPTOT	,D10
	PRDTOT	,D10
	PRTTYP	,A1
	REGENT	,D4
	REGTOT	,D10
	REGDIS	,D10
	RGDTOT	,D10
	RGFLG	,A1
	RPTNUM	,D3
	SPLFIL	,A14
	SWITCH	,D1,1
	SYSTEM	,D1
	LOKCTL	,D1
	READ	,D1,	0
	WRITE	,D1,	1
	V	,D1,1
	VOIDNT	,D3
	WTARG	,D1,4
PROC
	XCALL TERID (V)
	V = 1
	XCALL ENVRN(SYSTEM)

	CALL OPENS


	USING RGFLG SELECT
	('R'),	BEGIN
		CALL REG
		END
	('E'),	BEGIN
		CALL EDIT
		IF (LPSW.NE.0)
		THEN GO TO BEGIN
		ELSE GO TO EXIT
		END
	ENDUSING

	XCALL OUTPT(2,1,2,TITLE(22,30),1)

BEGIN,
	LOKCTL = 1
	XCALL IOS (4,NEWAP,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOF
READ,
	LOKCTL = 1
	XCALL IOS (4,NEWAP,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOF
	IF (NEWAP.EQ.']]]]]]') GOTO EOF
BUFF,
	PLINE (1,6) = WVCHNO
	PLINE (10,13) = WVENNO
	XCALL LEFTJ (PLINE(10,13),4)
	PLINE (16,40) = WNAME
	PLINE (43,50) = WINVNO

	XCALL DATE8(WINVDT, D_OUT, D_OUTR, D_FMT, D_SW)
	PLINE (53,60) = D_OUT,DTMASK
;;;	PLINE (53,60) = WINVDT,DTMASK

	PLINE (63,73) = WINVAM,MASK
	IF (WDPCT.NE.0.AND.WNDISC.NE.-1) PLINE (77,82) = WDPCT,'ZX.X%'

	IF (WDISDT.NE.0) 
		BEGIN
		XCALL DATE8(WDISDT, D_OUT, D_OUTR, D_FMT, D_SW)
		PLINE (86,93) = D_OUT,DTMASK
		END
	IF (WCHKNO.EQ.0.AND.WINVAM.NE.0) PLINE (96,103) = WAPACT, ACTMSK
	IF (WCHKNO.GT.0.AND.WINVAM.NE.0) PLINE (96,103) = WCSACT, ACTMSK
	CALL VEREXP
	N = 1
	IF (EXPFLG.EQ.0) GO TO BUFF2
	PLINE (113,120) = WDACTS(N), ACTMSK
	PLINE (122,132) = WDAMTS(N),MASK
BUFF2,
	CALL PRINT
	PLINE (11,14) = 'REF:'
	PLINE (16,40) = WDESC
	IF (WCHKNO.GT.0.AND.WNDISC.NE.-1) PLINE (1,9) = '*PREPAID*'
	IF (WNDISC.EQ.-1) PLINE (1,9) = '*MANUAL*'
	IF (WINVAM.EQ.0.AND.EXPFLG.EQ.0) PLINE (1,9) = '**VOID**'
	IF (WINVAM.EQ.0.AND.EXPFLG.EQ.1) PLINE (1,9) = '*ADJ EXP*'
	IF (PLINE(1,8).EQ.BLANKS) PLINE (1,9) = '*REGULAR*'
	IF (WNAME.EQ.'000000') PLINE (1,9) = '*DELETED*'
	PLINE (43,50) = WPCHNO

	XCALL DATE8(WVCHDT, D_OUT, D_OUTR, D_FMT, D_SW)
	PLINE (53,60) = D_OUT,DTMASK
;;;	PLINE (53,60) = WVCHDT,DTMASK

	IF (WDAMT.NE.0) PLINE (75,83) = WDAMT, MASK
	IF (WDAMT.EQ.0.AND.WNDISC.NE.-1) CALL GETDSC

	XCALL DATE8(WDUEDT, D_OUT, D_OUTR, D_FMT, D_SW)
	PLINE (86,93) = D_OUT,DTMASK
;;;	PLINE (86,93) = WDUEDT,DTMASK

	IF (WNDISC.EQ.-1) GO TO BUFF3
	AFLD = WNDISC,'$$,$$X.XX'
	I =
DSCLP,
	INCR I
	IF (I.GT.9) GOTO EXTDCS
	IF (AFLD(I,I).NE.'$') GOTO DSCLP
EXTDCS,
	AFLD (I,I) = '('
	PLINE (64,72) = AFLD
	PLINE (73,73) = ')'
	IF (WNDISC.EQ.0) PLINE (64,73) =
BUFF3,
	IF (WCHKNO.GT.0.AND.WDAMT.NE.0) PLINE (96,103) = WDSACT, ACTMSK
	IF (WCHKNO.NE.0) PLINE (105,110) = WCHKNO,'ZZZXXX'
	IF (WNAME.EQ.'000000') INCR DELENT
	IF (WNAME.EQ.'000000') GO TO DISTRB
	IF (WNDISC.EQ.-1) GO TO MANUAL
	IF (WCHKNO.NE.0) GOTO PRPAID
REGULR,
	REGTOT = REGTOT + WINVAM - WDAMT
	REGDIS = REGDIS + WDAMT
	RGDTOT = RGDTOT + WDAMTS(N)
	IF (WINVAM.EQ.0) GO TO ADJVD
	INCR REGENT
	GOTO DISTRB
PRPAID,
	PRPTOT = PRPTOT + WINVAM - WDAMT
	CSHDIS = CSHDIS + WDAMT
	PRDTOT = PRDTOT + WDAMTS (N)
	IF (WINVAM.EQ.0) GO TO ADJVD
	INCR PRPENT
DISTRB,
	IF (EXPFLG.EQ.0) GO TO ENDPR
	INCR N
	IF (N.GT.9) GOTO ENDPR
	IF (WDACTS(N).EQ.0) GOTO DISTRB
	PLINE (113,120) = WDACTS(N), ACTMSK
	PLINE (122,132) = WDAMTS(N), MASK
	CALL PRINT
	IF (WNAME.EQ.'000000') GO TO DISTRB
	IF (WCHKNO.EQ.0) RGDTOT = RGDTOT + WDAMTS(N)
	IF (WCHKNO.NE.0) PRDTOT = PRDTOT + WDAMTS(N)
	GOTO DISTRB
ADJVD,
	IF (EXPFLG.EQ.0) INCR VOIDNT
	IF (EXPFLG.NE.0) INCR ADJENT
	GO TO DISTRB
MANUAL,
	MANTOT = MANTOT + WINVAM - WDAMT
	MANDIS = MANDIS + WDAMT
	INCR MANENT
	GO TO ENDPR
GETDSC,
	WDAMT = ((WINVAM - WNDISC) * WDPCT) #3
	IF (WDAMT.NE.0) PLINE (75,83) = WDAMT, MASK
	RETURN
VEREXP,
	EXPFLG =
	N =
VEREX2,
	INCR N
	IF (N.GT.9) RETURN
	IF (WDAMTS(N).EQ.0.OR.WDACTS(N).EQ.0) GO TO VEREX2
	EXPFLG = 1
	RETURN
ENDPR,
	IF (PLINE(86,93).NE.BLANKS) CALL PRINT
	LNFEED = 1
	CALL LINFD
	GOTO READ
EOF,
	CALL PRINT_TOTALS

	USING RGFLG SELECT
	('R'),	begin
		close 14
		xcall jnl (pdffil,'Voucher_Register.pdf')
		LPSW = 1
		pgcnt = 0
		lincnt = 60
		XCALL LPON (LPSW,SPLFIL)
		call print_reg_totals
		xcall lpoff (lpsw, splfil, pgcnt)
		end
	('E'),	begin
		XCALL LPOFF (LPSW,SPLFIL,PGCNT)
		goto end2
		end
	ENDUSING

	CALL CLOSE

;;;	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
;;;EOF2,
	IF (RGFLG.NE.'R') GO TO EXIT


	XCALL FILES (4,'U',14,5)
	LOKCTL = 1
	XCALL IO (4,NEWAP,1,READ,LOKCTL)
	REGNUM = PGCNT (1,3)
	LOKCTL = 1
	XCALL IO (4,NEWAP,1,WRITE,LOKCTL)
	CLOSE 4
	XCALL FILES (3,'U',15,5)
	LOKCTL = 1
	XCALL IO (3,NEWAPD,1,READ,LOKCTL)
	PRGNAM = 'PSTAPO'
	LOKCTL = 1
	XCALL IO (3,NEWAPD,1,WRITE,LOKCTL)

;--------------------------------------------------------------------
;;; 10-26-15 ssq: replace SRTAPD w/ internal sort...

	CLOSE 3

	FILNUM = XFIL			;HARD CODE IF NO MESSAGE
	XCALL FFILE (FILNUM,REDFIL,CLCTL)	;GET FILE SPEC
	HFILE = REDFIL
 
	XCALL OUTPT (2,1,2,HEADER,1)
 
	SORT (IN=REDFIL,
&		RECORD=SRTREC,
&		KEY = (NTYPE,NACCT,NDATE,NVENNO,NVCHNO)
&		)
 
	XCALL PGCHN ('AP:APDSUM',1)
 

;--------------------------------------------------------------------

PRINT,
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,'NO HDR',
&			LEGND,'NO LEGEND',' ',0,0,0,0,LPSW,RPTNUM,PRTTYP)
	RETURN

PRINT_TOTALS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	LNFEED = 2
	CALL LINFD
	HDR1 =
	HDR2 =
PRINT_REG_TOTALS,
	PLINE (24,40) = 'NET-CHANGE-TO-A/P'
	PLINE (44,61) = 'NET-CHANGE-TO-CASH'
	PLINE (65,82) = 'NET-DISCOUNT-TAKEN'
	PLINE (86,104) = 'DISTRIBUTION-TOTALS'
	CALL PRINT
	LNFEED = 1
	CALL LINFD
	PLINE (1,5) = REGENT,MASK1
	PLINE (7,21) = 'REGULAR ENTRIES'
	PLINE (24,37) = REGTOT + REGDIS, MASK
	PLINE (88,101) = RGDTOT,MASK
	CALL PRINT
	IF (PRPENT.EQ.0) GO TO MANTOT
	PLINE (1,5) = PRPENT, MASK1
	PLINE (7,21) = 'PREPAID ENTRIES'
	PLINE (45,58) = PRPTOT, MASK
	PLINE (66,79) = CSHDIS, MASK
	PLINE (88,101) = PRDTOT,MASK
	CALL PRINT
MANTOT,
	IF (MANENT.EQ.0) GO TO VOIDS
	PLINE (1,5) = MANENT, MASK1
	PLINE (7,21) = 'MANUAL PAYMENTS'
	PLINE (45,58) = MANTOT, MASK
	PLINE (66,79) = MANDIS, MASK
	PLINE (24,37) = -MANTOT - MANDIS, MASK
	CALL PRINT
VOIDS,
	IF (VOIDNT.EQ.0) GO TO ADJEXP
	PLINE (1,5) = VOIDNT, MASK1
	PLINE (7,17) = 'VOID CHECKS'
	CALL PRINT
ADJEXP,
	IF (ADJENT.EQ.0) GO TO DELETS
	PLINE (1,5) = ADJENT, MASK1
	PLINE (7,25) = 'EXPENSE ADJUSTMENTS'
	CALL PRINT
DELETS,
	IF (DELENT.EQ.0) GO TO TOTENT
	PLINE (1,5) = DELENT, MASK1
	PLINE (7,22) = 'DELETED VOUCHERS'
	CALL PRINT
TOTENT,
	LNFEED = 1
	CALL LINFD
	PLINE (1,5) = REGENT + PRPENT + MANENT + VOIDNT +
&			ADJENT + DELENT, MASK1
	PLINE (7,19) = 'TOTAL ENTRIES'
	CALL PRINT
	
	RETURN
;--------------------------------------------------------------------
EXITR,
	XCALL WATE(3,V)
	XCALL FILES(3,'O',15,4)
	XCALL FILES(6,'U',16,4)
	XCALL FILES(7,'U',17,4)
END2,
	XCALL FILES(4,'I',14,4)
EXIT,
	XCALL WATE(3,V)
	XCALL PGCHN('AP:VCHENT',1)
LINFD,
	XCALL LINFD (LNFEED)
	LINCNT = LINCNT + LNFEED
	RETURN

REG,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; REGISTER SETUP
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	open (14,o,pdffil)		;12-15-10

	xcall ascii (27, esc_11)
	set esc_12, esc_13 = esc_11
	display (14, comp14)

	RETURN
;-----------------------------------------------------

EDIT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; EDIT LIST SETUP
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	TITLE (22,30) = 'EDIT LIST'
	LPSW = 1
	XCALL OUTPT(2,1,2,TITLE(22,30),1)
	XCALL LPON (LPSW,SPLFIL)

	RETURN
;------------------------------------------------------
OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	
	CLEAR OPNOK
	XCALL FILES(4,'I',14,5)
	LOKCTL = 1
	XCALL IO (4,NEWAP,1,READ,LOKCTL)
	CLOSE 4

	RGFLG = REGFLG
	USING REGFLG SELECT
	('E'),	BEGIN
		XCALL FILES(4,'I',14,SWITCH)		;FILE # 14 -- NEWAP FILE
		IF (SWITCH.EQ.9) RETURN
		END
	('R'),	BEGIN
		XCALL FILES(4,'I',14,5)
		END
	ENDUSING

	CHN014 = 4
	OPNOK = 1
	RETURN
;-------------------------------------------------------

CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	IF (.NOT.CHN014) RETURN

	USING RGFLG SELECT
	('E'),	XCALL FILES (4,'I',14,4)
	('R'),	CLOSE CHN014
	ENDUSING

	RETURN
;-------------------------------------------------------



END
