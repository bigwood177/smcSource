; VENINQ / AP 
;
;
;		GIVES A FULL PRINTOUT OF EACH VENDOR'S ACCOUNT
;

RECORD	D_STUFF
	D_IN	,D8		;DATE-IN, ANY FORMAT
	D_OUT	,D6		;RETURN VALUE MMDDYY
	D_OUTR	,D8		;RETURN VALUE CCYYMMDD
	D_FMT	,A10		;RETURN VALUE MM/DD/CCYY
	D_SW	,A2		;"99" = DATE CONVERSION ERROR


RECORD APOPEN	
		.INCLUDE 'DEF:RD017A.DEF'
RECORD ,X	
		.INCLUDE 'DEF:RD017B.DEF'

;;;RECORD VENIDX	
;;;		.INCLUDE 'DEF:RD012A.DEF'

RECORD VENMAS	
		.INCLUDE 'DEF:RD011A.DEF'
RECORD	,X	
		.INCLUDE 'DEF:RD011B.DEF'
RECORD TITLE
		,A22,'VENDOR ACCOUNT INQUIRY'
RECORD LEGND1
		,A53,'FLAGS ARE: 0=REGULAR VOUCHER   1=DEFERRED   2=PREPAID'
		,A40,'   3=COMPUTER PAID   4=0 TOTAL VENDOR   '
		,A36,'5=CR TOTAL VENDOR   6=UNPAID PARTIAL'
RECORD LEGND2
		,A11
		,A54,'7=PAID PARTIAL-TEMPORARY   8=PAID PARTIAL-PERMANENT   '
		,A52,'9=DEFERRED PAID PARTIAL   -1=DEFERRED UNPAID PARTIAL'
		,A15,'(0 OR CR TOTAL)'
RECORD LEGND3
		,A11
		,A56,'-2=VOID CHECK   -3=MANUALLY PAID   -4=PARTIAL MANUAL PYT'
		,A24,'   -9=PAID AND PROCESSED'
RECORD HDR1
		,A15,'---------------'
		,A6,'VENDOR'
		,A15,'---------------'
		,A2
		,A7,'VOUCHER'
		,A2
		,A7,'INVOICE'
		,A6
		,A6,'AMOUNT'
		,A3
		,A8,'DUE/DISC'
		,A2
		,A7,'CHECK-#'
		,A3
		,A8,'A/P-ACCT'
		,A19
		,A9,'CASH-ACCT'
		,A2
		,A4,'FLAG'
RECORD HDR2
		,A10,' NO   NAME'
		,A30
		,A2,'NO'
		,A5
		,A7,'NO/DATE'
		,A4
		,A8,'DISCOUNT'
		,A5
		,A4,'DATE'
		,A4
		,A8,'CHK-DATE'
		,A2
		,A9,'REFERENCE'
		,A18
		,A9,'DISC-ACCT'
RECORD HDR3
		,A58
		,A8,'AMT-PAID'
		,A3
		,A8,'DISC-TKN'

RECORD	VARS
	XDATE	,D8
	STVEN	,A4
	ENVEN	,A4
	STINV	,A8
	ENINV	,A8
	STDAT	,D8
	ENDAT	,D8
	STREF	,A25
	ENREF	,A25
	ACTMSK	,A8,'ZXXX-XXX'
	BSEND	,D5
	BSMID	,D5
	CNGCTL	,D1
	DASHES	,A40,'----------------------------------------'
	DTMASK	,A8,'XX/XX/XX'
	ENTRY	,A30
	BLANKS	,A30
	ENDNO	,A10
	FSTFLG	,D1,1
	FSTVEN	,D1,1
	INXCTL	,D1
	KEY	,A4
	LINCNT	,D2,61
	LNFEED	,D1
	LPSW	,D1
	LSTVND	,A4
	MASK1	,A11,'ZZZ,ZZZ.XX-'
	MASK2	,A6,'ZZZZZZ'
	ORGVEN	,D5
	OSUB	,D1
	PGCNT	,D3
	PLINE	,A132
	PRTCNT	,D5
	PRTCTL	,D3
	PRTFIL	,A14
	PRTTYP	,A1
	RPTNUM	,D3
	SAVLPS	,D1
	SPLFIL	,A14
	SRCCTL	,D1
	STRTNO	,A10
	STXCTL	,D1
	SWITCH	,D1,1
	LOKCTL	,D1
	READ	,D1,	0
	WRITE	,D1,	1
	V	,D1,1
	VCHCNT	,D5
	WHATNO	,D1
	WTARG	,D1,2
PROC
	XCALL TERID (V)
	V = 1

;;;	XCALL FILES(2,'I',12,SWITCH)		;FILE # 12 -- VENIDX FILE
;;;	IF (SWITCH.EQ.9) GO TO EXIT

	XCALL FILES(7,'I',17,SWITCH)		;FILE # 17 -- APOPEN FILE
	IF (SWITCH.NE.9) GO TO OPEN1
	CALL CLOSE1
	GO TO EXIT
OPEN1,
	XCALL FILES(1,'SI',11,SWITCH)			;FILE # 11 -- VENMAS FILE
	IF (SWITCH.EQ.9) GO TO END
BEGIN,
	XCALL OUTPT(1,1,2,'VENDOR ACCOUNT INQUIRY',V)
	LINCNT = 61
	SPLFIL (5,6) = 'FG'
	LPSW = 1
	LSTVND =
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.NE.0) GO TO BEGIN2
	CALL CLOSE3
	GO TO EXIT
BEGIN2,
;;;	LOKCTL = 1
;;;	XCALL IO (1,VENMAS,1,READ,LOKCTL)
;;;	ORGVEN = ORG011

	WTARG = 2
	IF (LPSW.EQ.2) WTARG = 4
	FSTFLG =
	VCHCNT =
	PRTCNT =

DISPLA,
	XCALL OUTPT(1,1,2,'VENDOR ACCOUNT INQUIRY',V)
	XCALL OUTPT(06,4,1,'1. VENDOR #              THRU: ',1)
	XCALL OUTPT(08,4,1,'2. INVOIC #              THRU: ',1)
	XCALL OUTPT(10,4,1,'3. INV DAT               THRU: ',1)
	XCALL OUTPT(12,4,1,'4. REF                                  THRU:',1)
VEN,
	XCALL OUTPT(06,4,1,'1. VENDOR # ALL          THRU: ',1)
	XCALL INPUT(6,16,04,00,'AE',ENTRY,INXCTL,1)
	GOTO(DISPLA,END),INXCTL
	STVEN=ENTRY(1,4)
	IF(STVEN.EQ.BLANKS)
	THEN	BEGIN
		ENVEN='ZZZZ'
		XCALL OUTPT(6,16,1,'ALL',1)
		END
	ELSE	BEGIN
		XCALL INPUT(6,35,04,00,'AE',ENTRY,INXCTL,1)
		GOTO(DISPLA,END),INXCTL
		ENVEN=ENTRY(1,4)
		IF(ENVEN.EQ.BLANKS)
			BEGIN
			ENVEN=STVEN
			XCALL OUTPT(6,35,0,ENVEN,1)
			END
		END
	GOTO (ANYCNG),CNGCTL

INV,
	XCALL OUTPT(08,4,1,'2. INVOIC #              THRU: ',1)
	XCALL INPUT(8,16,08,00,'A ',ENTRY,INXCTL,1)
	GOTO(DISPLA),INXCTL
	STINV=ENTRY(1,8)
	IF(STINV.EQ.BLANKS)
	THEN	BEGIN
		ENINV='ZZZZZZZZ'
		XCALL OUTPT(8,16,1,'ALL',1)
		END
	ELSE	BEGIN
		XCALL INPUT(8,35,08,00,'A ',ENTRY,INXCTL,1)
		GOTO(DISPLA),INXCTL
		ENINV=ENTRY(1,8)
		IF(ENINV.EQ.BLANKS)
			BEGIN
			ENINV=STINV
			XCALL OUTPT(8,35,0,ENINV,1)
			END
		END
	GOTO(ANYCNG),CNGCTL
DAT,
	XCALL OUTPT(10,4,1,'3. INV DAT               THRU: ',1)
	XCALL INPUT(10,16,08,00,'D ',ENTRY,INXCTL,1)
	GOTO(DISPLA),INXCTL
	STDAT=ENTRY(1,8)
	IF(STDAT.EQ.0)
	THEN	BEGIN
		ENDAT=99999999
		XCALL OUTPT(10,16,1,'ALL',1)
		END
	ELSE	BEGIN
		XCALL INPUT(10,35,08,00,'D ',ENTRY,INXCTL,1)
		GOTO(DISPLA),INXCTL
		ENDAT=ENTRY(1,8)
		IF(ENDAT.EQ.0)
			BEGIN
			ENDAT=STDAT
			XDATE(1,4)=ENDAT(5,8)
			XDATE(5,8)=ENDAT(1,4)
			ENTRY(1,10)=XDATE, 'XX/XX/XXXX'
			XCALL OUTPT(10,35,0,ENTRY(1,10),1)
			END
		END
	GOTO (ANYCNG),CNGCTL
REF,
	XCALL OUTPT(12,4,1,'4. REF                                  THRU:',1)
	XCALL INPUT(12,16,25,00,'A ',ENTRY,INXCTL,1)
	GOTO(DISPLA),INXCTL
	STREF=ENTRY(1,25)
	IF(STREF.EQ.BLANKS)
	THEN	BEGIN
		ENREF='ZZZZZZZZZZZZZZZZZZZZZZZZZ'
		XCALL OUTPT(12,16,1,'ALL',1)
		END
	ELSE	BEGIN
		XCALL INPUT(12,50,25,00,'A ',ENTRY,INXCTL,1)
		GOTO(DISPLA),INXCTL
		ENREF=ENTRY(1,25)
		IF(ENREF.EQ.BLANKS)
			BEGIN
			STREF=ENREF
			XCALL OUTPT(12,50,1,ENREF,1)
			END
		END
	GOTO(ANYCNG),CNGCTL

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;	STXCTL = 3
;;;	XCALL STENO(STRTNO,ENDNO,'VENDOR #',4,STXCTL,2)
;;;	GO TO (END), STXCTL
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
ANYCNG,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO(PROCES),CNGCTL+1
	GOTO(VEN,INV,DAT,REF),WHATNO
	GOTO ANYCNG

PROCES,
	LOKCTL = 1
	XCALL IO (7,APOPEN,1,READ,LOKCTL)
	XCALL WATE(WTARG,V)
NXTREC,
	LOKCTL = 1
	XCALL IOS (7,APOPEN,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO LSTREC
	IF (AINVNO.EQ.']]]]]]') GOTO LSTREC
	IF (AINVNO.EQ.']]]DEL') GOTO NXTREC
	IF (REDVEN.LT.STVEN) GOTO NXTREC
	IF (REDVEN.GT.ENVEN) GO TO LSTREC
;;;	IF (AINVNO.EQ.']]]DEL') GOTO NXTREC
	IF (STINV.GT.BLANKS .AND. (AINVNO.LT.STINV.OR.AINVNO.GT.ENINV))GOTO NXTREC
	IF (STDAT.GT.0 .AND. (AINVDT.LT.STDAT.OR.AINVDT.GT.ENDAT))GOTO NXTREC
	IF (STREF.GT.BLANKS .AND. (ADESC.LT.STREF.OR.ADESC.GT.ENREF))GOTO NXTREC
	IF (AVENNO.NE.LSTVND) CALL GETVEN
	INCR PRTCNT
	PLINE(39,44) = AVCHNO
	PLINE(47,54) = AINVNO
	PLINE (56,66) = AINVAM, MASK1
	IF (AFLAG.GE.-1.AND.AFLAG.NE.2.AND.AFLAG.NE.8.AND.AFLAG.NE.9)
		BEGIN
		XCALL DATE8(ADUEDT, D_OUT, D_OUTR, D_FMT, D_SW)
		PLINE (69,78) = D_FMT
		END
	PLINE (81,86) = ACHKNO, MASK2
	IF (AAPACT.NE.0) PLINE (90,97) = AAPACT, ACTMSK
	IF (ACSACT.NE.0) PLINE (117,124) = ACSACT, ACTMSK
	PLINE (129,130) = AFLAG, 'X-'
	LINCNT = LINCNT - 1
	CALL PRINT

	XCALL DATE8(AINVDT, D_OUT, D_OUTR, D_FMT, D_SW)
	PLINE (45,54) = D_FMT

	PLINE (58,66) = ADSCAM, MASK1
	IF (ADISDT.NE.0) 
		BEGIN
		XCALL DATE8(ADISDT, D_OUT, D_OUTR, D_FMT, D_SW)
		PLINE (69,78) = D_FMT
		END
	IF (AFLAG.LT.-1.OR.AFLAG.EQ.2.OR.AFLAG.EQ.8.OR.AFLAG.EQ.9)
		BEGIN
		XCALL DATE8(ADUEDT, D_OUT, D_OUTR, D_FMT, D_SW)
		PLINE (80,89) = D_FMT
		END
	PLINE (91,115) = ADESC
	IF (ADSACT.NE.0) PLINE (117,124) = ADSACT, ACTMSK
	CALL PRINT
	IF (APAID.EQ.0.AND.ADSTKN.EQ.0) GO TO NOPAID
	PLINE (57,67) = APAID, MASK1
	PLINE (69,78) = ADSTKN, MASK1
	CALL PRINT
NOPAID,
	LNFEED = 1
	CALL LINFD
	INCR LINCNT
	INCR VCHCNT
	GOTO NXTREC
PRINT,
	IF (LINCNT.GE.57) LINCNT = 61
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,HDR3,
&			LEGND1,LEGND2,LEGND3,0,132,PRTCTL,1,LPSW,RPTNUM,PRTTYP)
	RETURN
GETVEN,
	CALL DASHES
	FSTVEN =
	LSTVND = AVENNO
	CALL MCHVND
	VCHCNT =
	RETURN
DASHES,
	IF (FSTVEN) RETURN
	IF (VCHCNT.EQ.0) RETURN
	LINCNT = LINCNT - 2
	PLINE (1,40) = DASHES
	PLINE (41,80) = DASHES
	PLINE (81,120) = DASHES
	PLINE (121,132) = DASHES
	CALL PRINT
	LINCNT = LINCNT + 2
	RETURN
RDVMAS,
;;;	LOKCTL = 1
;;;	XCALL IO (1,VENMAS,IRC011,READ,LOKCTL)
	GO TO MACH
MCHVND,
;;;	BSEND = ORGVEN
;;;	KEY = AVENNO
;;;	XCALL SERCH (2,VENIDX,KEY,1,4,BSEND,BSMID,SRCCTL,4,6,10,0,0,0,0)

	LOKCTL = 1
	XCALL ISIO (1, VENMAS, AVENNO, READ, LOKCTL)
	IF (LOKCTL .EQ. 0)
	THEN 	SRCCTL = 0
	ELSE	BEGIN
		CLEAR VENMAS
		SRCCTL = 1
		END

	GOTO (RDVMAS), SRCCTL+1
	NAME = '** NOT ON VENDOR FILE **'
MACH,
	PLINE(1,4) = AVENNO
	XCALL LEFTJ (PLINE(1,4),4)
	PLINE(7,36) = NAME
	RETURN
LSTREC,
	CALL DASHES
	IF (VCHCNT.EQ.0) XCALL MESAG ('NO VENDORS WITHIN SELECTED RANGE',2)
	SAVLPS = LPSW
	IF (PRTCNT.EQ.0.AND.LPSW.NE.2) CLOSE 14
	IF (PRTCNT.EQ.0.AND.LPSW.NE.2) GO TO BEGIN
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	IF (SAVLPS.EQ.2) GO TO END
	IF (SAVLPS.EQ.-4) PGCNT =
	GO TO BEGIN
END,
	CALL CLOSE3
EXIT,
	XCALL PGCHN('AP:APMENU',1)
CLOSE3,
	XCALL WATE(3,V)
	XCALL FILES(1,'I',11,4)
	XCALL FILES(7,'I',17,4)
CLOSE1,
	XCALL FILES(2,'I',12,4)
	RETURN
LINFD,
	XCALL LINFD (LNFEED)
	LINCNT = LINCNT + LNFEED
	RETURN
END
