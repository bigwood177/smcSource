; PRECHK / AP 
;
;
;		PRINTS PRE-CHECK-WRITING REPORT
;
;

RECORD	D_STUFF
	D_IN	,D8		;DATE-IN, ANY FORMAT
	D_OUT	,D6		;RETURN VALUE MMDDYY
	D_OUTR	,D8		;RETURN VALUE CCYYMMDD
	D_FMT	,A10		;RETURN VALUE MM/DD/CCYY
	D_SW	,A2		;"99" = DATE CONVERSION ERROR


RECORD VENMAS	
		.include 'def:rd011a.def'
record,x
		.INCLUDE 'DEF:RD011B.DEF'
RECORD APOPEN	,X
		.INCLUDE 'DEF:RD017A.DEF'
RECORD TITLE
		,A41, 'ACCOUNTS PAYABLE PRE-CHECK-WRITING REPORT'
RECORD HDR1
		,A6,'VENDOR'
		,A2
		,A7,'VOUCHER'
		,A29
		,A6,'------'
		,A7,'INVOICE'
		,A6,'------'
		,A5
		,A4,'DISC'
		,A6
		,A3,'DUE'
		,A9
		,A7,' AMOUNT'
		,A5
		,A6,'DISCNT'
		,A8
		,A3,'NET'
		,A7
		,A4,'A/P '
RECORD HDR2
		,A2
		,A2,'NO'
		,A7
		,A1,'#'
		,A4
		,A11,'DESCRIPTION'
		,A17
		,A6,'NUMBER'
		,A7
		,A4,'DATE'
		,A7
		,A4,'DATE'
		,A6
		,A4,'DATE'
		,A9
		,A6,' DUE  '
		,A6
		,A5,'TAKEN'
		,A7
		,A6,'AMOUNT'
		,A3
		,A7,'ACCOUNT'
RECORD LGND
		,A14,'FOR PAYMENT ON'
		,A1
	DATPMT	,A10
		,A5,' FOR '
	ACTLIT	,A20
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2

RECORD	VARS
	TDATE	,D6
	TODAY	,D8
	ADATE	,A10
	ALPHA	,A8
	AMTREM	,D8
	APACT	,D7
	APCNT	,D5
	BLANKS	,A5
	CHKDTE	,D8
	CHKTOT	,D9
	CNGCTL	,D1
	DASHES	,A40,'----------------------------------------'
	DECMAL	,D18
	DSCREM	,D6
	DUEDT2	,D8
	ENTRY	,A8
	INXCTL	,D1
	LINCNT	,D2,60
	LNFEED	,D1
	LPSW	,D1
	LSTVND	,A4
	MASK1	,A8,'XX/XX/XX'
	MASK2	,A13,'Z,ZZZ,ZZZ.XX-'
	MASK4	,A8,'ZXXX-XXX'
	MASK5	,A3,'XXX'
	OPTION	,D1
	PGCNT	,D3
	PLINE	,A132
	PMTDT2	,D8
	PRTCTL	,D3
	PRTFIL	,A14
	PRTTYP	,A1
	SAVAP	,D7
	RPTNUM	,D3
	SPLFIL	,A14
	SUBINV	,D9
	SUBDSC	,D7
	SWITCH	,D1,1
	TOTINV	,D9
	TOTDSC	,D7
	LOKCTL	,D1
	READ	,D1,	0
	WRITE	,D1,	1
	V	,D1,1
	VCHCNT	,D3,000
	WHATNO	,D2
	WTARG	,D1,2
PROC
	XCALL TERID (V)
	V = 1

	XCALL RDATE(TDATE)
	XCALL DATE8(TDATE, D_OUT, TODAY, D_FMT, D_SW)

	XCALL FILES(1,'SI',11,5)			;VENMAS FILE
	LOKCTL = 1
	CLEAR VENMAS
	VENNO = '   #'				;HEADER
	XCALL ISIO (1, VENMAS, VENNO, READ, LOKCTL)

;;;	LOKCTL = 1
;;;	XCALL IO (1,VENMAS,1,READ,LOKCTL)

	CLOSE 1
	SAVAP = VENDEF(2,8)
	XCALL FILES(7,'I',17,SWITCH)		;FILE # 17 -- APOPEN FILE
	IF (SWITCH.EQ.9) GO TO EXIT
	XCALL OUTPT(1,1,2,'ACCOUNTS PAYABLE PRE-CHECK-WRITING REPORT',V)
	LPSW = 1
	SPLFIL (5,6) = 'FJ'
	XCALL LPON (LPSW,SPLFIL)
	GO TO (END), LPSW + 1
DISPLA,
	XCALL OUTPT(12,20,2,'PLEASE ENTER INTENDED PAYMENT DATE',V)
	XCALL OUTPT(14,20,0,'PLEASE ENTER A/P ACCOUNT NUMBER',V)
PMTDAT,
	CTL = '12,56,08,00,DE'
	CALL INPUT
	GO TO (DISPLA,END), INXCTL
	CHKDTE = ENTRY(1,8)
	IF (CHKDTE.EQ.0) CHKDTE = TODAY
	IF (CHKDTE.EQ.0) GOTO PMTDAT

	XCALL DATE8(CHKDTE, D_OUT, D_OUTR, ADATE, D_SW)
;;;	ADATE = CHKDTE, MASK1
	DATPMT = ADATE
	XCALL OUTPT(12,56,0,ADATE,V)
	CTL = '14,56,04,00,#T'
	CALL INPUT
	GO TO (DISPLA,DISPLA,DEFAP), INXCTL
	APACT (1,4) = ENTRY (1,4)
	IF (APACT(1,4).EQ.0) GO TO ALLACT
	XCALL OUTPT(14,60,0,'-',V)
	COL = 61
	MAX = 3
	CALL INPUT
	GO TO (DISPLA), INXCTL
	IF (ENTRY.EQ.BLANKS) XCALL OUTPT(14,60,1,'\',V)
	APACT (5,7) = ENTRY (1,3)
	GO TO ANYCNG
ALLACT,
	APACT =
	XCALL OUTPT(14,56,1,'ALL',V)
	GOTO ANYCNG
DEFAP,
	APACT = SAVAP
	ALPHA = APACT, MASK4
	XCALL OUTPT(14,56,1,ALPHA,V)
	GO TO ANYCNG
PROCES,
	ACTLIT = 'ALL A/P ACCOUNTS'
	IF (APACT.EQ.0) GO TO PROCS2
	ACTLIT (1,12) = 'A/P ACCOUNT '
	ACTLIT (13,16) = APACT (1,4)
	ACTLIT (17,17) = '-'
	ACTLIT (18,20) = APACT (5,7), MASK5
PROCS2,
	IF (LPSW.EQ.2) WTARG = 4
	XCALL WATE(WTARG,V)
;;;	PMTDT2(1,2) = CHKDTE(5,6)
;;;	PMTDT2(3,6) = CHKDTE(1,4)
	APCNT = 2
READ,
	LOKCTL = 1
	XCALL IO (7,APOPEN,APCNT,READ,LOKCTL)
	INCR APCNT
	IF (AINVNO.EQ.']]]]]]') GOTO EOF
	IF (AINVNO.EQ.']]]DEL') GOTO READ
	IF (AAPACT.NE.APACT.AND.APACT.NE.0) GO TO READ
	IF (AFLAG.EQ.1.OR.AFLAG.EQ.2.OR.AFLAG.GE.8) GO TO READ
	IF (AFLAG.LT.-1) GOTO READ
	IF (AFLAG.EQ.-1.OR.AFLAG.EQ.7) AFLAG = 6
	IF (AVENNO.NE.LSTVND) CALL SUBTOT
	PLINE(9,14) = AVCHNO
	PLINE(17,41) = ADESC
	PLINE(45,52) = AINVNO

	XCALL DATE8(AINVDT, D_OUT, D_OUTR, D_FMT, D_SW)
	PLINE(56,63) = D_OUT,MASK1

	XCALL DATE8(ADISDT, D_OUT, D_OUTR, D_FMT, D_SW)
	PLINE(67,74) = D_OUT,MASK1

	XCALL DATE8(ADUEDT, D_OUT, D_OUTR, D_FMT, D_SW)
	PLINE(77,84) = D_OUT,MASK1

	AMTREM = AINVAM - APAID
	DSCREM = ADSCAM - ADSTKN
	IF (AINVAM.GT.0.AND.DSCREM.LT.0) DSCREM = 0
	IF (AFLAG.EQ.6) PLINE (45,74) = 'PARTIAL TO BE PAID'
	PLINE(88,98) = AMTREM,MASK2
	SUBINV = SUBINV + AMTREM
	IF (AFLAG.GT.5.OR.DSCREM.LE.0) GO TO TAKDSC

;;;	DUEDT2(1,2) = ADISDT(5,6)
;;;	DUEDT2(3,6) = ADISDT(1,4)
;;;	IF (PMTDT2.GT.DUEDT2) DSCREM =
	IF (CHKDTE .GT. ADISDT) DSCREM = 

TAKDSC,
	IF (DSCREM.NE.0) PLINE(101,109) = DSCREM,MASK2
	SUBDSC = SUBDSC+DSCREM
	PLINE(112,122) = AMTREM-DSCREM,MASK2
ENDBUF,
	PLINE(125,132) = AAPACT,MASK4
	CALL PRINT
	INCR VCHCNT
	GO TO READ
SUBTOT,
	IF (LSTVND.EQ.BLANKS) GO TO ENDSUB
	IF (VCHCNT.EQ.1.AND.SUBINV-SUBDSC.GT.0) GO TO ONLY1
	LNFEED = 1
	CALL LINFD
	PLINE(59,74) = 'VENDOR TOTALS:'
	PLINE(86,98) = SUBINV,MASK2
	PLINE(100,109) = SUBDSC,MASK2
	PLINE(110,122) = SUBINV-SUBDSC,MASK2
	CHKTOT = SUBINV - SUBDSC
	IF (CHKTOT.LE.0) CALL NOCHEK
	LINCNT = LINCNT - 1
	CALL PRINT
ONLY1,
	LNFEED = 1
	CALL LINFD
	INCR LINCNT
	TOTINV = TOTINV+SUBINV
	TOTDSC = TOTDSC+SUBDSC
	SUBINV =
	SUBDSC =
	CHKTOT =
	VCHCNT =
ENDSUB,
	CALL DASHES
	PLINE(2,5) = AVENNO
	XCALL LEFTJ (PLINE(2,5),4)
	LSTVND = AVENNO
	RETURN
NOCHEK,
	PLINE (100,132) =
	PLINE(110,123) = '  **NO CHECK**'
	SUBINV =
	SUBDSC =
	RETURN
DASHES,
	PLINE (1,40) = DASHES
	PLINE (41,80) = DASHES
	PLINE (81,120) = DASHES
	PLINE (121,132) = DASHES
	CALL PRINT
	RETURN
PRINT,
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,'NO HDR',
&		LGND,'NO LEGEND',' ',0,132,PRTCTL,1,LPSW,RPTNUM,PRTTYP)
	RETURN
EOF,
	CALL SUBTOT
	LNFEED = 1
	CALL LINFD
	PLINE =
	PLINE(62,74) = 'GRAND TOTALS:'
	PLINE(86,98) = TOTINV,MASK2
	PLINE(100,109) = TOTDSC,MASK2
	PLINE(111,122) = TOTINV-TOTDSC,MASK2
	LINCNT = LINCNT - 1
	CALL PRINT
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
END,
	XCALL WATE(3,V)
	XCALL FILES(7,'I',17,4)
	XCALL PGCHN('AP:DEFSEL',1)
EXIT,
	XCALL PGCHN('AP:APMENU',1)
INPUT,
	XCALL INPUT(ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,V)
	RETURN
ANYCNG,
	CNGCTL = 2
	XCALL ANYCN (CNGCTL,WHATNO)
	GOTO (PROCES,DISPLA), CNGCTL + 1
LINFD,
	XCALL LINFD (LNFEED)
	LINCNT = LINCNT + LNFEED
	RETURN
END
