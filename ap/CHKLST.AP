;  CHKLST / PR & AP 
;
;
;
;		PRINTS PAYROLL CHECK RECONCILIATION REPORT
;
;

RECORD	D_STUFF
	D_IN	,D8		;DATE-IN, ANY FORMAT
	D_OUT	,D6		;RETURN VALUE MMDDYY
	D_OUTR	,D8		;RETURN VALUE CCYYMMDD
	D_FMT	,A10		;RETURN VALUE MM/DD/CCYY
	D_SW	,A2		;"99" = DATE CONVERSION ERROR


RECORD RECONC	
		.INCLUDE 'DEF:RD078A.DEF'
RECORD DUMREC	
		.INCLUDE 'DEF:RD078C.DEF'
RECORD BRACKS	
		.INCLUDE 'DEF:RD078D.DEF'
RECORD PRACCT	
		.INCLUDE 'DEF:RD098A.DEF'
RECORD	,X	
		.INCLUDE 'DEF:RD098B.DEF'
RECORD	,X	
		.INCLUDE 'DEF:RD013F.DEF'
RECORD TITLE
		,A27,'CHECK RECONCILIATION REPORT'
RECORD HDR1
		,A5,'CHECK'
		,A3
		,A3,'PKG'
		,A4
		,A5,'CHECK'
		,A3
		,A9,'VENDOR-OR'
		,A2
		,A4,'NAME'
		,A28
		,A12,'UNRECONCILED'
		,A3
		,A10,'RECONCILED'
		,A5
		,A4,'DATE'
		,A2
		,A5
		,A4,'CASH'
RECORD HDR2
		,A2
		,A1,'#'
		,A12
		,A4,'DATE'
		,A5
		,A6,'EMPL-#'
		,A36
		,A12,'CHECK-AMOUNT'
		,A2
		,A12,'CHECK-AMOUNT'
		,A1
		,A10,'RECONCILED'
		,A2
		,A2
		,A4,'ACCT'
RECORD LGND1
		,A17,'FOR CASH ACCOUNT:'
		,A1
	LGCASH	,A8
		,A2
	LGDESC	,A30
RECORD LGND2
		,A12,'CHECK DATES:'
		,A1
	LGSTR	,A10
		,A4,' TO '
	LGEND	,A10
		,A3
		,A20,'RECONCILIATION DATE:'
		,A1
	LGREC	,A10

RECORD OUTNAM
	OUTDEV	,A2
		,A7,':CHKREC'
RECORD RCVMSG
	PROGNM	,A10
RECORD
	ARCNT	,D2
	BSEND	,D5
	BSMID	,D5
	CHKCNT	,D4,0000
	ENDT2	,D8
	ENTRY	,A2
	INXCTL	,D1
	KEY	,A7
	LINCNT	,D2,60
	LPSW	,D1
	MASK1	,A8,'ZX/XX/XX'
	MASK2	,A14,'ZZ,ZZZ,ZZZ.XX-'
	MASK4	,A8,'ZXXX-XXX'
	MSGCTL	,D1
	N	,D2,01
	PGCNT	,D3
	PLINE	,A132
	PRTCTL	,D3
	PRTTYP	,A1
	RPTNUM	,D3
	RDCNT	,D5
	RECCNT	,D5
	SPLFIL	,A14
	SRCCTL	,D1
	STRDT2	,D8
	SWITCH	,D1,1
	TOTREC	,D10
	TOTUNR	,D10
	LOKCTL	,D1
	READ	,D1,	0
	WRITE	,D1,	1
	V	,D1,1
	WRKDTE	,D8
	WRTCNT	,D5
	WTARG	,D1,2
PROC
	XCALL TERID (V)
	V = 1
	MSGCTL = 1
	XCALL SNMSG (RCVMSG,MSGCTL)
	XCALL OUTPT(2,1,2,'PRINT REPORT',1)
	LPSW = 1
	SPLFIL (5,6) = 'GZ'
	IF (RCVMSG(1,2).EQ.'AP') SPLFIL (5,6) = 'FO'
	XCALL LPON (LPSW,SPLFIL)
	GO TO (EXIT), LPSW + 1
	IF (LPSW.EQ.2) WTARG = 4
	XCALL WATE(WTARG,V)
	SWITCH = 1
	XCALL FILES (10,'I',78,SWITCH)		; RECONC FILE
	IF (SWITCH.EQ.9) GO TO EXIT
	IF (PROGNM.EQ.'PR') GO TO PAYROL
	XCALL FILES (3,'I',13,SWITCH)		; EXPACT FILE
	IF (SWITCH.NE.9) GO TO BEGIN
	CALL CLOSE1
	GO TO EXIT
PAYROL,
	XCALL FILES (3,'I',98,SWITCH)		; PRACCT FILE
	IF (SWITCH.NE.9) GO TO BEGIN
	CALL CLOSE1
	GO TO EXIT
BEGIN,
	LOKCTL = 1
	XCALL IO (3,PRACCT,1,READ,LOKCTL)
	LOKCTL = 1
	XCALL IO (10,DUMREC,1,READ,LOKCTL)
	BSEND = ORG098
	IF (CSHACT.EQ.-1) GO TO ALLCSH
	LGCASH = CSHACT, MASK4
	KEY = CSHACT, 'XXXXXXX'

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL ACCT(CSHACT, DESCR, SRCCTL)

;;;	XCALL SERCH(3,PRACCT,KEY,1,7,BSEND,BSMID,SRCCTL,4,32,37,0,0,0,0)
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (SRCCTL.EQ.1) DESCR = '** ACCOUNT NOT ON FILE **'
	LGDESC = DESCR
	HDR1 (101,109) =
	HDR2 (104,109) =
	GO TO CONTIN
ALLCSH,
	LGCASH = 'ALL'
CONTIN,
	IF (PROGNM.EQ.'AP') XCALL FILES (3,'I',13,4)
	IF (PROGNM.EQ.'PR') XCALL FILES (3,'I',98,4)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;	STRDT2 (1,2) = STCHDT (5,6)
;;;	STRDT2 (3,6) = STCHDT (1,4)
;;;	ENDT2 (1,2) = ENCHDT (5,6)
;;;	ENDT2 (3,6) = ENCHDT (1,4)

	STRDT2 = STCHDT
	ENDT2 = ENCHDT
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	IF (STCHDT.EQ.0) GO TO ALLDAT
	XCALL DATE8(STCHDT, D_OUT, D_OUTR, LGSTR, D_SW)
	XCALL DATE8(ENCHDT, D_OUT, D_OUTR, LGEND, D_SW)
;;;	LGSTR = STCHDT, MASK1
;;;	LGEND = ENCHDT, MASK1
	GO TO LEGREC
ALLDAT,
	LGND2 (14,25) = 'ALL'
LEGREC,
	XCALL DATE8(RECDTE, D_OUT, D_OUTR, LGREC, D_SW)
;;;	LGREC = RECDTE, MASK1
	IF (RECDTE.EQ.0) LGREC = 'ALL'
READ,
	LOKCTL = 1
	XCALL IOS (10,RECONC,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOFREC
	IF (RECONC.EQ.BRACKS) GO TO EOFREC
	IF (RSYSTM.EQ.2.AND.PROGNM.EQ.'AP') GO TO READ
	IF (RSYSTM.EQ.1.AND.PROGNM.EQ.'PR') GO TO READ
	IF (RCSHAC.NE.CSHACT.AND.CSHACT.NE.-1) GO TO READ
	IF (STCHDT.EQ.0) GO TO SKIP1

	IF (RCHKDT.LT.STRDT2.OR.RCHKDT.GT.ENDT2) GO TO READ

;;;	WRKDTE (1,2) = RCHKDT (5,6)
;;;	WRKDTE (3,6) = RCHKDT (1,4)
;;;	IF (WRKDTE.LT.STRDT2.OR.WRKDTE.GT.ENDT2) GO TO READ
SKIP1,
	IF (RECDTE.EQ.0.OR.RDTEPD.EQ.0) GO TO SKIP2
	IF (RDTEPD.NE.RECDTE) GO TO READ
SKIP2,
	IF (RECFLG.NE.1.AND.RDTEPD.NE.0) GO TO READ
	IF (UNRCFL.NE.1.AND.RDTEPD.EQ.0) GO TO READ
	PLINE (1,6) = RCHKNO
	PLINE (9,11) = 'A/P'
	IF (RSYSTM.NE.1) PLINE (9,11) = 'PR'

;;;	PLINE (14,21) = RCHKDT, MASK1
	XCALL DATE8(RCHKDT, D_OUT, D_OUTR, D_FMT, D_SW)
	PLINE (13,22) = D_FMT

	PLINE (24,32) = RVENEM
	PLINE (35,64) = RNAME
	IF (RDTEPD.EQ.0) PLINE (67,79) = RCHAMT, MASK2
	IF (RDTEPD.NE.0) PLINE (81,93) = RCHAMT, MASK2
	IF (RDTEPD.EQ.0) TOTUNR = TOTUNR + RCHAMT
	IF (RDTEPD.NE.0) TOTREC = TOTREC + RCHAMT

;;;	IF (RDTEPD.NE.0) PLINE (95,102) = RDTEPD, MASK1
	IF (RDTEPD.NE.0) 
		BEGIN
		XCALL DATE8(RDTEPD, D_OUT, D_OUTR, D_FMT, D_SW)
		PLINE (95,104) = D_FMT
		END

	IF (CSHACT.NE.-1) GO TO SKIP3
	PLINE (106,113) = RCSHAC, 'XXXX-XXX'
SKIP3,
	IF (RCHAMT.EQ.0) PLINE (116,125) = '** VOID **'
	CALL PRINT
	INCR CHKCNT
	GO TO READ
EOFREC,
	XCALL LINFD (1)
	PLINE(3,6) = CHKCNT
	PLINE (8,13) = 'CHECKS'
	PLINE (58,64) = 'TOTALS:'
	PLINE (66,79) = TOTUNR, MASK2
	PLINE (80,93) = TOTREC, MASK2
	LINCNT = LINCNT - 1
	CALL PRINT
	GOTO END
PRINT,
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,'NO HDR3',
&		LGND1,LGND2,'NO LEGEND',0,132,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
END,
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	IF (TOTREC.EQ.0) GO TO END3
	XCALL OUTPT (24,1,0,'PURGE RECONCILED CHECKS JUST PRINTED ?',V)
	XCALL INPUT (24,41,1,1,'YN',ENTRY,INXCTL,V)
	GO TO (END3), INXCTL - 1
	XCALL FILES (10,'I',78,4)
	SWITCH = 3
	XCALL FILES (10,'U',78,SWITCH)
	IF (SWITCH.EQ.9) GO TO EXIT
	XCALL WATE(4,V)
	XCALL FILES (10,'U',78,5)
	LOKCTL = 1
	XCALL IO (10,DUMREC,1,READ,LOKCTL)
	RECCNT = REC078
	RDCNT = 2
	WRTCNT = 1
RDNEXT,
	LOKCTL = 1
	XCALL IO (10,RECONC,RDCNT,READ,LOKCTL)
	IF (RECONC.EQ.BRACKS) GO TO ENDPRG
	INCR RDCNT
	IF (STCHDT.EQ.0) GO TO SKIP11

	IF (RCHKDT.LT.STRDT2 .OR. RCHKDT.GT.ENDT2) GO TO WRITE
;;;	WRKDTE (1,2) = RCHKDT (5,6)
;;;	WRKDTE (3,6) = RCHKDT (1,4)
;;;	IF (WRKDTE.LT.STRDT2.OR.WRKDTE.GT.ENDT2) GO TO WRITE

SKIP11,
	IF (RDTEPD.EQ.0) GO TO WRITE
	IF (RDTEPD.NE.RECDTE.AND.RECDTE.NE.0) GO TO WRITE
	IF (RSYSTM.EQ.2.AND.PROGNM.EQ.'AP') GO TO WRITE		;AP24
	IF (RSYSTM.EQ.1.AND.PROGNM.EQ.'PR') GO TO WRITE		;AP24
	GO TO RDNEXT
WRITE,
	INCR ARCNT
	RECARR (ARCNT) = RECONC
	IF (ARCNT.EQ.20) CALL WRTARR
	IF (RDCNT.GT.RECCNT) GO TO ENDPRG
	GO TO RDNEXT
WRTARR,
	INCR WRTCNT
	LOKCTL = 1
	XCALL IO (10,RECARR(N),WRTCNT,WRITE,LOKCTL)
	IF (N.EQ.ARCNT) GO TO ENDWRT
	INCR N
	GO TO WRTARR
ENDWRT,
	ARCNT = 0
	N = 1
	RETURN
ENDPRG,
	IF (ARCNT.NE.0) CALL WRTARR
	REC078 = WRTCNT
	ORG078 = WRTCNT
	LOKCTL = 1
	XCALL IO (10,DUMREC,1,WRITE,LOKCTL)
WRTBRK,
	IF (WRTCNT.EQ.RECCNT) GO TO END3
	INCR WRTCNT
	LOKCTL = 1
	XCALL IO (10,BRACKS,WRTCNT,WRITE,LOKCTL)
	GO TO WRTBRK
END3,
	CALL CLOSE1
EXIT,
	MSGCTL = 5
	XCALL SNMSG (RCVMSG,MSGCTL)
	OUTDEV = PROGNM
	XCALL PGCHN (OUTNAM,1)

CLOSE1,
	XCALL WATE(3,V)
	XCALL FILES (10,'I',78,4)
	RETURN
END
