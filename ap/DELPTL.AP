; DELPTL / AP 
;
;
;			::PCPYAP.DEF::
;******************************************************************************
;		ACCOUNTS PAYABLE
;
;		RELEASED: AUGUST 1, 1984 (d70s10)
;******************************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;		DELETE PARTIAL PAYMENTS FROM THE APOPEN FILE
;
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD APOPEN	
		.INCLUDE 'DEF:RD017A.DEF'
RECORD DUMAPO,X	
		.INCLUDE 'DEF:RD017B.DEF'
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD
	BSEND	,D5
	BSMID	,D5
	CNGCTL	,D1
	DECMAL	,D18
	ENTRY	,A30
	FLAG	,D1
	INXCTL	,D1
	KEY	,A10
	KVCHNO	,D6
	KVENNO	,A4
	MAXREC	,D5
	NEWDUE	,D6
	OPTION	,D1
	SRCCTL	,D1
	STRCNT	,D5
	SWITCH	,D1,1
	LOKCTL	,D1
	READ	,D1,	0
	WRITE	,D1,	1
	V	,D1,1
	WHATNO	,D2
PROC
	XCALL TERID (V)
	V = 1
	XCALL FILES(7,'U',17,SWITCH)		;FILE # 17 --APOPEN FILE
	IF (SWITCH.EQ.9) GO TO EXIT
	XCALL FILES(10,'I',17,5)
	LOKCTL = 1
	XCALL IO (10,APOPEN,1,READ,LOKCTL)
	BSEND = ORG017
	MAXREC = MAX017
	XCALL OUTPT(2,1,2,'REMOVE POSTED PARTIALS',V)
	XCALL OUTPT(6,20,0,'PLEASE ENTER VENDOR #',V)
	XCALL OUTPT(8,20,0,'PLEASE ENTER VOUCHER #',V)
VENDNO,
	CTL = '06,47,04,01,AE'
	CALL INPUT
	GO TO (CLRDTA,ENDOFF), INXCTL
 	XCALL FRMAT (ENTRY(1,4),4)
	KVENNO = ENTRY(1,4)
CONT,
	CTL = '08,47,06,01,# '
	CALL INPUT
	GO TO (CLRDTA), INXCTL
	KVCHNO = ENTRY(1,6)
	CNGCTL = 2
	GO TO ANYCNG
CLRDTA,
	UNLOCK 7
	XCALL OUTPT(8,47,2,'\',V)
	XCALL OUTPT (27,65,-1,'CNTRL',V)
	XCALL OUTPT(0,0,1,'\',V)
	GO TO VENDNO
PROCES,
	KEY(1,4) = KVENNO
	KEY(5,10) = KVCHNO,'XXXXXX'
	SRCCTL = 2
	XCALL SERCH (10,APOPEN,KEY,1,10,BSEND,BSMID,SRCCTL,1,32,37,0,0,0,0)
	GOTO (BACKUP), SRCCTL+1
NOFIND,
	XCALL MESAG('PARTIAL NOT FOUND',1)
	GO TO CLRDTA
BACKUP,
	BSMID = BSMID - 1
	LOKCTL = 1
	XCALL IO (10,APOPEN,BSMID,READ,LOKCTL)
	IF (APOPEN(1,10).EQ.KEY) GOTO BACKUP
	STRCNT = BSMID + 1
NXTREC,
	INCR BSMID
	LOKCTL = 1
	XCALL IO (10,APOPEN,BSMID,READ,LOKCTL)
	IF (APOPEN(1,10).NE.KEY) GOTO NOFIND
	IF (AFLAG.NE.-1.AND.AFLAG.NE.6.AND.AFLAG.NE.7) GOTO NXTREC
	IF (AINVNO.EQ.']]]DEL') GOTO NXTREC
	XCALL OUTPT(10,27,0,'INVOICE #',V)
	XCALL OUTPT(12,27,0,'INVOICE DATE',V)
	XCALL OUTPT(14,27,0,'PARTIAL PYT',V)
	XCALL OUTPT(16,27,0,'DISCNT TO TAKE',V)
	XCALL OUTPT(18,27,0,'DUE DATE',V)
	XCALL OUTPT(10,47,0,AINVNO,V)
	CTL = '12,47,06'
	DECMAL (1,6) = AINVDT
	CALL DSPDTE
	CTL = '14,47,08'
	DECMAL = AINVAM
	CALL DSPDLR
	CTL = '16,47,06'
	DECMAL = ADSCAM
	CALL DSPDLR
	CTL = '18,47,06'
	DECMAL (1,6) = ADUEDT
	CALL DSPDTE
	XCALL OUTPT(24,1,0,'RIGHT PARTIAL ?',V)
	CTL = '24,18,01,00,YN'
	CALL INPUT
	GOTO (NXTREC), INXCTL-1
	XCALL OUTPT (24,1,1,'OK TO DELETE ?',V)
	CTL = '24,17,01,01,YN'
	CALL INPUT
	GO TO (CLRDTA), INXCTL - 1
LOKERR,
	LOKCTL = 0
	XCALL IO (7,APOPEN,BSMID,READ,LOKCTL)
	IF (LOKCTL.EQ.1) GO TO LOCKED
	AINVNO = ']]]DEL'
	LOKCTL = 1
	XCALL IO (7,APOPEN,BSMID,WRITE,LOKCTL)
	LOKCTL = 1
	XCALL IO (7,APOPEN,MAXREC,READ,LOKCTL)
	LOKCTL = 1
	XCALL IO (7,APOPEN,1,READ,LOKCTL)
	UNLOCK 7
	LOKCTL = 1
	XCALL IO (10,APOPEN,MAXREC,READ,LOKCTL)
	LOKCTL = 1
	XCALL IO (10,APOPEN,1,READ,LOKCTL)
	XCALL MESAG('PARTIAL DELETED',2)
	GO TO CLRDTA
ENDOFF,
	XCALL FILES(7,'U',17,4)
EXIT,
	XCALL WATE(3,V)
	XCALL PGCHN('AP:PRTIAL',1)
INPUT,
	XCALL INPUT(ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,V)
	RETURN
ANYCNG,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (PROCES,CLRDTA,CLRDTA), CNGCTL+1
DSPDTE,
	OPTION = 2
	GOTO CALDSP
DSPDLR,
	OPTION = 3
CALDSP,
	XCALL DSPLY(MAX,ROW,COL,DECMAL,OPTION,V)
	RETURN
LOCKED,
	XCALL OUTPT (7,0,-1,'CNTRL',V)
	GO TO CLRDTA
END
