;  APAMNT / AP 
;
;
;			::PCPYAP.DEF::
;******************************************************************************
;		ACCOUNTS PAYABLE
;
;		RELEASED: AUGUST 1, 1984 (d70s10)
;******************************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		STANDARD MASTER FILE MAINTENANCE MODULE
;		(BUT NO INDEX TO THIS MASTER FILE)
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD EXPACT	
		.INCLUDE 'DEF:RD013A.DEF'
RECORD DUMEXP	
		.INCLUDE 'DEF:RD013B.DEF'
RECORD	,X
		,A10
	PRGNME	,A6
		,A21
RECORD DUMREC
		,A37
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD SNDMSG
	PRGNAM	,A9,'AP:APACNT'
	RCNT	,D5
	OCNT	,D5
RECORD
	BSEND	,D5
	BSMID	,D5
	CNGCTL	,D1
	DECMAL	,D18
	ENTRY	,A30
	INXCTL	,D1
	KEY	,A47
	MODEID	,3A14,'ADD           ','CHANGE/INQUIRE','DELETE        '
	MSGCTL	,D1
	SELECT	,D1
	SRCCTL	,D1
	SWITCH	,D1
	SYSTEM	,D1
	LOKCTL	,D1
	READ	,D1,	0
	WRITE	,D1,	1
	V	,D1,1
	WHATNO	,D2
PROC
	XCALL TERID (V)
	V = 1
	XCALL ENVRN(SYSTEM)
	SWITCH = 1
	XCALL FILES (3,'U',13,SWITCH)		;FILE # 13 -- EXPACT FILE
	IF (SWITCH.EQ.9) GOTO EXIT
UPDFLG,
	LOKCTL = 1
	XCALL IO (3,DUMEXP,1,READ,LOKCTL)
	BSEND = ORG013
	DELFLG =
	SRTFLG =
	LOKCTL = 1
	XCALL IO (3,DUMEXP,1,WRITE,LOKCTL)
	UNLOCK 3
BEGIN,
	XCALL OUTPT (1,1,2,'VALID A/P ACCT FILE MAINTENANCE',1)
	XCALL OUTPT (10,20,0,'PLEASE SELECT APPLICATION',1)
	XCALL OUTPT(12,25,0,'1. ADD NEW ACCOUNTS',V)
	XCALL OUTPT(14,25,0,'2. CHANGE/INQUIRE ACCOUNTS',V)
	XCALL OUTPT(16,25,0,'3. DELETE ACCOUNTS',V)
	XCALL OUTPT(18,25,0,'4. PRINT OUT ACCOUNTS',V)
	IF (INTFCE.NE.1) GO TO MNUSEL
	XCALL OUTPT(20,25,0,'5. TRANSFER ACCOUNTS FROM G/L SYSTEM',V)
MNUSEL,
	CTL = '10,47,01,01,#E'
	CALL INPUT
	GO TO (MNUSEL,SELEND), INXCTL
	SELECT = ENTRY
	IF (SELECT.EQ.5.AND.INTFCE.NE.1) GO TO MNUSEL
	GO TO (DSPMOD,DSPMOD,DSPMOD,SELLST,OUTMEN), SELECT
	GO TO MNUSEL
DSPMOD,							; DISPLAY MODE
	XCALL OUTPT (2,1,2,MODEID(SELECT),1)
	GO TO OUTMEN
SELEND,
	SELECT =			; PASS BACK SELECT = 0 FOR END OF MENU
	GO TO OUTMEN
SELLST,
	XCALL OUTPT (2,1,2,'\',1)
	XCALL OUTPT (12,20,0,'SORT BEFORE PRINTING ?',1)
	CTL = '12,44,01,00,YN'
	CALL INPUT
	IF (INXCTL.EQ.1) SELECT = 6
OUTMEN,
	GOTO (DISPLA,DISPLA,DISPLA,LIST,TRNSFR,LIST), SELECT
	XCALL WATE(3,1)
LOOP,
	LOKCTL = 1
	XCALL IO (3,DUMEXP,1,READ,LOKCTL)
	IF (DELFLG.NE.1.AND.DEL013.GE.10) GOTO ORGACT
	IF (SRTFLG.NE.1.AND.(REC013-ORG013).GE.1) GOTO SRTIDX
ENDOFF,
	CALL CLOSES
EXIT,
	XCALL WATE(3,V)
	XCALL PGCHN('AP:APMENU',1)
LIST,
	CALL CLOSES
	IF (SELECT.EQ.4) XCALL PGCHN('AP:APALST',1)
SRTIX2,
	SWITCH = 3
	XCALL FILES(3,'U',13,SWITCH)
	IF (SWITCH.EQ.9) GO TO EXIT
	XCALL FILES(3,'U',13,5)
	LOKCTL = 1
	XCALL IO (3,DUMEXP,1,READ,LOKCTL)
	PRGNME = 'APMENU'
	IF (SELECT.EQ.6) PRGNME = 'APALST'
	SRTFLG = 1
	LOKCTL = 1
	XCALL IO (3,DUMEXP,1,WRITE,LOKCTL)
	CLOSE 3
	RCNT = REC013
	OCNT = ORG013
	MSGCTL = 5
	XCALL SNMSG (SNDMSG,MSGCTL)
	XCALL WATE(4,2)
	XCALL PGCHN('AP:SRTAPA',0)
ORGACT,
	DELFLG = 1
	LOKCTL = 1
	XCALL IO (3,DUMEXP,1,WRITE,LOKCTL)
	CALL CLOSES
	XCALL WATE(4,V)
	XCALL PGCHN('AP:ORGAPA',0)
SRTIDX,
	CALL CLOSES
	GO TO SRTIX2
CLOSES,
	XCALL WATE(3,V)
	XCALL FILES (3,'U',13,4)
	RETURN
TRNSFR,
	CALL CLOSES
	XCALL PGCHN('AP:GLTOAP',1)
PURGE,
	XCALL MESAG ('DELETED RECORDS MUST BE PURGED',1)
	GO TO LOOP
DISPLA,
	UNLOCK 3
	CNGCTL =
	EXPACT =
	XCALL OUTPT(12,20,2,'1. ACCOUNT NUMBER',V)
	XCALL OUTPT(14,20,0,'2. ACCOUNT DESCRIPTION',V)
	IF (SELECT.NE.1) XCALL OUTPT(12,19,0,'*',V)
ACTNUM,
	IF (SELECT.NE.1.AND.CNGCTL) GOTO BADCNG
	IF (SELECT.EQ.1.AND.REC013.GE.MAX013) GOTO FULL
	IF (SELECT.EQ.3.AND.DEL013.GE.95) GO TO PURGE
	CALL CLRSUB
	CTL = '12,44,04,03,#E'
	CALL INPUT
	GOTO (DISPLA,BEGIN), INXCTL
	ACTNO(1,4) = ENTRY(1,4)
	XCALL OUTPT(12,48,0,'-',V)
	CTL = '12,49,03,00,# '
	CALL INPUT
	GOTO (DISPLA), INXCTL
	IF (ENTRY(1,3).EQ.'   ') CALL CLRSUB
	ACTNO(5,7) = ENTRY(1,3)
	IF (ACTNO.EQ.0) GOTO ACTNUM
	KEY(1,7) = ACTNO,'XXXXXXX'
	LOKCTL = 1
	XCALL IO (3,DUMREC,MAX013,READ,LOKCTL)
	XCALL SERCH(3,DUMREC,KEY,1,7,BSEND,BSMID,SRCCTL,1,32,37,0,0,0,0)
	GOTO (FNDREC,FNDREC), SELECT-1
	GOTO (BADACT), SRCCTL+1
	GOTO (ANYCNG), CNGCTL
	GOTO DSCRIP
FULL,
	CALL CLOSES
	XCALL OUTPT(3,1,2,'\',1)
	XCALL OUTPT(12,20,0,'THE A/P ACCOUNT FILE IS NOW FULL.',V)
	XCALL OUTPT(14,20,0,'PLEASE RUN "XPAND" TO EXPAND "EXPACT"',V)
	XCALL OUTPT(16,20,0,'AND THEN PROCEED AS USUAL.',V)
	XCALL MESAG (' ',2)
	XCALL PGCHN ('AP:APMENU',1)
BADACT,
	UNLOCK 3
	XCALL MESAG('ACCOUNT # ALREADY USED',1)
	GOTO DISPLA
CLRSUB,
	XCALL OUTPT(12,48,1,'\',V)
	RETURN
DSCRIP,
	CTL = '14,44,30,00,L '
	CALL INPUT
	GOTO (DISPLA), INXCTL
	IF (ENTRY.EQ.'      ') GOTO DSCRIP
	ACDESC = ENTRY(1,30)
	GO TO ANYCNG
CNGBR,
	GOTO (ACTNUM,DSCRIP), WHATNO
BADCNG,
	CNGCTL = 3
	GOTO ANYCNG
PROCES,
	GOTO (ADD,CHANGE), SELECT
FNDREC,
	GOTO (NOFIND), SRCCTL
LOKERR,
	LOKCTL = 0
	XCALL IO (3,EXPACT,BSMID,READ,LOKCTL)
	IF (LOKCTL.EQ.1) GO TO DISPLA
	XCALL OUTPT(14,44,0,ACDESC,V)
	IF (SELECT.NE.3) GOTO ANYCNG
	XCALL OUTPT(24,1,0,'OK TO DELETE ?',V)
	CTL = '24,17,01,01,YN'
	CALL INPUT
	GOTO (DELETE,DISPLA), INXCTL
ADD,
	LOKCTL = 1
	XCALL IO (3,DUMEXP,1,READ,LOKCTL)
	BSMID = BSMID-1
	LOKCTL = 1
	XCALL IO (3,DUMREC,MAX013,READ,LOKCTL)
	XCALL SERCH(3,DUMREC,KEY,1,7,BSEND,BSMID,SRCCTL,5,32,37,0,0,0,0)
	GOTO (BADACT), SRCCTL+1
	XCALL WATE(3,V)
	INCR REC013
	IF (REC013.GT.MAX013) GOTO FULL
WRTERR,
	LOKCTL = 1
	XCALL IO (3,EXPACT,REC013,WRITE,LOKCTL)
	LOKCTL = 1
	XCALL IO(3,DUMEXP,1,WRITE,LOKCTL)
CLRBUF,
	LOKCTL = 1
	XCALL IO (3,DUMEXP,MAX013,READ,LOKCTL)
	LOKCTL = 1
	XCALL IO (3,DUMEXP,1,READ,LOKCTL)
	UNLOCK 3
	GOTO DISPLA
CHANGE,
	LOKCTL = 1
	XCALL IO (3,EXPACT,BSMID,WRITE,LOKCTL)
	IF (SELECT.EQ.3) XCALL MESAG('ACCOUNT DELETED',2)
	GOTO CLRBUF
DELETE,
	LOKCTL = 1
	XCALL IO (3,DUMEXP,1,READ,LOKCTL)
	INCR DEL013
	LOKCTL = 1
	XCALL IO(3,DUMEXP,1,WRITE,LOKCTL)
	ACDESC =
	ACDESC(25,30) = '000000'
	GOTO CHANGE
NOFIND,
	XCALL MESAG('ACCOUNT NOT FOUND',1)
	GOTO DISPLA
INPUT,
	XCALL INPUT(ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,V)
	RETURN
ANYCNG,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (PROCES,CNGBR,DISPLA), CNGCTL+1
DSPNUM,
	XCALL DSPLY(MAX,ROW,COL,DECMAL,1,V)
	RETURN
END
