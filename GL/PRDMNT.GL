;  PRDMNT / GL 
;
;
RECORD	D_STUFF
	D_IN	,D8		;DATE-IN, ANY FORMAT
	D_OUT	,D6		;RETURN VALUE MMDDYY
	D_OUTR	,D8		;RETURN VALUE CCYYMMDD
	D_FMT	,A10		;RETURN VALUE MM/DD/CC/YY
	D_SW	,A2		;"99" = DATE CONVERSION ERROR



RECORD MASTER		; 
		.INCLUDE 'DEF:RD070A.DEF'
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD TITLE
		,A27,'ACCOUNTING PERIOD PRINT-OUT'
RECORD HDR1
		,A27
		,A8,'STARTING'
		,A4
		,A2
		,A6,'ENDING'
RECORD HDR2
		,A29
		,A4,'DATE'
		,A7
		,A2
		,A4,'DATE'
RECORD HDR3
		,A24,'CURRENT REPORTING PERIOD'
		,A3
	STARNG	,A10
		,A3
	ENDNG	,A10
RECORD LEGEND
		,A7
		,A2
	CLTNAM	,A30

RECORD	VARS
	I	,D5
	ALPHA	,A30
	BLANKS	,A6
	CNGCTL	,D1
	CMPFLG	,D1
	DTMASK	,A8,'XX/XX/XX'
	DECMAL	,D8
	ENTRY	,A30
	INXCTL	,D1
	SWITCH	,D3
	LPSW	,D1
	LINCNT	,D2
	NOMESG	,D1
	PLINE	,A132
	PGCNT	,D3
	PRDCNT	,D2
	SELECT	,D1
	SPLNUM	,D6
	STRSTD	,D8
	V	,D1
	WHATNO	,D2
	YES	,D1,	1
RECORD
	COL2	,D2
	ROW2	,D2
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	PRTTYP	,A1
	RPTNUM	,D3
	SPLFIL	,A14
	SWKDTE	,D8
	EWKDTE	,D8
	TSTDTE	,D8
	TENDTE	,D8
;
; ABOVE 4 LINES ADDED AS PER MCBA PATCH GL5-DIBOL-7
;
PROC 
	XCALL TERID (V)
BEGIN,
	XCALL OUTPT (1,1,2,'ACCOUNTING PERIOD MAINTENANCE',V)
	LINCNT = 60
	PGCNT = 0
	PLINE =
	XCALL OUTPT (1,0,0,'\',V)
	XCALL OUTPT (5,20,0,'PLEASE SELECT APPLICATION',V)
	XCALL OUTPT (6,25,0,'1. BUILD NEW PERIOD FILE',V)
	XCALL OUTPT (7,25,0,'2. CHANGE/INQUIRE PERIODS',V)
	XCALL OUTPT (8,25,0,'3. PRINT OUT PERIODS',V)
	CTL = '05,47,01,01,#E'
	CALL INPUT
	GO TO (BEGIN,END), INXCTL
	SELECT = ENTRY (1,1)
	GO TO (BUILD,CHANGE,LIST), SELECT
	XCALL MESAG (' ',6)
	GO TO BEGIN
END,
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	IF (STRSTD.EQ.0 .OR. MCSTDT.EQ.STRSTD) GO TO STOP
	XCALL MESAG ('BE SURE YOU HAVE CLOSED THE PREVIOUS PERIOD',2)
STOP,
	XCALL PGCHN ('GL:GLMENU',1)
BUILD,
	XCALL OUTPT (12,1,0,'BUILD NEW PERIOD FILE - ARE YOU SURE ?',V)
	CTL = '12,41,01,00,YN'
	CALL INPUT
	GO TO (BEGIN), INXCTL - 1
	ON ERROR NOFIND
	SWITCH = 3
	XCALL FILES (1,'U',70,SWITCH)		;#70 MASTER FILE
	IF (SWITCH.EQ.9) GO TO STOP
	SWITCH = 5
	XCALL FILES(1,'U',70,SWITCH)
	IF (SWITCH.NE.9) GO TO RDMAS
	XCALL FILES (1,'U',70,4)
	GO TO STOP
RDMAS,
	LOKCTL = 1
	XCALL IO (1,MASTER,1,READ,LOKCTL)
;-----------------------------------------------------------
;;;	MASTER (118,291) =
	CLEAR MCSTNO, MCENNO, MCSTDT, MCENDT, MNOPRD
	FOR I FROM 1 THRU 13	CLEAR MSTDTS(I), MENDTS(I)
;-----------------------------------------------------------

	OFF ERROR
ABORT,
	CALL DSPLY
	CNGCTL = 0
	CTL = '04,19,08,05,DE'
	CALL INPUT
	GO TO (ABORT,CLOSE),INXCTL
	MCSTDT = ENTRY(1,8)
	COL = 30
	TYPE = 'D '
	CALL INPUT
	GO TO (ABORT), INXCTL
	MCENDT = ENTRY(1,8)
	GO TO (ANYCNG), CNGCTL
	PRDCNT = 1
INPT3,
	ROW = PRDCNT + 4
	COL = 19
	TYPE = 'DE'
	CALL CLRDTE
; ABOVE 1 LINE ADDED AS PER MCBA PATCH GL5-DIBOL-7
;
	CALL INPUT
	GO TO (ABORT,ANYCNG), INXCTL
	MSTDTS (PRDCNT) = ENTRY(1,8)
	COL = 30
	TYPE = 'D '
	CALL INPUT
	GO TO (INPT3), INXCTL
	IF (CNGCTL.EQ.0.OR.PRDCNT.GE.MNOPRD) MNOPRD = PRDCNT
	MENDTS(PRDCNT) = ENTRY(1,8)
	CALL CHECKD
;
; ABOVE 1 LINE ADDED AS PER MCBA PATCH GL5-DIBOL-7
;
	GO TO (ANYCNG), CNGCTL
	INCR PRDCNT
	IF (PRDCNT.LE.6) GO TO INPT3
INPT4,
	ROW = PRDCNT - 2
	COL = 50
	TYPE = 'DE'
	CALL CLRDTE
;
; ABOVE 1 LINE ADDED AS PER MCBA PATCH GL5-DIBOL-7
;
	CALL INPUT
	GO TO (ABORT,ANYCNG), INXCTL
	MSTDTS(PRDCNT) = ENTRY(1,8)
	COL = 61
	TYPE = 'D '
	CALL INPUT
	GO TO (INPT4), INXCTL
	IF (CNGCTL.EQ.0.OR.PRDCNT.GE.MNOPRD) MNOPRD = PRDCNT
	MENDTS(PRDCNT) = ENTRY(1,8)
	CALL CHECKD
; 
; ABOVE 1 LINE ADDED AS PER MCBA PATCH GL5-DIBOL-7
;
	GO TO (ANYCNG), CNGCTL
	INCR PRDCNT
	IF (PRDCNT.LE.13) GO TO INPT4
	GO TO ANYCNG
CHECKD,
;;;	SWKDTE = MSTDTS( PRDCNT )
;;;	EWKDTE = MENDTS( PRDCNT )
;;;	TSTDTE(1,2) = SWKDTE(5,6)
;;;	TSTDTE(3,6) = SWKDTE(1,4)
;;;	TENDTE(1,2) = EWKDTE(5,6)
;;;	TENDTE(3,6) = EWKDTE(1,4)
;;;	IF (TENDTE.LE.TSTDTE )
	IF (MENDTS(PRDCNT) .LE. MSTDTS(PRDCNT))
		BEGIN
		     XCALL MESAG('END DATE MUST BE GREATER THAN START DATE',1)
			MSTDTS( PRDCNT ) =
			MENDTS( PRDCNT ) =
			TSTDTE =
			TENDTE =
			PRDCNT = PRDCNT - 1
		END
	RETURN
;
;
CHANGE,
	ON ERROR NOFIND
	SWITCH = 3
	XCALL FILES (1,'U',70,SWITCH)
	IF (SWITCH.EQ.9) GO TO STOP
	SWITCH = 5
	XCALL FILES(1,'U',70,SWITCH)
	IF (SWITCH.EQ.9) GO TO CLOSE
CHNG1,
	LOKCTL = 1
	XCALL IO (1,MASTER,1,READ,LOKCTL)
	STRSTD = MCSTDT
	OFF ERROR
	CALL DSPLY

	CTL = '04,19,08'
	DECMAL = MCSTDT
	CALL DSPDTE
	COL = 30
	DECMAL = MCENDT
	CALL DSPDTE
	PRDCNT = 1
DATES1,
	IF (PRDCNT.GT.MNOPRD) GO TO ANYCNG
	ROW = PRDCNT + 4
	COL = 19
	DECMAL = MSTDTS(PRDCNT)
	CALL DSPDTE
	COL = 30
	DECMAL = MENDTS(PRDCNT)
	CALL DSPDTE
	INCR PRDCNT
	IF (PRDCNT.LE.6) GO TO DATES1
DATES2,
	IF (PRDCNT.GT.MNOPRD) GO TO ANYCNG
	ROW = PRDCNT - 2
	COL = 50
	DECMAL = MSTDTS(PRDCNT)
	CALL DSPDTE
	COL = 61
	DECMAL = MENDTS(PRDCNT)
	CALL DSPDTE
	INCR PRDCNT
	IF (PRDCNT.LE.13) GO TO DATES2
	GO TO ANYCNG
CURDTS,
	CTL(1,5) = '04,19'
	CALL CLRDTE
	CTL = '04,19,08,05,D '
	CALL INPUT
	GO TO (CHNG1), INXCTL
	MCSTDT = ENTRY(1,8)
	COL = 30
	CALL INPUT
	GO TO (CURDTS), INXCTL
	MCENDT = ENTRY(1,8)
	GO TO ANYCNG
INPT1,
	CTL = '00,19,08,05,D '
	ROW = PRDCNT + 4
	CALL CLRDTE
	CALL INPUT
	GO TO (CHNG1), INXCTL
	MSTDTS (PRDCNT) = ENTRY(1,8)
	COL = 30
	CALL INPUT
	GO TO (INPT1), INXCTL
	MENDTS(PRDCNT) = ENTRY(1,8)
	GOTO CHKDTE
; 
; ABOVE 1 LINE ADDED AS PER MCBA PATCH GL5-DIBOL-7
;

;	GO TO ANYCNG
;
; ABOVE 1 LINE REMOVED AS PER MCBA PATCH GL5-DIBOL-7
;

INPT2,
	CTL = '00,50,08,05,D '
	ROW = PRDCNT - 2
	CALL CLRDTE
	CALL INPUT
	GO TO (CHNG1), INXCTL
	MSTDTS(PRDCNT) = ENTRY(1,8)
	COL = 61
	CALL INPUT
	GO TO (INPT2), INXCTL
	IF (CNGCTL.EQ.0.OR.PRDCNT.GE.MNOPRD) MNOPRD = PRDCNT
	MENDTS(PRDCNT) = ENTRY(1,8)
;	GO TO ANYCNG
;
; ABOVE 1 LINE REMOVED A SPER MCBA PATCH GL5-DIBOL-7
;

CHKDTE,
;;;	SWKDTE = MSTDTS( PRDCNT )
;;;	EWKDTE = MENDTS( PRDCNT )
;;;	TSTDTE(1,2) = SWKDTE(5,6)
;;;	TSTDTE(3,6) = SWKDTE(1,4)
;;;	TENDTE(1,2) = EWKDTE(5,6)
;;;	TENDTE(3,6) = EWKDTE(1,4)
;;;	IF (TENDTE.LE.TSTDTE)
	IF (MENDTS(PRDCNT) .LE. MSTDTS(PRDCNT))
		BEGIN
		    XCALL MESAG('END DATE MUST BE GREATER THAN START DATE',1)
			MSTDTS( PRDCNT ) =
			MSTDTS( PRDCNT ) =
			TSTDTE =
			TENDTE =
			IF (PRDCNT.LE.6)  GOTO INPT1
			IF (PRDCNT.LE.13) GOTO INPT2
		END
	GOTO ANYCNG
;
; ABOVE "CHKDTE," PARAGRAPH ADDED AS PER MCBA PATCH GL5-DIBOL-7
;

CLRDTE,
	XCALL OUTPT (ROW,COL,0,PLINE (1,26) ,V)
	RETURN
DSPLY,
	XCALL OUTPT (3,14,2,'PRD   START       END',V)
	XCALL OUTPT (3,45,0,'PRD   START       END',V)
	XCALL OUTPT (4,5,0,'(CURRENT) 0',V)
	XCALL OUTPT (5,15,0,'1',V)
	XCALL OUTPT (5,46,0,'7',V)
	XCALL OUTPT (6,15,0,'2',V)
	XCALL OUTPT (6,46,0,'8',V)
	XCALL OUTPT (7,15,0,'3',V)
	XCALL OUTPT (7,46,0,'9',V)
	XCALL OUTPT (8,15,0,'4',V)
	XCALL OUTPT (8,45,0,'10',V)
	XCALL OUTPT (9,15,0,'5',V)
	XCALL OUTPT (9,45,0,'11',V)
	XCALL OUTPT (10,15,0,'6',V)
	XCALL OUTPT (10,45,0,'12',V)
	XCALL OUTPT (11,45,0,'13',V)
	RETURN
PROCES,
	PRDCNT = 0
	CMPFLG = 0
FNDPRD,
	IF (PRDCNT.GE.MNOPRD) GO TO BADCUR
	INCR PRDCNT
	GO TO (ENDNO), CMPFLG
	IF (MSTDTS(PRDCNT).NE.MCSTDT) GO TO FNDPRD
	MCSTNO = PRDCNT
	CMPFLG = 1
ENDNO,
	IF (MENDTS(PRDCNT).NE.MCENDT) GO TO FNDPRD
	MCENNO = PRDCNT
WRITE,
	LOKCTL = 1
	XCALL IO (1,MASTER,1,WRITE,LOKCTL)
CLOSE,
	XCALL FILES (1,'U',70,4)
	GO TO BEGIN
LIST,
	ON ERROR NOFIND
	SWITCH = 1
	XCALL FILES (1,'U',70,SWITCH)
	IF (SWITCH.EQ.9) GO TO STOP
	LOKCTL = 1
	XCALL IO (1,MASTER,1,READ,LOKCTL)
	XCALL FILES (1,'U',70,4)
	OFF ERROR
	LPSW = 3		; PRINT ONLY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO BEGIN
	XCALL OUTPT (2,1,2,'\',V)
	XCALL WATE (2,V)
	CLTNAM = MCLTNM
	PRDCNT = 1

	XCALL DATE8(MCSTDT, D_OUT, D_OUTR, STARNG, D_SW)
	XCALL DATE8(MCENDT, D_OUT, D_OUTR, ENDNG, D_SW)
;;;	STARNG = MCSTDT, DTMASK
;;;	ENDNG	= MCENDT, DTMASK
LOOP,
	PLINE (16,21) = 'PERIOD'
	PLINE (23,24) = PRDCNT

	XCALL DATE8(MSTDTS(PRDCNT), D_OUT, D_OUTR, D_FMT, D_SW)
;;;	PLINE (28,35) = MSTDTS(PRDCNT), DTMASK
	XCALL DATE8(MENDTS(PRDCNT), D_OUT, D_OUTR, D_FMT, D_SW)
;;;	PLINE (39,46) = MENDTS(PRDCNT), DTMASK
	PLINE (28,37) = D_FMT
	PLINE (41,50) = D_FMT
	CALL PRINT
	INCR PRDCNT
	IF (PRDCNT.LE.MNOPRD) GO TO LOOP
DUMMY,
	IF (PRDCNT.GE.14) GO TO ENDPRT
	PLINE (16,21) = 'PERIOD'
	PLINE (23,24) = PRDCNT
	PLINE (28,52) = '-------UNDEFINED-------'
	CALL PRINT
	INCR PRDCNT
	GO TO DUMMY
ENDPRT,
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	GO TO BEGIN
NOFIND,
	XCALL MESAG ('MASTER RECORD NOT FOUND',1)
	GO TO END
BADCUR,
	MCSTNO =
	MCENNO =
	XCALL MESAG ('CURRENT PERIOD IS NOT A REGULAR ACCOUNTING PERIOD',1)
	CNGCTL = 5
	XCALL ANYCN (CNGCTL,WHATNO)
	GO TO (WRITE,CNGBR), CNGCTL+1
CNGBR,
	PRDCNT = WHATNO
	GO TO (CURDTS), WHATNO + 1
	IF (SELECT.EQ.2.AND.WHATNO.GT.MNOPRD) GO TO BADCNG
	IF (WHATNO.GE.0.AND.WHATNO.LE.6) GO TO INPT1
	IF (WHATNO.GE.0.AND.WHATNO.LE.13) GO TO INPT2
BADCNG,
	XCALL MESAG (' ',6)
	GO TO ANYCNG
DSPDTE,
	XCALL DATE8(DECMAL, D_OUT, D_OUTR, D_FMT, D_SW)
	XCALL OUTPT (ROW, COL, 0, D_FMT, V)
;;;	XCALL DSPLY (MAX,ROW,COL,DECMAL,2,V)
	RETURN
PRINT,
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,HDR3,'NO LEGEND',' ',
&			' ',0,80,50,0,LPSW,RPTNUM,PRTTYP)
	RETURN
INPUT,
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,V)
	RETURN
ANYCNG,
	CNGCTL = 5
	XCALL ANYCN (CNGCTL,WHATNO)
	GO TO (PROCES,PROCES,CNGBR), CNGCTL+2
END
