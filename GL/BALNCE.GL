;  BALNCE / GL 
;
;
;
;		PGM TO PRINT BALANCE SHEET
;

RECORD	D_STUFF
	D_IN	,D8		;DATE-IN, ANY FORMAT
	D_OUT	,D6		;RETURN VALUE MMDDYY
	D_OUTR	,D8		;RETURN VALUE CCYYMMDD
	D_FMT	,A10		;RETURN VALUE MM/DD/CC/YY
	D_SW	,A2		;"99" = DATE CONVERSION ERROR


RECORD GLAMAS		; 
		.INCLUDE 'DEF:RD031A.DEF'
RECORD, X		; 
		.INCLUDE 'DEF:RD031C.DEF'
RECORD DUMGLA		; 
		.INCLUDE 'DEF:RD031B.DEF'
RECORD CONAME
		,A50
RECORD YTDGLT		; 
		.INCLUDE 'DEF:RD033A.DEF'
RECORD DUMYTD		; 
		.INCLUDE 'DEF:RD033B.DEF'
RECORD TFSIDX		; 
		.INCLUDE 'DEF:RD036A.DEF'
RECORD LEGEND
		,A5,	'AS OF'
		,A2
	LGNDTE	,A10
		,A42
		,A5,	'AS OF'
		,A2
	LGNLYR	,A10
RECORD PBUF
	PLINE	,A132
RECORD TBUF
	TLINE	,A132
RECORD
	AMNT	,D10
	BALOR	,D1
	BLSHTT	,A30,	'         BALANCE SHEET        '
	BRACKS	,A6,	']]]]]]'
	CONTIN	,A9
	COL	,D3
	CLP	,D3
	CD	,A2
	CURCOM	,D10
	DTMASK	,A8,	'XX/XX/XX'
	DOUBLE	,A16,	'================'
	DLRSW	,D1
	DIVSN	,A30
	DESCR2	,A30
	ENDTE2	,D8
	FSCM	,3A30
	GRDCOM	,D10
	GRDAMT	,D10
	GLDTE2	,D8
	INSERT	,A2
	SWITCH	,D1,1
	I	,D1
	LYRDTE	,D8
	LINCNT	,D2,	60
	MASK1	,A13,	'ZZ,ZZZ,ZZZ.XX'
	NOTTL	,D1
	PROGRM	,A10,	'GL:PSCHED '
	PRTCTL	,D3
	PFCSW	,D1
	PAGSIZ	,D3,	132
	PGCNT	,D3
	PFC	,D3
	RECNO	,D4
	SVPRTC	,D3
	SVC	,D3
	SUBCOM	,D10
	SUBAMT	,D10
	SRCCTL	,D1
	SECCOM	,D10
	SECAMT	,D10
	TITLE	,D1,	-0
	TYPBA2	,A1
	TXI	,3D1
	TOTAMT	,D10
	TCD	,D3
	TACT	,D4
	UNDER	,A16,	'----------------'
	V	,D1
	VIPAMT	,D10
	YTDCOM	,D10
	YTDAMT	,D10
	YES	,D1,	1
RECORD
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	LPSW	,D2
	STRTSW	,D1	,0
	LPARG	,D1,2
	PRTTYP	,A1
	RPTNUM	,D3
	SPLFIL	,A14
PROC 
	XCALL TERID (V)
	XCALL OUTPT (2,1,2,'BALANCE SHEET',1)
	XCALL FILES (5,'I',36,5)		;FILE # 036 -- TFSIDX FILE
	LOKCTL = 1
	XCALL IO (5,TFSIDX,1,READ,LOKCTL)
	XCALL FILES (1,'I',31,5)		;FILE # 031 -- GLAMAS FILE
	LOKCTL = 1
	XCALL IO (1,DUMGLA,1,READ,LOKCTL)

;;;	ENDTE2(1,2) = CCENDT(5,6)
;;;	ENDTE2(3,6) = CCENDT(1,4)
	XCALL FILES (13,'I',99,5)		; CONAME FILE
	LOKCTL = 1
	XCALL IO (13,CONAME,1,READ,LOKCTL)
	CLOSE 13
	XCALL FILES (4,'I',33,5)		;FILE # 033 -- YTDGLT FILE
	LOKCTL = 1
	XCALL IO (4,DUMYTD,1,READ,LOKCTL)
	SPLFIL (5,6) = 'BH'
	LPSW = 4	;AUTO-SPOOLED
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) XCALL PGCHN ('GL:UNPRFS',1)
	IF (LPSW.EQ.2) LPARG = 4
	SLSLCU =

;;;	GLDTE2 = CCENDT
;;;	LGNDTE = GLDTE2,DTMASK
;;;	LYRDTE = GLDTE2
;;;	IF (COMP.EQ.2) LYRDTE = GLDTE2 - 1
;;;	LGNLYR = LYRDTE,DTMASK

	LYRDTE = CCENDT
	IF (COMP.EQ.2) LYRDTE(1,4) = CCENDT(1,4) - 1
;;;	IF (COMP.EQ.2) LYRDTE(3,4) = CCENDT(3,4) - 1
	IF (COMP.EQ.0) LEGEND (20,84) =
	XCALL DATE8(CCENDT, D_OUT, D_OUTR, LGNDTE, D_SW)
	XCALL DATE8(LYRDTE, D_OUT, D_OUTR, LGNLYR, D_SW)

	PFC = PFCCDE(SUB)
	BALOR = NUMBAL(SUB)
	IF (BALOR.LE.0) GO TO END
BLANCE,
	XCALL WATE (LPARG,V)
	IF (COMP .EQ. 0)  PAGSIZ = 80
	IF (CCSTNO.NE.0.AND.CCENNO.NE.0) GO TO PRFCRD
	LEGEND (68,82) =
	PAGSIZ = 80
PRFCRD,
	LOKCTL = 1
	XCALL IOS (5,TFSIDX,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOF
	IF (TFSIDX.EQ.BRACKS) GO TO EOF
	IF (TFSTCD(1,1).NE.'B') GO TO PRFCRD
	LOKCTL = 1
	XCALL IO (1,GLAMAS,TCRCNO,READ,LOKCTL)
	IF (ACCTNO(1,4).NE.9990) GO TO NTFND
	IF (ACCTNO(5,7).NE.PFC) GO TO PRFCRD
	DIVSN = DESCR
	GO TO TEXT
NTFND,
	IF (PFC.EQ.0) GO TO ALLD
;;;ssq 1-29-04>>>	IF (PFC(2,3).EQ.0) GOTO ALLD	;;;
	DIVSN = '     PROFIT CENTER -'
	DIVSN (22,24) = PFC
	GO TO NXT
ALLD,
	DIVSN = '        ALL DIVISIONS'
	GO TO NXT
TEXT,
	LOKCTL = 1
	XCALL IOS (5,TFSIDX,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOF
	IF (TFSIDX.EQ.BRACKS) GO TO EOF
	LOKCTL = 1
	XCALL IO (1,GLAMAS,TCRCNO,READ,LOKCTL)
NXT,
	TACT = ACCTNO (1,4)
	IF (TACT.EQ.9990) GO TO TEXT
	IF (TACT.GT.9993) GO TO NEXT
	IF (TACT.LT.9991) GO TO NOTITL
	I = ACCTNO (4,4)
	FSCM (I) = DESCR
	TXI (I) = 1
	GO TO TEXT
NEXT,
	IF (TACT.NE.9994) GO TO NOTITL
	BLSHTT = DESCR
	GO TO BEGIN
NOTITL,
	NOTTL = 1
	GO TO BEGIN
PRTHDR,
	SVPRTC = PRTCTL
	TBUF = PBUF
	PBUF =
	LINCNT =
	INCR PGCNT
	IF (STRTSW) XCALL LINFD (0)
	STRTSW = 1
	PLINE (16,65) = CONAME
	PLINE (73,76) = 'PAGE'
	PLINE (78,80) = PGCNT
	CALL PRINT1
	PLINE (26,55) = DIVSN
	CALL PRINT1
	XCALL LINFD (1)
	INCR LINCNT
	I = 1
TXLP,
	IF (TXI(I).NE.1) GO TO INCI
	PLINE (26,55) = FSCM (I)
	CALL PRINT1
INCI,
	INCR I
	IF (I.LT.4) GO TO TXLP
	XCALL LINFD (1)
	INCR LINCNT
	PLINE (26,55) = BLSHTT
	IF (COMP.EQ.1) PLINE (95,106) = '  BUDGETED  '
	IF (COMP.EQ.2) PLINE (95,106) = 'COMPARATIVES'
	CALL PRINT1
	PLINE (34,115) = LEGEND 
	CALL PRINT1
	XCALL LINFD (1)
	INCR LINCNT
	PBUF = TBUF
	TBUF =
	PRTCTL = SVPRTC
	RETURN
BEGIN,
	GO TO (FRST), NOTTL
READAC,
	LOKCTL = 1
	XCALL IOS (5,TFSIDX,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOF
	IF (TFSIDX.EQ.BRACKS) GO TO EOF
	IF (TFSTCD(1,1).EQ.'P') GO TO EOF
	IF (TFSTCD(1,1).NE.'B') GO TO READAC
FRST,
	LOKCTL = 1
	XCALL IO (1,GLAMAS,TCRCNO,READ,LOKCTL)
TEST,
	CD = TFSTCD (6,7)
	IF (CD.EQ.'LF') GO TO FORMS
	IF (CD.EQ.'HL') GO TO HEADLN
	IF (CD.NE.'HF') GO TO TYPBL
	LINCNT = 60
TYPBL,
	TYPBA2 = TYPBAL
	IF (TFSTCD(7,7) .EQ.'P') GO TO VPIPNP
	IF (SAFCDE.NE.' ') GO TO READAC
	PLINE (4,31) = DESCR
	IF (CD.EQ.'GH') CALL HEADER
	IF (CD.EQ.'TX') CALL PRINT
	DLRSW =
	IF (CD.EQ.'SS') CALL SECSUB
	IF (CD.EQ.'ST') CALL SUBTOT
	IF (CD.EQ.'TT') CALL TOTAL
	IF (CD.EQ.'GT') CALL GRDTOT
	GO TO READAC
HEADLN,
	PLINE =
	PLINE (35,64) = DESCR
	CALL HEADER
	GO TO READAC
FORMS,
	XCALL LINFD (1)
	INCR LINCNT
	GO TO READAC
VPIPNP,
	CALL TSTPFC
	IF (TFSTCD(6,6).EQ.'N') GO TO NET
	DESCR2 = DESCR
	IF (TFSTCD(6,6).EQ.'I') GO TO NXTACT
	CALL GTLYR
FNDIDX,
	IF (PFC.EQ.0) GO TO RDMAIN
	IF (PFC(2,3).EQ.0.AND.TFSPFC(1,1).EQ.PFC(1,1)) GOTO RDMAIN	;;;
	IF (TFSPFC.NE.PFC) GO TO NXTACT
RDMAIN,
	IF (TYRCNO.EQ.0) GOTO NXTACT
	LOKCTL = 1
	XCALL IO (4,YTDGLT,TYRCNO,READ,LOKCTL)
	INCR TYRCNO
	IF (YACTNO.NE.ACCTNO) GO TO NXTACT
;--------------------------------------------
;;;	GLDTE2(1,2) = YTRXDT(5,6)
;;;	GLDTE2(3,6) = YTRXDT(1,4)
;;;	IF (GLDTE2.GT.ENDTE2) GO TO NXTACT

	IF (YTRXDT .GT. CCENDT) GOTO NXTACT
;--------------------------------------------
	VIPAMT = VIPAMT + YTRXAM
	GO TO RDMAIN
NXTACT,
	LOKCTL = 1
	XCALL IOS (5,TFSIDX,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOF
	IF (TFSIDX.EQ.BRACKS) GO TO EOF
	IF (TFSTCD(1,1).NE.'B') GO TO NXTACT
	LOKCTL = 1
	XCALL IO (1,GLAMAS,TCRCNO,READ,LOKCTL)
	IF (TFSTCD(6,7).EQ.'NS') GO TO NS
	IF (TFSTCD(6,7).NE.'VS') GO TO VIPTOT
	CALL GTLYR
	CALL TSTPFC
	GO TO FNDIDX
VIPTOT,
	IF (PFCSW.EQ.0) GO TO TEST
	IF (VIPAMT.NE.0) GO TO PRTVIP
	IF (CCSTNO.EQ.0) GO TO NOPRT
	IF (CURCOM.EQ.0) GO TO NOPRT
PRTVIP,
	PLINE (2,31) = DESCR2
	PLINE (35,47) = VIPAMT,MASK1
	COL = 34
	AMNT = VIPAMT
	CALL TYPE
	IF (CCSTNO.EQ.0.AND.CCENNO.EQ.0) GO TO NOCOM
	IF (COMP.EQ.0) GO TO NOCOM
	PLINE (87,99) = CURCOM, MASK1
	COL = 86
	AMNT = CURCOM
	CALL TYPE
NOCOM,
	CALL PRINT 
	SECAMT = SECAMT + VIPAMT
	SECCOM = SECCOM + CURCOM
	SUBAMT = SUBAMT + VIPAMT
	SUBCOM = SUBCOM + CURCOM
	VIPAMT =
	CURCOM =
NOPRT,
	PFCSW =
	DLRSW = 1
	GO TO TEST
NS,
	PFCSW = 1
	VIPAMT = VIPAMT + NETYTD
	IF (COMP.EQ.1) THEN
		CURCOM = CURCOM + NETYBD
	ELSE
		CURCOM = CURCOM + NETYCM
	GO TO RDIDX
NET,
	VIPAMT = NETYTD
	CURCOM = NETYCM
	IF (COMP.EQ.1) CURCOM = NETYBD
	DESCR2 =
	DESCR2 = 'NET PROFIT (OR LOSS)'
	PFCSW = 1
RDIDX,
	LOKCTL = 1
	XCALL IOS (5,TFSIDX,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOF
	IF (TFSIDX.EQ.BRACKS) GO TO EOF
	LOKCTL = 1
	XCALL IO (1,GLAMAS,TCRCNO,READ,LOKCTL)
	GO TO VIPTOT
TYPE,
	INSERT =
	IF (TYPBA2.EQ.'C') GO TO TYPECR
	IF (AMNT.LT.0) GO TO PARENS
	GO TO PERTYP
TYPECR,
	IF (AMNT.LE.0) GO TO PERTYP
PARENS,
	PLINE (COL+14,COL+14) = ')'
	INSERT (1,1) = '('
	GO TO (JUSTFY), DLRSW
	GO TO DOLLAR
PERTYP,
	IF (DLRSW.EQ.1) RETURN
DOLLAR,
	PLINE (COL,COL) = '$'
JUSTFY,
	INCR COL
	IF (PLINE (COL,COL).EQ.' ') GO TO JUSTFY
	PLINE (COL-1,COL-1) = INSERT (1,1)
	RETURN
TSTPFC,
	IF (PFC.EQ.0) GO TO SETSW
	IF (PFC(2,3).EQ.0.AND.ACCTNO(5,5).EQ.PFC(1,1)) GOTO SETSW	;;;
	IF (ACCTNO(5,7).NE.PFC) RETURN
SETSW,
	PFCSW = 1
	RETURN
SECSUB,
	YTDAMT = SECAMT
	YTDCOM = SECCOM
	SVC = 33
	CALL STOT
	CLP = 35
	CALL BUFF2
	SECAMT = 
	SECCOM =
	YTDAMT =
	YTDCOM =
	RETURN
SUBTOT,
	YTDAMT = SUBAMT
	YTDCOM = SUBCOM
	SVC = 33
	CALL STOT
	CLP = 51
	CALL BUFF2
	TOTAMT = TOTAMT + SUBAMT
	SLSLCU = SLSLCU + SUBCOM
	SECAMT =
	SECCOM =
	SUBAMT = 
	SUBCOM =
	RETURN
TOTAL,
	YTDAMT = TOTAMT
	YTDCOM = SLSLCU
	SVC = 49
	CALL STOT
	CLP = 51
	CALL BUFF2
	GRDAMT = GRDAMT + TOTAMT
	GRDCOM = GRDCOM + SLSLCU
	TOTAMT =
	SLSLCU =
	RETURN
BUFF2,
	PLINE (CLP,CLP+12) = YTDAMT, MASK1
	COL = CLP - 1
	AMNT = YTDAMT
	CALL TYPE
	IF (CCSTNO.EQ.0.AND.CCENNO.EQ.0) GO TO NOTCM
	PLINE (CLP+52,CLP+64) = YTDCOM, MASK1
	COL = CLP + 51
	AMNT = YTDCOM
	CALL TYPE
NOTCM,
	CALL PRINT
	RETURN
GRDTOT,
	SVC = 49
	CALL STOT
	PLINE (67,79) = GRDAMT + TOTAMT, MASK1
	COL = 66
	AMNT = GRDAMT + TOTAMT
	CALL TYPE
	IF (CCSTNO.EQ.0.AND.CCENNO.EQ.0) GO TO NOGCM
	PLINE (119,131) = GRDCOM + SLSLCU, MASK1
	COL = 118
	AMNT = GRDCOM + SLSLCU
	CALL TYPE
NOGCM,
	CALL PRINT
	CALL GTOT
	GRDAMT = 	
	TOTAMT =
	GRDCOM =
	SLSLCU =
	RETURN
HEADER,
	XCALL LINFD (1)
	INCR LINCNT
PRINT,
	PRTCTL = PAGSIZ
	IF (LINCNT.GE.60) CALL PRTHDR
PRINT1,
	LINCNT = LINCNT - 1
	XCALL LPOUT (LINCNT,PGCNT,PLINE (1,PAGSIZ),'NO TITLE','NO HDR',
&		' ',' ','NO LEG',' ',' ',0,0,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
EOF,
	BALOR = BALOR - 1
	IF (BALOR.LE.0) GO TO END
	LOKCTL = 1
	XCALL IO (5,TFSIDX,1,READ,LOKCTL)
	NOTTL =
	LINCNT = 60
	VIPAMT =
	GO TO BLANCE
END,
	CLOSE 1
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	IF (NUMSCH(SUB).EQ.0) GO TO END1
STOP,
	XCALL PGCHN (PROGRM,1)
END1,
	PROGRM(4,9) = 'FINPOS'
	IF (NUMSAF(SUB).EQ.0) PROGRM(4,9) = 'SARATO'
	GO TO STOP
GTOT,
	LINCNT = LINCNT - 1
	PLINE =
	PLINE (65,80) = DOUBLE
	IF (CCSTNO.EQ.0.AND.CCENNO.EQ.0) GO TO GTP
	IF (COMP.EQ.0) GO TO GTP
	PLINE (117,132) = DOUBLE
GTP,
	CALL PRINT
	INCR LINCNT
	RETURN
STOT,
	LINCNT = LINCNT - 1
	PLINE =
	PLINE (SVC,SVC+(15)) = UNDER
	IF (CCSTNO.EQ.0.AND.CCENNO.EQ.0) GO TO STP
	IF (COMP.EQ.0) GO TO STP
	PLINE (SVC+53,SVC+68) = UNDER
STP,
	CALL PRINT
	INCR LINCNT
	PLINE (4,31) = DESCR
	RETURN
GTLYR,
	IF (CCSTNO.EQ.0.AND.CCENNO.EQ.0) RETURN
	IF (PFC.EQ.0) GO TO AOK
	IF (PFC(2,3).EQ.0.AND.ACCTNO(5,5).EQ.PFC(1,1)) GOTO AOK	;;;
	IF (ACCTNO(5,7).NE.PFC) RETURN
AOK,
	IF (COMP.EQ.1) CURCOM = CURCOM + BUDGET (CCENNO)
	IF (COMP.EQ.2) CURCOM = CURCOM + LYRBAL (CCENNO)
	RETURN
ENDOFF,
	XCALL WATE(3,V)
	CLOSE 4
	XCALL PGCHN ('GL:UNPRFS',1)
END
