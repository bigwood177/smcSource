;ORDER JNLMAS BY ORDER ACCOUNTS ADDED, 2ND KEY IS JNL, ACCT
;JEMAIN.GL
;
;		AUTHOR: SHERWOOD S. QUIRING
;		DATE  : 16-JAN-1997

;		RE-OCURRING JOURNAL ENTRIES


	.INCLUDE 'WND:WINDOWS.DEF'
	.DEFINE POOLSIZE	,25000
	.DEFINE WNDCHNL		,15
	.DEFINE MAXWINS		,15

RECORD	D_STUFF
	D_IN	,D8		;DATE-IN, ANY FORMAT
	D_OUT	,D6		;RETURN VALUE MMDDYY
	D_OUTR	,D8		;RETURN VALUE CCYYMMDD
	D_FMT	,A10		;RETURN VALUE MM/DD/CCYY
	D_SW	,A2		;"99" = DATE CONVERSION ERROR


RECORD	GLAMAS
	.INCLUDE 'DEF:RD031A.DEF'
RECORD,X
	.INCLUDE 'DEF:RD031B.DEF'

RECORD	GLAIDX
	.INCLUDE 'DEF:RD032A.DEF'

RECORD	JEMAIN
	.INCLUDE 'DEF:RD030A.DEF'

RECORD	JNLMAS
	.INCLUDE 'DEF:RD028A.DEF'

RECORD	JCTMAS
	.INCLUDE 'DEF:RD029A.DEF'

RECORD	JE_KEY
		,A2,	'JE'
	JEK1	,D2

RECORD	JJ_KEY
	JJK1	,A4
	JJK2	,D7
	JJK3	,D10
	JJK4	,D10

RECORD	FUNKEY
;;;	.INCLUDE 'FKEY.DEF'
	.INCLUDE 'DEF:FUNKEY.DEF'
RECORD	POPAR
;;;	.INCLUDE 'POP1.DEF'
	.include 'def:pop1.def'

RECORD	CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2

RECORD
	DEC	,D4
	ALPHA	,A3
	F_KEY	,D3

RECORD	CHANNEL
	CHN031	,D2
	CHN032	,D2
	CHN028	,D2
	CHN028B	,D2
	CHN029	,D2
	CHN030	,D2

RECORD	VARS
	BLANKS	,A25
	LAST_CAT	,A1
	O_DR	,D10
	O_CR	,D10
	HASH	,D10
	TITLEN	,D3
	WND_TITLE	,A*,	'REOCURRING JOURNAL ENTRIES'
	W_ID	,D4
	WND_1	,D4
	WND_2	,D4
	WND_3	,D4
	WND_4	,D4
	OPNOK	,D1
	BADACT	,D1
	BSEND	,D5
	BSMID	,D5
	SRCCTL	,D1
	ENTRY	,A30
	INXCTL	,D1
	CNGCTL	,D1
	WHATNO	,D2
	LOKCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	FIND	,D1,2
	STORE	,D1,2
	DELETE	,D1,3
	ABORT	,D1
	XJNL	,D2
	X_ACT	,D7
	SELECT	,D1
	SWITCH	,D1
	I	,D4
	V	,D1

PROC
	XCALL TERID (V)
	CALL OPENS
	IF (.NOT. OPNOK)
		BEGIN
		CALL CLOSE
		XCALL PGCHN ('GL:JEMENU',1)
		END
	CALL INIT_WINDOW

DISP_1,
	W_ID = WND_1
	CLEAR CNGCTL
	XCALL W_DISP(WND_1,WD_CLEAR)
	XCALL W_DISP(WND_1,WD_POS,4,4,"1. JNL #              <TAB> = LIST ALL")
	XCALL W_DISP(WND_1,WD_POS,5,4,"2. DESC")
	XCALL W_DISP(WND_1,WD_POS,6,4,"3. CAT")
	XCALL W_UPDT

JE_JNL,
	IF (SELECT.EQ.2 .AND. CNGCTL) GOTO ANYCN1
	CTL = '04,13,02,00,#X'
	CALL INPUT
	CASE INXCTL OF
	BEGINCASE
	1:	GOTO DISP_1
	2:	GOTO ENDOFF
	3:	BEGIN
		CALL JE_LIST
		IF (NUMARA .EQ. 0) GOTO DISP_1
		SELECT = 2
		CALL DISP_JE
		GOTO ANYCN1
		END
	ENDCASE

	XJNL = ENTRY(1,2)
	JEK1 = XJNL
	XCALL ISIO (CHN030,JEMAIN,JE_KEY,READ,LOKCTL)
	IF (LOKCTL .EQ. 0)
	THEN	BEGIN
		SELECT = 2	;CHANGE MODE
		CALL DISP_JE
		GOTO ANYCN1
		END
	ELSE	BEGIN
		SELECT = 1	;ADD MODE
		CLEAR JEMAIN
		JE_JNL(1,2) = "JE"
		JE_JNL(3,4) = XJNL,	'XX'
		END
JE_DES,
	CTL = '05,13,20,00,A '
	CALL INPUT
	GOTO (DISP_1),INXCTL
	JE_DES = ENTRY(1,20)
	GOTO (ANYCN1),CNGCTL
JE_CAT,
	CTL = '06,13,01,00,A '
	CALL INPUT
	GOTO (DISP_1),INXCTL
	JE_CAT = ENTRY(1,1)
	XCALL ISIO (CHN029,JCTMAS,JE_CAT,READ,LOKCTL)
	IF (LOKCTL .NE. 0) C_DES = "** NOT ON FILE **"
	XCALL W_DISP(WND_1,WD_POS,6,15,C_DES)
	IF (CNGCTL .AND. (JE_CAT .NE. LAST_CAT))
		BEGIN
		XCALL ISIO (CHN028,JNLMAS,JE_JNL,READ,LOKCTL)
		WHILE (J_JNL.EQ.JE_JNL .AND. LOKCTL.EQ.0)
			BEGIN
			J_CAT = JE_CAT
			XCALL ISIO (CHN028,JNLMAS,J_KEY,WRITE,LOKCTL)
			XCALL IOS (CHN028,JNLMAS,READ,LOKCTL)
			END
		END
	LAST_CAT = JE_CAT

ANYCN1,
	XCALL W_UPDT
	XCALL W_DISP(WND_1,WD_POS,20,1,WD_CLR,WDC_LIN,"FIELD:")
	XCALL W_DISP(WND_1,WD_POS,21,1,"<F1 = JOURNAL ENTRIES>    <F4 = DELETE>")
	CTL = '20,08,01,00,# '
;;;	CALL INPUT
	XCALL WINPT(W_ID,ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,F_KEY)
	IF (F_KEY .EQ. F_01) 
		BEGIN
		CALL WRITE_JEMAIN	;WRITE/STORE JEMAIN RECORD
		CALL JNLMAS		;JOURNAL ENTRIES
		GOTO DISP_1
		END
	IF (F_KEY .EQ. F_04)
		BEGIN
		XCALL W_DISP(WND_1,WD_CLR,WDC_LIN,WD_POS,20,1,"DELETE, ARE YOU SURE ?")
		CTL = '20,25,01,01,YN'
		CALL INPUT
		GOTO (ANYCN1),INXCTL-1
		SELECT = 3			;DELETE
		GOTO PROCES_1
		END

	WHATNO = ENTRY(1,1)
	IF (WHATNO .EQ. 0) GOTO PROCES_1
	CNGCTL = 1
	GOTO (JE_JNL,JE_DES,JE_CAT),WHATNO
	GOTO ANYCN1

PROCES_1,
	CALL WRITE_JEMAIN		;WRITE/STORE JEMAIN RECORD
	GOTO DISP_1

WRITE_JEMAIN,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CASE SELECT OF
	BEGINCASE
	1:	XCALL ISIO (CHN030,JEMAIN,JE_JNL,STORE,LOKCTL)
	2:	XCALL ISIO (CHN030,JEMAIN,JE_JNL,WRITE,LOKCTL)
	3:	BEGIN
		XCALL ISIO (CHN030,JEMAIN,JE_JNL,DELETE,LOKCTL)
		DISPLAY (15,7,7)
		XCALL W_DISP(WND_1,WD_POS,20,1,WD_CLR,WDC_EOW)
		XCALL W_DISP(WND_1,WD_POS,21,1,'RECORD DELETED !!')
		XCALL WINPT(W_ID,21,18,01,00,'C ',ENTRY,INXCTL)
		END
	ENDCASE
	RETURN
;---------------------------------------------------------------

JE_LIST,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		;;; POP-UP WINDOW OF  JEMAIN RECORDS
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CALL CLEAR_POP
	CLEAR JEK1
	XCALL ISIO (CHN030,JEMAIN,JE_KEY,READ,LOKCTL)		;HEADER
	CLEAR LOKCTL
	CLEAR I
	WHILE (LOKCTL.EQ.0 .AND. I.LT.MAXARA)
		BEGIN
		DLINE (1,4) = JE_JNL
		DLINE (6,35) = JE_DES
		INCR I
		PARRY(I) = DLINE
		XCALL GETRFA (CHN030,parrfa(I))
		XCALL IOS (CHN030,JEMAIN,READ,LOKCTL)
		END

	NUMARA = I
	IF (NUMARA .EQ. 0) RETURN

CONTIN,
	DLINE = '   JNL DESCRIPTION'
	POP_TITLE = "JOURNALS"

	WX = 3
	XCALL GLPOP (POPAR)
	CASE P_ACTION OF
	BEGINCASE
	0:	BEGIN
		NUMARA = 0
		RETURN
		END
	1-3:	BEGIN
		XCALL POSRFA (CHN030,parrfa(PI))
		XCALL IOS (CHN030,JEMAIN,READ,LOKCTL)
		XCALL ISIO (CHN030,JEMAIN,JE_JNL,READ,LOKCTL)
		RETURN
		END
	ENDCASE

	GOTO CONTIN
;======================================

	RETURN
;---------------------------------------------------------------

DISP_JE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL W_DISP(WND_1,WD_POS,4,13,JE_JNL(3,4) )
	XCALL W_DISP(WND_1,WD_POS,5,13,JE_DES)
	XCALL W_DISP(WND_1,WD_POS,6,13,JE_CAT)
	XCALL ISIO (CHN029,JCTMAS,JE_CAT,READ,LOKCTL)
	IF (LOKCTL .NE. 0) C_DES = "** NOT ON FILE **"
	XCALL W_DISP(WND_1,WD_POS,6,15,C_DES)
	RETURN
;---------------------------------------------------------------

;===============================================================
JNLMAS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; JOURNAL ENTRIES MAINTENANCE
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL W_DISP(WND_1,WD_HLINE,08,1,78)

	CLEAR HASH, LOKCTL

	XCALL ISIO (CHN028,JNLMAS,JE_JNL,READ,LOKCTL)
	WHILE (J_JNL.EQ.JE_JNL .AND. LOKCTL.EQ.0)
		BEGIN
		HASH = HASH + J_DR - J_CR
		XCALL IOS (CHN028,JNLMAS,READ,LOKCTL)
		END
DISP_2,
	CLEAR CNGCTL, JNLMAS, SELECT
	ENTRY(1,14) = HASH,	'ZZ,ZZZ,ZZX.XX-'
	XCALL W_DISP(WND_1,WD_CLR,WDC_LIN,WD_POS,9,35,'BALANCE: ',ENTRY(1,14) )

	XCALL W_DISP(WND_1,WD_POS,10,4,"1. ACCT #                <TAB = LIST ALL>",WD_CLR,WDC_EOW)
	XCALL W_DISP(WND_1,WD_POS,11,4,"2. DR AMT")
	XCALL W_DISP(WND_1,WD_POS,12,4,"3. CR AMT")
	XCALL W_DISP(WND_1,WD_POS,13,4,"4. REF")
	XCALL W_DISP(WND_1,WD_POS,14,4,"5. DATE")
	XCALL W_DISP(WND_1,WD_POS,15,4,"6. FLG")
	XCALL W_UPDT
J_ACT,
	CTL = '10,15,04,00,#X'
	CALL INPUT
	CASE INXCTL OF
	BEGINCASE
	1:	GOTO DISP_2
	2:	GOTO ENDOFF_2
	3:	BEGIN
		CALL JJ_LIST
		IF (NUMARA .EQ. 0) GOTO DISP_2
		SELECT = 2
		CALL DISP_JJ
		GOTO ANYCN2
		END
	ENDCASE
	XCALL W_DISP(WND_1,WD_POS,10,19,'-')
	X_ACT(1,4) = ENTRY(1,4)
	CTL = '10,20,03,00,# '
	CALL INPUT
	X_ACT(5,7) = ENTRY(1,3)
	CALL GET_ACT
	IF (BADACT) GOTO J_ACT
	XCALL W_DISP(WND_1,WD_POS,ROW,25,DESCR)

	CLEAR JJ_KEY
	JJK1 = JE_JNL
	JJK2 = X_ACT

;;;	XCALL ISIO (CHN028,JNLMAS,JE_JNL,FIND,LOKCTL)
	FIND(CHN028,JNLMAS,JE_JNL)[ERR=NO_JNL]
NO_JNL,
	CLEAR LOKCTL

	DO	XCALL IOS (CHN028,JNLMAS,READ,LOKCTL)
	UNTIL ((J_JNL.EQ.JE_JNL.AND.J_ACT.EQ.X_ACT) .OR. LOKCTL.NE.0)

	IF (LOKCTL .EQ. 0)
	THEN	BEGIN
		SELECT = 2	;CHANGE MODE
		CALL DISP_JJ
		GOTO ANYCN2
		END
	ELSE	BEGIN
		SELECT = 1	;ADD MODE
		CLEAR JNLMAS
		J_JNL = JE_JNL
		J_ACT = X_ACT
		END

	ENTRY(1,8) = X_ACT,	'ZXXX-XXX'
	XCALL W_DISP(WND_1,WD_POS,10,15,ENTRY(1,8) )
J_DR,
	CTL = '11,15,10,00,$ '
	CALL INPUT
	GOTO (DISP_2),INXCTL
	J_DR = ENTRY(1,10)
	IF (J_DR.NE.0 .AND. J_CR.NE.0) GOTO J_CR
	GOTO (ANYCN2),CNGCTL
;;;	GOTO J_REF
J_CR,
	CTL = '12,15,10,00,$ '
	CALL INPUT
	GOTO (DISP_2),INXCTL
	J_CR = ENTRY(1,10)
	IF (J_DR.NE.0 .AND. J_CR.NE.0) GOTO J_DR
	GOTO (ANYCN2),CNGCTL

J_REF,
	CTL = '13,15,25,00,A '
	CALL INPUT
	GOTO (DISP_2),INXCTL
	J_REF = ENTRY(1,25)
	IF (J_REF .EQ. BLANKS)
		BEGIN
		J_REF = JE_DES
		XCALL W_DISP(W_ID,WD_POS,13,15,J_REF)
		END

	GOTO (ANYCN2),CNGCTL
J_DAT,
	CTL = '14,15,08,00,D '
	CALL INPUT
	GOTO (DISP_2),INXCTL
	J_DAT = ENTRY(1,8)
	GOTO (ANYCN2),CNGCTL
J_FLG,
	CTL = '15,15,01,00,A '
	CALL INPUT
	GOTO (DISP_2),INXCTL
	J_FLG = ENTRY(1,1)
	
ANYCN2,
	XCALL W_UPDT
	IF (J_DR.NE.0 .AND. J_CR.NE.0) GOTO J_DR
	XCALL W_DISP(WND_1,WD_POS,20,1,WD_CLR,WDC_LIN,"FIELD:")
	XCALL W_DISP(WND_1,WD_POS,21,1,"<F4 = DELETE>")
	CTL = '20,08,01,00,# '
	XCALL WINPT(W_ID,ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,F_KEY)
	IF (F_KEY .EQ. F_04)
		BEGIN
		IF (SELECT .EQ. 1) GOTO DISP_2	;not added yet
		XCALL W_DISP(WND_1,WD_CLR,WDC_LIN,WD_POS,20,1,"DELETE, ARE YOU SURE ?")
		CTL = '20,25,01,01,YN'
		CALL INPUT
		GOTO (ANYCN2),INXCTL-1
		SELECT = 3			;DELETE
		GOTO PROCES_2
		END

	CNGCTL = 1
	WHATNO = ENTRY(1,1)
	IF (WHATNO .EQ. 0) GOTO PROCES_2
	
	GOTO (J_ACT,J_DR,J_CR,J_REF,J_DAT,J_FLG),WHATNO
	GOTO ANYCN2

PROCES_2,
	CALL WRITE_JNLMAS		;WRITE/STORE JNLMAS RECORD
	GOTO DISP_2

WRITE_JNLMAS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	J_JNL = JE_JNL
	JJK1 = J_JNL
	JJK2 = J_ACT
	JJK3 = J_CR
	JJK4 = J_DR
	J_CAT = JE_CAT
	CASE SELECT OF
	BEGINCASE
	1:	BEGIN
		XCALL ISIO (CHN028,JNLMAS,JJ_KEY,STORE,LOKCTL)
		IF (LOKCTL .NE. 0) XCALL MESAG ('ERROR STORING JNLMAS',1)
		HASH = HASH + J_DR - J_CR
		END
	2:	BEGIN
		XCALL ISIO (CHN028,JNLMAS,JJ_KEY,WRITE,LOKCTL)
		IF (LOKCTL .NE. 0) XCALL MESAG ('ERROR WRITING JNLMAS',1)
		HASH = HASH + ((J_DR - J_CR) - (O_DR - O_CR))
		END
	3:	BEGIN
		XCALL ISIO (CHN028,JNLMAS,JJ_KEY,DELETE,LOKCTL)
		DISPLAY (15,7,7)
		XCALL W_DISP(WND_1,WD_POS,20,1,WD_CLR,WDC_EOW)
		XCALL W_DISP(WND_1,WD_POS,21,1,'RECORD DELETED !!')
		XCALL WINPT(W_ID,21,18,01,00,'C ',ENTRY,INXCTL)
		END
	ENDCASE
	RETURN
;---------------------------------------------------------------
JJ_LIST,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		;;;	POP-UP WINDOW OF  JNLMAS RECORDS
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CALL CLEAR_POP
	CLEAR JJ_KEY
	JJK1 = JE_JNL
	XCALL ISIO (CHN028,JNLMAS,JJ_KEY,READ,LOKCTL)		;HEADER
	CLEAR LOKCTL
	CLEAR I
	WHILE (LOKCTL.EQ.0 .AND. I.LT.MAXARA .AND. J_JNL.EQ.JE_JNL)
		BEGIN
		DLINE (1,4) = J_JNL
		DLINE (6,13) = J_ACT,	'ZXXX-XXX'
		DLINE (15,25) = J_DR,	'ZZZZ,ZZX.XX'
		DLINE (26,36) = J_CR,	'ZZZZ,ZZX.XX'
		XCALL DATE8(J_DAT, D_OUT, D_OUTR, D_FMT, D_SW)
		DLINE (39,48) = D_FMT
	;;;	DLINE (39,46) = J_DAT	,'XX/XX/XX'

		INCR I
		PARRY(I) = DLINE
		XCALL GETRFA (CHN028,parrfa(I))
		XCALL IOS (CHN028,JNLMAS,READ,LOKCTL)
		END

	NUMARA = I
	IF (NUMARA .EQ. 0) RETURN

CONT2,
	DLINE = '   JNL ACCT           DEBIT     CREDIT      DATE'
	POP_TITLE = "JOURNALS ENTRIES"

	WX = 10
	XCALL GLPOP (POPAR)
	CASE P_ACTION OF
	BEGINCASE
	0:	BEGIN
		NUMARA = 0
		RETURN
		END
	1-3:	BEGIN
		XCALL POSRFA (CHN028,parrfa(PI))
		XCALL IOS (CHN028,JNLMAS,READ,LOKCTL)
	;;;	XCALL ISIO (CHN028,JNLMAS,J_JNL,READ,LOKCTL)
		RETURN
		END
	ENDCASE

	GOTO CONT2
;======================================

	RETURN
;---------------------------------------------------------------

DISP_JJ,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	ENTRY = J_ACT,	'ZZXX-XXX' [LEFT]
	XCALL W_DISP(WND_1,WD_POS,10,15,ENTRY)
	ENTRY = J_DR,	'Z,ZZZ,ZZX.XX' [LEFT]
	XCALL W_DISP(WND_1,WD_POS,11,15,ENTRY)
	ENTRY = J_CR,	'Z,ZZZ,ZZX.XX' [LEFT]
	XCALL W_DISP(WND_1,WD_POS,12,15,ENTRY)
	XCALL W_DISP(WND_1,WD_POS,13,15,J_REF)

	XCALL DATE8(J_DAT, D_OUT, D_OUTR, D_FMT, D_SW)
;;;	ENTRY = J_DAT,	'XX/XX/XX' [LEFT]
;;;	XCALL W_DISP(WND_1,WD_POS,14,15,ENTRY)
	XCALL W_DISP(WND_1, WD_POS, 14, 15, D_FMT)

	XCALL W_DISP(WND_1,WD_POS,15,15,J_FLG)

	O_DR = J_DR
	O_CR = J_CR
	RETURN
;---------------------------------------------------------------
ENDOFF_2,

	RETURN
;;;;;;;;;;;;;;;;;; END OF JOURNAL ENTRY MAINTENANCE ;;;;;;;;;;;;
;===============================================================

INPUT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL WINPT(W_ID,ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL)
	RETURN
;---------------------------------------------------------------

;;;INP,	;;;;;;;;;;;;;;;;;;;;;;;
;;;	;;; FUNCTION KEY INPUT
;;;	;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;;	XCALL W_DISP(W_ID,WD_POS,ROW,COL)
;;;	XCALL ACCHR (27,13,8)
;;;	XCALL W_DISP(W_ID,WD_READS,ALPHA)
;;;	XCALL DACHR (27,13)
;;;	CASE $RDTRM OF
;;;	BEGINCASE
;;;	027:	BEGIN
;;;		XCALL FLAGS (10000, 1)	;TURN OFF ECHO
;;;		XCALL W_DISP(WND_1,WD_ACCEPT,ALPHA)
;;;		IF (ALPHA .EQ. '[')CALL GETCHR
;;;		IF (ALPHA .EQ. 'O')CALL GETCHR	;PFX KEYS
;;;		F_KEY = DEC
;;;		XCALL FLAGS (10000, 0)		;TURN ON ECHO
;;;		END
;;;
;;;	008:	F_KEY=008		;<BS>
;;;	013:	F_KEY=013		;<CR>
;;;	ENDCASE
;;;
;;;	RETURN
;;;
;;;GETCHR,	
;;;	XCALL W_DISP(W_ID,WD_ACCEPT,ALPHA)
;;;	XCALL DECML (ALPHA, DEC)
;;;	RETURN
;---------------------------------------------------------------

GET_ACT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL SERCH (CHN032,GLAIDX,X_ACT,1,7,BSEND,BSMID,SRCCTL,4,18,22,0,0,0,0)
	IF (SRCCTL .OR. IRC031.EQ.0)
	THEN	BEGIN
		BADACT = 1
		XCALL MESAG ('ACCOUNT NOT ON FILE',1)
		END
	ELSE	BEGIN
		XCALL IO (CHN031,GLAMAS,IRC031,READ,LOKCTL)
		CLEAR BADACT
		END

	RETURN
;---------------------------------------------------------------
ENDOFF,

	CALL CLOSE
	XCALL PGCHN ('GL:JEMENU',1)
	STOP

CLEAR_POP,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		;;; INIT POPUP VALUES
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	FOR I FROM 1 THRU 40 
		BEGIN
		CLEAR PARRY(I)
		CLEAR PFLAGS(I)
		CLEAR parrfa(I)
		END

	CLEAR I
	CLEAR DLINE
	RETURN
;-----------------------------------------------

OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 1
	XCALL FILES (1,'SU',028,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN028 = 1

	XCALL FILES (2,'SI',029,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN029 = 2

	XCALL FILES (3,'SU',030,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN030 = 3

	XCALL FILES (5,'I',031,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN031 = 5

	XCALL FILES (6,'I',032,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN032 = 6

	LOKCTL = 1
	XCALL IO (CHN031,GLAMAS,1,READ,LOKCTL)
	BSEND = ORG031

	OPNOK = 1
	RETURN

;---------------------------------------------------------------

CLOSE,
	IF (CHN031) XCALL FILES (CHN031,'I',031,4)
	IF (CHN032) XCALL FILES (CHN032,'I',032,4)
	IF (CHN028) XCALL FILES (CHN028,'SU',028,4)
	IF (CHN029) XCALL FILES (CHN029,'SI',029,4)
	IF (CHN030) XCALL FILES (CHN030,'SU',030,4)
	RETURN
;---------------------------------------------------------------

INIT_WINDOW,
	XCALL W_INIT(POOLSIZE,WNDCHNL,MAXWINS)
	XCALL W_PROC(WP_CREATE,WND_1,"JEMAIN",22,78)
	TITLEN = %TRIM(WND_TITLE)
	IF (TITLEN .GT.0)
&	 XCALL W_BRDR(WND_1,WB_TITLE,WND_TITLE(1,TITLEN),WB_TPOS,WBT_TOP,WBT_CENTER)
	XCALL W_PROC(WP_PLACE,WND_1,1,1)
	XCALL W_DISP(WND_1,WD_CLEAR)
;----------------------------------------
; set values for pop-up window
	WX = 3
	WY = 25
;;;	PLEN = 50
	PLEN = 52
	NUMROW = 15
	POP_WID = "P-JEMAIN"
	RETURN
;--------------------------------------------------------
;---------------------------------------------------------------


	END


