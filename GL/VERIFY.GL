;  VERIFY / GL 
;
;
;		::PCPYGL.DEF::
;******************************************************************************
;		GENERAL LEDGER
;
;		RELEASED: AUGUST 1, 1984 (d70s10)
;******************************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;		PROGRAM TO PRE-READ YTDTRX AND VERIFY THAT CORRESPONDING
;		CHART RECORDS ARE PRESENT
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD MASTER		; 
		.INCLUDE 'DEF:RD070A.DEF'
RECORD YTDGLT		; 
		.INCLUDE 'DEF:RD033A.DEF'
RECORD GLAIDX		; 
		.INCLUDE 'DEF:RD032A.DEF'
RECORD PBUF
	ACCNT	,A8
		,A4
	TEXT	,A32,'NO CHART OF ACCOUNTS RECORD     '
RECORD
	ACTEOF	,D1
	BADFLG	,D1
	CURACT	,D7
	ENTRY	,A3
	INXCTL	,D1
	SWITCH	,D1,3
	LINCNT	,D2
	PGCNT	,D3
	V	,D1
RECORD
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	LPSW	,D1
	PRTTYP	,A1
	RPTNUM	,D3
	SPLFIL	,A14
PROC 
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'YEAR END PROCESSING',V)
	XCALL OUTPT (6,20,0,'ARE YOU SURE YOU WANT TO RUN THIS PROGRAM ?',V)
	XCALL INPUT(6,65,1,1,'YN',ENTRY,INXCTL,V)
	GO TO (END), INXCTL - 1
	LPSW = 3		;PRINT ONLY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) XCALL PGCHN ('GL:GLSPFN',1)
	XCALL WATE (4,V)
	XCALL OUTPT (2,1,1,'VERIFY FILES',1)
	XCALL FILES (2,'U',32,SWITCH)		;#32 GLAIDX FILE
	IF (SWITCH.EQ.9) GO TO END
	XCALL FILES (4,'U',33,SWITCH)		;#33 YTDGLT FILE
	IF (SWITCH.EQ.9) GO TO END1
	XCALL FILES(1,'U',31,SWITCH)
	IF (SWITCH.EQ.9) GO TO END2
	XCALL FILES(10,'U',70,SWITCH)		;#70 MASTER FILE
	IF (SWITCH.EQ.9) GO TO END3
	SWITCH = 5
	XCALL FILES(10,'I',70,SWITCH)
	IF (SWITCH.EQ.9) GO TO END4
	LOKCTL = 1
	XCALL IO (10,MASTER,1,READ,LOKCTL)
	IF (MNOPRD.EQ.0) GO TO NOPRDS
	XCALL FILES(2,'I',32,5)
	XCALL FILES(4,'I',33,5)
	LOKCTL = 1
	XCALL IO (2,GLAIDX,1,READ,LOKCTL)
	LOKCTL = 1
	XCALL IO (4,YTDGLT,1,READ,LOKCTL)
READ,
	LOKCTL = 1
	XCALL IOS (4,YTDGLT,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOFTRX
	IF (YTDGLT.EQ.']]]]]]') GO TO EOFTRX
	IF (YACTNO.NE.CURACT) CALL NEWACT
	GO TO READ
NOPRDS,
	XCALL MESAG('MUST ENTER PERIODS FIRST',1)
	GO TO END4
NEWACT,
	IF (YACTNO.EQ.IACTNO) GO TO OK
	IF (YACTNO.LT.IACTNO) GO TO NOACT
REREAD,
	GO TO (NOACT), ACTEOF
	LOKCTL = 1
	XCALL IOS (2,GLAIDX,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOFACT
	IF (GLAIDX.EQ.']]]]]]') GO TO EOFACT
	IF (IRC031.EQ.0) GO TO REREAD
	GO TO NEWACT
NOACT,
	BADFLG = 1
	ACCNT = YACTNO, 'XXXX-XXX'
	WRITES (14,PBUF)
OK,
	CURACT = YACTNO
	RETURN
EOFACT,
	IACTNO = 9999999
	ACTEOF = 1
	GO TO NOACT
EOFTRX,
	GO TO (NOUPDT), BADFLG
CONTIN,
	XCALL WATE(3,V)
	XCALL PGCHN ('GL:UPDPRD',1)
NOUPDT,
	XCALL LINFD (3)
	WRITES (14,'PLEASE MAKE ANY NECESSARY CORRECTIONS BEFORE PROCEEDING')
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	XCALL OUTPT (6,1,0,'*********** MISSING CHART RECORDS - SEE PRINTER FOR CORRECTION LIST ***********',V)
	XCALL OUTPT (7,0,-1,'CNTRL',V)
	XCALL OUTPT (0,0,0,'\',V)
	XCALL OUTPT (7,14,0,'DO YOU STILL WISH TO CONTINUE YEAR-END PROCESSING ?',V)
	XCALL INPUT (7,67,1,1,'YN',ENTRY,INXCTL,V)
	IF (INXCTL.EQ.1) GO TO CONTIN
END4,
	XCALL WATE(3,V)
	XCALL FILES(10,'I',70,4)
END3,
	XCALL FILES(1,'I',31,4)
END2,
	XCALL FILES(4,'I',33,4)
END1,
	XCALL FILES(2,'I',32,4)
END,
	XCALL WATE(3,V)
	XCALL PGCHN('GL:GLSPFN',1)
END
