;  GNJENT / GL 
;
;
;		GENERAL JOURNAL TRX ENTRY PGM
; 
; 
RECORD	D_STUFF
	D_IN	,D8		;DATE-IN, ANY FORMAT
	D_OUT	,D6		;RETURN VALUE MMDDYY
	D_OUTR	,D8		;RETURN VALUE CCYYMMDD
	D_FMT	,A10		;RETURN VALUE MM/DD/CC/YY
	D_SW	,A2		;"99" = DATE CONVERSION ERROR


RECORD GLAMAS		; 
		.INCLUDE 'DEF:RD031A.DEF'
RECORD MASTER ,X		; :
		.INCLUDE 'DEF:RD070A.DEF'
RECORD DUMGLA		; 
		.INCLUDE 'DEF:RD031B.DEF'
RECORD GLAIDX		; 
		.INCLUDE 'DEF:RD032A.DEF'
RECORD GLWORK		; 
		.INCLUDE 'DEF:RD034A.DEF'
RECORD DUMGLW		; 
		.INCLUDE 'DEF:RD034B.DEF'
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD
	ADDCNT	,D4
	ALPHA	,A8
	BSEND	,D5
	BSMID	,D5
	BLANKS	,A8
	CNGCTL	,D1
	DECMAL	,D18
	DGACNO	,D7
	DTMASK	,A8,'XX/XX/XX'
	DGJSRC	,A4
	DGJREF	,A25
	EDATED	,D8
	ENDAT	,D8
	ENTRY	,A30
	SWITCH	,D1,1
	GJDAT2	,D8
	HSHBAL	,D10
	OLDAMT	,D10
	INXCTL  ,D1
	KEY   	,A7
	MAXCNT	,D5
	OPTION	,D1
	RECNO	,D4
	SDATED	,D8
	SELECT	,D1
	SRCCTL	,D1
	STRAMT	,A10
	STRDAT	,D8
	SVHBAL	,D10
	THBAL	,D10
	V	,D1
	WHATNO	,D2
	YES	,D1,	1
RECORD
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
PROC 
	XCALL TERID (V)
	XCALL FILES(10,'I',70,SWITCH)
	IF (SWITCH.EQ.9) GO TO ENDOFF
	LOKCTL = 1
	XCALL IO (10,MASTER,1,READ,LOKCTL)
	XCALL FILES(10,'I',70,4)
	IF (MNOPRD.EQ.0) GO TO NOPRDS

;;;	STRDAT (1,2) = MCSTDT (5,6)
;;;	STRDAT (3,6) = MCSTDT (1,4)
;;;	ENDAT (1,2) = MCENDT (5,6)
;;;	ENDAT (3,6) = MCENDT (1,4)
	STRDAT = MCSTDT
	ENDAT = MCENDT

	SDATED = MCSTDT
	EDATED = MCENDT
	XCALL FILES (3,'I',32,SWITCH)		;#32 GLAIDX FILE
	IF (SWITCH.EQ.9) GO TO ENDOFF
	XCALL FILES (2,'I',31,SWITCH)		;#31 GLAMAS FILE
	IF (SWITCH.EQ.9) GO TO END1
	XCALL FILES (5,'U',34,SWITCH)		;#34 GLWORK FILE
	IF (SWITCH.EQ.9) GO TO END2
	LOKCTL = 1
	XCALL IO (5,DUMGLW,1,READ,LOKCTL)
	ADDCNT = REC034
	MAXCNT = MAX034
	HSHBAL = HASH2
	REGFLG = 'E'
	LOKCTL = 1
	XCALL IO (5,DUMGLW,1,WRITE,LOKCTL)
	LOKCTL = 1
	XCALL IO (2,DUMGLA,1,READ,LOKCTL)
	XCALL FILES(13,'I',34,5)
	BSEND = ORG031
BEGIN,
	SVHBAL = HASH2
	XCALL TMENU ('GENERAL JOURNAL TRX ENTRY & EDITING',SELECT,V)
	GO TO (DISPLA,DISPLA,DISPLA,LIST,POST), SELECT
	XCALL WATE(3,V)
	XCALL FILES(5,'U',34,4)
END2,
	XCALL FILES(2,'I',31,4)
END1,
	XCALL FILES(3,'I',32,4)
ENDOFF,
	XCALL WATE(3,V)
	XCALL PGCHN ('GL:GLMENU',1)
NOPRDS,
	XCALL MESAG('MUST BUILD PERIOD FILE FIRST',1)
	GO TO ENDOFF
LIST,
	CALL CLOSE3
	XCALL PGCHN ('GL:GNJREG',1)
POST,
	XCALL OUTPT (2,1,2,'POST',1)
	LOKCTL = 1
	XCALL IO (5,DUMGLW,1,READ,LOKCTL)
	REGFLG = 'R'
	REGNUM =
	LOKCTL = 1
	XCALL IO (5,DUMGLW,1,WRITE,LOKCTL)
	XCALL WATE(4,V)
	GO TO LIST
CLOSE3,
	XCALL FILES (2,'I',31,4)
	XCALL FILES (3,'I',32,4)
	XCALL FILES (5,'U',34,4)
	RETURN
DISPLA,
	XCALL OUTPT (1,41,0,'CURRENT PERIOD: ',V)
	XCALL DATE8(SDATED, D_OUT, D_OUTR, D_FMT, D_SW)
;;;	ALPHA = SDATED, DTMASK
	XCALL OUTPT (0,0,0,D_FMT,V)
	XCALL OUTPT (0,0,0,' TO ',V)
	XCALL DATE8(EDATED, D_OUT, D_OUTR, D_FMT, D_SW)
;;;	ALPHA = EDATED, DTMASK
	XCALL OUTPT (0,0,0,D_FMT,V)
	UNLOCK 5
	CNGCTL =
	GJAMT =
	IF (INXCTL.EQ.1.AND.SELECT.GT.1) HSHBAL = SVHBAL
	THBAL = HSHBAL
	XCALL OUTPT (2,1,2,'\',V)
	CALL DHSHB
	XCALL OUTPT (4,20,0,'1. G/L ACCOUNT #',V)
	XCALL OUTPT (5,20,0,'2. TRX DATE',V)
	XCALL OUTPT (6,20,0,'3. DEBIT AMOUNT',V)
	XCALL OUTPT (7,20,0,'4. CREDIT AMOUNT',V)
	XCALL OUTPT (8,20,0,'5. SOURCE',V)
	XCALL OUTPT (9,20,0,'6. TRX REFERENCE',V)
	GO TO (ACTNUM), SELECT
	XCALL OUTPT (4,19,0,'*',V)
	XCALL OUTPT (8,19,0,'*',V)
ACTNUM,
	IF (SELECT.EQ.1.AND.REC034.GE.MAXCNT) GO TO FULL
	XCALL OUTPT (4,39,1,'\',V)
	CTL = '04,39,04,00,#E'
	CALL INPUT
	GO TO (DISPLA,BEGIN), INXCTL
	IF (ENTRY.EQ.BLANKS) GO TO SAMACT
	DGACNO (1,4) = ENTRY (1,4)
	XCALL OUTPT (4,43,0,'-',V)
	CTL = '04,44,03,00,# '
	CALL INPUT
	GO TO (ACTNUM), INXCTL
	DGACNO(5,7) = ENTRY(1,3)
	IF (ENTRY(1,3).EQ.BLANKS) CALL NOSUBA
	IF (CNGCTL.EQ.0.AND.SELECT.GT.1) GO TO SRCDOC
	KEY = DGACNO,'XXXXXXX'
	LOKCTL = 1
	XCALL IO (3,GLAIDX,MAX031,READ,LOKCTL)
	XCALL SERCH (3,GLAIDX,KEY,1,7,BSEND,BSMID,SRCCTL,4,18,22,0,0,0,0)
	GO TO (BADACC), SRCCTL
	LOKCTL = 1
	XCALL IO (2,GLAMAS,IRC031,READ,LOKCTL)
	IF (FSTCDE(6,7).NE.'VS'.AND.FSTCDE(6,7).NE.'VP') GO TO INVACC
	XCALL OUTPT (4,50,0,DESCR,V)
	GJACCT = DGACNO
	GJDESC = DESCR
	GO TO (ANYCNG), CNGCTL
	GO TO TRXDTE
SAMACT,
	IF (CNGCTL.EQ.0.AND.SELECT.GT.1) GO TO ACTNUM
	IF (GJACCT.EQ.0) GO TO ACTNUM
	ALPHA = GJACCT, 'ZZZZ-XXX'
	XCALL OUTPT (4,39,0,ALPHA,V)
	XCALL OUTPT (4,50,0,GJDESC,V)
	GO TO (ANYCNG), CNGCTL
	GO TO TRXDTE
FULL,
	CALL CLOSE3
	XCALL OUTPT (3,0,3,'\',V)
	XCALL OUTPT (5,20,0,'THE G/L WORK FILE IS FULL. PLEASE',V)
	XCALL OUTPT (6,20,0,'RUN "XPAND" TO EXPAND THE FILE',V)
	XCALL OUTPT (7,20,0,'"GLWORK" AS NECESSARY, THEN CONTINUE.',V)
	XCALL MESAG (' ',2)
	XCALL PGCHN ('GL:GLMENU',1)
BADACC,
	XCALL MESAG ('ACCOUNT NOT FOUND',1)
	GO TO ACTNUM
INVACC,
	XCALL OUTPT (11,1,0,FSTCDE,V)
	XCALL MESAG('NOT A POSTING ACCOUNT',1)
	XCALL OUTPT (11,1,1,'\',V)
	GO TO ACTNUM
NOSUBA,
	XCALL OUTPT (4,43,1,'\',V)
	RETURN
TRXDTE,
	CTL = '05,39,08,00,D '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	IF (ENTRY.EQ.BLANKS) GO TO SAMDTE
	GJDATE = ENTRY(1,8)
	IF (GJDATE.EQ.0) GO TO TRXDTE

;;;	GJDAT2 (1,2) = GJDATE (5,6)
;;;	GJDAT2 (3,6) = GJDATE (1,4)
;;;	IF (GJDAT2.LT.STRDAT.OR.GJDAT2.GT.ENDAT) CALL WRNING
	IF (GJDATE.LT.STRDAT .OR. GJDATE.GT.ENDAT) CALL WRNING
	GO TO (ANYCNG), CNGCTL
	GO TO DRAMT
WRNING,
	XCALL MESAG('DATE NOT IN CURRENT PERIOD',2)
	RETURN
SAMDTE,
	IF (GJDATE.EQ.0) GO TO TRXDTE
	XCALL DATE8(GJDATE, D_OUT, D_OUTR, D_FMT, D_SW)
	XCALL OUTPT (ROW, COL, 0, D_FMT, V)
;;;	DECMAL(1,6) = GJDATE
;;;	CALL DSPDTE
	GO TO (ANYCNG), CNGCTL
DRAMT,
	IF (GJAMT.LT.0) GO TO BADCNG
	CTL = '06,39,10,00,$ '
	CALL INPUT
	GO TO (DISPLA),INXCTL
	GJAMT = ENTRY (1,10)
	STRAMT =
	STRAMT = ENTRY(1,10)
	IF (GJAMT.EQ.0) GO TO CRAMT
	IF (STRAMT.EQ.'0') GO TO SKIPCR
	IF (STRAMT.EQ.'          ') GO TO CRAMT
	THBAL = HSHBAL + GJAMT
	CALL DHSHB
	GO TO (ANYCNG), CNGCTL
	GO TO SRCDOC
CRAMT,
	IF (GJAMT.GT.0) GO TO BADCNG
	CTL = '07,39,10,00,$ '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	GJAMT = ENTRY(1,10)
	STRAMT =
	STRAMT = ENTRY(1,10)
	IF (GJAMT.EQ.0) GO TO DRAMT
	IF (STRAMT.EQ.'          ') GO TO DRAMT
	GJAMT = GJAMT*(-1)
	THBAL = HSHBAL + GJAMT
SKIPCR,
	CALL DHSHB
	GO TO (ANYCNG), CNGCTL
SRCDOC,
	CTL = '08,39,04,00,A '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	IF (ENTRY(1,4).EQ.BLANKS) GO TO SAMSRC
	DGJSRC = ENTRY(1,4)
	IF (CNGCTL.EQ.0.AND.SELECT.GT.1) GO TO FNDTRX
	GJSRCE = DGJSRC
	GO TO (ANYCNG), CNGCTL
	GO TO SRCREF
SAMSRC,
	XCALL OUTPT (8,39,0,GJSRCE,V)
	IF (GJSRCE.EQ.BLANKS) GO TO SRCDOC
	IF (CNGCTL.EQ.0.AND.SELECT.GT.1) GO TO SRCDOC
	GO TO (ANYCNG), CNGCTL
SRCREF,
	CTL = '09,39,25,00,A '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	IF (ENTRY.EQ.BLANKS) GO TO SAMDTL
	DGJREF = ENTRY (1,25)
	GJREF = DGJREF
	GO TO ANYCNG
SAMDTL,
	XCALL OUTPT (9,39,0,GJREF,V)
	IF (GJREF.EQ.BLANKS) GO TO SRCREF
	GO TO ANYCNG
CNGBR,
	GO TO (ACTNUM,TRXDTE,DRAMT,CRAMT,SRCDOC,SRCREF), WHATNO
BADCNG,
	CNGCTL = 3
	GO TO ANYCNG
PROCES,
	HSHBAL = HSHBAL + GJAMT
	THBAL = HSHBAL
	CALL DHSHB
	XCALL WATE (3,V)
	GO TO (ADD,CHANGE), SELECT
FNDTRX,
	RECNO = 1
	SVHBAL = HSHBAL
	LOKCTL = 1
	XCALL IO (13,GLWORK,MAXCNT,READ,LOKCTL)
	ONERROR NOFIND
READ,
	UNLOCK 5
	INCR RECNO
	LOKCTL = 1
	XCALL IO (13,GLWORK,RECNO,READ,LOKCTL)
	IF (GLWORK.EQ.']]]]]]') GO TO NOFIND
	IF (GJACCT.NE.DGACNO) GO TO READ 
	IF (GJSRCE.NE.DGJSRC) GO TO READ
	IF (GJDESC.EQ.']]]DEL') GO TO READ
	LOKCTL = 1
	XCALL IO (5,GLWORK,RECNO,READ,LOKCTL)
	OFFERROR
	CTL = '05,39'
	XCALL DATE8(GJDATE, D_OUT, D_OUTR, D_FMT, D_SW)
	XCALL OUTPT (ROW, COL, 0, D_FMT, V)
;;;	DECMAL(1,6) = GJDATE
;;;	CALL DSPDTE
	XCALL OUTPT (6,39,1,'\',V)
	XCALL OUTPT (7,39,1,'\',V)
	IF (GJAMT.GT.0) GO TO DEBIT
	CTL = '07,39,10'
	DECMAL = GJAMT*(-1)
	CALL DSPDLR
	GO TO ENDSP
DEBIT,
	CTL = '06,39,10'
	DECMAL = GJAMT
	CALL DSPDLR
ENDSP,
	XCALL OUTPT (9,39,0,GJREF,V)
	XCALL OUTPT (12,1,2,'RIGHT TRX ?',V)
	CTL = '12,14,01,01,YN'
	CALL INPUT
	GO TO (READ),INXCTL - 1
	OLDAMT = GJAMT
	HSHBAL = HSHBAL - GJAMT
	IF (SELECT.NE.3) GO TO ANYCNG
	HSHBAL = HSHBAL + GJAMT
	XCALL OUTPT (12,1,1,'OK TO DELETE ?',V)
	CTL = '12,17,01,00,YN'
	CALL INPUT
	GO TO (DISPLA), INXCTL - 1
	HSHBAL = HSHBAL - GJAMT
	GJDESC(1,6) = ']]]DEL'
	GO TO CHANGE
NOFIND,
	XCALL MESAG ('TRX NOT FOUND',1)
	GLWORK =
	GO TO DISPLA
ADD,
	LOKCTL = 1
	XCALL IO (5,DUMGLW,1,READ,LOKCTL)
	INCR REC034
	IF (REC034.GT.MAX034) GO TO FULL
	HASH2 = HASH2 + GJAMT
	THBAL = HASH2
	CALL DHSHB
	HSHBAL = HASH2
	LOKCTL = 1
	XCALL IO (5,DUMGLW,1,WRITE,LOKCTL)
	LOKCTL = 1
	XCALL IO (5,GLWORK,REC034,WRITE,LOKCTL)
	GO TO FREBUF
CHANGE,
	LOKCTL = 1
	XCALL IO (5,DUMGLW,1,READ,LOKCTL)
	HASH2 = HASH2 - OLDAMT
	IF (SELECT.EQ.2) HASH2 = HASH2 + GJAMT
	THBAL = HASH2
	CALL DHSHB
	HSHBAL = HASH2
	LOKCTL = 1
	XCALL IO (5,DUMGLW,1,WRITE,LOKCTL)
	LOKCTL = 1
	XCALL IO (5,GLWORK,RECNO,WRITE,LOKCTL)
	IF (SELECT.EQ.3) XCALL MESAG ('TRX DELETED',2)
	SVHBAL = HSHBAL
FREBUF,
	LOKCTL = 1
	XCALL IO (5,DUMGLW,MAXCNT,READ,LOKCTL)
	LOKCTL = 1
	XCALL IO (5,DUMGLW,1,READ,LOKCTL)
	GO TO DISPLA
DHSHB,
	XCALL OUTPT (11,43,0,'ACCUMULATED BALANCE =',V)
	XCALL OUTPT (11,65,2,'\',V)
	DECMAL = THBAL
	CTL = '11,65'
	CALL DSPDLR
	RETURN
INPUT,
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,V)
	RETURN
ANYCNG,
	XCALL ANYCN (CNGCTL,WHATNO)
	GO TO (PROCES,CNGBR,DISPLA), CNGCTL+1
DSPNUM,
	OPTION = 1
	GO TO CALDSP
DSPDTE,
	OPTION = 2
	GO TO CALDSP
DSPDLR,
	OPTION = 3
CALDSP,
	XCALL DSPLY (MAX,ROW,COL,DECMAL,OPTION,V)
	RETURN
END
