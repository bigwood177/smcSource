;CATDAT.GL
;
;	CHANGE REOCURRING JOURNAL DATES
;

RECORD	D_STUFF
	D_IN	,D8		;DATE-IN, ANY FORMAT
	D_OUT	,D6		;RETURN VALUE MMDDYY
	D_OUTR	,D8		;RETURN VALUE CCYYMMDD
	D_FMT	,A10		;RETURN VALUE MM/DD/CCYY
	D_SW	,A2		;"99" = DATE CONVERSION ERROR


RECORD	JNLMAS
	.INCLUDE 'DEF:RD028A.DEF'

RECORD	JCTMAS
	.INCLUDE 'DEF:RD029A.DEF'

RECORD	JEMAIN
	.INCLUDE 'DEF:RD030A.DEF'

RECORD	JNL_ERR
	J_ERR	,A4
		,A*,	' MISSING FROM JEMAIN'

RECORD	CAT_ERR
	C_ERR	,A1
		,A*,	' MISSING FROM JCTMAS'

RECORD	CHANNEL
	CHN028	,D2
	CHN029	,D2
	CHN030	,D2

RECORD	VARS
	ROW	,D2
	OPNOK	,D1
	SAVJNL	,A4
	ENTRY	,A30
	INXCTL	,D1
	CNGCTL	,D1
	WHATNO	,D2
	LOKCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	XDATE	,D8
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'CHANGE JOURNAL DATES',1)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO ENDOFF

	CALL DSP_CAT
PROCES,
	XCALL OUTPT (2,2,0,'UPDATE JOURNAL DATES ?',1)
	XCALL INPUT (2,25,01,01,'YN',ENTRY,INXCTL,1)
	GOTO (ENDOFF),INXCTL-1

	SAVJNL = '****'
LOOP,
	LOKCTL = 1
	XCALL IOS (CHN028,JNLMAS,READ,LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF

	IF (J_JNL .NE. SAVJNL) CALL NEWJNL

	J_DAT = XDATE
	XCALL ISIO (CHN028,JNLMAS,J_KEY,WRITE,LOKCTL)

	GOTO LOOP

NEWJNL,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	
	CLEAR XDATE
	SAVJNL = J_JNL

	XCALL ISIO (CHN030,JEMAIN,J_JNL,READ,LOKCTL)
	IF (LOKCTL .NE. 0)
		BEGIN
		J_ERR = J_JNL
		XCALL MESAG (JNL_ERR,1)
		RETURN
		END

	XCALL ISIO (CHN029,JCTMAS,JE_CAT,READ,LOKCTL)
	IF (LOKCTL .NE. 0)
		BEGIN
		C_ERR = JE_CAT
		XCALL MESAG (CAT_ERR,1)
		RETURN
		END

	XDATE = C_DAT
	RETURN
;-------------------------------------------------------
EOF,
ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('GL:JEMENU',1)
;==========================================================
DSP_CAT,
	ROW = 4
	XCALL IOS (CHN029,JCTMAS,READ,LOKCTL)
	WHILE (LOKCTL .EQ. 0)
		BEGIN
		IF (C_DAT .NE. 0)
			BEGIN
			INCR ROW
		;;;	ENTRY(1,8) = C_DAT,	'ZX/XX/XX'
			XCALL OUTPT (ROW,2,0,C_CAT,1)
			XCALL OUTPT (ROW,4,0,C_DES,1)
			XCALL DATE8(C_DAT, D_OUT, D_OUTR, D_FMT, D_SW)
			XCALL OUTPT (ROW,25,0,D_FMT,1)
			END
		XCALL IOS (CHN029,JCTMAS,READ,LOKCTL)
		END
	RETURN

OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 1
	XCALL FILES (1,'SI',029,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN029 = 1

	XCALL FILES (2,'SI',030,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN030 = 2

	XCALL FILES (3,'SU',028,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN028 = 3

	OPNOK = 1
	RETURN
;-------------------------------------------

CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	IF (CHN028) XCALL FILES (CHN028,'SU',028,4)
	IF (CHN029) XCALL FILES (CHN029,'SI',029,4)
	IF (CHN030) XCALL FILES (CHN030,'SI',030,4)
	RETURN
;-------------------------------------------

