;  CVGL70 / GL 
;
;
;		::PCPYGL.DEF::
;*****************************************************************************
;		GENERAL LEDGER
;
;		RELEASED: AUGUST 1, 1984 (d70s10)
;*****************************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		CONVERTS EXISTING G/L FILES TO THE NEW BAPS FORMAT
;
;
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD MASTER
	MPROT	,D1
	MCCTNO	,D4
	MCLTNM	,A30
	MADD1	,A30
	MADD2	,A30
	MTYPE	,A1
	MFEDNO	,D9
	MSTNO	,D9
	MSURAT	,D3
	MCSTNO	,D2
	MCENNO	,D2
	MCSTDT	,D6
	MCENDT	,D6
	MNOPRD	,D2
	MSTDTS	,13D6
	MENDTS	,13D6
RECORD GLAOLD
	ACTNO	,D7
	GDESCR	,A30
	FSTCOD	,A6
	SCHCOD	,A6
	PAREN	,A1
	BUDGT	,13D8
	PYRCOM	,14D10
RECORD	,X
	GLSTDT	,13D6
	GLENDT	,13D6
		,A118
	DELFLG	,D1
	SRTFLG	,D1
	ORGCNT	,D5
	RECCNT	,D5
	MAXREC	,D5
	DELCNT	,D3
RECORD YTDOLD
	GLACNO	,D7
	GLDATE	,D6
	GLAMT	,D10
	GLSRCE	,A3
	GLREF	,A30
RECORD	,X
		,A3
	STRPRD	,D2
	ENDPRD	,D2
	STRDAT	,D6
	ENDAT	,D6
	PROFCN	,D3
	CMPFLG	,D1
	BLSFLG	,D1
	SCHFLG	,D1
		,A13
	YORGCT	,D5
	YRECCT	,D5
	YMAXRC	,D5
		,A3
RECORD GLANEW
	ACCTNO	,D7
	DESCR	,A30
	TYPBAL	,A1
	FSTCDE	,A7
	FSTCD2	,A7
	SCHCDE	,A7
	SCHCD2	,A7
	SAFCDE	,A1
	ACTCDE	,D2
	USRCDE	,A1
	SSTCDE	,A1
	SMPCDE	,A1
	CMPCDE	,A1
	CTRACT	,D7
	PYREND	,D10
	LYRBAL	,13D10
	BUDGET	,13D10
RECORD	,X
		,A332
	ORG031	,D5
	REC031	,D5
	MAX031	,D5
	DEL031	,D3
RECORD GLINEW
	IACTNO	,D7
	IFSTCD	,A7
	ISAFCD	,A1
	IACTCD	,D2
	IRC031	,D5
RECORD YTDNEW
	YACTNO	,D7
	YTRXDT	,D6
	YSRCE	,A4
	YPROJ	,A5
	YTRXRF	,A25
	YTRXAM	,D10
	YTRAIL	,A5
RECORD	,X
		,A44
	ORG033	,D5
	REC033	,D5
	MAX033	,D5
	DEL033	,D3
RECORD BRACKS
	,A50,']]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]'
	,A50,']]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]'
	,A50,']]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]'
	,A50,']]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]'
	,A50,']]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]'
	,A50,']]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]'
	,A50,']]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]'
RECORD
	ACCUM	,D10
	BLANKS	,A291
	COUNT	,D4,0001
	SWITCH	,D3
	SUB	,D2
	V	,D1
	TOTREC	,D5
	SIZE	,D3
	ANS	,A1
	CODE	,A3
;
RECORD
	FILES	,A14
	FILE1	,A14,'FIL:GLAMAS.XXX'
	FILE2	,A14,'FIL:YTDGLT.XXX'
	FILE3	,A14,'FIL:GLAIDX.XXX'
	FILE4	,A14,'FIL:MASTER.XXX'
;
RECORD
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
PROC
	OPEN (15,U,'TT:')
	DISPLAY (15,27,'[',1,';',1,'H')
	DISPLAY (15,27,'[0J')
	DISPLAY (15,27,72)
	DISPLAY (15,27,74)
	DISPLAY (15,27,66)
	DISPLAY (15,27,66)
	DISPLAY (15,13,10,10,'        CONVERT GENERAL LEDGER FILES FROM' 
&	'REL 6.1/1.1 TO REL 7.0')
	DISPLAY (15,13,10,10,'   ',
&	13,10,'Data files should be placed in logical device FIL:',
&	13,10,'All users must be loged off the system.',
&	13,10,'   ',
&	13,10,'ENTER THE COMPANY CODE FOR CONVERSION : ')
	READS (15,CODE)
	DISPLAY (15,13,10,10,'   ',
&	13,10,'HAVE YOU BACKED UP YOUR DATA FILES <N> ')
	READS (15,ANS)
	IF (ANS.NE.'Y') GOTO END 
	DISPLAY (15,13,10,10,'   ',
&	13,10,'PROCESSING OCCURING...PLEASE WAIT')
	ON ERROR ERROR1
	FILE1(12,14) = CODE
	FILES = FILE1
	OPEN (1,I,FILE1)
	FILE2(12,14) = CODE
	FILES = FILE2
	OPEN (2,I,FILE2)
	FILES = 'FIL:GLAMAS.NEW'
	OPEN (3,O,'FIL:GLAMAS.NEW')
	FILES = 'FIL:GLAIDX.NEW'
	OPEN (4,O,'FIL:GLAIDX.NEW')
	FILES = 'FI:YTDGLT.NEW'
	OPEN (5,O,'FIL:YTDGLT.NEW')
	FILES = 'FIL:MASTER.NEW'
	OPEN (6,O,'FIL:MASTER.NEW')
	FILE3 (12,14) = CODE
	FILES = FILE3
	OPEN (7,I,FILE3)
	FILE4 (12,14) = CODE
	FILES = FILE4
	OPEN (8,I,FILE4)
	FILES = 
	OFF ERROR
REDCTL,
	ON ERROR ERROR
	READS (1,GLAOLD)
	XCALL RSTAT (SIZE,ANS)
	IF (SIZE.NE.215) GOTO ERROR
	OFF ERROR
	ORG031 = 1
	REC031=RECCNT
	MAX031=MAXREC
	DEL031=DELCNT
	TOTREC=MAXREC
	WRITES (3,GLANEW)
	GLINEW=
	WRITES (4,GLINEW)
BEGIN,
	INCR COUNT
	READS (1,GLAOLD,YTD)
	IF (GLAOLD.EQ.BRACKS)GO TO WRTBRK
	ACCTNO=ACTNO
	DESCR=GDESCR
	TYPBAL=PAREN
	FSTCDE(1,1) = FSTCOD(1,1)
	FSTCDE(2,2) = '0'
	FSTCDE(3,7) = FSTCOD(2,6)
	FSTCD2  = FSTCDE
	IF (GDESCR.EQ.']]]DEL')FSTCDE=']]]DEL'
	SCHCDE(1,1) = SCHCOD(1,1)
	SCHCDE(2,2) = '0'
	SCHCDE(3,7) = SCHCOD(2,6)
	SCHCD2 = SCHCDE
	SAFCDE=
	ACTCDE =
	USRCDE =
	SSTCDE='N'
	CMPCDE='N'
	SMPCDE='N'
	CTRACT =
	PYREND=PYRCOM(1)
	LYRBAL(1) = PYRCOM (2)
	SUB=1
LOOP,
	INCR SUB
	IF (SUB.GE.14) GO TO WRTMS1
	LYRBAL(SUB) = PYRCOM(SUB+1)
	BUDGET(SUB-1) =
	GO TO LOOP
WRTMS1,
	WRITES (3,GLANEW)
	IACTNO=ACTNO
	IFSTCD=FSTCDE
	ISAFCD =
	IACTCD =
	IRC031=COUNT
	IF (FSTCDE.EQ.']]]DEL') IRC031=
	WRITES (4,GLINEW)
	GO TO BEGIN
WRTBRK,
	GLANEW=BRACKS
	GLINEW=BRACKS
	WRITES (3,GLANEW)
	WRITES (4,GLINEW)
	IF (COUNT.GT.TOTREC) GO TO YTD
	INCR COUNT
	GO TO WRTBRK
YTD,
	READS (2,YTDOLD)
	ORG033=YORGCT
	REC033=YRECCT
	MAX033=YMAXRC
	DEL033=
	TOTREC=YMAXRC
	WRITES (5,YTDNEW)
	COUNT = 1
BEGIN2,
	INCR COUNT
	READS (2,YTDOLD,MASTR)
	IF (YTDOLD.EQ.BRACKS)GO TO WRTYTD
	YACTNO=GLACNO
	YTRXDT=GLDATE
	YSRCE=GLSRCE
	YPROJ=
	YTRXRF=GLREF
	YTRXAM=GLAMT
	YTRAIL=
	WRITES (5,YTDNEW)
	GO TO BEGIN2
WRTYTD,
	YTDNEW=BRACKS
	WRITES (5,YTDNEW)
	IF (COUNT.GT.TOTREC) GO TO MASTR
	INCR COUNT
	GO TO WRTYTD
MASTR,
	LOKCTL = 1
	WRITE (1,GLAOLD,1)
	MASTER (1,117) =
	COUNT =
	SUB =
DATELP,
	INCR SUB
	IF (SUB.GT.13) GO TO OUTLP
	IF (GLSTDT(SUB).EQ.0) GO TO OUTLP
	MSTDTS(SUB) = GLSTDT(SUB)
	MENDTS(SUB) = GLENDT(SUB)
	GO TO DATELP
OUTLP,
	MCSTNO = 1
	MCENNO = SUB-1
	MCSTDT = MSTDTS(1)
	MCENDT = MENDTS(SUB-1)
	MNOPRD = SUB-1
	WRITE (6,MASTER,1)
	GOTO CLOSE
;
ERROR,
	DISPLAY (15,13,10,10,'   ',
&	13,10,'?UT - GENERAL LEDGER FILES HAVE BEEN CONVERTED ALREADY',7)
	STOP
ERROR1,
	DISPLAY (15,13,10,10,'   ',
&	13,10,'?UT - UNEXPECTED ERROR WHILE OPENING FILE ',FILES,7)
	STOP
CLOSE,
	DISPLAY (15,13,10,10,'END OF G/L CONVERSION')
	CLOSE 1
	CLOSE 2
	CLOSE 3
	CLOSE 4
	CLOSE 5
	CLOSE 6
	CLOSE 7
	CLOSE 8
	ON ERROR DISTAS
	XCALL DELET (2,FILE1)
	XCALL DELET (3,FILE2)
	XCALL DELET (7,FILE3)
	XCALL DELET (8,FILE4)
	XCALL RENAM (3,FILE1,'FIL:GLAMAS.NEW')
	XCALL RENAM (4,FILE3,'FIL:GLAIDX.NEW')
	XCALL RENAM (5,FILE2,'FIL:YTDGLT.NEW')
	XCALL RENAM (6,FILE4,'FIL:MASTER.NEW')
	OFF ERROR
	DISPLAY (15,13,10,10,'   ',
&	13,10,'CONVERSION COMPLETE - THANKYOU')
	CLOSE 15
	GOTO END
DISTAS,
	DISPLAY (15,13,10,10,'   ',
&	13,10,'?UT - UNEXPECTED ERROR FILES MUST BE RESTORED FROM BACKUP',7)
	STOP
END,	
	STOP
	END
