;  JCFACE / GL 
;
;
;		::PCPYGL.DEF::
;******************************************************************************
;		GENERAL LEDGER
;
;		RELEASED: AUGUST 1, 1984 (d70s10)
;******************************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		GENERAL LEDGER INTERFACE FOR JOB COSTING
;
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD JCTOGL		;
		.INCLUDE 'DEF:RD085A.DEF'
RECORD JGLCTL, X	;
		.INCLUDE 'DEF:RD085B.DEF'
RECORD GLAMAS		;
		.INCLUDE 'DEF:RD031A.DEF'
RECORD	,X		;
		.INCLUDE 'DEF:RD031B.DEF'
RECORD GLAIDX		;
		.INCLUDE 'DEF:RD032A.DEF'
RECORD GLWORK		;
		.INCLUDE 'DEF:RD034A.DEF'
RECORD DUMGLW		;
		.INCLUDE 'DEF:RD034B.DEF'
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD EXPAND
	TRXCNA	,A6
		,A27,	' RECORDS BEFORE PROCEEDING.'
RECORD
	BLANKS	,A5
	BRACKS	,A25,	']]]]]]]]]]]]]]]]]]]]]]]]]'
	BSEND	,D5
	BSMID	,D5
	COL2	,D2
	CNGCTL	,D1
	COMPRS	,A1
	DATTOT	,D10
	DSTCNT	,D5
	DSTREC	,D5
	DTMASK	,A8,	'XX/XX/XX'
	ENDFLG	,D1
	ENTRY	,A6
	HSHADD	,D10
	INXCTL	,D1
	KEY	,A7
	KEYMSK	,A7,'XXXXXXX'
	MAXCNT	,D5
	POSTNG	,D1
	RECNO	,D5
	RECSIZ	,D2
	SAVACT	,D7
	SAVDAT	,D6
	SRCCTL	,D1
	SWITCH	,D1
	THRUDT	,D6
	TODAYA	,A8
	TODAY	,D6
	TRXCNT	,D5
	TRXDAT	,D6
	V	,D1
	READ	,D1,0
	WRITE	,D1,1
	LOKCTL	,D1
	WHATNO	,D1
PROC
	XCALL TERID (V)
	XCALL WATE(4,V)
	XCALL RDATE(TODAY)
	SWITCH = 3
	XCALL FILES (1,'U',34,SWITCH)	;OPEN & PROTECT FILE # 34 -- GLWORK FILE
	IF (SWITCH.EQ.9) GO TO ENDOFF
	SWITCH = 1
	XCALL FILES (4,'I',31,SWITCH)
	IF (SWITCH.EQ.9) GO TO CLOSE1
	XCALL FILES (5,'I',32,SWITCH)
	IF (SWITCH.EQ.9) GO TO CLOSE2
;	READ (4,GLAMAS,1)
	LOKCTL =
	XCALL IO (4,GLAMAS,1,READ,LOKCTL)
	IF (LOKCTL) GO TO CLOSE3
	BSEND = ORG031
	SWITCH = 5
	XCALL FILES (1,'U',34,SWITCH)
	IF (SWITCH.EQ.9) GO TO CLOSE3
;	READ (1,DUMGLW,1)
	LOKCTL =
	XCALL IO (1,DUMGLW,1,READ,LOKCTL)
	IF (LOKCTL) GO TO CLOSE4
	TRXCNT = REC034
	MAXCNT = MAX034
	GJSRCE(3,4) = 'IN'
	SAVACT = 0
	SAVDAT = 0
	POSTNG = 0
	HSHADD = 0
	GO TO JCFACE
FNDACT,
	XCALL SERCH(5,GLAIDX,KEY,1,7,BSEND,BSMID,SRCCTL,4,18,22,0,0,0,0)
	GO TO (NODESC), SRCCTL
;	READ (4,GLAMAS,IRC031)
	LOKCTL = 1
	XCALL IO (4,GLAMAS,IRC031,READ,LOKCTL)
	GJDESC = DESCR
	RETURN
NODESC,
	GJDESC = '*** ACCOUNT NOT ON FILE ***'
	RETURN
JCFACE,
	SWITCH = 3
	XCALL FILES (2,'U',85,SWITCH) ;OPEN & PROTECT FILE # 85 -- JCTOGL FILE
	IF (SWITCH.EQ.9) GO TO CLOSE3
	SWITCH = 5
	XCALL FILES (2,'U',85,SWITCH)
	IF (SWITCH.EQ.9) GO TO CLOSE4
	DSTCNT = 0
STRTPR,
;	READ (2,JCTOGL,1)
	LOKCTL = 1
	XCALL IO (2,JCTOGL,1,READ,LOKCTL)
	DSTREC = REC085 - 1
	IF (DSTREC.EQ.0) GO TO NODIST
	IF (TRXCNT + DSTREC.GT.MAXCNT) GO TO NOROOM
	RECNO = 1
RDNXT2,
	INCR RECNO
	IF (RECNO.GT.DSTREC+1) GO TO ENDJC
;	READ (2,JCTOGL,RECNO)
	LOKCTL = 1
	XCALL IO (2,JCTOGL,RECNO,READ,LOKCTL)
	TRXDAT (1,2) = JTRXDT (5,6)
	TRXDAT (3,6) = JTRXDT (1,4)
	IF (JTRXAC.EQ.SAVACT) GO TO WRITE2
	KEY = JTRXAC, KEYMSK
	CALL FNDACT
	SAVACT = JTRXAC
WRITE2,
	GJACCT = JTRXAC
	GJDATE = JTRXDT
	GJAMT = JTRXAM
	GJSRCE (1,2) = 'JC'
	GJREF (1,3) = 'JC-'
	GJREF (4,11) = TODAY, DTMASK
	GJREF (12,12) = '-'
	GJREF (13,25) = 'JOB COSTING'
	HSHADD = HSHADD + GJAMT
	IF (GJAMT.EQ.0) GO TO RDNXT2
	INCR REC034
;	WRITE (1,GLWORK,REC034)
	LOKCTL = 1
	XCALL IO (1,GLWORK,REC034,WRITE,LOKCTL)
	GO TO RDNXT2
ENDJC,
	HASH2 = HASH2 + HSHADD
;	WRITE (1,DUMGLW,1)
	LOKCTL = 1
	XCALL IO (1,DUMGLW,1,WRITE,LOKCTL)
	XCALL FILES (1,'U',34,4)	;CLOSE FILE # 34 -- GLWORK FILE
	CALL FILLER
	XCALL OUTPT (10,15,0,'INTERFACE FROM JOB COSTING HAS BEEN COMPLETED.',1)
	XCALL OUTPT (12,15,0,'THE INTERFACED TRX MAY NOW BE EDITED OR POSTED',1)
	XCALL OUTPT (14,15,0,'USING THE G/L TRX ENTRY & EDITING APPLICATION.',1)
	XCALL MESAG (' ',2)
	XCALL WATE(3,V)
	XCALL FILES (5,'I',32,4)
	XCALL FILES (4,'I',31,4)
	XCALL FILES (2,'U',85,4)	;CLOSE FILE # 85 -- JCTOGL FILE
	GO TO ENDOFF
FILLER,
;	READ (2,JCTOGL,1)
	LOKCTL = 1
	XCALL IO (2,JCTOGL,1,READ,LOKCTL)
	REC085 = MAX085 + 1
WRTBRA,
	REC085 = REC085 - 1
	IF (REC085.EQ.1) GO TO CNTRL
;	WRITE (2,BRACKS,REC085)
	LOKCTL = 1
	XCALL IO (2,BRACKS,REC085,WRITE,LOKCTL)
	GO TO WRTBRA
CNTRL,
;	WRITE (2,JGLCTL,1)
	LOKCTL = 1
	XCALL IO (2,JGLCTL,1,WRITE,LOKCTL)
	RETURN
NODIST,
	XCALL MESAG ('THERE ARE NO DISTRIBUTIONS TO INTERFACE',1)
	GO TO CLOSE4
NOROOM,
	TRXCNT = TRXCNT + DSTREC
	XCALL OUTPT (10,15,0,'INTERFACE FROM JOB COSTING CANNOT BE CARRIED OUT'
&	 ,1)
	XCALL OUTPT (12,15,0,'BECAUSE THE "GLWORK" FILE NEEDS MORE ROOM.',1)
	TRXCNA = TRXCNT, 'ZZ,ZZX'
	XCALL OUTPT (14,15,0,'PLEASE EXPAND THE "GLWORK" FILE TO AT LEAST',1)
	XCALL OUTPT (16,15,0,EXPAND,1)
	XCALL MESAG (' ',1)
CLOSE4,
	XCALL FILES (2,'U',85,4)
CLOSE3,
	XCALL FILES (5,'I',32,4)
CLOSE2,
	XCALL FILES (4,'I',31,4)
CLOSE1,
	XCALL FILES (1,'U',34,4)		;CLOSE FILE # 34 -- GLWORK FILE
ENDOFF,
	XCALL WATE(3,V)
	XCALL PGCHN ('GL:GLMENU',1)
INPUT,
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,V)
	RETURN

END

