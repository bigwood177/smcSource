;RCFACE.GL - ROCKFORD
;
;  ARFACE / GL 
;
;
;
;
RECORD	D_STUFF
	D_IN	,D8		;DATE-IN, ANY FORMAT
	D_OUT	,D6		;RETURN VALUE MMDDYY
	D_OUTR	,D8		;RETURN VALUE CCYYMMDD
	D_FMT	,A10		;RETURN VALUE MM/DD/CC/YY
	D_SW	,A2		;"99" = DATE CONVERSION ERROR


RECORD GLAMAS		;
		.INCLUDE 'DEF:RD031A.DEF'
RECORD	,X		;
		.INCLUDE 'DEF:RD031B.DEF'
RECORD GLAIDX		;
		.INCLUDE 'DEF:RD032A.DEF'
RECORD GLWORK		;
		.INCLUDE 'DEF:RD034A.DEF'
RECORD DUMGLW		;
		.INCLUDE 'DEF:RD034B.DEF'
RECORD ARDIST		;
		.INCLUDE 'DEF:RD008A.DEF'
RECORD	,X		;
		.INCLUDE 'DEF:RD008B.DEF'
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD EXPAND
	TRXCNA	,A6
		,A27,	' RECORDS BEFORE PROCEEDING.'
RECORD	VARS
	REDFIL	,A14
	WRKACT	,D7
	BLANKS	,A5
	BSEND	,D5
	BSMID	,D5
	CNGCTL	,D1
	COMPRS	,A1
	DATTOT	,D10
	DSTCNT	,D5
	DSTREC	,D5
	DTMASK	,A8,	'XX/XX/XX'
	ENDFLG	,D1
	ENTRY	,A8
	HSHADD	,D10
	INXCTL	,D1
	KEY	,A7
	KEYMSK	,A7,'XXXXXXX'
	MAXCNT	,D5
	POSTNG	,D1
	PSTDAT	,D8
	RECNO	,D5
	RECSIZ	,D2
	SAVACT	,D7
	SAVDAT	,D8
	SAVTYP	,D1
	SRCCTL	,D1
	SWITCH	,D1,3
	THRUDT	,D8
	TODAYA	,A10
	TODAY	,D8
	TDATE	,D6
	TRXCNT	,D5
	TRXDAT	,D8
	V	,D1
	WHATNO	,D1
RECORD
	READ	,D1,0
	WRITE	,D1,1
	LOKCTL	,D1
PROC
	XCALL TERID (V)
	V = 1
	XCALL RDATE(TDATE)
	XCALL DATE8(TDATE, D_OUT, TODAY, D_FMT, D_SW)
BEGIN,
	CNGCTL =
	XCALL OUTPT(2,1,2,'INTERFACE FROM A/R TO G/L -- ROCKFORD',V)
	XCALL OUTPT(10,15,0,'PLEASE ENTER:',V)
	XCALL OUTPT(12,20,0,'INTERFACE CUT-OFF DATE',V)
	XCALL OUTPT(14,20,0,'COMPRESS BY DATE ?',V)
THRUDT,
	CTL = '12,44,08,00,DE'
	CALL INPUT
	GO TO (BEGIN,ENDOFF), INXCTL
	THRUDT = ENTRY (1,8)
	IF (THRUDT.EQ.0) CALL TODAY
	IF (THRUDT.EQ.0) GO TO THRUDT
	PSTDAT = THRUDT
	GO TO COMPRS
TODAY,
	THRUDT = TODAY
	IF (THRUDT.EQ.0) RETURN
	XCALL DATE8(THRUDT, D_OUT, D_OUTR, TODAYA, D_SW)
	XCALL OUTPT(12,44,0,TODAYA,V)
	RETURN
COMPRS,
	CTL = '14,44,01,00,YY'
	CALL INPUT
	COMPRS = ENTRY (1,1)
	GO TO ANYCNG
PROCES,
	XCALL WATE(4,V)
	XCALL FILES (1,'U',34,SWITCH)	;OPEN & PROTECT FILE # 34 -- GLWORK FILE
	IF (SWITCH.EQ.9) GO TO ENDOFF
	SWITCH = 1
	XCALL FILES(4,'I',31,SWITCH)
	IF (SWITCH.EQ.9) GO TO CLOSE1
	XCALL FILES(5,'I',32,SWITCH)
	IF (SWITCH.EQ.9) GO TO CLOSE2
	LOKCTL = 1
	XCALL IO (4,GLAMAS,1,READ,LOKCTL)
	BSEND = ORG031
	XCALL FILES(1,'U',34,5)
	LOKCTL = 1
	XCALL IO (1,DUMGLW,1,READ,LOKCTL)
	TRXCNT = REC034
	MAXCNT = MAX034
	GJSRCE(3,4) = 'IN'
	SAVTYP = -1
	SAVACT = 0
	SAVDAT = 0
	POSTNG = 0
	HSHADD = 0
	GO TO ARFACE
FNDACT,
	XCALL SERCH(5,GLAIDX,KEY,1,7,BSEND,BSMID,SRCCTL,4,18,22,0,0,0,0)
	GO TO (NODESC), SRCCTL
	LOKCTL = 1
	XCALL IO (4,GLAMAS,IRC031,READ,LOKCTL)
	GJDESC = DESCR
	RETURN
NODESC,
	GJDESC = '*** ACCOUNT NOT ON FILE ***'
	RETURN
ARFACE,
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;	SSQ 1-22-04 get Rockford A/R distributions...

	XCALL FFILE (8,REDFIL,SWITCH)	;GET FILE SPEC
	REDFIL(1,3) = 'ROC'
	REDFIL(12,14) = 'ROC'
	OPEN (2,U,REDFIL)		;OPEN ROC:ARDIST.ROC

;;;	SWITCH = 3
;;;	XCALL FILES (2,'U',8,SWITCH)	;OPEN & PROTECT FILE # 8 -- ARDIST FILE
;;;	IF (SWITCH.EQ.9) GO TO CLOSE3
;;;	SWITCH = 5
;;;	XCALL FILES(2,'U',8,SWITCH)
;;;	IF (SWITCH.EQ.9) CALL CLOSED
;;;	IF (SWITCH.EQ.9) GO TO CLOSE3
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	DSTCNT = 0
STRTAP,
	LOKCTL = 1
	XCALL IO (2,ARDIST,1,READ,LOKCTL)
;;;ssq 01-27-04>>>	IF (GLFLAG.NE.'Y') GO TO NOINTF
	DSTREC = REC008
	RECNO = 1
RDNXT2,
	INCR RECNO
	IF (RECNO.GT.DSTREC) GO TO END2
	LOKCTL = 1
	XCALL IO (2,ARDIST,RECNO,READ,LOKCTL)
	IF (ADSFLG.EQ.1) GO TO RDNXT2
	TRXDAT = ADSDAT
	IF (TRXDAT.GT.PSTDAT) GO TO RDNXT2

;---------------------------------------------------------------------
; ssq 9-25-98
; consolidate state sales tax accounts...
	USING ADSACT SELECT
	(2000000 THRU 2000999),	WRKACT = 2055100	;IOWA
	(2100000 THRU 2100999),	WRKACT = 2059100	;ILL
	(2200000),		WRKACT = 2050100	;MN
	(2200001),		WRKACT = 2051100	;MPLS
	(2200002 THRU 2200999),	WRKACT = 2051100	;MPLS
	(2210000),		WRKACT = 2051100	;MPLS(non-tax)6/17/02 ssq
	(2400000 THRU 2400999),	WRKACT = 2060100	;NEB
	(2600000 THRU 2600999),	WRKACT = 2058100	;SD
	(2800000 THRU 2800999),	WRKACT = 8000100	;WISC
	(),			BEGIN
				WRKACT = ADSACT		;ANYTHING ELSE
				WRKACT(5,7) = 200	;ROCKFORD
				END
	ENDUSING
;---------------------------------------------------------------------

;;; ssq 4-10-07
;;; Sales to SMC go into intercompany account...
	IF (ADSCUS .EQ. 65545)
		BEGIN
		USING ADSTYP SELECT
		(1),	WRKACT = 1017200		;INCOME (SALES)
		(2),	WRKACT = 1017200		;OTHER CHARGES
		(3),	WRKACT = 1017200		;SALES TAX
		(4),	WRKACT = 1017200		;FREIGHT
		ENDUSING
		END
;-------------------------------------------------------


	IF (COMPRS.EQ.'Y') GO TO CMPRS2
	IF (WRKACT.NE.SAVACT.AND.POSTNG) GO TO ADSACT
	IF (POSTNG) GO TO WRITE2
	INCR DSTCNT
	GO TO RDNXT2
ADSACT,
	KEY = WRKACT, KEYMSK
	CALL FNDACT
	SAVACT = WRKACT
WRITE2,
	GJACCT = WRKACT

	GJDATE = ADSDAT
	GJAMT = ADSAMT
	SAVTYP = ADSTYP
	ADSFLG = 1
	CALL CHKTP2
	HSHADD = HSHADD + GJAMT
	CALL WRITE
	GO TO RDNXT2
CMPRS2,
	ADSFLG = 1
	IF (POSTNG) CALL WRITED
	IF (ADSTYP.NE.SAVTYP) GO TO NEWTP2
	IF (WRKACT.NE.SAVACT) GO TO NEWAC2
CNTNU2,
	IF (POSTNG) DATTOT = DATTOT + ADSAMT
	GO TO RDNXT2
NEWDT2,
	IF (SAVTYP.EQ.-1) GO TO FSTDT2
	IF (POSTNG) CALL WRTAPX
FSTDT2,
	IF (ENDFLG.EQ.1) RETURN
	SAVDAT = ADSDAT
	DATTOT = 0
	IF (ADSTYP.NE.SAVTYP.OR.WRKACT.NE.SAVACT) RETURN
	IF (POSTNG.EQ.0) INCR DSTCNT
	GO TO CNTNU2
NEWAC2,
	CALL NEWDT2
	IF (ENDFLG.EQ.1) RETURN
	KEY = WRKACT, KEYMSK
	IF (POSTNG) CALL FNDACT
	SAVACT = WRKACT
	IF (ADSTYP.NE.SAVTYP) RETURN
	IF (POSTNG.EQ.0) INCR DSTCNT
	GO TO CNTNU2
NEWTP2,
	CALL NEWAC2
	IF (ENDFLG.EQ.1) RETURN
	SAVTYP = ADSTYP
	IF (POSTNG.EQ.0) INCR DSTCNT
	GO TO CNTNU2
END2,
	ENDFLG = 1
	IF (COMPRS.EQ.'Y') CALL NEWTP2
	IF (POSTNG) GO TO ENDAR
	IF (DSTCNT.EQ.0) GO TO NODIST
	TRXCNT = TRXCNT + DSTCNT
	IF (TRXCNT.GT.MAXCNT) GO TO NOROOM
	SAVTYP = -1
	SAVACT = 0
	SAVDAT = 0
	DATTOT = 0
	POSTNG = 1
	ENDFLG = 0
	GO TO STRTAP
WRTAPX,
	GJACCT = SAVACT
	KEY = GJACCT, KEYMSK
	CALL FNDACT
	GJDATE = THRUDT		;consolidate all trx for account
	GJAMT = DATTOT
	CALL CHKTP2
	HSHADD = HSHADD + GJAMT
	CALL WRITE
	RETURN
CHKTP2,
	IF (SAVTYP.NE.0.AND.SAVTYP.NE.6.AND.SAVTYP.NE.7) GJAMT = GJAMT * (-1)
	GJSRCE (1,2) = 'AR'
	GJREF (1,3) = 'AR-'
	GJREF (4,11) = TDATE, DTMASK
	GJREF (12,12) = '-'
	IF (SAVTYP.EQ.0) GJREF (13,25) = 'DEBIT TO A/R'
	IF (SAVTYP.EQ.1) GJREF (13,25) = 'INCOME'
	IF (SAVTYP.EQ.2) GJREF (13,25) = 'OTHER CHARGES'
	IF (SAVTYP.EQ.3) GJREF (13,25) = 'SALES TAX'
	IF (SAVTYP.EQ.4) GJREF (13,25) = 'FREIGHT'
	IF (SAVTYP.EQ.5) GJREF (13,25) = 'FINANCE CHGS'
	IF (SAVTYP.EQ.6) GJREF (13,25) = 'CASH RECEIVED'
	IF (SAVTYP.EQ.7) GJREF (13,25) = 'DISC/ALLOWNCE'
	IF (SAVTYP.EQ.8) GJREF (13,25) = 'CREDIT TO A/R'
	IF (SAVTYP.EQ.9) GJREF (13,25) = 'NON A/R INC'
	RETURN
ENDAR,
	HASH2 = HASH2 + HSHADD
	LOKCTL = 1
	XCALL IO (1,DUMGLW,1,WRITE,LOKCTL)
	XCALL FILES (1,'U',34,04)	;CLOSE FILE # 34 -- GLWORK FILE

	CLOSE 8				;SSQ 1-22-04
;;;	XCALL FILES (2,'U',8,04)	;CLOSE FILE # 8 -- ARDIST FILE
	GO TO DONMSG
WRITE,
	IF (ENDFLG.NE.1) CALL WRITED
	IF (GJAMT.EQ.0) RETURN
	INCR REC034
	LOKCTL = 1
	XCALL IO (1,GLWORK,REC034,WRITE,LOKCTL)
	RETURN
WRITED,
	LOKCTL = 1
	XCALL IO (2,ARDIST,RECNO,WRITE,LOKCTL)
	RETURN
NOINTF,
	XCALL MESAG('SYSTEM NOT SET TO INTERFACE TO G/L',1)
	CALL CLOSED
	GO TO CLOSE3
DONMSG,
	XCALL OUTPT(10,15,2,'INTERFACE FROM A/R HAS BEEN COMPLETED.',V)
	XCALL OUTPT(12,15,0,'THE INTERFACED TRX MAY NOW BE EDITED OR POSTED',V)
	XCALL OUTPT(14,15,0,'USING THE G/L TRX ENTRY & EDITING APPLICATION.',V)
	XCALL MESAG (' ',2)
	XCALL WATE(3,V)
	XCALL FILES(5,'I',32,4)
	XCALL FILES(4,'I',31,4)
	GO TO ENDOFF
CLOSED,

	CLOSE 8			;SSQ 1-22-04
;;;	XCALL FILES (2,'I',8,04)
	RETURN
NODIST,
	XCALL MESAG ('THERE ARE NO DISTRIBUTIONS TO INTERFACE',1)
	CALL CLOSED
	GO TO CLOSE3
NOROOM,
	CALL CLOSED
	XCALL OUTPT(10,15,2,'INTERFACE FROM A/R CANNOT BE CARRIED OUT',V)
	XCALL OUTPT(12,15,0,'BECAUSE THE "GLWORK" FILE NEEDS MORE ROOM.',V)
	TRXCNA = TRXCNT, 'ZZ,ZZX'
	XCALL OUTPT(14,15,0,'PLEASE EXPAND THE "GLWORK" FILE TO AT LEAST',V)
	XCALL OUTPT(16,15,0,EXPAND,V)
	XCALL MESAG (' ',1)
CLOSE3,
	XCALL FILES(5,'I',32,4)
CLOSE2,
	XCALL FILES(4,'I',31,4)
CLOSE1,
	XCALL FILES(1,'U',34,4)		;CLOSE FILE # 34 -- GLWORK FILE
ENDOFF,
	XCALL WATE(3,V)
	XCALL PGCHN ('GL:GLMENU',1)
INPUT,
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,V)
	RETURN
ANYCNG,
	CNGCTL = 2
	XCALL ANYCN (CNGCTL,WHATNO)
	GO TO (PROCES,BEGIN,BEGIN), CNGCTL + 1
END
