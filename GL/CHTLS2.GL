;CHTLS2.GL
;  CHTLST / GL 
;
RECORD MASTER		; 
		.INCLUDE 'DEF:RD070A.DEF'
RECORD GLAMAS		; 
		.INCLUDE 'DEF:RD031A.DEF'
RECORD GLAIDX		; 
		.INCLUDE 'DEF:RD032A.DEF'
RECORD TITLE
		,A22,'CHART OF ACCOUNTS LIST'
RECORD LEGND1
		,A7
		,A2
	CLTNAM	,A30
RECORD LEGND2
		,A6,'CODES:'
		,A2
		,A72,'TYP=DR/CR  FINCL=FINANCIAL FMT  SCHED=SCHEDULE FMT  SAF=SRC&APP OF FUNDS'
RECORD LEGND3
		,A8
		,A72,'USR=USER DEFINED  SST=SECTION SUBTOT  SPC=SUMMARY POST  CMP=COMPRESS FLG'
RECORD HDR1
		,A16,'----------------'
		,A7,'ACCOUNT'
		,A17,'-----------------'
		,A2
		,A27,'---------------------------'
		,A5,'CODES'
		,A27,'---------------------------'
RECORD HDR2
		,A3
		,A2,'NO'
		,A5
		,A11,'DESCRIPTION'
		,A21
		,A3,'TYP'
		,A2
		,A5,'FINCL'
		,A4
		,A6,'FINCL2'
		,A3
		,A5,'SCHED'
		,A4
		,A6,'SCHED2'
		,A2
		,A3,'SAF'
		,A1
		,A3,'USR'
		,A1
		,A3,'SST'
		,A1
		,A3,'SPC'
		,A1
		,A3,'CMP'
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2

RECORD	FFILE
	REDFIL	,A14
	WRKFIL	,A14
	CLCTL	,D1

RECORD	VARS
	ACONFI	,D5
	ALLCTL	,D1
	BLANKS	,A6
	COMPSW	,D1
	CNGCTL	,D1
	ENTRY	,A30
	ENDACT	,D7
	INXCTL  ,D1
	SWITCH	,D1,1
	LPSW	,D1
	LINCNT	,D2,60
	MASK	,A14,'ZZZZZZZZZZ.XX-'
	PFCODE	,D3
	PGCNT	,D3
	PLINE	,A132
	PAGSIZ	,D3
	PRNTSW	,D1
	STRACT	,D7
	SUBEND	,D12
	SUBBUD	,D12
	TOTEND	,D12
	TOTBUD	,D12
	V	,D1
	WHATNO	,D2
	YES	,D1,	1

	COL2	,D2
	READ	,D1	,0
	WRITE	,D1	,1
	LPARG	,D1,2
	RPTNUM	,D3
	PRTTYP	,A1
	SPLFIL	,A14
	LOKCTL	,D1

PROC 
	XCALL TERID (V)
	V = 1
BEGIN,
	XCALL FILES (1,'I',70,SWITCH)		;#70 MASTER FILE
	IF (SWITCH.EQ.9) GO TO END
	LOKCTL = 1
	XCALL IO (1,MASTER,1,READ,LOKCTL)
	CLTNAM = MCLTNM

	XCALL FFILE (32, REDFIL, CLCTL)
	WRKFIL = REDFIL
	WRKFIL(12,14) = 'FST'
	XCALL COPY (REDFIL, WRKFIL)
	SORT (IN=WRKFIL, RECORD=GLAIDX, KEY=(IFSTCD, IACTNO))
	OPEN (3,I,WRKFIL)

	XCALL FILES (2,'I',31,SWITCH)		;#31 GLAMAS FILE
	IF (SWITCH.EQ.9) GO TO END1
;;;	XCALL FILES (3,'I',32,SWITCH)		;#32 GLAIDX FILE
	IF (SWITCH.EQ.9) GO TO END2
	PAGSIZ = 132
SCREEN,
	IF (ALLCTL.EQ.1.AND.INXCTL.EQ.0) GO TO DONE
	XCALL OUTPT (1,1,2,'CHART OF ACCOUNTS FINANCIAL STATEMENT CODES',V)
	XCALL OUTPT (2,1,1,'PRINT OUT',V)
	XCALL OUTPT (10,20,2,'PLEASE ENTER SELECTION PARAMETERS',V)
	XCALL OUTPT (12,25,0,'1. STARTING G/L ACCOUNT #',V)
	XCALL OUTPT (14,25,0,'2. ENDING G/L ACCOUNT #',V)
	XCALL OUTPT (16,25,0,'3. ONE PROFIT CENTER (OR "ALL") ',V)
	XCALL OUTPT (18,25,0,'4. SHOW BUDGETS & COMPARATIVES ?',V)
STNO,
	XCALL OUTPT (12,63,0,BLANKS,V)
	CTL = '12,59,04,00,AE'
	CALL INPUT
	GO TO (SCREEN,DONE), INXCTL
	ALLCTL =
	IF (ENTRY.EQ.'ALL   '.OR.ENTRY.EQ.BLANKS) GO TO SETALL
	ON ERROR STNO
	STRACT (1,4) = ENTRY (1,4)
	OFF ERROR
	XCALL OUTPT (12,63,0,'-',V)
	CTL = '12,64,03,00,# '
	CALL INPUT
	GO TO (STNO), INXCTL
	IF (ENTRY(1,3).NE.BLANKS) GO TO STNXT
	XCALL OUTPT (12,63,1,'\',V)
STNXT,
	STRACT (5,7) = ENTRY (1,3)
	GO TO (ANYCNG), CNGCTL
ENDNO,
	GO TO (ANYCNG), ALLCTL
	XCALL OUTPT (14,63,0,BLANKS,V)
	CTL = '14,59,04,00,# '
	CALL INPUT
	GO TO (SCREEN), INXCTL
	IF (ENTRY.EQ.BLANKS) GO TO SAME
	ENDACT (1,4) = ENTRY (1,4)
	XCALL OUTPT (14,63,0,'-',V)
	CTL = '14,64,03,00,# '
	CALL INPUT 
	GO TO (ENDNO), INXCTL
	IF (ENTRY.NE.BLANKS) GO TO ENDNXT
	XCALL OUTPT (14,63,1,'\',V)
ENDNXT,
	ENDACT (5,7) = ENTRY (1,3)
	GO TO (ANYCNG), CNGCTL
	GO TO PFCNTR
SAME,
	ENDACT = STRACT
	ENTRY (1,8) = ENDACT, 'XXXX-XXX'
	XCALL OUTPT (14,59,0,ENTRY(1,8),V)
	GO TO (ANYCNG), CNGCTL
PFCNTR,
	CTL = '16,59,03,00,A '
	CALL INPUT
	GO TO (SCREEN), INXCTL
	IF (ENTRY(1,3) .EQ. '   ')  	XCALL OUTPT (16,59,0,'ALL',V)
	IF (ENTRY(1,3).EQ.'ALL') ENTRY =
	ON ERROR INVNUM
	PFCODE = ENTRY(1,3)
	OFF ERROR
	GO TO (ANYCNG),CNGCTL
LYRBL,
	CTL = '18,59,01,00,A '
	XCALL OUTPT (24,1,1,'"N"=NEITHER   "Y"=BOTH   "B"=BUDGETS   "C"=COMPARATIVES',V)
	CALL INPUT
	GO TO (SCREEN), INXCTL
	IF (ENTRY(1,1).EQ.' ') ENTRY (1,1) = 'N'
	XCALL OUTPT (18,59,0,ENTRY(1,1),V)
	COMPSW=-1
	IF (ENTRY(1,1).EQ.'N') COMPSW = 0
	IF (ENTRY(1,1).EQ.'B') COMPSW = 1
	IF (ENTRY(1,1).EQ.'C') COMPSW = 2
	IF (ENTRY(1,1).EQ.'Y') COMPSW = 3
	IF (COMPSW.EQ.-1) XCALL OUTPT (07,0,-1,'CNTRL',V)
	IF (COMPSW.EQ.-1) GO TO LYRBL
	XCALL OUTPT (24,1,1,'\',V)
	GO TO ANYCNG
SETALL,
	XCALL OUTPT (12,59,0,'ALL  ',V)
	XCALL OUTPT (14,59,1,'\',V)
	ALLCTL = 1
	GO TO (ANYCNG), CNGCTL
	GO TO PFCNTR
ALL,
	ACONFI = 0
	TOTBUD =
	TOTEND =
	GO TO READ
CNGBR,
	GO TO (STNO,ENDNO,PFCNTR,LYRBL),WHATNO
	GO TO SCREEN
INVNUM,
	OFF ERROR
	XCALL MESAG (' ',3)
	GO TO PFCNTR
PROCES,
	XCALL OUTPT (3,1,2,'\',V)
	LOKCTL = 1
	XCALL IO (3,GLAIDX,1,READ,LOKCTL)
	GO TO (ALL), ALLCTL
READ,
	LOKCTL = 1
	XCALL IOS (3,GLAIDX,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOF
	IF (GLAIDX.EQ.']]]]]]') GO TO EOF
	IF (IRC031.EQ.0) GO TO READ
	IF (PFCODE.NE.0.AND.IACTNO(5,7).NE.PFCODE) GO TO CHKSUB
	GO TO (PALL),ALLCTL
	IF (IACTNO.LT.STRACT) GO TO READ
	IF (IACTNO.GT.ENDACT) GO TO READ
;;;	IF (IACTNO.GT.ENDACT) GO TO ENDRNG
PALL,
	LOKCTL = 1
	XCALL IO (2,GLAMAS,IRC031,READ,LOKCTL)
	PLINE (1,8) = ACCTNO,'XXXX-XXX'
	PLINE (11,40) = DESCR
	PLINE (44,44) = TYPBAL
	PLINE (47,53) = FSTCDE
	PLINE (56,62) = FSTCD2
	PLINE (65,71) = SCHCDE
	PLINE (74,80) = SCHCD2
	PLINE (84,84) = SAFCDE
	PLINE (88,88) = USRCDE
	PLINE (92,92) = SSTCDE
	PLINE (96,96) = SMPCDE
	PLINE (100,100) = CMPCDE
	IF (COMPSW.EQ.0) GO TO PCALL
	IF (LINCNT.LT.55) GO TO PCALL
	LINCNT = 60
PCALL,
	CALL PRINT
	INCR ACONFI
	XCALL LINFD (1)
	INCR LINCNT
	IF (FSTCDE(6,6).NE.'V') GO TO READ
	IF (COMPSW.EQ.0) GO TO READ
	IF (COMPSW.GE.2) GO TO PBAL
	GO TO PBAL2
CHKSUB,
	IF (ALLCTL) GO TO CHKSB2
	IF (IACTNO.LT.STRACT.OR.IACTNO.GT.ENDACT) GO TO READ
CHKSB2,
	IF (COMPSW.NE.1.AND.COMPSW.NE.3) GO TO READ
	LOKCTL = 1
	XCALL IO (2,GLAMAS,IRC031,READ,LOKCTL)
	IF (SSTCDE.NE.'Y') GO TO READ
	GO TO ADJSUB
ENDRNG,
	IF (PRNTSW.EQ.1) CALL LPOFF
	GO TO SCREEN
EOF,
	IF (ALLCTL.NE.1) GO TO ENDRNG
	IF (COMPSW.EQ.0)GO TO EOF2
	IF (COMPSW.EQ.2.OR.COMPSW.EQ.3) PLINE (1,13) = TOTEND,MASK
	IF (COMPSW.EQ.2.OR.COMPSW.EQ.3) PLINE (15,30) = '= ENDING BALANCE'
	IF (COMPSW.EQ.1.OR.COMPSW.EQ.3) PLINE (53,66) = 'TOTAL BUDGET ='
	IF (COMPSW.EQ.1.OR.COMPSW.EQ.3) PLINE (67,80) = TOTBUD,MASK
	CALL PRINT
	XCALL LINFD (1)
	INCR LINCNT
EOF2,
	PLINE (4,8) = ACONFI
	PLINE (10,29) = 'ACCOUNTS ON FILE    '
	CALL PRINT
DONE,
	IF (PRNTSW.EQ.1) CALL LPOFF
	XCALL WATE(3,V)
;;;	XCALL FILES (3,'I',32,4)
END2,
	XCALL FILES (2,'I',31,4)
END1,
	XCALL FILES (1,'I',70,4)
END,
	XCALL PGCHN ('GL:GLMENU',1)
PBAL,
	PLINE (1,12) = PYREND,MASK
	SUBEND = SUBEND + PYREND
	TOTEND = TOTEND + PYREND
	PLINE (14,51) = '= PRIOR YREND / ----COMPARATIVES---- \'
	CALL PRINT
	PLINE (4,15) = LYRBAL (1), MASK
	PLINE (17,28) = LYRBAL (2), MASK
	PLINE (30,41) = LYRBAL (3), MASK
	PLINE (43,54) = LYRBAL (4), MASK
	PLINE (56,67) = LYRBAL (5), MASK
	PLINE (69,80) = LYRBAL (6), MASK
	CALL PRINT
	PLINE (4,15) = LYRBAL (7), MASK
	PLINE (17,28) = LYRBAL (8), MASK
	PLINE (30,41) = LYRBAL (9), MASK
	PLINE (43,54) = LYRBAL (10), MASK
	PLINE (56,67) = LYRBAL (11), MASK
	PLINE (69,80) = LYRBAL (12), MASK
	IF (MNOPRD.GE.7) CALL PRINT
	PLINE =
	PLINE (4,15) = LYRBAL(13),MASK
	IF (MNOPRD.GE.13) CALL PRINT
	PLINE =
	IF (COMPSW.EQ.3) GO TO PBAL2
	GO TO ADJSUB
PBAL2,
	SUBBUD = SUBBUD + BUDGET(MNOPRD)
	TOTBUD = TOTBUD + BUDGET(MNOPRD)
	PLINE (27,52) = '/ -------BUDGETS------- \'
	CALL PRINT
	PLINE (4,15) = BUDGET (1), MASK
	PLINE (17,28) = BUDGET (2), MASK
	PLINE (30,41) = BUDGET (3), MASK
	PLINE (43,54) = BUDGET (4), MASK
	PLINE (56,67) = BUDGET (5), MASK
	PLINE (69,80) = BUDGET (6), MASK
	CALL PRINT
	PLINE (4,15) = BUDGET (7), MASK
	PLINE (17,28) = BUDGET (8), MASK
	PLINE (30,41) = BUDGET (9), MASK
	PLINE (43,54) = BUDGET (10), MASK
	PLINE (56,67) = BUDGET (11), MASK
	PLINE (69,80) = BUDGET (12), MASK
	IF (MNOPRD.GE.7) CALL PRINT
	PLINE =
	PLINE (4,15) = BUDGET (13), MASK
	IF (MNOPRD.GE.13) CALL PRINT
ADJSUB,
	PLINE =
	CALL PRINT
SUBTOT,
	IF (SSTCDE.NE.'Y') GO TO READ
	IF (COMPSW.EQ.0)GO TO READ
	IF (COMPSW.EQ.2.OR.COMPSW.EQ.3) PLINE (1,13) = SUBEND,MASK
	IF (COMPSW.EQ.2.OR.COMPSW.EQ.3) PLINE (15,35) ='=ENDING BAL SUB-TOTAL'
	IF (COMPSW.EQ.1.OR.COMPSW.EQ.3) PLINE (46,63) = 'SUB-TOTAL BUDGET ='
	IF (COMPSW.EQ.1.OR.COMPSW.EQ.3) PLINE (65,80) = SUBBUD,MASK
	CALL PRINT
	XCALL LINFD (2)
	LINCNT = LINCNT +2
	SUBBUD =
	GO TO READ
PRINT,
	IF (PRNTSW.EQ.0) CALL LPON
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,'NO HDR3',LEGND2,LEGND3,
&		'NO LEG',0,PAGSIZ,132,1,LPSW,RPTNUM,PRTTYP)
	RETURN
LPON,
	LPSW = 1
	SPLFIL (5,6) = 'BA'
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO DONE
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTSW = 1
	RETURN
LPOFF,
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	PGCNT =
	LINCNT = 60
	PRNTSW =
	RETURN
INPUT,
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,V)
	RETURN
ANYCNG,
	WHATNO =
	XCALL ANYCN (CNGCTL,WHATNO)
	IF (WHATNO.GT.4) GO TO ANYCNG
	GO TO (PROCES,CNGBR,SCREEN), CNGCTL+1
	GO TO ANYCNG
END

