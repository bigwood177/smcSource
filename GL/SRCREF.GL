;  SRCREF / GL 
;
;
;		THIS PROGRAM PRINTS THE GENERAL LEDGER SOURCE CROSS REFERENCE
;
;

RECORD	D_STUFF
	D_IN	,D8		;DATE-IN, ANY FORMAT
	D_OUT	,D6		;RETURN VALUE MMDDYY
	D_OUTR	,D8		;RETURN VALUE CCYYMMDD
	D_FMT	,A10		;RETURN VALUE MM/DD/CC/YY
	D_SW	,A2		;"99" = DATE CONVERSION ERROR


RECORD GLAMAS		; 
		.INCLUDE 'DEF:RD031A.DEF'
RECORD DUMGLA		; 
		.INCLUDE 'DEF:RD031B.DEF'
RECORD GLAIDX		; 
		.INCLUDE 'DEF:RD032A.DEF'
RECORD SRCIDX		; 
		.INCLUDE 'DEF:RD035A.DEF'
RECORD YTDGLT		; 
		.INCLUDE 'DEF:RD033A.DEF'
RECORD	,X		; 
		.INCLUDE 'DEF:RD033B.DEF'
RECORD TITLE
		,A22,'SOURCE CROSS REFERENCE'
RECORD HDR1
		,A9,'G/L ACCT:'
		,A1
	GLACCT	,A8
		,A2
	GLNAME	,A30
		,A30
RECORD HDR2
		,A80
RECORD HDR3
		,A6,'SOURCE'
		,A4
		,A8,'TRX-DATE'
		,A2
		,A13,'TRX-REFERENCE'
		,A21
		,A10,'TRX-AMOUNT'
		,A8
		,A7,'BALANCE'
		,A1
RECORD LEGND2
		,A7,'PERIOD:'
		,A2
	PDFROM	,A8
		,A4,' TO '
	PDTO	,A8
		,A26
		,A14,'PROFIT CENTER:'
		,A2
	PCNTR	,A3
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD
	BALACT	,A7
	BALANC	,D10
	BSEND	,D5
	BSMID	,D5
	CNGCTL	,D1
	COUNTR	,D5
	COUNT	,D5
	DASHES	,A20,	'--------------------'
	DETLSW	,A1
	DTMASK	,A8,'XX/XX/XX'
	ENDFLG	,D1
	ENDNO	,A7
	ENTRY	,A8
	GLTRCT	,D5
	GTXAMT	,D11
	IDXCNT	,D5
	INXCTL	,D1
	SWITCH	,D1,1
;	KEY	,D7			;GL16
	KEY	,A7			;GL16
	KEY2	,D5
	LPSW	,D1
	LINCNT	,D2,60
	LSTACC	,A7,'*******'
	LSTSRC	,A4,'****'
	MAXCNT	,D5
	MASK	,A8,'XXXX-XXX'
	MASK2	,A15,'ZZZ,ZZZ,ZZZ.XX-'
	MINUS	,D10
	MULSRC	,A4
	NEWSRC	,D1
	NODAYS	,D4
	NUMSRC	,D3
	PAGSIZ	,D3,080
	PGCNT	,D3
	PLINE	,A80
	PRTCTL	,D3
	PRFCTR	,D3
	RECCNT	,D10
	RECNO	,D5
	SRCCNT	,D4
	SRCCTL	,D1
	SRCFLG	,D1
	SSTCXT	,D4
	SSTXAT	,D10
	SSTAMT	,D10
	STRACT	,A7
	STRBAL	,D10
	STRHD1	,A80
	STRHD2	,A80
	STRHD3	,A80
	STRORG	,D5
	STRTNO	,A7
	STRYAC	,A7
	STXCNT	,D4
	TEST	,D4
	TRXCNT	,D5
	V	,D1
	WHATNO	,D2
	YES	,D1,1
	YTRXCT	,D6
RECORD
	COL2	,D2
	READ	,D1,0
	WRITE	,D1	,1
	LOKCTL	,D1
	LPARG	,D1,2
	PRNTSW	,D1
	PRTTYP	,A1
	RPTNUM	,D3
	SPLFIL	,A14
	TOTBAL  ,D10
PROC 
	XCALL TERID (V)
	STRHD1=HDR1
	STRHD2=HDR2
	STRHD3=HDR3
START,
	XCALL OUTPT (1,1,0,'PRINT ',V)
	XCALL OUTPT (0,0,0,TITLE,V)
	XCALL OUTPT (2,0,0,'\',V)
	XCALL OUTPT (2,1,1,'PRINT OUT',1)
	XCALL FILES (1,'I',31,SWITCH)		;FILE # 031 -- GLAMAS FILE
	IF (SWITCH.EQ.9) GO TO ENDOFF
	LOKCTL = 1
	XCALL IO (1,DUMGLA,1,READ,LOKCTL)
	MAXCNT=MAX031
	PDFROM = SCSTDT,DTMASK
	PDTO = SCENDT,DTMASK
	XCALL FILES (2,'I',32,SWITCH)		;FILE # 032 -- GLAIDX FILE
	IF (SWITCH.NE.9) GO TO OPEN3
	CALL CLOSE1
	GO TO ENDOFF
OPEN3,
	XCALL FILES (4,'I',33,SWITCH)		;FILE # 033 -- YTDGLT FILE
	IF (SWITCH.NE.9) GO TO OPEN5
	CALL CLOSE2
	GO TO ENDOFF
OPEN5,
	XCALL FILES (5,'I',35,5)		;FILE # 035 -- SRCIDX FILE
	LOKCTL = 1
	XCALL IO (5,SRCIDX,1,READ,LOKCTL)
	PRFCTR=SISRCE
	PCNTR='ALL'
	IF (PRFCTR.NE.0) PCNTR=PRFCTR,'XXX'
BEGIN,
	XCALL OUTPT (4,20,2,'PLEASE ENTER PRINT PARAMETERS',V)
	XCALL OUTPT (5,25,0,'1. SOURCE DESIGNATION (OR "ALL")',V)
	XCALL OUTPT (6,25,0,'2. STARTING G/L ACCOUNT #',V)
	XCALL OUTPT (7,25,0,'3. ENDING G/L ACCOUNT #',V)
	XCALL OUTPT (8,25,0,'4. SHOW TRANSACTION DETAIL ?',V)
SOURCE,
	CTL = '05,59,04,00,AE'
	CALL INPUT
	GO TO (BEGIN,END), INXCTL
	IF (ENTRY.EQ.'    ')	XCALL OUTPT (5,59,0,'ALL',V)
	IF (ENTRY(1,3).EQ.'ALL') ENTRY=
	MULSRC=ENTRY(1,4)
	GO TO (CHANGE), CNGCTL
STRTGL,
	CTL = '06,52,04,00,# '
	XCALL OUTPT (6,59,0,' ',V)
	CALL INPUT
	GO TO (BEGIN), INXCTL
	IF (ENTRY(1,4).EQ.'    ') GO TO ALL
	STRTNO(1,4) = ENTRY(1,4)
	XCALL OUTPT (6,56,0,'-',V)
	CTL (4,8) = '57,03'
	CALL INPUT
	GO TO (STRTGL), INXCTL
	IF (ENTRY(1,3).EQ.'   ') 	XCALL OUTPT (6,57,0,'000',V)
	IF (ENTRY(1,3).EQ.'   ') ENTRY(1,3) = '000'
	STRTNO(5,7) = ENTRY(1,3)
	GO TO (CHANGE), CNGCTL
ENDGL,
	IF (STRTNO.EQ.'ALL') GO TO CHANGE
	CTL = '07,52,04,00,# '
	XCALL OUTPT (7,59,0,' ',V)
	CALL INPUT
	GO TO (BEGIN), INXCTL
	IF (ENTRY(1,4).EQ.'    ') GO TO SAME
	ENDNO(1,4)=ENTRY(1,4)
	XCALL OUTPT (7,56,0,'-',V)
	CTL (4,8) = '57,03'
	CALL INPUT
	GO TO (ENDGL), INXCTL
	IF (ENTRY(1,3).EQ.'   ') 	XCALL OUTPT (7,57,0,'000',V)
	IF (ENTRY(1,3).EQ.'   ') ENTRY(1,3)='000'
	ENDNO(5,7) = ENTRY(1,3)
	GO TO ENDONE
SAME,
	ENDNO = STRTNO
	YACTNO = ENDNO
	ENTRY (1,8) = YACTNO, MASK
	XCALL OUTPT (7,52,0,ENTRY(1,8),V)
ENDONE,
	GO TO (CHANGE), CNGCTL
	GO TO DTLAGE
ALL,
	STRTNO='ALL    '
	XCALL OUTPT (6,52,0,STRTNO,V)
	ENDNO=
	XCALL OUTPT (7,52,0,ENDNO,V)
	XCALL OUTPT (7,59,0,' ',V)
	GO TO (CHANGE), CNGCTL
DTLAGE,
	CTL = '08,55,01,00,A '
	CALL INPUT
	GO TO (BEGIN), INXCTL
	IF (ENTRY(1,1).EQ.' ') ENTRY(1,1)='N'
	XCALL OUTPT (8,55,0,ENTRY(1,1),V)
	IF (ENTRY(1,1).NE.'Y'.AND.ENTRY(1,1).NE.'N')  GO TO DTLAGE
	DETLSW=ENTRY(1,1)
CHANGE,
	CNGCTL=
	XCALL ANYCN (CNGCTL,WHATNO)
	GO TO (PROCES,CNGBR,BEGIN), CNGCTL+1
CNGBR,
	GO TO (SOURCE,STRTGL,ENDGL,DTLAGE), WHATNO
PROCES,
	HDR2=STRHD2
	HDR3=STRHD3
	LOKCTL = 1
	XCALL IO (5,SRCIDX,1,READ,LOKCTL)
	IDXCNT=SIRCNO
	COUNTR=1
READIX,
	INCR COUNTR
	IF (COUNTR.GT.IDXCNT) GO TO EOF
	LOKCTL = 1
	XCALL IO (5,SRCIDX,COUNTR,READ,LOKCTL)
	IF (MULSRC.EQ.'   '.OR.SISRCE.EQ.MULSRC) GO TO SRCOK
	GO TO READIX
SRCOK,
	IF (STRTNO.NE.ENDNO.AND.LSTSRC.EQ.'****') GO TO FORM2
	IF (STRTNO.NE.ENDNO) GO TO READY2
READYT,
	LOKCTL = 1
	XCALL IO (4,YTDGLT,SIRCNO,READ,LOKCTL)
	STRACT = YACTNO,'XXXXXXX'
	IF (STRACT.LT.STRTNO) GO TO READIX
	IF (STRACT.EQ.STRTNO) GO TO ACCTOK
	IF (STRACT.GT.STRTNO.AND.MULSRC.EQ.'    ') GO TO READIX
	GO TO EOF
ACCTOK,
	IF (SISRCE.NE.LSTSRC) CALL SRCSUB
	IF (YACTNO.NE.IACTNO)CALL FNDACT
	GLACCT=ACCTNO,MASK
	GLNAME=DESCR
PLINES,
	IF (NEWSRC.NE.1) GO TO PRNTSR
	PLINE (1,1) = '='
	PLINE (2,6) = SISRCE
	PLINE (7,7) = '='
PRNTSR,
	IF (BALANC.NE.0)PLINE (67,80) = BALANC,MASK2
	IF (DETLSW.NE.'N') GO TO PLCONT
	HDR2 =
	HDR3 (1,58) =
PLCONT,
	IF (NEWSRC.EQ.1) CALL PRINT
	IF (NEWSRC.EQ.1) PLINE =
	IF (DETLSW.EQ.'N') GO TO SKPLIN
	XCALL DATE8(YTRXDT, D_OUT, D_OUTR, D_FMT, D_SW)
	PLINE (9,20) = D_FMT
;;;	PLINE (11,18) = YTRXDT,DTMASK
	PLINE (21,45) = YTRXRF
	PLINE (52,65) = YTRXAM,MASK2
SKPLIN,
	IF (YTRXAM.LT.0)MINUS=MINUS + YTRXAM
	BALANC = BALANC + YTRXAM
	SSTAMT = SSTAMT + YTRXAM
	GTXAMT = GTXAMT + YTRXAM
	PLINE (67,80) = BALANC,MASK2
	STRBAL = BALANC
	IF (DETLSW.NE.'N') CALL PRINT
	IF (DETLSW.EQ.'N') PLINE (51,66) =		
	INCR TRXCNT
	INCR GLTRCT
	NEWSRC=
	GO TO READIX
SRCSUB,
	CALL BNCFWD
	IF (LSTSRC.EQ.'****') GO TO ENDSUB
	IF (DETLSW.NE.'N')PLINE (52,64) = DASHES
	IF (DETLSW.NE.'N')PLINE (67,79) = DASHES
	IF (DETLSW.NE.'N')CALL PRINT
	PLINE (11,14) = TRXCNT
	PLINE (17,19) = 'TRX'
	PLINE (29,46) = 'SOURCE SUB-TOTALS:'
	PLINE (52,65) = SSTAMT,MASK2
	PLINE (67,80) = STRBAL,MASK2
SRCONT,
	CALL PRINT
	INCR SRCCNT
	TRXCNT =
	SSTAMT =
	PLINE (1,20) = DASHES
	PLINE (21,40) = DASHES
	PLINE (41,60) = DASHES
	PLINE (61,80) = DASHES
	CALL PRINT
	IF (ENDFLG.EQ.1) GO TO GLTOTL
	IF (STRACT.GT.STRTNO.AND.MULSRC.NE.'    ') GO TO GLTOTL
ENDSUB,
	NEWSRC=1
	LSTSRC = SISRCE
	IF (ENDFLG.EQ.1) GO TO END
	RETURN
GLTOTL,
	PLINE =
	PLINE (1,4) = SRCCNT
	PLINE (6,9) = 'SRCS'
	PLINE (11,15) = GLTRCT
	PLINE (17,19) = 'TRX'
	PLINE (29,44) = 'G/L ACCT TOTALS:'
	PLINE (51,65) = GTXAMT,MASK2
GLCONT,
	CALL PRINT
	SRCCNT =
	GLTRCT =
	GTXAMT=
	GO TO END
FORM2,
	HDR2=STRHD2
	HDR3=STRHD3
	HDR1 = 
	HDR2(1,80) = HDR3(1,80)
	HDR3(1,80) =
READY2,
	LOKCTL = 1
	XCALL IO (4,YTDGLT,SIRCNO,READ,LOKCTL)
	STRACT = YACTNO,'XXXXXXX'
	IF (STRTNO.EQ.'ALL    ') GO TO CHKSRC
	IF (STRACT.LT.STRTNO.OR.STRACT.GT.ENDNO.AND.MULSRC.EQ.'    ') GO TO READIX
	IF (STRACT.GT.ENDNO) GO TO EOF
CHKSRC,
	IF (SISRCE.EQ.LSTSRC.AND.STRACT.EQ.LSTACC) GO TO SKPHDR
	IF (SISRCE.NE.LSTSRC) GO TO GLSUB
	SRCFLG = 1
CHEKGL,
	IF (LSTACC.EQ.'*******')SRCFLG=
CONT,
	IF (STRACT.NE.LSTACC) GO TO GLSUB
PLINE1,
	IF (YACTNO.NE.IACTNO) CALL FNDACT
	PLINE (1,2) = '=='
	PLINE (3,7) = SISRCE
	PLINE (8,9) = '=='
TSTDON,
	IF (SRCFLG.EQ.1)PLINE=
	IF (DETLSW.NE.'N') GO TO PL1CON
	HDR2(10,58) =
	HDR3 =
PL1CON,
	IF (DETLSW.EQ.'D') HDR3=
	IF (SRCFLG.NE.1)CALL PRINT
	IF (DETLSW.NE.'N') CALL FORMS
	IF (DETLSW.NE.'N') INCR LINCNT
	PLINE (1,9) = 'G/L ACCT:'
	PLINE (11,18) = ACCTNO,MASK
	PLINE (21,50) = DESCR
	IF (DETLSW.EQ.'N') CALL NODETL
	IF (DETLSW.EQ.'N') GO TO PLCONT
	IF (BALANC.NE.0) PLINE (67,80) = BALANC,MASK2
	CALL PRINT
	XCALL LINFD (1)
	INCR LINCNT
	GO TO PLCONT
NODETL,
	PLINE =
	PLINE (1,8) = ACCTNO,MASK
	PLINE (10,39) = DESCR
	RETURN
SKPHDR,
	PLINE =
	IF (DETLSW.EQ.'D') HDR3=
	IF (DETLSW.NE.'N') GO TO PLCONT
	PLINE (1,8) = ACCTNO,MASK
	PLINE (10,39) = DESCR
	HDR2(10,80) =
	HDR3 =
	GO TO PLCONT
GLSUB,
	CALL BNCFWD
	IF (LSTSRC.EQ.'****') GO TO SETSRC
	IF (DETLSW.EQ.'N') GO TO SKIPLN
	PLINE (52,64) = DASHES
	PLINE (67,79) = DASHES
	CALL PRINT
SKIPLN,
	PLINE (44,47) = TRXCNT
	PLINE (49,51) = 'TRX'
	PLINE (52,65) = SSTAMT,MASK2
	PLINE (67,80) = STRBAL,MASK2
	TOTBAL = TOTBAL + STRBAL
GLCON1,
	CALL PRINT
	INCR SRCCNT
	TRXCNT =
	SSTAMT =
;	LSTACC = YACTNO				;GL16
	LSTACC = YACTNO,'XXXXXXX'		;GL16
	IF (SISRCE.NE.LSTSRC.OR.ENDFLG.EQ.1) GO TO NEWSRC
	GO TO CHEKGL
NEWSRC,
	LSTSRC = SISRCE
SRCTOT,
	PLINE =
	XCALL LINFD (1)
	INCR LINCNT
	PLINE (1,4) = SRCCNT
	PLINE (6,10) = 'ACCTS'
	PLINE (11,15) = GLTRCT
	PLINE (17,19) = 'TRX'
	PLINE (29,46) = 'SOURCE SUB-TOTALS:'
	PLINE (52,65) = GTXAMT,MASK2
	PLINE (67,80) = TOTBAL,MASK2
SRCON1,
	CALL PRINT
	PLINE (1,20) = DASHES
	PLINE (21,40) = DASHES
	PLINE (41,60) = DASHES
	PLINE (61,80) = DASHES
	CALL PRINT
	XCALL LINFD (1)
	LINCNT = LINCNT +1
	SRCCNT =
	SRCFLG =
	GLTRCT =
	GTXAMT =
	TOTBAL =
	IF (MULSRC.NE.'   '.OR.ENDFLG.EQ.1) GO TO END
	IF (STRTNO.EQ.'ALL    '.OR.LSTACC.LE.ENDNO)GO TO PLINE1
	GO TO END
SETSRC,
	LSTSRC=SISRCE
	LSTACC = STRACT
	GO TO CONT
BNCFWD,
	BALANC =
	COUNT = SIRCNO
BNCLP,
	COUNT = COUNT - 1
	IF (COUNT.LE.1) GO TO BNCRET
	LOKCTL = 1
	XCALL IO (4,YTDGLT,COUNT,READ,LOKCTL)
;	BALACT = YACTNO				;GL16
	BALACT = YACTNO,'XXXXXXX'		;GL16
	IF (BALACT.NE.STRACT) GO TO BNCRET
	IF (YSRCE.EQ.SISRCE) BALANC = BALANC + YTRXAM
	GO TO BNCLP
BNCRET,
	LOKCTL = 1
	XCALL IO (4,YTDGLT,SIRCNO,READ,LOKCTL)
	RETURN
FNDACT,
	BSEND = ORG031
;	KEY = YACTNO				;GL16
	KEY = YACTNO,'XXXXXXX'			;GL16
	LOKCTL = 1
	XCALL IO (2,GLAIDX,MAXCNT,READ,LOKCTL)
	XCALL SERCH (2,GLAIDX,KEY,1,7,BSEND,BSMID,SRCCTL,4,18,22,0,0,0,0)
	GO TO (NOFIND), SRCCTL
	LOKCTL = 1
	XCALL IO (1,GLAMAS,IRC031,READ,LOKCTL)
	RETURN
PRINT,
	IF (PRNTSW.EQ.0) CALL LPON
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1(1,PAGSIZ),HDR2(1,PAGSIZ),
&		HDR3(1,PAGSIZ),LEGND2,'NO LEG',' ',0,PAGSIZ,PAGSIZ,1,LPSW,
&		RPTNUM,PRTTYP)
	RETURN
LPON,
	LPSW = 1
	SPLFIL (5,6) = 'BF'
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO END
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTSW = 1
	RETURN
LPOFF,
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	PGCNT =
	LINCNT = 60
	PRNTSW =
	RETURN
NOFIND,
	ACCTNO = YACTNO
	DESCR = '** DESCRIPTION NOT ON FILE **'
	RETURN
NOSRCE,
	XCALL MESAG ('SOURCE NOT ON FILE',1)
	GO TO BEGIN
NONAME,
	PLINE (10,34) = '                        '
	RETURN
NOACCT,
	XCALL MESAG ('NO TRX FOR STARTING ACCT #',1)
	GO TO BEGIN
CLOSES,
	IF (PRNTSW.EQ.1) CALL LPOFF
	CLOSE 5
	XCALL FILES (4,'I',33,4)
CLOSE2,
	XCALL FILES (2,'I',32,4)
CLOSE1,
	XCALL FILES (1,'I',31,4)
	RETURN
EOF,
	IF (LSTSRC.EQ.'****'.AND.MULSRC.NE.'    ')GO TO NOSRCE
	IF (LSTACC.EQ.'*******'.AND.STRTNO.NE.ENDNO) GO TO NOACCT
	ENDFLG=1
	IF (STRTNO.EQ.ENDNO) GO TO SRCSUB
	IF (STRTNO.NE.ENDNO) GO TO GLSUB
END,
	LSTSRC = '****'
	LSTACC = '*******'
	ENDFLG=
	IF (INXCTL.EQ.2.OR.LPSW.EQ.0) GO TO GOAWAY
	HDR1=STRHD1
	HDR2=STRHD2
	HDR3=STRHD3
	COUNTR=1
	CALL CLOSES
	GO TO START
GOAWAY,
	CALL CLOSES
ENDOFF,
	IF (PRNTSW.EQ.1) CALL LPOFF
	XCALL WATE(3,V)
	XCALL FILES (5,'I',35,7)
	XCALL PGCHN ('GL:GLMENU',1)
INPUT,
	XCALL INPUT(ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,V)
	RETURN
FORMS,
	XCALL LINFD (1)
	RETURN
END
