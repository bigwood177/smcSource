;  GNJREG / GL 
;
;
;		GENERAL JOURNAL TRANSACTION REGISTER
;
;

RECORD	D_STUFF
	D_IN	,D8		;DATE-IN, ANY FORMAT
	D_OUT	,D6		;RETURN VALUE MMDDYY
	D_OUTR	,D8		;RETURN VALUE CCYYMMDD
	D_FMT	,A10		;RETURN VALUE MM/DD/CC/YY
	D_SW	,A2		;"99" = DATE CONVERSION ERROR


RECORD MASTER			; 
		.INCLUDE 'DEF:RD070A.DEF'
RECORD GLWORK	,X		; 
		.INCLUDE 'DEF:RD034A.DEF'
RECORD DUMGLW			; 
		.INCLUDE 'DEF:RD034B.DEF'
RECORD SNDMSG
		,A3,'GL:'
	PRGNAM	,A6,'PSTGNJ'
	SRTREC	,D5
	ORGREC	,D5,00001
		,D3,034
RECORD TITLE
		,A37,'GENERAL JOURNAL TRANSACTION REGISTER '
RECORD HDR1
		,A7,'ACCT-NO'
		,A4
		,A16,'ACCT DESCRIPTION'
		,A19
		,A4,'DATE'
		,A10
		,A5,'DEBIT'
		,A8
		,A7,'CREDIT '
RECORD HDR2
		,A10
		,A4,'SRCE'
		,A3
		,A13,'TRX-REFERENCE'
RECORD LEGND2
		,A7,'PERIOD:'
		,A2
	STRPRD	,A10
		,A2
		,A2,'TO'
		,A2
	ENDPRD	,A10
RECORD
	DIFF	,D13
	DTMASK	,A8,'XX/XX/XX'
	ENTRY	,A3
	ENTRIS	,D4
	HACCNT	,D10
	INXCTL	,D1
	SWITCH	,D1,1
	LPSW	,D1
	LINCNT	,D2,60
	MASK	,A13,'ZZ,ZZZ,ZZZ.XX'
	PGCNT	,D3
	PLINE	,A132
	PAGSIZ	,D3
	TOTDR	,D13
	TOTCR	,D13
	V	,D1
	YES	,D1,	1
RECORD
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	LPARG	,D1,2
	PRTTYP	,A1
	RPTNUM	,D3
	SPLFIL	,A14
PROC 
	XCALL TERID (V)
	V = 1
	XCALL FILES (1,'I',70,SWITCH)		;#70 MASTER FILE
	IF (SWITCH.EQ.9) GO TO END
	LOKCTL = 1
	XCALL IO (1,MASTER,1,READ,LOKCTL)

	XCALL DATE8(MCSTDT, D_OUT, D_OUTR, STRPRD, D_SW)
	XCALL DATE8(MCENDT, D_OUT, D_OUTR, ENDPRD, D_SW)
;;;	STRPRD=MCSTDT,DTMASK
;;;	ENDPRD=MCENDT,DTMASK

	XCALL FILES (1,'I',70,4)
	XCALL FILES (2,'I',34,SWITCH)		;#34 GLWORK FILE
	IF (SWITCH.EQ.9) GO TO END
	LOKCTL = 1
	XCALL IOS (2,DUMGLW,READ,LOKCTL)
	IF (REGFLG.EQ.'E')	XCALL OUTPT (2,1,1,'EDIT LIST',V)
	IF (REGFLG.EQ.'R')	XCALL OUTPT (2,1,1,'PRINT REGISTER',V)
	TITLE(29,37) = 'EDIT LIST'
	IF (REGFLG.NE.'R')GO TO CONTIN
	TITLE(29,37) = 'REGISTER '
ADDS,
	LOKCTL = 1
	XCALL IOS (2,GLWORK,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO START
	IF (GLWORK.EQ.']]]]]]') GO TO START
	IF (GJDESC.EQ.']]]DEL') GO TO ADDS
	TOTDR = TOTDR + GJAMT
	GO TO ADDS
START,
	IF (TOTDR.EQ.0) GO TO PROTEC
	XCALL OUTPT (12,15,0,'TRX OUT OF BALANCE - DO YOU WISH TO POST ANYWAY ?',V)
	XCALL INPUT (12,66,1,1,'YN',ENTRY,INXCTL,V)
	GO TO (ABORT2), INXCTL - 1
PROTEC,
	XCALL FILES (2,'I',34,4)		; GLWORK FILE
	SWITCH = 3
	XCALL FILES (2,'I',34,SWITCH)
	IF (SWITCH.EQ.9) GOTO END
	SWITCH = 5
	XCALL FILES (2,'I',34,SWITCH)
	IF (SWITCH.EQ.9) GO TO ABORT2
	SWITCH = 3
	XCALL FILES (3,'U',33,SWITCH)		; YTDGLT FILE
	IF (SWITCH.EQ.9) GOTO ABORT2
CONTIN,
	TOTDR =
	LOKCTL = 1
	XCALL IO (2,GLWORK,1,READ,LOKCTL)
	PAGSIZ=80
	CALL LPONT
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
READ,
	LOKCTL = 1
	XCALL IOS (2,GLWORK,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOF
	IF (GLWORK.EQ.']]]]]]') GO TO EOF
	IF (GJDESC.EQ.']]]DEL')GO TO READ
	PLINE (1,8)=GJACCT,'ZZZZ-XXX'
	INCR ENTRIS
	HACCNT=HACCNT+GJACCT
	PLINE (12,41)=GJDESC

	XCALL DATE8(GJDATE, D_OUT, D_OUTR, D_FMT, D_SW)
	PLINE (43,52) = D_FMT

	IF (GJAMT.LT.0)GO TO CRT
	PLINE (54,66)=GJAMT,MASK
	TOTDR=TOTDR+GJAMT
	GO TO DDESC
CRT,
	PLINE (68,80)=GJAMT,MASK
	TOTCR=TOTCR+GJAMT
DDESC,
	if (lincnt .ge. 58) lincnt = 60
	CALL PRINT
	LINCNT=LINCNT-1
	PLINE (11,14)=GJSRCE
	PLINE (18,42)=GJREF
	CALL PRINT
	XCALL LINFD (1)
	INCR LINCNT
	LINCNT=LINCNT+1
	GO TO READ
EOF,
	IF (TOTDR.NE.TOTCR)DIFF=TOTDR+TOTCR
	XCALL LINFD (1)
	INCR LINCNT
	IF (LINCNT.LT.55)GO TO NXT
	LINCNT=60
NXT,
	HDR1 =
	HDR2 =
	PLINE (1,5)=ENTRIS
	PLINE (7,13)='ENTRIES'
	PLINE (43,49)='TOTALS:'
	PLINE (54,66)=TOTDR,MASK
	PLINE (68,80)=TOTCR,MASK
	CALL PRINT
	XCALL LINFD (1)
	INCR LINCNT
	IF (DIFF.EQ.0)GO TO PRTHSH
	PLINE (39,59)='*** OUT OF BALANCE BY'
	PLINE (61,73)=DIFF,MASK
	PLINE (75,77)='***'
	CALL PRINT
PRTHSH,
	PLINE (1,10)=HACCNT
	PLINE (12,29)='HASH OF ACCOUNT #S'
	CALL PRINT
DONE,
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	XCALL OUTPT (2,1,1,'\',V)
	IF (REGFLG.NE.'R') GO TO ABORT2
	XCALL WATE(4,V)
	CLOSE 2
	XCALL FILES(2,'U',34,5)
	LOKCTL = 1
	XCALL IO (2,DUMGLW,1,READ,LOKCTL)
	REGNUM = RPTNUM
	LOKCTL = 1
	XCALL IO (2,DUMGLW,1,WRITE,LOKCTL)
	CLOSE 2
	SRTREC = REC034
	SWITCH = 2
	XCALL SNMSG (SNDMSG,SWITCH)
	XCALL PGCHN ('GL:SRTGNJ',1)
ABORT1,
	IF (REGFLG.EQ.'R') XCALL FILES (3,'U',33,4)
ABORT2,
	XCALL FILES (2,'I',34,4)
END,
	XCALL WATE(3,V)
	XCALL PGCHN ('GL:GNJENT',1)
PRINT,
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,'NO HDR',LEGND2,
&		'NO LEG',' ',0,80,80,1,LPSW,RPTNUM,PRTTYP)
	RETURN
LPONT,
	IF (REGFLG.EQ.'R') LPSW = 4	; NOT DISPLAYABLE - AUTO SPOOL
	IF (REGFLG.EQ.'E') LPSW = 1	; PRINT, SPOOL OR DISPLAY
	IF (REGFLG.EQ.'R') SPLFIL (5,6) = 'BC'
	IF (REGFLG.EQ.'E') SPLFIL (5,6) = 'BB'
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO ABORT1
	RETURN
END
