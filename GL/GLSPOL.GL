;  GLSPOL / GL  
;
;
;		::PCPYGL.DEF::
;******************************************************************************
;		GENERAL LEDGER
;
;		RELEASED: AUGUST 1, 1984 (d70s10)
;******************************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		HANDLES SPOOLED REPORTS FOR THE GL PACKAGE
;
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'
RECORD MESARA
			.INCLUDE 'DEF:MES001.DEF'
RECORD SPLDIR
			.INCLUDE 'DEF:SPL001.DEF'
RECORD	,X
			.INCLUDE 'DEF:SPL002.DEF'
RECORD	,X
			.INCLUDE 'DEF:SPL003.DEF'
RECORD BRACKS
		,A31,']]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]'
RECORD TITLES
		,A31,'ACHART OF ACCOUNTS LIST        '
		,A31,'BGENERAL JOURNAL TRX EDIT LIST '
		,A31,'CGENERAL JOURNAL TRX REGISTER  '
		,A31,'DGENERAL LEDGER PRINT-OUT      '
		,A31,'EG/L TRIAL BALANCE             '
		,A31,'FSOURCE CROSS REFERENCE        '
		,A31,'GPROFIT & LOSS STATEMENT       '
		,A31,'HBALANCE SHEET                 '
		,A31,'IP & L SUPPORTING SCHEDULES    '
		,A31,'JBALANCE SHEET SCHEDULES       '
		,A31,'KCASH FLOW REPORT              '
		,A31,'LCHANGES IN FINANCIAL POSITION '
		,A31,'MCOMPONENTS OF WORKING CAPITAL '
RECORD	,X
	RPTNAM	,13A31
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD DSPLIN
		,A1
	TAGLIT	,A5,'   . '
	DSPTIT	,A30
		,A2
	DSPTRM	,A2
		,A4
	DSPSEQ	,A2
		,A3
	DSPDAT	,A9
		,A2
	DSPTIM	,A5
		,A2
	DSPPGS	,A3
		,A2
	DSPDEV	,A3
RECORD
	ACHAR	,A1
	ARYEND	,D3
	ARYPNT	,D3
	BLANKS	,A3
	COMCOD	,A3
	DELNAM	,A14
	DSPCNT	,D3
	DSPROW	,D2
	ENTRY	,A4
	ERROR	,D3
	FORMFD	,A1
	FUNCTN	,A1
	I	,D2
	INXCTL	,D1
	J	,D2
	KTERMN	,D2
	LOKCTL	,D1
	PAGSKP	,D3
	PROGNM	,A9,	'GL:GLSPFN'
	RDCNT	,D3
	READ	,D1,	0
	RECNO	,D3,	001
	REMOVE	,D1
	RPTNUM	,D3
	RPTPNT	,D3
	SCARAY	,200D3
	SCRNI	,D2
	SELECT	,D1
	SPLCNT	,D3
	SPLCTL	,D1
	SWITCH	,D1
	SYSTYP	,A1,	'B'
	TERMNO	,D2
	TBLEND	,D2,	13
	TITLE	,A31
	V	,D1
	WRTCNT	,D3
	WRITE	,D1,	1
	SYTTNO	,D3
	MCTTNO	,D2
	TTNOSW	,D1
PROC
	XCALL TERID(V)
	XCALL TTNO (SYTTNO,MCTTNO,TTNOSW)
	TERMNO = MCTTNO
	TERMNO = TERMNO + 2
	V = 1
	XCALL OPENF (1,'U','UT:SPLDIR.DDF',ERROR,0,SWITCH)
	IF (SWITCH) GO TO NODIR
	XCALL OPENF (13,'I','UT:MESARA.DDF',ERROR,0,SWITCH)
	IF (SWITCH) GO TO NOMES
	LOKCTL = 1
	XCALL IO (13,MESARA,TERMNO,READ,LOKCTL)
	LOKCTL = 0
	XCALL IO (1,SPLDIR,1,READ,LOKCTL)
	UNLOCK 1				;GL18
	IF (LOKCTL) GO TO END3
	SPLCNT = SPLREC
	UNLOCK 1
DISPLA,
	REMOVE =
	XCALL OUTPT(1,1,2,
&	'PRINT SPOOLED REPORTS - GENERAL LEDGER',1)
	XCALL OUTPT(3,7,0,'REPORT TITLE',1)
	XCALL OUTPT(3,38,0,'TRM#  SEQ#    DATE     TIME  PAGES DEV',1)
	SCRNI =
	SCARAY (1,600) =
	ARYPNT =
	RECNO = 1
	CALL SETUP
	IF (ARYEND.EQ.0) GO TO NOFILS
	CALL NXTSCN
SELECT,
	CTL = '24,00,03,00,NX'
	XCALL OUTPT(24,1,1,'SELECT REPORT',1)
	IF (ARYPNT.LT.ARYEND) XCALL OUTPT(0,0,0,' OR (CR) FOR NEXT SCREEN',1)
	IF (SCRNI.GT.1) XCALL OUTPT(0,0,0,' OR (TAB) FOR PREVIOUS SCREEN',1)
	COL = 16
	IF (ARYPNT.LT.ARYEND) COL = COL + 24
	IF (SCRNI.GT.1) COL = COL + 29
	CALL INPUT
	GO TO (SELECT,END,SETPRV), INXCTL
	IF (ENTRY.EQ.BLANKS) GO TO SETNXT
	RPTNUM = ENTRY (1,3)
	IF (RPTNUM.GT.ARYPNT) GO TO SELECT
	IF (RPTNUM.LT.(SCRNI-1)*16+1) GO TO SELECT
	RPTPNT = SCARAY (RPTNUM)
	LOKCTL = 1
	XCALL IO (1,SPLDIR,RPTPNT,READ,LOKCTL)
	XCALL OPENF (2,'I',SPLNAM,ERROR,0,LOKCTL)
	IF (LOKCTL) GO TO NORPT
	CLOSE 2
	DSPROW = RPTNUM - (SCRNI - 1)*16 + 4
	XCALL OUTPT(DSPROW,78,0,'***',V)
	XCALL PRSPL (SPLNAM,SELECT,TERMNO,DEFPTR,TERMID,SPLCTL)
	GO TO (DISPLA,END), SPLCTL
	IF (SELECT.EQ.4) REMOVE = 1	;ALREADY DELETED FILE, REMOVE FROM SPLDIR
	GO TO (DELETE,DELET2), SELECT - 2
	GO TO DISPLA
SETNXT,
	IF (ARYPNT.GE.ARYEND) GO TO SELECT
	CALL NXTSCN
	GO TO SELECT
SETPRV,
	IF (SCRNI.LE.1) GO TO SELECT
	SCRNI = SCRNI - 2
	ARYPNT = ARYPNT - DSPCNT - 16
	CALL NXTSCN
	GO TO SELECT
DELETE,
	XCALL OUTPT (24,1,1,'DELETE - ARE YOU SURE ?',1)
	CTL = '24,26,01,01,YN'
	CALL INPUT
	GO TO (SELECT), INXCTL - 1
DELET2,
	LOKCTL = 1
	XCALL IO (1,SPLDIR,RPTPNT,READ,LOKCTL)
	DELNAM = SPLNAM
	SPLDEL = '00'
	LOKCTL = 1
	XCALL IO (1,SPLDIR,RPTPNT,WRITE,LOKCTL)
	GO TO (DISPLA), REMOVE
	XCALL DELET (2,DELNAM)
	XCALL MESAG ('REPORT DELETED',2)
	GO TO DISPLA
SETUP,
	INCR RECNO
	IF (RECNO.GT.SPLCNT) GO TO SETEND
	LOKCTL = 1
	XCALL IO (1,SPLDIR,RECNO,READ,LOKCTL)
	IF (SPCOMP.NE.CMPCOD) GO TO SETUP
	IF (SPNAME(1,1).NE.SYSTYP) GO TO SETUP
	IF (SPLDEL.EQ.'00') GO TO SETUP
	INCR ARYPNT
	SCARAY (ARYPNT) = RECNO
	GO TO SETUP
SETEND,
	UNLOCK 1				;GL18
	ARYEND = ARYPNT
	ARYPNT =
	RETURN
NOFILS,
	XCALL MESAG('NO MORE SPOOLED REPORTS',1)
	GO TO END
NORPT,
	OFF ERROR
	REMOVE =
	XCALL OUTPT(24,1,1,'REPORT NOT FOUND - REMOVE FROM LIST ?',1)
	CTL = '24,40,01,01,YN'
	CALL INPUT
	GO TO (SELECT), INXCTL - 1
	REMOVE = 1
	GO TO DELET2
NXTSCN,
	IF (ARYPNT.GE.ARYEND) GO TO NOFILS
	INCR SCRNI
	XCALL OUTPT(5,1,2,'\',1)
	DSPCNT =
NXTLP,
	INCR ARYPNT
	IF (ARYPNT.GT.ARYEND) GO TO RETNXT
	RECNO = SCARAY (ARYPNT)
	LOKCTL = 1
	XCALL IO (1,SPLDIR,RECNO,READ,LOKCTL)
	UNLOCK 1				;GL18
	INCR DSPCNT
	CALL DSPLIN
	IF (DSPCNT.EQ.16) RETURN
	GO TO NXTLP
RETNXT,
	ARYPNT = ARYPNT - 1
	RETURN
DSPLIN,
	CALL FNDTIT
	TAGLIT (1,3) = (SCRNI - 1)*16 + DSPCNT,'ZZX'
	ROW = DSPCNT + 4
	DSPTIT = TITLE (2,31)
	DSPTRM = SPTERM
	DSPSEQ = SPSEQ
	DSPDAT = SPDATE
	DSPTIM = SPTIME
	DSPPGS = SPPAGS
	DSPDEV = SPOLDV
	XCALL OUTPT(ROW,1,1,DSPLIN,1)
	RETURN
FNDTIT,
	J =
FNDLP,
	INCR J
	IF (J.GT.TBLEND) GO TO FNDLP2
	IF (RPTNAM(J).NE.SPNAME(2,2)) GO TO FNDLP
	TITLE = RPTNAM (J)
	RETURN
FNDLP2,
	TITLE = '** NO TITLE *'
	RETURN
NODIR,
	XCALL MESAG('SPOOLER DIRECTORY NOT FOUND',2)
	GO TO END3
NOMES,
	XCALL MESAG ('UT:MESARA.DDF FILE NOT FOUND',2)
	GO TO END3
END,
	XCALL WATE(3,V)
	RDCNT = 1
	WRTCNT = 1
	LOKCTL = 1
	XCALL IO (1,SPLDIR,1,READ,LOKCTL)
	RECNO = SPLREC
PRGLP,
	INCR RDCNT
	IF (RDCNT.GT.RECNO) GO TO END2
	LOKCTL = 1
	XCALL IO (1,SPLDIR,RDCNT,READ,LOKCTL)
	IF (SPLDEL.EQ.'00') GO TO PRGLP
	INCR WRTCNT
	LOKCTL = 1
	XCALL IO (1,SPLDIR,WRTCNT,WRITE,LOKCTL)
	GO TO PRGLP
END2,
	LOKCTL = 1
	XCALL IO (1,SPLDIR,1,READ,LOKCTL)
	SPLREC = WRTCNT
	LOKCTL = 1
	XCALL IO (1,SPLDIR,1,WRITE,LOKCTL)
BRACKS,
	INCR WRTCNT
	IF (WRTCNT.GT.RECNO) GO TO END3
	LOKCTL = 1
	XCALL IO (1,BRACKS,WRTCNT,WRITE,LOKCTL)
	GO TO BRACKS
END3,
	CLOSE 1
	XCALL PGCHN(PROGNM,1)
INPUT,
	XCALL INPUT(ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,V)
	RETURN
END
