;  PROFMA / GL 
;
;
;		::PCPYGL.DEF::
;******************************************************************************
;		GENERAL LEDGER
;
;		RELEASED: AUGUST 1, 1984 (d70s10)
;******************************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;;		CREATES SAMPLE YTD FILE RECORDS FOR EACH POSTABLE ACCOUNT
;
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD MASTER		; 
		.INCLUDE 'DEF:RD070A.DEF'
RECORD GLAMAS		; 
		.INCLUDE 'DEF:RD031A.DEF'
RECORD GLAIDX		; 
		.INCLUDE 'DEF:RD032A.DEF'
RECORD YTDGLT		; 
		.INCLUDE 'DEF:RD033A.DEF'
RECORD DUMYTD		; 
		.INCLUDE 'DEF:RD033B.DEF'
RECORD BRACKS		; 
		.INCLUDE 'DEF:RD033C.DEF'
RECORD
	BBDATE	,D6
	CLNTNO	,A34
	CLTNAM	,A30
	CNGCTL	,D1
	ENTRY	,A10
	SWITCH	,D1,1
	INXCTL	,D1
	PROFMA	,D1
	RECCNT	,D5
	V	,D1
	WHATNO	,D2
	YES	,D1,	1
RECORD
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
PROC 
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'CREATE SAMPLE YEAR-TO-DATE RECORDS',V)
	XCALL OUTPT (5,20,0,'THIS PROGRAM WILL CREATE ONE DUMMY YTD',V)
	XCALL OUTPT (6,20,0,'TRANSACTION RECORD FOR EVERY POSTABLE',V)
	XCALL OUTPT (7,20,0,'ACCOUNT ON THE CURRENT CHART OF ACCOUNTS.',V)
	XCALL OUTPT (8,20,0,'IT WILL DESTROY THE EXISTING YTD FILE.',V)
	XCALL OUTPT (10,20,0,'ARE YOU SURE YOU WANT TO RUN THIS PROGRAM ?',V)
	XCALL INPUT(10,65,01,01,'YN',ENTRY,INXCTL,V)
	GO TO (END), INXCTL - 1
	CLTNAM = CLNTNO(5,34)
	XCALL WATE (4,V)
	XCALL FILES (1,'I',70,SWITCH)		;#70 MASTER FILE
	IF (SWITCH.EQ.9) GO TO END
	LOKCTL = 1
	XCALL IO (1,MASTER,1,READ,LOKCTL)
	XCALL FILES (1,'I',70,4)
	IF (MNOPRD.EQ.0) GO TO NOPRDS
	BBDATE = MENDTS(MNOPRD) - 1
	XCALL FILES (1,'I',31,SWITCH)		; GLAMAS FILE
	IF (SWITCH.EQ.9) GO TO END
	XCALL FILES (2,'I',32,SWITCH)		; GLAIDX FILE
	IF (SWITCH.EQ.9) GO TO END1
	LOKCTL = 1
	XCALL IO (2,GLAIDX,1,READ,LOKCTL)
	SWITCH = 3
	XCALL FILES (4,'U',33,SWITCH)		; YTDGLT FILE
	IF (SWITCH.EQ.9) GO TO END2
	XCALL FILES(4,'U',33,5)
	LOKCTL = 1
	XCALL IO (4,YTDGLT,1,READ,LOKCTL)
	YTRXDT = MSTDTS(1)
	YSRCE = 'SMPL'
	YTRXRF = 'SAMPLE ENTRY'
	YTRXAM = 100000
	YTRAIL = 'DUMMY'
	RECCNT = 1
READ,
	LOKCTL = 1
	XCALL IOS (2,GLAIDX,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOF
	IF (GLAIDX.EQ.']]]]]]') GO TO EOF
	IF (IRC031.EQ.0) GO TO READ
	IF (IFSTCD(6,6).NE.'V') GO TO READ
	LOKCTL = 1
	XCALL IO (1,GLAMAS,IRC031,READ,LOKCTL)
	YACTNO = ACCTNO
	YTRXAM = 100000
	IF (TYPBAL.EQ.'C') YTRXAM = - YTRXAM
	INCR RECCNT
	LOKCTL = 1
	XCALL IO (4,YTDGLT,RECCNT,WRITE,LOKCTL)
	GO TO READ
NOPRDS,
	XCALL MESAG('MUST ENTER PERIODS FIRST',1)
	GO TO END
EOF,
	LOKCTL = 1
	XCALL IO (4,DUMYTD,1,READ,LOKCTL)
	REC033 = RECCNT
	LOKCTL = 1
	XCALL IO (4,DUMYTD,1,WRITE,LOKCTL)
LOOP,
	INCR RECCNT
	IF (RECCNT.GT.MAX033) GO TO OUT
	LOKCTL = 1
	XCALL IO (4,BRACKS,RECCNT,WRITE,LOKCTL)
	GO TO LOOP
OUT,
	XCALL FILES (4,'U',33,4)
END2,
	XCALL FILES(2,'I',32,4)
END1,
	XCALL FILES(1,'I',31,4)
END,
	XCALL WATE(3,V)
	XCALL PGCHN ('GL:GLSPFN',1)
END
