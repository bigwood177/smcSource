;  PRFLOS / GL 
;
;
;		PGM TO PRINT PROFIT & LOSS STATEMENT
;

RECORD	D_STUFF
	D_IN	,D8		;DATE-IN, ANY FORMAT
	D_OUT	,D6		;RETURN VALUE MMDDYY
	D_OUTR	,D8		;RETURN VALUE CCYYMMDD
	D_FMT	,A10		;RETURN VALUE MM/DD/CC/YY
	D_SW	,A2		;"99" = DATE CONVERSION ERROR


RECORD GLAMAS		; 
		.INCLUDE 'DEF:RD031A.DEF'
RECORD, X		; 
		.INCLUDE 'DEF:RD031C.DEF'
RECORD DUMGLA		; 
		.INCLUDE 'DEF:RD031B.DEF'
RECORD CONAME
		,A50
RECORD YTDGLT		; 
		.INCLUDE 'DEF:RD033A.DEF'
RECORD DUMYTD		; 
		.INCLUDE 'DEF:RD033B.DEF'
RECORD TFSIDX		; 
		.INCLUDE 'DEF:RD036A.DEF'
RECORD
	LINCNT	,D2,	60
	SWITCH	,D1,1
	PAGSIZ	,D3,	132
	PGCNT	,D3
	PRTCTL	,D3
RECORD TBUF
	TLINE	,A132
RECORD PBUF
	PLINE	,A132
RECORD LEGEND
		,A3,	'FOR'
		,A1
		,A3,	'THE'
		,A1
		,A6,	'PERIOD'
		,A2
	LGNDT1	,A10
		,A2
		,A2,	'TO'
		,A2
	LGNDT2	,A10
RECORD HEAD5
		,A14,	'CURRENT-PERIOD'
		,A12
		,A12,	'YEAR-TO-DATE'
		,A11
	HEAD5C	,A18
		,A8
	HEAD5D	,A17
RECORD HEAD6
		,A6,	'AMOUNT'
		,A4
		,A5,	'RATIO'
		,A10
		,A6,	'AMOUNT'
		,A4
		,A5,	'RATIO'
		,A12
		,A6,	'AMOUNT'
		,A4
	HEAD6C	,A5
		,A10
		,A6,	'AMOUNT'
		,A4
	HEAD6D	,A5
RECORD CLRFLD
	VPCUR	,D10
	VPPRE	,D10
	CURCOM	,D10
	YTDCOM	,D10
	CURBUD	,D10
	YTDBUD	,D10
	RATIO1	,D5
	RATIO2	,D5
	RATIO3	,D5
	RATIO4	,D5
RECORD SEC
	SECCUR	,D10
	SECYTD	,D10
	SECCOM	,D10
	SECYCM	,D10
	SECBUD	,D10
	SECYBD	,D10
RECORD CLRSUB
	SUBCUR	,D10
	SUBYTD	,D10
	SUBCOM	,D10
	SUBYCM	,D10
	SUBBUD	,D10
	SUBYBD	,D10
RECORD
	CURAMT	,D10
	YTDAMT	,D10
	TOTCUR	,D10
	TOTYTD	,D10
	TOTCOM	,D10
	TOTYCM	,D10
	TOTBUD	,D10
	TOTYBD	,D10
	CBDVAR	,D10
	YBDVAR	,D10
	CPYVAR	,D10
	YPYVAR	,D10
	AMNT	,D10
	BRACKS	,A6, ']]]]]]'
	BUDAYS	,D3
	CD	,A2
	COL	,D3
	DOUBLE	,A16,	'================'
	DLRSW	,D1
	DIVSN	,A30
	DESCR2	,A30
	ENDTE2	,D8
	FSCM	,3A30
	GLDTE2	,D8
	INSERT	,A2
	I	,D1
	MASK4	,A6,	'ZZZZ.X'
	MASK3	,A13,	'ZZ,ZZZ,ZZZ.XX'
	MASK2	,A6,	'ZZZ.XX'
	MASK1	,A8,	'XX/XX/XX'
	NOTTL	,D1
	PROGRM	,A10,	'GL:BALNCE '
	PNLOR	,D1
	PLTT	,A30,	'   PROFIT & LOSS STATEMENT    '
	PFCSW	,D1
	PFC	,D3
	STRDT2	,D8
	SAVPRT	,D3
	TYPBA2	,A1
	TXI	,3D1
	TCD	,D3
	TACT	,D4
	TITLE	,D1,-0
	UNDER	,A16,	'----------------'
	V	,D1
	YES	,D1,	1
RECORD
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	LPSW	,D1
	RATIO	,A5	,'RATIO'
	VARPRC	,A5	,'%-VAR'
	COMPER	,A18	,'COMPARATIVE-PERIOD'
	BUDPER	,A18	,'  BUDGETED-PERIOD '
	YTDVAR	,A18	,'   YTD-VARIANCE   '
	LSTYTD	,A17	,'LAST-YEAR-TO-DATE'
	BUDYTD	,A17	,'BUDGETED-TO-DATE '
	PERVAR	,A17	,' PERIOD-VARIANCE '
	STRTSW	,D1	,0
	LPARG	,D1,2
	PRTTYP	,A1
	RPTNUM	,D3
	SPLFIL	,A14
PROC 
	XCALL TERID (V)
	XCALL OUTPT (2,1,1,'PROFIT & LOSS STATEMENT',1)
	XCALL FILES (1,'I',31,5)		;FILE # 031 -- GLAMAS FILE
	LOKCTL = 1
	XCALL IO (1,DUMGLA,1,READ,LOKCTL)
	XCALL FILES (4,'I',33,5)		;FILE # 033 -- YTDGLT FILE
	LOKCTL = 1
	XCALL IO (4,DUMYTD,1,READ,LOKCTL)
	PFC = PFCCDE(SUB)

	XCALL DATE8(CCSTDT, D_OUT, D_OUTR, LGNDT1, D_SW)
	XCALL DATE8(CCENDT, D_OUT, D_OUTR, LGNDT2, D_SW)

;;;	STRDT2(1,2) = CCSTDT (5,6)
;;;	STRDT2(3,6) = CCSTDT (1,4)
;;;	LGNDT1 = CCSTDT, MASK1
;;;	GLDTE2 = CCENDT
;;;	ENDTE2(1,2) = GLDTE2(5,6)
;;;	ENDTE2(3,6) = GLDTE2(1,4)
;;;	LGNDT2 = GLDTE2, MASK1

	IF (COMP.LT.1.OR.COMP.GT.2) GOTO STHDR4
	HEAD6C = RATIO
	HEAD6D = RATIO
	IF (COMP.NE.1) GOTO STHDR1
	HEAD5C = BUDPER
	HEAD5D = BUDYTD
STHDR1,
	IF (COMP.NE.2) GOTO STHDR2
	HEAD5C = COMPER
	HEAD5D = LSTYTD
STHDR2,
	IF (TYPCMP.NE.2) GOTO STHDR3
	HEAD5D = PERVAR
	HEAD6D = VARPRC
STHDR3,
	IF (TYPCMP.NE.3) GOTO STHDR4
	HEAD5C = YTDVAR
	HEAD6C = VARPRC
STHDR4,
	XCALL FILES (5,'I',36,5)		;FILE # 036 -- TFSIDX FILE
	LOKCTL = 1
	XCALL IO (5,TFSIDX,1,READ,LOKCTL)
	SPLFIL (5,6) = 'BG'
	LPSW = 4	;AUTO-SPOOLED
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) XCALL PGCHN ('GL:UNPRFS',1)
	IF (LPSW.EQ.2) LPARG = 4
	XCALL FILES (13,'I',99,5)		; CONAME FILE
	LOKCTL = 1
	XCALL IO (13,CONAME,1,READ,LOKCTL)
	CLOSE 13
	PNLOR = NUMPNL(SUB)
	PFC = PFCCDE (SUB)
	IF (COMP .EQ. 0)  PAGSIZ = 80
	IF (CCSTNO.NE.0.AND.CCENNO.NE.0) GO TO PRFCRD
	HEAD5 (49,92) =
	HEAD6 (53,92) =
	PAGSIZ = 80
PRFCRD,
	LOKCTL = 1
	XCALL IOS (5,TFSIDX,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOF
	IF (TFSIDX.EQ.BRACKS) GO TO EOF
	IF (TFSTCD(1,1).NE.'P') GO TO PRFCRD
	LOKCTL = 1
	XCALL IO (1,GLAMAS,TCRCNO,READ,LOKCTL)
	IF (ACCTNO(1,4).NE.9995) GO TO NTFND
	IF (ACCTNO(5,7).NE.PFC) GO TO PRFCRD
	DIVSN = DESCR
	GO TO TEXT
NTFND,
	IF (PFC.EQ.0) GO TO ALLD
;;;	IF (PFC(2,3).EQ.0) GO TO ALLD	;;;
	DIVSN = '     PROFIT CENTER -'
	DIVSN (22,24) = PFC
	GO TO NXT
ALLD,
	DIVSN = '        ALL DIVISIONS'
	GO TO NXT
TEXT,
	LOKCTL = 1
	XCALL IOS (5,TFSIDX,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOF
	IF (TFSIDX.EQ.BRACKS) GO TO EOF
	LOKCTL = 1
	XCALL IO (1,GLAMAS,TCRCNO,READ,LOKCTL)
NXT,
	TACT = ACCTNO(1,4)
	IF (TACT.EQ.9995) GO TO TEXT
	IF (TACT.GT.9998) GO TO NEXT
	IF (TACT.LT.9996) GO TO NOTITL
	LOKCTL = 1
	XCALL IO (1,GLAMAS,TCRCNO,READ,LOKCTL)
	I = ACCTNO (4,4) - 5
	FSCM (I) = DESCR
	TXI(I) = 1
	GO TO TEXT
NEXT,
	IF (TACT.NE.9999) GO TO NOTITL
	PLTT = DESCR
	GO TO BEGIN
NOTITL,
	NOTTL = 1
	GO TO BEGIN
PRTHDR,
	SAVPRT = PRTCTL
	TBUF = PBUF
	PBUF =
	LINCNT =
	INCR PGCNT
	IF (STRTSW) XCALL LINFD (0)
	STRTSW = 1
	PLINE (16,65) = CONAME
	PLINE (73,76) = 'PAGE'
	PLINE (78,80) = PGCNT
	CALL PRINT1
	PLINE (26,55) = DIVSN
	CALL PRINT1
	XCALL LINFD (1)
	INCR LINCNT
	I = 1
TXLP,
	IF (TXI(I).NE.1) GO TO INCI
	PLINE (26,55) = FSCM (I)
	CALL PRINT1
INCI,
	INCR I
	IF (I.LT.4) GO TO TXLP
	XCALL LINFD (1)
	INCR LINCNT
	PLINE (26,55) = PLTT
	CALL PRINT1
	XCALL LINFD (1)
	INCR LINCNT
;;;	PBUF (22,132) = LEGEND(1,38)
	PBUF (18,132) = LEGEND(1,42)
	CALL PRINT1
	XCALL LINFD (1)
	INCR LINCNT
	PBUF (40,132) = HEAD5(1,92)
	CALL PRINT1
	PBUF (39,132) = HEAD6(1,92)
	CALL PRINT1
	XCALL LINFD (1)
	INCR LINCNT
	PBUF = TBUF
	TBUF =
	PRTCTL = SAVPRT
	RETURN
BEGIN,
	XCALL WATE (LPARG,V)
	GO TO (FRST), NOTTL
READAC,
	LOKCTL = 1
	XCALL IOS (5,TFSIDX,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOF
	IF (TFSIDX.EQ.BRACKS) GO TO EOF
FRST,
	IF (TFSTCD(1,1).NE.'P') GO TO READAC
	LOKCTL = 1
	XCALL IO (1,GLAMAS,TCRCNO,READ,LOKCTL)
TEST,
	CD = TFSTCD(6,7)
	IF (CD.EQ.'LF') GO TO FORMS
	IF (CD.EQ.'HL') GO TO HEADLN
	IF (CD.NE.'HF') GO TO TYPBL
	LINCNT = 60
TYPBL,
	TYPBA2 = TYPBAL
	IF (TFSTCD(7,7).EQ.'P') GO TO VPIP
	IF (SAFCDE.NE.' ') GO TO READAC
	PLINE (3,32) = DESCR
	IF (CD.EQ.'GH'.OR.CD.EQ.'SR') CALL HEADER
	IF (CD.EQ.'TX') CALL PRINT
	DLRSW = 
	IF (CD.EQ.'SS') CALL SECSUB
	IF (CD.EQ.'ST'.OR.CD.EQ.'ER') CALL SUBTOT
	IF (CD.EQ.'TT') CALL TOTAL
	IF (CD.EQ.'NT') GO TO NET
	GO TO READAC
HEADLN,
	PLINE = 
	PLINE (35,64) = DESCR
	CALL HEADER
	GO TO READAC
FORMS,
	XCALL LINFD (1)
	INCR LINCNT
	GO TO READAC
VPIP,
	CALL TSTPFC
	DESCR2 = DESCR
	IF (TFSTCD(6,6).EQ.'I') GO TO NXTACT
	CALL GTLYR
FNDIDX,
	IF (PFC.EQ.0) GO TO RDMAIN
;;;	IF (PFC(2,3).EQ.0.AND.TFSPFC(1,1).EQ.PFC(1,1)) GOTO RDMAIN	;;;
	IF (TFSPFC.NE.PFC) GO TO NXTACT
RDMAIN,
	IF (TYRCNO.EQ.0) GOTO NXTACT
	LOKCTL = 1
	XCALL IO (4,YTDGLT,TYRCNO,READ,LOKCTL)
	INCR TYRCNO
	IF (YACTNO.NE.ACCTNO) GO TO NXTACT
	IF (YTRXDT .GT. CCENDT) GOTO NXTACT
	IF (YTRXDT .LT. CCSTDT) GOTO PRECUR

;;;	GLDTE2(1,2) = YTRXDT(5,6)
;;;	GLDTE2(3,6) = YTRXDT(1,4)
;;;	IF (GLDTE2.GT.ENDTE2) GO TO NXTACT
;;;	IF (GLDTE2.LT.STRDT2) GO TO PRECUR
;-------------------------------------------

	VPCUR = VPCUR + YTRXAM
	GO TO RDMAIN
PRECUR,
	VPPRE = VPPRE + YTRXAM
	GO TO RDMAIN
NXTACT,
	OFF ERROR
	LOKCTL = 1
	XCALL IOS (5,TFSIDX,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOF
	IF (TFSIDX.EQ.BRACKS) GO TO EOF
	IF (TFSTCD(1,1).NE.'P') GO TO NXTACT
	LOKCTL = 1
	XCALL IO (1,GLAMAS,TCRCNO,READ,LOKCTL)
	IF (TFSTCD(6,7).NE.'VS') GO TO VIPTOT
	CALL GTLYR
	CALL TSTPFC
	GO TO FNDIDX
VIPTOT,
	IF (PFCSW.EQ.0)GO TO TEST
	CURAMT = VPCUR
	YTDAMT = VPCUR+VPPRE
	IF (CURAMT.NE.0.OR.YTDAMT.NE.0) GO TO PRTVIP
	IF (CCSTNO.EQ.0) GO TO NOPRT
	IF (CURBUD.NE.0) GO TO PRTVIP
	IF (CURCOM.NE.0) GO TO PRTVIP
	IF (YTDCOM.EQ.0) GO TO NOPRT
PRTVIP,
	PLINE (1,30) = DESCR2
	CALL BUFF3
	SUBCUR = SUBCUR + VPCUR
	SUBYTD = SUBYTD + VPCUR + VPPRE
	SUBCOM = SUBCOM + CURCOM
	SUBYCM = SUBYCM + YTDCOM
	SUBBUD = SUBBUD + CURBUD
	SUBYBD = SUBYBD + YTDBUD
	SECCUR = SECCUR + VPCUR
	SECYTD = SECYTD + VPCUR + VPPRE
	SECCOM = SECCOM + CURCOM
	SECYCM = SECYCM + YTDCOM
	SECBUD = SECBUD + CURBUD
	SECYBD = SECYBD + YTDBUD
	CLRFLD =
NOPRT,
	DLRSW = 1
	PFCSW =
	GO TO TEST
TSTPFC,
	IF (PFC.EQ.0) GO TO SETSW
;;;	IF (PFC(2,3).EQ.0.AND.ACCTNO(5,5).EQ.PFC(1,1)) GO TO SETSW	;;;
	IF (ACCTNO(5,7).NE.PFC) RETURN
SETSW,
	PFCSW = 1
	RETURN
BUFF3,
	CBDVAR = CURAMT - CURBUD
	YBDVAR = YTDAMT - YTDBUD
	CPYVAR = CURAMT - CURCOM
	YPYVAR = YTDAMT - YTDCOM
	RATIO1 =
	ONERROR NXTOP1
	RATIO1 = (CURAMT*100000/SLSCUR)#1
NXTOP1,
	RATIO2 =
	ONERROR NXTOP2
	RATIO2 = (YTDAMT*100000/SLSYTD)#1
NXTOP2,
	IF (COMP.EQ.0) GOTO NXTOP4
	IF (CCSTNO.EQ.0.AND.CCENNO.EQ.0) GOTO NXTOP4
	RATIO3 =
 	ONERROR NXTOP3
	IF (COMP.EQ.1.AND.TYPCMP.NE.3) RATIO3 = (CURBUD*100000/SLSBCU)#1
	IF (COMP.EQ.1.AND.TYPCMP.EQ.3) RATIO3 = (YBDVAR*10000/YTDBUD)#1
	IF (COMP.EQ.2.AND.TYPCMP.NE.3) RATIO3 = (CURCOM*100000/SLSLCU)#1
	IF (COMP.EQ.2.AND.TYPCMP.EQ.3) RATIO3 = (YPYVAR*10000/YTDCOM)#1
NXTOP3,
	RATIO4 = 
	ONERROR NXTOP4
	IF (COMP.EQ.1.AND.TYPCMP.NE.2) RATIO4 = (YTDBUD*100000/SLSBYR)#1
	IF (COMP.EQ.1.AND.TYPCMP.EQ.2) RATIO4 = (CBDVAR*10000/CURBUD)#1
	IF (COMP.EQ.2.AND.TYPCMP.NE.2) RATIO4 = (YTDCOM*100000/SLSLYR)#1
	IF (COMP.EQ.2.AND.TYPCMP.EQ.2) RATIO4 = (CPYVAR*10000/CURCOM)#1
NXTOP4,
	OFFERROR
	AMNT = CURAMT
	PLINE(35,47) = AMNT,MASK3
	PLINE(49,54) = RATIO1,MASK2
	COL = 34
	CALL TYPE
	AMNT = YTDAMT
	PLINE(60,72) = AMNT,MASK3
	PLINE(74,79) = RATIO2,MASK2
	COL = 59
	CALL TYPE
	IF (COMP.EQ.0) GOTO NOCOM
	IF (CCSTNO.EQ.0.AND.CCENNO.EQ.0) GOTO NOCOM
	IF (COMP.EQ.1.AND.TYPCMP.NE.3) AMNT = CURBUD
	IF (COMP.EQ.1.AND.TYPCMP.EQ.3) AMNT = YBDVAR
	IF (COMP.EQ.2.AND.TYPCMP.NE.3) AMNT = CURCOM
	IF (COMP.EQ.2.AND.TYPCMP.EQ.3) AMNT = YPYVAR
	PLINE(87,99) = AMNT,MASK3
	PLINE(101,106) = RATIO3,MASK2
	IF (TYPCMP.EQ.3) PLINE(101,106) = RATIO3,MASK4
	COL = 86
	CALL TYPE
	IF (COMP.EQ.1.AND.TYPCMP.NE.2) AMNT = YTDBUD
	IF (COMP.EQ.1.AND.TYPCMP.EQ.2) AMNT = CBDVAR
	IF (COMP.EQ.2.AND.TYPCMP.NE.2) AMNT = YTDCOM
	IF (COMP.EQ.2.AND.TYPCMP.EQ.2) AMNT = CPYVAR
	PLINE(112,124) = AMNT,MASK3
	PLINE(126,131) = RATIO4,MASK2
	IF (TYPCMP.EQ.2) PLINE(126,131) = RATIO4,MASK4
	COL = 111
	CALL TYPE
NOCOM,
	CALL PRINT
	RETURN
TYPE,
	INSERT =
	IF (TYPBA2.EQ.'C') GO TO TYPECR
	IF (AMNT.LT.0) GO TO PARENS
	GO TO PERTYP
TYPECR,
	IF (AMNT.LE.0) GO TO PERTYP
PARENS,
	PLINE (COL+21,COL+21) = '-'
	PLINE (COL+14,COL+14) = ')'
	INSERT(1,1) = '('
	GO TO (JUSTFY), DLRSW
	GO TO DOLLAR
PERTYP,
	IF (DLRSW.EQ.1) RETURN
DOLLAR,
	PLINE (COL,COL) = '$'
JUSTFY,
	INCR COL
	IF (PLINE (COL,COL).EQ.' ') GO TO JUSTFY
	PLINE (COL-1,COL-1) = INSERT (1,1)
	RETURN
SECSUB,
	CURAMT = SECCUR
	YTDAMT = SECYTD
	CURCOM = SECCOM
	YTDCOM = SECYCM
	CURBUD = SECBUD
	YTDBUD = SECYBD
	CALL STOT
	CALL BUFF3
	SEC =
	CLRFLD =
	RETURN
SUBTOT,
	CURAMT = SUBCUR
	YTDAMT = SUBYTD
	CURCOM = SUBCOM 
	YTDCOM = SUBYCM
	CURBUD = SUBBUD
	YTDBUD = SUBYBD
	CALL STOT
	CALL BUFF3
	TOTCUR = TOTCUR + SUBCUR
	TOTYTD = TOTYTD + SUBYTD
	TOTCOM = TOTCOM + SUBCOM
	TOTYCM = TOTYCM + SUBYCM
	TOTBUD = TOTBUD + SUBBUD
	TOTYBD = TOTYBD + SUBYBD
	CLRSUB =
	SEC =
	CLRFLD = 
	RETURN
TOTAL,
	CALL SETAMT
	CALL STOT
	CALL BUFF3
	CLRFLD =
	RETURN
NET,
	CALL STOT
	PLINE (2,31) =
	CALL SETAMT
	PLINE (10,29) = 'NET INCOME (OR LOSS)'
	CALL BUFF3
	CALL GTOT
	GO TO EOF
SETAMT,
	TOTCUR = TOTCUR + SUBCUR
	TOTYTD = TOTYTD + SUBYTD
	TOTCOM = TOTCOM + SUBCOM
	TOTYCM = TOTYCM + SUBYCM
	TOTBUD = TOTBUD + SUBBUD
	TOTYBD = TOTYBD + SUBYBD
	CLRSUB =
	SEC =
	CLRFLD =
	CURAMT = TOTCUR
	YTDAMT = TOTYTD
	CURCOM = TOTCOM
	YTDCOM = TOTYCM
	CURBUD = TOTBUD
	YTDBUD = TOTYBD
	RETURN
HEADER,
	XCALL LINFD (1)
	INCR LINCNT
PRINT,
	PRTCTL = PAGSIZ
	IF (LINCNT .GE. 60)  CALL PRTHDR
PRINT1,
	XCALL LPOUT (LINCNT,PGCNT,PLINE (1,PAGSIZ),'NO TITLE','NO HDR',
&		' ',' ','NO LEGEND',' ',' ',0,0,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
EOF,
	PNLOR = PNLOR - 1
	IF (PNLOR.LE.0) GO TO END
	LOKCTL = 1
	XCALL IO (5,TFSIDX,1,READ,LOKCTL)
	NOTTL =
	LINCNT = 60
	TOTCUR =
	TOTCOM =
	TOTYTD =
	TOTYCM =
	TOTBUD =
	TOTYBD =
	CLRFLD =
	GO TO BEGIN
END,
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	CLOSE 1
	XCALL FILES (1,'U',31,5)		;FILE # 031 -- GLAMAS FILE
	LOKCTL = 1
	XCALL IO (1,DUMGLA,1,READ,LOKCTL)
	NETCUR = TOTCUR
	NETYTD = TOTYTD
	NETCOM = TOTCOM
	NETYCM = TOTYCM
	IF (COMP.EQ.1) NETYBD = TOTYBD
	LOKCTL = 1
	XCALL IO (1,DUMGLA,1,WRITE,LOKCTL)
	CLOSE 1
	CLOSE 4
	IF (NUMBAL(SUB).EQ.0) GO TO END1
STOP,
	XCALL PGCHN (PROGRM,1)
END1,
	IF (NUMSCH(SUB).EQ.0) GO TO END2
	PROGRM(4,9) = 'PSCHED'
	GO TO STOP
END2,
	IF (NUMSAF(SUB).EQ.0) GO TO END3
	PROGRM(4,9) = 'FINPOS'
	GO TO STOP
END3,
	PROGRM(4,9) = 'SARATO'
	GO TO STOP
GTOT,
	LINCNT = LINCNT - 1
	PLINE =
	PLINE (33,48) = DOUBLE
	PLINE (58,73) = DOUBLE
	IF (CCSTNO.EQ.0.AND.CCENNO.EQ.0) GO TO GTP
	IF (COMP.EQ.0) GO TO GTP
	PLINE (85,100) = DOUBLE
	PLINE (110,125) = DOUBLE
GTP,
	CALL PRINT
	INCR LINCNT
	RETURN
STOT,
	LINCNT = LINCNT - 1
	PLINE =
	PLINE (33,48) = UNDER
	PLINE (58,73) = UNDER
	IF (CCSTNO.EQ.0.AND.CCENNO.EQ.0) GO TO STP
	IF (COMP.EQ.0) GO TO STP
	PLINE (85,100) = UNDER
	PLINE (110,125) = UNDER
STP,
	CALL PRINT
	INCR LINCNT
	PLINE (3,32) = DESCR
	RETURN
GTLYR,
	IF (CCSTNO.EQ.0.AND.CCENNO.EQ.0) RETURN
	IF (PFC.EQ.0) GO TO AOK
;;;	IF (PFC(2,3).EQ.0.AND.ACCTNO(5,5).EQ.PFC(1,1)) GO TO AOK	;;;
	IF (ACCTNO(5,7).NE.PFC) RETURN
AOK,
	IF (CCSTNO.EQ.1) CURCOM = (CURCOM+LYEAR(CCENNO+1))
	IF (CCSTNO.GT.1) CURCOM = CURCOM + (LYEAR(CCENNO+1) - LYEAR(CCSTNO))
	YTDCOM = YTDCOM + LYEAR(CCENNO+1)
	IF (CCSTNO.EQ.1) CURBUD = CURBUD + BUDGET(CCENNO)
	IF (CCSTNO.GT.1) CURBUD = CURBUD + (BUDGET(CCENNO) - BUDGET(CCSTNO-1))
	YTDBUD = YTDBUD + BUDGET(CCENNO)
	RETURN
ENDOFF,
	XCALL WATE(3,V)
	CLOSE 4
	XCALL PGCHN ('GL:UNPRFS',1)
END
