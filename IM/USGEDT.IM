; USGEDT / IM
;
;	TRANSACTION ENTRY EDIT LIST/JOURNAL OF USAGE TRANSACTIONS
;
;
RECORD	D_STUFF
	D_IN	,D8		;DATE-IN, ANY FORMAT
	D_OUT	,D6		;RETURN VALUE MMDDYY
	D_OUTR	,D8		;RETURN VALUE CCYYMMDD
	D_FMT	,A10		;RETURN VALUE MM/DD/CCYY
	D_SW	,A2		;"99" = DATE CONVERSION ERROR

RECORD USGTRX
		.INCLUDE 'DEF:RD043A.DEF'
RECORD USGCTL
		.INCLUDE 'DEF:RD043B.DEF'
;:
RECORD HDR1
		,A50,'--------- TAG ----------------   OLD QTY       QTY'
		,A50,'        UNIT        DOLLARS        QTY     USAGE  '
RECORD HDR2
		,A50,'NUMBER    DESCRIPTION             ON HND      USED'
		,A50,'        COST           USED     REMAIN      DATE  '
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD
	TITLE	,A30,'USAGE TRANSACTION EDIT LIST'
	V	,D1
	SWITCH	,D1
	KEY	,A15
	BSMID	,D5
	SRCCTL	,D1
	LOKCTL	,D1
	READ	,D1	,0
	PLINE	,A132
	PGCNT	,D2
	LINCNT	,D2	,62
	PRTTYP	,A1
	RPTNUM	,D3
	PRTCTL	,D1
	LPSW	,D1
	SPLFIL	,A14
	MASK	,A7	,'ZZZZZX-'
	DTMASK	,A8	,'XX/XX/XX'
	DP4MSK	,A13	,'ZZZZ,ZZZ.XXXX'
	RECNO	,D5
	PSTMSG	,A9
	POST	,D1
	MSGCTL	,D1
	TAGCNT	,D6
	USGQGT	,D6
	USGCST	,D10
	EXTCST	,D9
	TDESCR	,A20
;
PROC
	XCALL TERID (V)
	MSGCTL = 1
	XCALL SNMSG (PSTMSG,MSGCTL)
	IF (MSGCTL.EQ.9) GOTO OPEN1
	MSGCTL = 3
	XCALL SNMSG (KEY,MSGCTL)
	KEY =
	IF (PSTMSG.EQ.'IM:USGPST')
	BEGIN
	  POST = 1
	  TITLE = 'USAGE TRANSACTION JOURNAL'
	END
	XCALL OUTPT (1,2,2,TITLE,1)
OPEN1,
	SWITCH = 1
	IF (POST) SWITCH = 5
	XCALL FILES (3,'I',43,SWITCH)		;FILE 43 - USGTRX
	IF (SWITCH.NE.9) GOTO OPENLP
	GOTO ABORT
OPENLP,
	LPSW = 1
	IF (POST) LPSW = 3
	SPLFIL (5,6) = 'FQ'
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.NE.0) GOTO RDHDR
	CALL CLOSE1
	XCALL FILES (3,'U',43,4)
	GOTO ABORT
CLOSES,
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
CLOSE1,
	CLOSE 3
	IF (POST.EQ.0) XCALL FILES (3,'U',43,4)
	RETURN
RDHDR,
	XCALL WATE (2,2)
	LOKCTL = 1
	XCALL IO (3,USGCTL,1,READ,LOKCTL)
	RECNO = 1
RDLOOP,
	INCR RECNO
	LOKCTL = 1
	XCALL IO (3,USGTRX,RECNO,READ,LOKCTL)
	IF (USGTRX.EQ.']]]]]]]]]]') GOTO EOF
	IF (TUSGDT.EQ.0) GOTO RDLOOP
	PLINE (1,7) = TTAGNO,MASK
	TDESCR(1,3) = TMATTY
	TDESCR(4,6) = ' - '
	TDESCR(7,9) = TGAGE
	TDESCR(10,12) = ' x '
	TDESCR(13,19) = TWIDTH,'ZZZ.XXX'
	PLINE (11,30) = TDESCR
	PLINE (35,41) = TQTYOH,MASK
	PLINE (45,51) = TQTY,MASK
	PLINE (53,63) = TCOST,DP4MSK
	EXTCST = TQTY*TCOST
	PLINE (66,78) = EXTCST,DP4MSK
	PLINE (83,89) = (TQTYOH - TQTY),MASK

	XCALL DATE8(TUSGDT, D_OUT, D_OUTR, D_FMT, D_SW)
	PLINE (91,100) = D_FMT
	CALL PRINT
	INCR TAGCNT
	USGQGT = USGQGT + TQTY
	USGCST = USGCST + EXTCST
	GOTO RDLOOP
EOF,
	XCALL LINFD (2)
	PLINE (1,7) = TAGCNT,MASK
	PLINE (11,35) = 'TAGS ENTERED - SUBTOTAL: '
	PLINE (44,51) = USGQGT,MASK
	PLINE (66,78) = USGCST,DP4MSK
	CALL PRINT
	CALL CLOSES
	IF (POST.EQ.0) GOTO ABORT
	XCALL PGCHN (PSTMSG,1)
ABORT,
	XCALL PGCHN ('IM:USGENT',1)
PRINT,
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,'NO HDR3',
&		'NO LEGEND',' ',' ',0,132,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
END
;--------- TAG ----------------   OLD QTY       QTY        UNIT        DOLLARS        QTY     USAGE
;NUMBER    DESCRIPTION             ON HND      USED        COST           USED     REMAIN      DATE
;XXXXXX-   XXXXXXXXXXXXXXXXXXXX    XXXXXX-   XXXXXX-   XXXX.XXXX    XX,XXX.XXXX    XXXXXX-   XX/XX/XX
;XXXXXX-   XXXXXXXXXXXXXXXXXXXX    XXXXXX-   XXXXXX-   XXXX.XXXX    XX,XXX.XXXX    XXXXXX-   XX/XX/XX
