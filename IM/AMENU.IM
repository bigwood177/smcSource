SUBROUTINE AMENU
;
;  AMENU / IM 
;
;
;		::PCPYIM.DEF::
;*****************************************************************************
;		INVENTORY MANAGEMENT  
;
;		RELEASED: AUGUST 1, 1984 (d70s10)
;*****************************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;		ROUTINE TO DISPLAY STANDARD MASTER FILE
;		MAINTENANCE MENU & PASS BACK OPTION SELECTED
;		AS MODIFIED FOR PURCHASE ADVICE REPORT
;
;	SELECTION DATA FOR SUBROUTINE AMENU:
;
;		IF SLOC = DEFAULT LOCATION, CALCULATIONS WILL BE BASED ON THE
;		   SUM OF ALL LOCATIONS - AND REPORT WILL PRINT THIS LOC ONLY
;
;		IF SLOC = 'ALL', CALCULATIONS WILL BE BASED ON THE SUM OF ALL
;		   LOCATIONS - BUT THE REPORT WILL PRINT LOCATION DETAIL
;
;		IF SLOC = ONE LOC, ONLY THIS LOCATION WILL PRINT
;
;		AVFLAG IS SET IF QTY AVAILABLE SHOULD INCLUDE QTY ON ORDER
;
;		MPFLAG IS SET FOR ITEMS PURCHASED(P), MANUFACTURED(M) OR
;		   BOTH(B) TO BE INCLUDED IN REPORT
;
;		SELECT(1,1): 1 = BY ITEM, 2 = BY LOCATION, 3 = BY VENDOR
;
;		IF SELECT(1,1) = 1 OR 2, SELECT(2,2):  1 = ONE LOC AND %
;		   FROM REOLVL, 2 = ONE LOC AND # FROM REOLVL, 3 = ALL LOCS
;		   AND % FROM REOLVL, AND 4 = ALL LOCS AND # FROM REOLVL
;
	MENUID	,A	; TITLE TO DISPLAY AT TOP OF SCREEN
	FIELD	,D	; PERCENT OR NUMBER PASSED BACK
	SLOC	,A	; LOCATION PASSED BACK FOR REPORT
	AVFLAG	,D	; FLAG FOR AVAILABLE INCLUDING ON ORDER
	MPFLAG	,A	; FLAG FOR MANUFACTURED OR PURCHASED ITEMS
	SELECT	,D	; VALUE OF OPTION PASSED BACK
	V	,D	; VT50/52 CONTROL
RECORD	CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD
	WHATNO	,D1
	CNGCTL	,D1
	INXCTL	,D1	; PARAMETERS
	ENTRY	,A4	; FOR INPUT
	MODEID	,3A12,' BY ITEM    ',' BY LOCATION',' BY VENDOR  '
PROC (1)
RETRY,	XCALL OUTPT (1,1,2,MENUID,V)
	XCALL OUTPT (5,20,0,'PLEASE SELECT APPLICATION',V)
	XCALL OUTPT (6,25,0,'1. PRINT ADVICE BY ITEM',V)
	XCALL OUTPT (7,25,0,'2. PRINT ADVICE BY LOCATION',V)
	XCALL OUTPT (8,25,0,'3. PRINT ADVICE BY VENDOR',V)
MNUSEL,
	CTL = '05,47,01,01,#E'
	CALL INPUT
	GO TO (MNUSEL,SELEND), INXCTL
	SELECT(1,1) = ENTRY
	IF (SELECT(1,1).LT.1.OR.SELECT(1,1).GT.3) GO TO BADSEL
DSPMOD,							; DISPLAY MODE
	XCALL OUTPT (1,1,2,MENUID,V)
	XCALL OUTPT (0,0,0,MODEID(SELECT(1,1)),V)
PRLOC,
	IF (SELECT(1,1).EQ.3) RETURN
	CNGCTL =
	XCALL OUTPT (2,1,2,'SELECT CRITERIA',1)
	XCALL OUTPT (5,10,0,'1. LOCATION',V)
	XCALL OUTPT (6,10,0,'2. PERCENT FROM REORDER LEVEL',V)
	XCALL OUTPT (7,10,0,'   NUMBER FROM REORDER LEVEL',V)
	XCALL OUTPT (8,10,0,
&	'3. SHOULD QUANTITY AVAILABLE INCLUDE ON-ORDER ?',V)
	XCALL OUTPT (9,10,0,
&	'4. FOR ITEMS MANUFACTURED(M), PURCHASED(P), OR BOTH(B) ?',V)
LOC,
	CTL = '05,68,02,00,AE'
	CALL INPUT
	GO TO (PRLOC,RETRY) ,INXCTL
	SLOC = ENTRY
	IF (SLOC.EQ.'  ') XCALL OUTPT (5,68,0,'ALL',V)
	GO TO (SETSEL) , CNGCTL
PCENT,
	XCALL OUTPT (7,68,0,'     ',V)
	CTL = '06,68,02,00,#-'
	CALL INPUT
	GO TO (PRLOC) ,INXCTL
	FIELD = ENTRY
	ENTRY = FIELD, '-ZX%'
	IF (FIELD.NE.0) XCALL OUTPT (6,68,0,ENTRY,V)
	ENTRY =
	IF (FIELD.NE.0) GO TO SETSEL
NUMB,
	CTL = '07,68,03,00,#-'
	CALL INPUT
	GO TO (PRLOC) ,INXCTL
SETSEL,
	IF (SLOC.NE.'  '.AND.FIELD.NE.0) SELECT(2,2) = 1
	IF (SLOC.EQ.'  '.AND.FIELD.NE.0) SELECT(2,2) = 3
	IF (SLOC.NE.'  '.AND.FIELD.EQ.0) SELECT(2,2) = 2
	IF (SLOC.EQ.'  '.AND.FIELD.EQ.0) SELECT(2,2) = 4
	IF (FIELD.EQ.0.AND.(CNGCTL.EQ.0.OR.WHATNO.EQ.2)) FIELD = ENTRY
	GO TO (ANYCNG) ,CNGCTL
AVAIL,
	CTL = '08,68,01,01,YN'
	CALL INPUT
	AVFLAG = INXCTL
	GO TO (ANYCNG) ,CNGCTL
MFGPUR,
	CTL = '09,68,01,00,A '
	CALL INPUT
	GO TO (PRLOC) ,INXCTL
	IF (ENTRY(1,1).EQ.' ') ENTRY(1,1) = 'B'
	XCALL OUTPT (9,68,0,ENTRY(1,1),V)
	IF (ENTRY(1,1).NE.'B'.AND.ENTRY(1,1).NE.'P'.AND.ENTRY.NE.'M')
&		GO TO MFGPUR
	MPFLAG = ENTRY(1,1)
ANYCNG,
	XCALL ANYCN (CNGCTL,WHATNO)
	GO TO (CNGBR) ,CNGCTL
	XCALL OUTPT (3,1,2,'\',1)
	RETURN
CNGBR,
	GO TO (LOC,PCENT,AVAIL,MFGPUR) ,WHATNO
	GO TO ANYCNG
INPUT,
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,V)
	RETURN
BADSEL,
	XCALL MESAG (' ',6)
	GO TO MNUSEL
SELEND,
	SELECT =
	RETURN

END
