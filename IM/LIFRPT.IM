;  LIFRPT / IM 
;
;
;		::PCPYIM.DEF::
;*****************************************************************************
;		INVENTORY MANAGEMENT  
;
;		RELEASED: AUGUST 1, 1984 (d70s10)
;*****************************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;		PROGRAM TO REPORT A "LIFO" INVENTORY EVALUATION.
;		IE, LAST YEARS' QUANTITY ON HAND & COST VERSUS
;		THE CURRENT QUANTITY ON HAND & COSTS.
;
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD ITMMAS		;
		.INCLUDE 'DEF:RD041A.DEF'
RECORD DUMINV		;
		.INCLUDE 'DEF:RD041B.DEF'
RECORD ITMIDX		;
		.INCLUDE 'DEF:RD042A.DEF'
RECORD TITLE
		,A25,	'LIFO INVENTORY EVALUATION'
RECORD HDR1
		,A1
		,A11,	'ITEM NUMBER'
		,A6
		,A11,	'DESCRIPTION'
		,A20
		,A4,	'UOFM'
		,A2
		,A8,	'LIFO QTY'
		,A3
		,A9,	'LIFO COST'
		,A5
		,A7,	'ON-HAND'
		,A4
		,A12,	'AVERAGE COST'
		,A6
		,A19,	'-- DOLLAR CHANGE ++'
RECORD SUBHDR
		,A32
		,A13,	'OLD LIFO BASE'
		,A8
		,A10,	'NET CHANGE'
		,A12
		,A9,	'NEW VALUE'
RECORD PLINE
		,A132
RECORD
	SPLFIL	,A14

RECORD
	BADOPN	,D1
	BRACKS	,A15,	']]]]]]]]]]]]]]]'
	CHNGE1	,D12
	CNGCTL	,D1
	COUNT	,D5
	DECMAL	,D15
	ENTRY	,A15
	INXCTL	,D1
	LINCNT	,D2
	LOCCTR	,D2
	MSK11	,A10,	'ZZ,ZZX.XX-'
	MSK21	,A18,	'ZZ,ZZZ,ZZZ,ZZX.XX-'
	MSK31	,A19,	'ZZ,ZZZ,ZZZ,ZZX.XXX-'
	MSKQTY	,A8,	'ZZZ,ZZX-'
	NLIFO1	,D12
	OLIFO1	,D12
	PGCNT	,D6
	QTY	,D6
	SWITCH	,D1
	TOTL11	,D13
	TOTL21	,D12
	TOTL31	,D13
	V	,D1
	WHATNO	,D2
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	LPSW	,D2
	LPARG	,D1
	RPTNUM	,D3
	PRTTYP	,A1
PROC (1)
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'LIFO INVENTORY EVALUATION & COST ROLL-OVER',V)
	XCALL OUTPT (6,16,0,
&	'THIS REPORT WILL PRINT ALL THE ITEMS IN INVENTORY.',V)
	XCALL OUTPT (7,16,0,'DO YOU WISH TO PRINT THIS "LIFO" EVALUATION ?',V)
	XCALL INPUT (7,63,1,1,'YN',ENTRY,INXCTL,V)
	IF (ENTRY.EQ.'N') XCALL PGCHN ('IM:IMSFMN',1)
	XCALL OUTPT (2,1,2,'PRINT OUT',1)
	SPLFIL(5,6) = 'CH'
	LPSW = 2	; NOT DISPLAYABLE
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) XCALL PGCHN ('IM:IMSFMN',1)
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)			;PROCESSING MESSAGE
	CALL OPENS				;OPEN ALL NECESSARY FILES
	IF (BADOPN) XCALL PGCHN ('IM:IMSFMN',1)		;RETURN TO SPECIAL MENU
	LOKCTL = 1
	XCALL IOS (1,ITMIDX,READ,LOKCTL)	;SKIP 1ST INDEX RECORD
	IF (LOKCTL.EQ.2) GO TO EOF
	PGCNT =
	LINCNT = 60
NXTIDX,
	LOKCTL = 1
	XCALL IOS (1,ITMIDX,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOF
	IF (IITMNO.EQ.BRACKS) GO TO EOF		;END-OF-FILE CONDITION
	IF (IRC041.EQ.0) GO TO NXTIDX		;INDEX FLAGGED FOR DELETION
	LOKCTL = 1
	XCALL IO (2,ITMMAS,IRC041,READ,LOKCTL)
	IF (DESCR.EQ.']]]DEL') GO TO NXTIDX	;ITMMAS RECORD FLAGGED
						;FOR DELETION
	CALL REPORT				;GENERATE REPORT
	INCR COUNT				;INCREMENT COUNT OF ITEMS
	GO TO NXTIDX
EOF,
	XCALL LINFD (2)
	LINCNT = LINCNT + 2
	PLINE = SUBHDR
	CALL PRINT
	PLINE (30,47) = TOTL11, MSK21
	PLINE (51,67) = TOTL21, MSK21
	PLINE (71,88) = TOTL31, MSK21
	CALL PRINT
	XCALL LINFD (2)
	PLINE (14,18) = COUNT, 'ZZZZX'
	PLINE (20,32) = 'ITEMS COUNTED'
	CALL PRINT
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	XCALL WATE (3,V)
	CALL CLOSES				;CLOSE ALL OPEN DATA FILES
QUEST,
	XCALL OUTPT (2,1,2,'\',1)
	XCALL OUTPT (6,17,0,'DO YOU WISH TO "ROLL-OVER" THE ITEM',V)
	XCALL OUTPT (7,17,0,'MASTER LIFO COSTS AND QUANTITIES ?',V)
	XCALL INPUT (7,53,1,1,'YN',ENTRY,INXCTL,V)
	CNGCTL = 2				;DO NOT GET "WHAT NUMBER"
	XCALL ANYCN (CNGCTL,WHATNO)
	GO TO (QUEST), CNGCTL			;YES, CHANGE DESIRED
	IF (ENTRY.EQ.'Y') XCALL PGCHN ('IM:LIFROL',1)	;CHAIN TO LIFO-ROLL PROGRAM
	XCALL PGCHN ('IM:IMSFMN',1)				;NO ROLL-OVER -- GO BACK TO MENU
;************************************************************************
REPORT,
	PLINE (2,16) = ITEMNO
	PLINE (19,49) = DESCR
	PLINE (51,52) = SUOFM
	QTY =
	LOCCTR =
SUM,
	INCR LOCCTR
	IF (LOCCTR.GT.NUMLOC.OR.LOC(LOCCTR).EQ.'  ') GO TO ENDSUM
	QTY = QTY + QTYONH (LOCCTR)
	GO TO SUM
ENDSUM,
	PLINE (57,64) = LIFOBQ, MSKQTY
	PLINE (67,76) = LIFOBP, MSK11
	PLINE (81,88) = QTY, MSKQTY
	PLINE (92,103) = AVGCST, MSK31
	IF (QTY.LE.LIFOBQ) GO TO DECRES
INCRES,
	DECMAL = ((QTY-LIFOBQ) * AVGCST) # 1
	IF (DECMAL.NE.0) PLINE (117,131) = DECMAL, MSK21
						;SHOW INCREASE IN LIFO BASE
	TOTL31 = TOTL31 + ((QTY-LIFOBQ)*AVGCST)#1+(LIFOBQ*LIFOBP)
						;INCREMENT SUBTOTAL OF NEW
						;LIFO BASE VALUE
	GO TO PRNTIT
DECRES,
	DECMAL = (QTY - LIFOBQ) * LIFOBP
	IF (DECMAL.NE.0) PLINE (108,122) = DECMAL, MSK21
						;SHOW DECREASE IN LIFO BASE
	TOTL31 = TOTL31 + (QTY*LIFOBP)
						;LIFO BASE VALUE
PRNTIT,
	TOTL11 = TOTL11 + (LIFOBQ * LIFOBP)	;SUBTOTAL OLD LIFO BASE VALUE
	TOTL21 = TOTL21 + DECMAL		;SUBTOTAL NET CHANGE DOLLARS
	CALL PRINT
	XCALL LINFD (1)
	INCR LINCNT
	RETURN
;************************************************************************

OPENS,
	SWITCH = 1
	XCALL FILES (1,'I',42,SWITCH)		;ITMIDX FILE #42
	IF (SWITCH.EQ.9) GO TO INUSE1
	SWITCH = 1
	XCALL FILES (2,'I',41,SWITCH)		;ITMMAS FILE #41
	IF (SWITCH.EQ.9) GO TO INUSE2
	LOKCTL = 1
	XCALL IO (2,DUMINV,1,READ,LOKCTL)
	BADOPN =
	RETURN
INUSE2,
	XCALL FILES (1,'I',42,4)		;CLOSE ITMIDX FILE #42
INUSE1,
	BADOPN = 1				;FLAG THAT FILES ARE NOT OPEN
	RETURN
;************************************************************************
CLOSES,
	XCALL FILES (2,'I',42,4)		;CLOSE ITMIDX FILE #42
	XCALL FILES (1,'I',41,4)		;CLOSE ITMMAS FILE #41
	RETURN
;************************************************************************

PRINT,
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,'NO HDR','NO HDR',
&	 'NO LEGEND','NO LEGEND','NO LEGEND',0,132,132,0,LPSW,RPTNUM,PRTTYP)
	RETURN

END
