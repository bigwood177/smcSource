; INVPRT / IM
;
;	R/M INVENTORY FILE PRINTOUT
;
;
RECORD	D_STUFF
	D_IN	,D8		;DATE-IN, ANY FORMAT
	D_OUT	,D6		;RETURN VALUE MMDDYY
	D_OUTR	,D8		;RETURN VALUE CCYYMMDD
	D_FMT	,A10		;RETURN VALUE MM/DD/CCYY
	D_SW	,A2		;"99" = DATE CONVERSION ERROR

RECORD INVMAS
		.INCLUDE 'DEF:RD132A.DEF'
RECORD INVCTL
		.INCLUDE 'DEF:RD132B.DEF'
RECORD INVIDX
		.INCLUDE 'DEF:RD133A.DEF'
;:
RECORD HDR1
		,A50,'--------- TAG ----------------    UNIT         QTY'
		,A50,'        UNIT        DOLLARS       DATE            '
RECORD HDR2
		,A50,'NUMBER    DESCRIPTION             MEAS      ON HND'
		,A50,'        COST        ON HAND     RECEIVED    NOTES '

RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD
	TITLE	,A30,'INVENTORY FILE PRINT OUT'
	V	,D1
	SWITCH	,D1
	KEY	,A15
	BSMID	,D5
	SRCCTL	,D1
	LOKCTL	,D1
	READ	,D1	,0
	PLINE	,A132
	PGCNT	,D2
	LINCNT	,D2	,62
	PRTTYP	,A1
	RPTNUM	,D3
	PRTCTL	,D1
	LPSW	,D1
	SPLFIL	,A14
	MASK	,A11	,'ZZ,ZZZ,ZZX-'
	DTMASK	,A8	,'XX/XX/XX'
	DP4MSK	,A15	,'Z,ZZZ,ZZZ.XXXX-'
	RECNO	,D5
	PSTMSG	,A9
	POST	,D1
	MSGCTL	,D1
	TAGCNT	,D6
	TOTQTY 	,D8
	TOTCST	,D12
	EXTCST	,D10
;
PROC
	XCALL TERID (V)
	XCALL OUTPT (2,1,2,TITLE,1)
OPEN1,
	SWITCH = 1
	XCALL FILES (3,'I',133,SWITCH)		;FILE 133 - RINVIX
	IF (SWITCH.NE.9) GOTO OPEN2
	GOTO ABORT
OPEN2,
	XCALL FILES (2,'I',132,SWITCH)		;FILE 132 - RINVMS
	IF (SWITCH.NE.9) GOTO OPENLP
	CALL CLOSE1
	GOTO ABORT
OPENLP,
	LPSW = 1
	SPLFIL (5,6) = 'FR'
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.NE.0) GOTO RDHDR
	CALL CLOSE2
	GOTO ABORT
CLOSES,
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
CLOSE2,
	XCALL FILES (2,'I',132,4)
CLOSE1,
	XCALL FILES (3,'I',133,4)
	RETURN
RDHDR,
	XCALL WATE (2,2)
	LOKCTL = 1
	XCALL IO (2,INVCTL,1,READ,LOKCTL)
	RECNO = 1
RDLOOP,
	INCR RECNO
	LOKCTL = 1
	XCALL IO (3,INVIDX,RECNO,READ,LOKCTL)
	IF (INVIDX.EQ.']]]]]]]]]]') GOTO EOF
	IF (IRC132.EQ.0) GOTO RDLOOP
	LOKCTL = 1
	XCALL IO (2,INVMAS,IRC132,READ,LOKCTL)
	PLINE (1,6) = TAGNO,'ZZZZZX'
	PLINE (11,13) = MATTYP
	PLINE (14,16) = ' - '
	PLINE (17,19) = GAGE
	PLINE (20,22) = ' x '
	PLINE (23,29) = WIDTH,'ZZZ.XXX'
	PLINE (36,39) = UOFM
	PLINE (44,51) = QTYONH,MASK
	PLINE (55,63) = COST,DP4MSK
	EXTCST = QTYONH * COST
	PLINE (67,78) = EXTCST,DP4MSK

	XCALL DATE8(DTRCVD, D_OUT, D_OUTR, D_FMT, D_SW)
	PLINE (81,90) = D_FMT

	PLINE (95,129) = NOTES
	CALL PRINT
	INCR TAGCNT
	TOTQTY = TOTQTY + QTYONH
	TOTCST = TOTCST + EXTCST
	GOTO RDLOOP
EOF,
	XCALL LINFD (2)
	PLINE (1,7) = TAGCNT,MASK
	PLINE (11,35) = 'TAGS ON FILE - SUBTOTAL: '
	PLINE (41,51) = TOTQTY,MASK
	PLINE (64,78) = TOTCST,DP4MSK
	CALL PRINT
	CALL CLOSES
ABORT,
	XCALL PGCHN ('IM:INVMNT',1)
PRINT,
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,'NO HDR3',
&		'NO LEGEND',' ',' ',0,132,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
END
;--------- TAG ----------------    UNIT         QTY        UNIT        DOLLARS       DATE
;NUMBER    DESCRIPTION             MEAS      ON HND        COST        ON HAND     RECEIVED    NOTES
;XXXXXX-   XXXXXXXXXXXXXXXXXXXX    XXXX      XXXXXX-   XXXX.XXXX    XX,XXX.XXXX    XX/XX/XX    XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
;XXXXXX-   XXXXXXXXXXXXXXXXXXXX    XXXX      XXXXXX-   XXXX.XXXX    XX,XXX.XXXX    XX/XX/XX    XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
