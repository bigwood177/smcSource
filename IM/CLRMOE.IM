;  CLRMOE / IM 
;
;
;		::PCPYIM.DEF::
;*****************************************************************************
;		INVENTORY MANAGEMENT  
;
;		RELEASED: AUGUST 1, 1984 (d70s10)
;*****************************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;		CLEARS ITEM MASTER OF MONTH-TO-DATE FIGURES
;		AND RECALCULATES AVGUSE,AVGERR,SUMERR,REOLVL,RECMIN, & SAFSTK
;		IF SOLD TO CUSTOMER OR ISSUED OUT OF INVENTORY
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD ITMMAS		;
		.INCLUDE 'DEF:RD041A.DEF'
RECORD ,X		;
		.INCLUDE 'DEF:RD041B.DEF'
RECORD
	DECMAL	,D5
	MTDQTY	,D6
	DEFALT	,A2
	CTR	,D2
	LOCTNS	,D2
	RECCTR	,D5,00002
	ENTRY	,A2
	INXCTL	,D1
	SWITCH	,D1,1
	V	,D1
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
PROC (1)
	XCALL TERID (V)
	SWITCH = 3
	XCALL FILES (1,'U',41,SWITCH)		;FILE # 41 -- ITMMAS FILE
	IF (SWITCH.EQ.9) GO TO END
	SWITCH = 1
	XCALL FILES (2,'I',42,SWITCH)		;FILE # 42 -- ITMIDX FILE
	IF (SWITCH.EQ.9) GO TO END1
	SWITCH = 5
	XCALL FILES (1,'U',41,SWITCH)
	IF (SWITCH.EQ.9) GO TO EOF
	XCALL OUTPT (1,1,2,
&	'CLEAR ITEM MONTH-TO-DATE FIELDS & RESET REORDER FIELDS',V)
	XCALL OUTPT (6,10,0,
&	'ARE YOU SURE YOU WANT TO CLEAR AND UPDATE THESE FIELDS ?',V)
	XCALL INPUT (6,68,1,1,'YN',ENTRY,INXCTL,V)
	GO TO (EOF), INXCTL-1
	XCALL WATE (4,V)
	LOKCTL = 1
	XCALL IO (1,ITMMAS,1,READ,LOKCTL)
	DEFALT = DFLTLO
	LOCTNS = NUMLOC
READ1,
	LOKCTL = 1
	XCALL IO (1,ITMMAS,RECCTR,READ,LOKCTL)
	CTR = 1
	IF (ITMMAS(1,6).EQ.']]]]]]') GO TO EOF
	IF (LOC(CTR).NE.DEFALT) GO TO FINDLO
;	IF (SAVTYP.EQ.1.OR.SAVTYP.EQ.3.OR.SAVTYP.EQ.5) GO TO CLEAR
					;NO LONGER RESTRICT IMFMNT SCREEN
					;DUE TO TYPSYS
CALCUL,
	MTDQTY = (USEFLT*AVGUSE)#1
	IF (USEMTD.LT.MTDQTY.OR.MTDQTY.EQ.0) MTDQTY = USEMTD
	SUMERR = (AVGUSE - MTDQTY) + SUMERR
	DECMAL = MTDQTY - AVGUSE
	IF (DECMAL.LT.0) DECMAL = -1 * DECMAL
	AVGERR = ((USEWGT * DECMAL) + ((100 - USEWGT) * AVGERR)) #2
	SAFSTK = (SAFFCT * AVGERR) #1
	AVGUSE = ((USEWGT * MTDQTY) + ((100 - USEWGT) * AVGUSE)) #2
	RECMIN = ((AVGUSE * LEADTM) #1)
	IF (RECMIN.LT.0) RECMIN =
	REOLVL(CTR) = ((AVGUSE * LEADTM) #1) + SAFSTK
CLEAR,
	QTYMTD =
	USEMTD =
	SLSMTD =
	CSTMTD =
	LOKCTL = 1
	XCALL IO (1,ITMMAS,RECCTR,WRITE,LOKCTL)
	INCR RECCTR
	GO TO READ1
FINDLO,
	INCR CTR
	IF (CTR.GT.LOCTNS.OR.LOC(CTR).EQ.'  ') GO TO CLEAR
	IF (LOC(CTR).NE.DEFALT) GO TO FINDLO
	GO TO CALCUL
EOF,
	XCALL FILES (2,'I',42,4)
END1,
	XCALL FILES (1,'U',41,4)
END,
	XCALL PGCHN ('IM:IMSFMN',1)

END
