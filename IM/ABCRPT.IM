;  ABCRPT / IM 
;
;
;		::PCPYIM.DEF::
;*****************************************************************************
;		INVENTORY MANAGEMENT  
;
;		RELEASED: AUGUST 1, 1984 (d70s10)
;*****************************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		PRINTS ABC ANALYSIS REPORT
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD ITMMAS		;
		.INCLUDE 'DEF:RD041A.DEF'
RECORD INVCTL		;
		.INCLUDE 'DEF:RD041B.DEF'
RECORD ITMIDX		;
		.INCLUDE 'DEF:RD042A.DEF'
RECORD ABCIDX		;
		.INCLUDE 'DEF:RD131A.DEF'
RECORD ABCCTL,X		;
		.INCLUDE 'DEF:RD131C.DEF'
RECORD ABCCT2,X		;
		.INCLUDE 'DEF:RD131B.DEF'
RECORD TITLE
		,A19,	'ABC ANALYSIS REPORT'
RECORD LEGND1
		,A15,	'CLASS A -- TOP '
	LAPRCT	,A3
		,A19,	'%, CLASS B -- NEXT '
	LBPRCT	,A3
		,A21,	'%, CLASS C -- BOTTOM '
	LCPRCT	,A3
		,A26,	'% OF TOTAL INVENTORY VALUE'
RECORD HDR1
		,A51
		,A11,	'UNITS USAGE'
		,A4
		,A7,	'AVERAGE'
		,A4
		,A10,	'DOLLARIZED'
		,A5
		,A19,	'--INVENTORY CLASS--'
		,A2
		,A16,	'DOLLARIZED USAGE'
RECORD HDR2
		,A2
		,A11,	'ITEM NUMBER'
		,A15
		,A11,	'DESCRIPTION'
		,A15
		,A5,	'Y-T-D'
		,A9
		,A4,	'COST'
		,A7
		,A5,	'USAGE'
		,A8
		,A7,	'PRESENT'
		,A3
		,A9,	'SUGGESTED'
		,A6
		,A9,	'SUB-TOTAL'
RECORD
	SPLFIL	,A14
RECORD
	V	,D1
	SWITCH	,D1
	PLINE	,A132
	PGCNT	,D6
	LINCNT	,D2,	66
	APERCT	,D3
	BPERCT	,D3
	CPERCT	,D3
	TOTALV	,D15
	SUBTOT	,D15
	SUGCLS	,A1,	'A'
	ENTRY	,A1
	INXCTL	,D1
	CNGCTL	,D1
	WHATNO	,D1
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	LPSW	,D2
	LPARG	,D1
	RPTNUM	,D3
	PRTTYP	,A1
PROC (1)
	XCALL TERID (V)
BEGIN,
	XCALL OUTPT (1,1,2,'PRINT ABC ANALYSIS REPORT',1)

	SWITCH = 5
	XCALL FILES (1,'I',131,SWITCH)		; FILE # 131 ABC INDEX
						; (PROTECTED IN ABCSEL)
	SWITCH = 5
	XCALL FILES (2,'I', 41,SWITCH)		; FILE # 41 INVENTORY MASTER
						; (PROTECTED IN ABCSEL)
	LOKCTL = 1
	XCALL IO (2,INVCTL,1,READ,LOKCTL)	; READ ITMMAS CONTROL RECORD
						; FOR VALUE OF JSTIFY
	SPLFIL(5,6) = 'CM'
	LPSW = 1
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO NOSET
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
; ***************** INPUT INDEX CONTROL VALUES ***************************
	LOKCTL = 1
	XCALL IOS (1,ABCCTL,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO LASTRD
	TOTALV = TOTVAL

	LOKCTL = 1
	XCALL IOS (1,ABCCT2,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO LASTRD
	APERCT = ABCPCT

	LOKCTL = 1
	XCALL IOS (1,ABCCT2,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO LASTRD
	BPERCT = ABCPCT

	LOKCTL = 1
	XCALL IOS (1,ABCCT2,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO LASTRD
	CPERCT = ABCPCT
	LAPRCT = APERCT, 'ZZX'
	LBPRCT = BPERCT, 'ZZX'
	LCPRCT = CPERCT, 'ZZX'
; ******************* READ INFO RECORDS, PRINT REPORT ********************
READLP,
	LOKCTL = 1
	XCALL IOS (1,ABCIDX,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO LASTRD
	IF (ABCIDX(1,6) .EQ. ']]]]]]') GO TO LASTRD
	LOKCTL = 1
	XCALL IO (2,ITMMAS,ABC041,READ,LOKCTL)
;
;	LOAD VALUE ON HAND -- MAKE SURE PRESENT SUGGESTED CLASS IS RIGHT.
;
	ON ERROR NOTOTL
	IF ((100*SUBTOT/TOTALV) .GT. APERCT) SUGCLS = 'B'
	IF ((100*SUBTOT/TOTALV) .GT. (APERCT + BPERCT)) SUGCLS = 'C'
NOTOTL,
	OFF ERROR
	SUBTOT = SUBTOT + (USEYTD * AVGCST)
	CALL LODLIN
	CALL PRINT
	GO TO READLP
LASTRD,
	CALL PRINT
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
LSTRD2,
	XCALL OUTPT (1,1,2,'PRINT ABC ANALYSIS REPORT',1)
	XCALL OUTPT (6,17,0,'SHOULD SUGGESTED INVENTORY CLASSES BE SET UP ?',V)
	XCALL INPUT (06,64,01,01,'YN',ENTRY,INXCTL,V)
	CNGCTL = 2
	XCALL ANYCN (CNGCTL,WHATNO)
	GO TO (LSTRD2), CNGCTL
	IF (INXCTL.NE.1) GO TO NOSET
STOSET,
	XCALL WATE (4,V)
	CLOSE 1
	CLOSE 2
	XCALL PGCHN ('IM:ABCSET',1)
; *************************************************************************
NOSET,
	XCALL WATE (4,V)
	SWITCH = 7
	XCALL FILES (1,'I',131,SWITCH);	DELETE INDEX
	XCALL FILES (2,'I',41,4)
	XCALL PGCHN ('IM:IMMENU',1)
; ************************************************************************
LODLIN,
	PLINE (1,15) = ITEMNO
	IF (JSTIFY) XCALL LEFTJ (PLINE(1,15),15)
	PLINE (20,49) = DESCR
	PLINE (54,60) = USEYTD, 'ZZZ,ZZX'
	PLINE (64,74) = AVGCST, 'ZZZ,ZZX.XXX'
	PLINE (76,90) = USEYTD * AVGCST, 'ZZZ,ZZZ,ZZX.XXX'
	PLINE (96,96) = INVCLS
	PLINE (107,107) = SUGCLS
	PLINE (115,129) = SUBTOT, 'ZZZ,ZZZ,ZZX.XXX'
	RETURN
; *************************************************************************
PRINT,
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,
&		'NO HDR',LEGND1,'NO LEGEND',' ',0,132,132,1,LPSW,RPTNUM,PRTTYP)
	RETURN
; *************************************************************************
END

