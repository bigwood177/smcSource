;  ITMSTS / IM 
;
;
;		::PCPYIM.DEF::
;*****************************************************************************
;		INVENTORY MANAGEMENT  
;
;		RELEASED: AUGUST 1, 1984 (d70s10)
;*****************************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;		PROGRAM TO PRINT INVENTORY STOCK STATUS REPORT BY ITEM
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD ITMMAS		;
		.INCLUDE 'DEF:RD041A.DEF'
RECORD DUMINV ,X		;
		.INCLUDE 'DEF:RD041B.DEF'
RECORD ITMIDX		;
		.INCLUDE 'DEF:RD042A.DEF'
RECORD TITLE
		,A27,	'STOCK STATUS REPORT BY ITEM'
RECORD LEGND1
		,A19,	'   BACK ORDER CODE:'
		,A36,	'   NO = ITEM MAY NOT BE BACK ORDERED'
		,A41,	'   (BLANK MEANS ITEM MAY BE BACK ORDERED)'
RECORD LEGND2
		,A16,	'   STOCK STATUS:'
		,A34,	'   REO = AT OR BELOW REORDER LEVEL'
		,A21,	'   O/S = OUT OF STOCK'
RECORD HDR1
		,A49
		,A3,	'PRD'
		,A7
		,A3,	'QTY'
		,A5
		,A3,	'QTY'
		,A5
		,A3,	'QTY'
		,A4
		,A5,	'REORD'
		,A5
		,A7,	'AVERAGE'
		,A5
		,A8,	'VALUE OF'
		,A3
		,A5,	'BKORD'
		,A2
		,A3,	'STK'
RECORD HDR2
		,A4
		,A7,	'ITEM NO'
		,A6
		,A16,	'ITEM DESCRIPTION'
		,A16
		,A3,	'CAT'
		,A1
		,A3,	'LOC'
		,A2
		,A5,	'ONHND'
		,A3
		,A5,	'ALLOC'
		,A3
		,A5,	'ONORD'
		,A3
		,A5,	'LEVEL'
		,A7
		,A4,	'COST'
		,A6
		,A9,	'INVENTORY'
		,A3
		,A4,	'CODE'
		,A2
		,A3,	'STS'
RECORD RCVMSG
	STRTIT	,A15
		,A5
RECORD NXTMSG
	ENDITM	,A15
	STRTLO	,A3
RECORD
	SPLFIL	,A14
	ITMCNT	,D5
	ITMVAL	,D9
	TOTVAL	,D10
	MASK1	,A10,	'ZZ,ZZZ.XXX'
	MASK2	,A14,	'ZZ,ZZZ,ZZZ.XX-'
	MASK3	,A8,	'ZZZ,ZZX-'
	LINCNT	,D2,	60
	PGCNT	,D3
	PLINE	,A132
	PRTCTL	,D3
	ENTRY	,A2
	INXCTL	,D1
	JUSTIF	,D1
	RECNO	,D5
	FRSTSW	,D2
	SWITCH	,D1,	1
	CTR	,D2
	SUMONH	,D7
	SUMCOM	,D7
	SUMVAL	,D10
	LOCTNS	,D2
	V	,D1
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	LPSW	,D2
	LPARG	,D1
	RPTNUM	,D3
	PRTTYP	,A1
	BLANKS	,A10
	MSGSWT	,D1
PROC (1)
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'PRINT STOCK STATUS REPORT BY ITEM',V)
	XCALL WATE (4,V)
	MSGSWT = 1
	XCALL SNMSG (RCVMSG,MSGSWT)
	MSGSWT = 3
	XCALL SNMSG (BLANKS,MSGSWT)		;PURGE MESSAGE STACK
	MSGSWT = 1
	XCALL SNMSG (NXTMSG,MSGSWT)
	MSGSWT = 3
	XCALL SNMSG (BLANKS,MSGSWT)
BEGIN,
	XCALL FILES (2,'I',42,SWITCH)		;FILE # 42 -- ITMIDX FILE
	IF (SWITCH.EQ.9) GO TO INU042
	XCALL FILES (1,'I',41,SWITCH)		;FILE # 41 -- ITMMAS FILE
	IF (SWITCH.EQ.9) GO TO INU041
	LOKCTL = 1
	XCALL IO (1,DUMINV,1,READ,LOKCTL)
	LOCTNS = NUMLOC
	JUSTIF = JSTIFY
	RECNO =
	SPLFIL(5,6) = 'CK'
	LPSW = 1
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO CLOSE
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
READ,
	INCR RECNO
	LOKCTL = 1
	XCALL IO (2,ITMIDX,RECNO,READ,LOKCTL)
	IF (IITMNO.EQ.']]]]]]') GO TO TOTAL
	IF (IRC041.EQ.0) GO TO READ
	IF (IITMNO.LT.STRTIT) GO TO READ
	IF (IITMNO.GT.ENDITM) GO TO READ
	LOKCTL = 1
	XCALL IO (1,ITMMAS,IRC041,READ,LOKCTL)
	FRSTSW =
BUFF,
	CTR =
	PLINE (1,15) = ITEMNO
	IF (JUSTIF) XCALL LEFTJ (PLINE(1,15),15)
	PLINE (18,47) = DESCR
	PLINE (50,51) = PRDCAT
MULTLO,
	INCR CTR
	IF (CTR.GT.LOCTNS.OR.LOC(CTR).EQ.'  ') GO TO NOMORE
	IF (STRTLO.NE.'ALL'.AND.LOC(CTR).NE.STRTLO) GO TO MULTLO
	PLINE (54,55) = LOC(CTR)
	PLINE (58,64) = QTYONH(CTR), MASK3
	SUMONH = SUMONH + QTYONH(CTR)
	PLINE (66,72) = QTYCOM(CTR),MASK3
	SUMCOM = SUMCOM + QTYCOM(CTR)
	PLINE (73,80) = QTYONO(CTR), MASK3
	PLINE (90,99) = AVGCST, MASK1
	PLINE (82,88) = REOLVL(CTR), MASK3
	IF (QTYONH(CTR).LT.0) QTYONH(CTR) = 0
	ITMVAL = (QTYONH(CTR)*AVGCST) #1
	SUMVAL = SUMVAL + ITMVAL
	TOTVAL = TOTVAL + ITMVAL
	PLINE (102,114) = ITMVAL, MASK2
	IF (STRTLO.NE.'ALL'.OR.FRSTSW.EQ.0) GO TO FIRST
	PLINE (1,51) =
	PLINE (89,99) =
	IF ((QTYONH(CTR)-QTYCOM(CTR)).LE.REOLVL(CTR)) PLINE (123,125) = 'REO'
	IF ((QTYONH(CTR)-QTYCOM(CTR)).LE.0) PLINE (123,125) = 'O/S'
	INCR FRSTSW
	CALL PRINT
	GO TO MULTLO
FIRST,
	IF (BOCODE.EQ.1) PLINE (118,119) = 'NO'
	IF ((QTYONH(CTR)-QTYCOM(CTR)).LE.REOLVL(CTR)) PLINE (123,125) = 'REO'
	IF ((QTYONH(CTR)-QTYCOM(CTR)).LE.0) PLINE (123,125) = 'O/S'
	INCR ITMCNT
	INCR FRSTSW
	CALL PRINT
	IF (STRTLO.NE.'ALL') GO TO ONELOC
	GO TO MULTLO
NOMORE,
	IF (STRTLO.NE.'ALL') GO TO READ
	IF (FRSTSW.LE.1.OR.CTR.LE.2) GO TO ONELOC
	XCALL LINFD (1)
	INCR LINCNT
	PLINE (45,55) = 'ITEM TOTAL:'
	PLINE (57,64) = SUMONH, MASK3
	PLINE (65,72) = SUMCOM, MASK3
	PLINE (101,114) = SUMVAL, MASK2
	CALL PRINT
ONELOC,
	SUMONH =
	SUMCOM =
	SUMVAL =
	XCALL LINFD (1)
	INCR LINCNT
	GO TO READ
PRINT,
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,'NO HDR',
&	LEGND1,LEGND2,'NO LEGEND',0,132,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
TOTAL,
	PLINE =
	PLINE (2,7) = ITMCNT, MASK3
	PLINE (8,24) = 'ITEMS AT LOCATION'
	PLINE (26,27) = STRTLO
	IF (STRTLO.EQ.'ALL') PLINE (17,29) = 'ALL LOCATIONS'
	PLINE (87,98) = 'TOTAL VALUE:'
	PLINE (101,114) = TOTVAL, MASK2
	XCALL LINFD (1)
	CALL PRINT
	XCALL LINFD (2)
	LINCNT = LINCNT + 3
	ITMCNT =
	TOTVAL =
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	XCALL FILES (1,'I',41,4)
	XCALL FILES (2,'I',42,4)
	IF (STRTIT.EQ.'   '.AND.ENDITM.EQ.'[[['.AND.STRTLO.EQ.'ALL') GO TO CLOSE
END,
	XCALL PGCHN ('IM:PSTKST',1)
CLOSE,
	XCALL FILES (1,'I',41,4)
INU041,
	XCALL FILES (2,'I',42,4)
INU042,
	XCALL PGCHN ('IM:IMMENU',1)
END
