; USGHS2 / IM
;
;	USAGE HISTORY REPORT
;
;
RECORD	D_STUFF
	D_IN	,D8		;DATE-IN, ANY FORMAT
	D_OUT	,D6		;RETURN VALUE MMDDYY
	D_OUTR	,D8		;RETURN VALUE CCYYMMDD
	D_FMT	,A10		;RETURN VALUE MM/DD/CCYY
	D_SW	,A2		;"99" = DATE CONVERSION ERROR

RECORD INVMAS
		.INCLUDE 'DEF:RD131A.DEF'
;;;RECORD INVCTL
;;;		.INCLUDE 'DEF:RD132B.DEF'

RECORD USGHST
		.INCLUDE 'DEF:RD134A.DEF'
RECORD INVTAG
		.INCLUDE 'DEF:RD040A.DEF'
;:
RECORD HDR1
		,A40,'                        ON HAND    STK U'
		,A40,'SED     AVERAGE     AVERAGE     AVERAGE '
RECORD HDR2
		,A40,'INVENTORY                 AS OF      WK '
		,A50,'END   USE/MONTH   USE/MONTH   USE/MONTH    MONTHS '
RECORD HDR3
		,A23,'DESCRIPTION            '
	HDTODA	,A10
		,A2
	HDWKEN	,A10
		,A6,'      '
	LSTYR	,D4
		,A7,'   QTR-'
	LQTR	,D1
		,A2
	LQYR	,D2
		,A8,'    YTD '
	CURYR	,D4
		,A11,'   ON HAND '
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD	VARS
	XDATE	,D6

RECORD
	TMPDAT	,D8
	WKBEG	,D8
	WKEND	,D8
	WEEKS	,D2
	DAYTOT	,D3
	WKNUM	,D2
	DAYS	,12D2,31,28,31,30,31,30,31,31,30,31,30,31
RECORD	,X
	TYY	,D4
	TMM	,D2
	TDD	,D2
	WBYY	,D4
	WBMM	,D2
	WBDD	,D2
	WEYY	,D4
	WEMM	,D2
	WEDD	,D2
RECORD ACCUM
	USGLYR	,52D7
	USGCYR	,52D7
	TUSGLY	,D8
	TUSGLQ	,D8
	DECMAL	,D18
	USGWKL	,D8
	ONHCUR	,D8
	AVGMUS	,D8
RECORD KY1ACC
	K1ONHC	,D8
	K1USLW	,D8
	K1USLY	,D8
	K1USLQ	,D8
	K1AVGM	,D8
	KY1CNT	,D3
RECORD KY2ACC
	K2ONHC	,D8
	K2USLW	,D8
	K2USLY	,D8
	K2USLQ	,D8
	K2AVGM	,D8
	KY2CNT	,D3
RECORD 
	GTONHC	,D8
	GTUSLW	,D8
	GTUSLY	,D8
	GTUSLQ	,D8
	GTAVGM	,D8
RECORD
	TITLE	,A30,'USAGE HISTORY REPORT          '
	V	,D1
	SWITCH	,D1
	KEY	,A15
	BSMID	,D5
	SRCCTL	,D1
	LOKCTL	,D1
	READ	,D1	,0
	PLINE	,A132
	PGCNT	,D2
	LINCNT	,D2	,62
	PRTTYP	,A1
	RPTNUM	,D3
	PRTCTL	,D1
	LPSW	,D1
	SPLFIL	,A14
	MASK	,A11	,'ZZ,ZZZ,ZZX-'
	DTMASK	,A8	,'XX/XX/XX'
	DP4MSK	,A15	,'Z,ZZZ,ZZZ.XXXX-'
	RECNO	,D5
	I	,D2
	LSTDSC	,A20
	BLANKS	,A20
;
PROC
	XCALL TERID (V)
	XCALL OUTPT (2,1,2,TITLE,1)
OPEN1,
	SWITCH = 5
	XCALL FILES (3,'I',040,SWITCH)		;FILE 040 - INVTAG
	IF (SWITCH.NE.9) GOTO OPEN2
	GOTO ABORT
OPEN2,
	XCALL FILES (2,'SI',132,SWITCH)		;FILE 132 - RINVMS
	IF (SWITCH.NE.9) GOTO OPEN3
	CALL CLOSE1
	GOTO ABORT
OPEN3,
	XCALL FILES (4,'I',134,SWITCH)		;FILE 134 - USGHST
	IF (SWITCH.NE.9) GOTO OPENLP
	CALL CLOSE2
	GOTO ABORT
OPENLP,
	LPSW = 1
	SPLFIL (5,6) = 'FT'
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.NE.0) GOTO RDHDR
	CALL CLOSES
	GOTO ABORT
CLOSES,
	XCALL FILES (4,'I',134,4)
CLOSE2,
	XCALL FILES (2,'SI',132,4)
CLOSE1,
	XCALL FILES (3,'I',040,4)
	RETURN
RDHDR,
	XCALL WATE (2,2)
 	RECNO = 1
	LOKCTL = 1
	XCALL IO (3,INVTAG,RECNO,READ,LOKCTL)
	XCALL RDATE (XDATE)
	XCALL DATE8(XDATE, D_OUT, D_OUTR, HDTODA, D_SW)

	XCALL DATE8(TDATE, D_OUT, D_OUTR, HDWKEN, D_SW)

	WKEND = TDATE
;;;	WKEND(1,2) = TDATE(5,6)
;;;	WKEND(3,6) = TDATE(1,4)
	WKBEG = WKEND
	WBDD = WBDD - 6
	IF (WBDD.LT.1)
	BEGIN
	  WBMM = WBMM - 1
	  IF (WBMM.LT.1) WBYY = WBYY - 1
	  IF (WBMM.LT.1) WBMM = 12
	  WBDD = WBDD + DAYS(WBMM)
	  IF (WBMM.EQ.2.AND.(((WBYY / 4) * 4) .EQ. WBYY) ) WBDD = WBDD + 1
	END	    
	LSTYR = WEYY-1
	IF (WEMM.GE.1.AND.WEMM.LE.3) LQTR = 4
	IF (WEMM.GE.4.AND.WEMM.LE.6) LQTR = 1
	IF (WEMM.GE.7.AND.WEMM.LE.9) LQTR = 2
	IF (WEMM.GE.10.AND.WEMM.LE.12) LQTR = 3
	LQYR = WEYY-1
	IF (LQTR.GE.1.AND.LQTR.LE.3) LQYR = WEYY
	CURYR = WEYY
	FOR I = 1 STEP 1 UNTIL WEMM-1 DO DAYTOT = DAYTOT + DAYS(I)
	DAYTOT = DAYTOT + WEDD
	IF (WEMM.GT.2.AND.(((WEYY / 4) * 4) .EQ. WEYY) ) DAYTOT = DAYTOT + 1
	WEEKS = DAYTOT/7
	IF (WEEKS.LT.1) WEEKS = 1
	IF (WEEKS.GT.52) WEEKS = 52
RDLOOP,
	INCR RECNO
	LOKCTL = 1
	XCALL IO (3,INVTAG,RECNO,READ,LOKCTL)
	IF (INVTAG.EQ.']]]]]]]]]]') GOTO EOF
;;;	IF (TRECNO.EQ.0) GOTO RDLOOP
	IF (TDESCR.NE.LSTDSC) CALL PRTLIN
	IF (TTYPE.EQ.'I')
	BEGIN
	  LOKCTL = 1
	  XCALL ISIO (2,INVMAS,TTAGNO,READ,LOKCTL)
	  IF (LOKCTL .NE. 0) CLEAR INVMAS
	  ONHCUR = ONHCUR + RMWGT
	END
	IF (TTYPE.EQ.'H')
	BEGIN
	  IF (TRECNO.EQ.0) GOTO RDLOOP	;moved here 11-27-17

	  LOKCTL = 1
	  XCALL IO (4,USGHST,TRECNO,READ,LOKCTL)
;;;	  TMPDAT(1,2) = TDATE(5,6)
;;;	  TMPDAT(3,6) = TDATE(1,4)
	  TMPDAT = TDATE
	  IF (TMPDAT.GT.WKEND) GOTO RDLOOP
	  IF (TYY.LT.WEYY-1) GOTO RDLOOP
	  DAYTOT =
	  FOR I = 1 STEP 1 UNTIL TMM-1 DO DAYTOT = DAYTOT + DAYS(I)
	  DAYTOT = DAYTOT + TDD
	  IF (TMM.GT.2.AND.(((TYY / 4) * 4) .EQ. TYY) ) DAYTOT=DAYTOT+1
	  WKNUM = DAYTOT/7
	  IF (WKNUM.LT.1) WKNUM = 1
	  IF (WKNUM.GT.52) WKNUM = 52
	  IF (TYY.EQ.WEYY-1) USGLYR(WKNUM) = USGLYR(WKNUM) + HQTY
	  IF (TYY.EQ.WEYY)   USGCYR(WKNUM) = USGCYR(WKNUM) + HQTY
	  IF (TMPDAT.LT.WKBEG) GOTO RDLOOP
	  USGWKL = USGWKL + HQTY
	END
	GOTO RDLOOP
PRTLIN,
	IF (LSTDSC.EQ.BLANKS) GOTO FSTTIM
	PLINE(1,20) = LSTDSC
	PLINE(23,32) = ONHCUR,MASK
	PLINE(35,44) = USGWKL,MASK
	TUSGLY =
	FOR I = 1 STEP 1 UNTIL 52 DO TUSGLY = TUSGLY + USGLYR(I)
	PLINE(47,56) = ((TUSGLY*100)/12)#2,MASK
	TUSGLQ =
	IF (LQTR.EQ.1) 
&	  FOR I = 1 STEP  1 UNTIL 13 DO TUSGLQ = TUSGLQ + USGCYR(I)
	IF (LQTR.EQ.2) 
&	  FOR I = 14 STEP 1 UNTIL 26 DO TUSGLQ = TUSGLQ + USGCYR(I)
	IF (LQTR.EQ.3) 
&	  FOR I = 27 STEP 1 UNTIL 39 DO TUSGLQ = TUSGLQ + USGCYR(I)
	IF (LQTR.EQ.4) 
&	  FOR I = 40 STEP 1 UNTIL 52 DO TUSGLQ = TUSGLQ + USGLYR(I)
	PLINE(59,68) = ((TUSGLQ*100)/3)#2,MASK
	DECMAL =
	FOR I = 1 STEP 1 UNTIL WEEKS DO DECMAL = DECMAL + USGCYR(I)
	AVGMUS = (((DECMAL*100)/WEEKS)*433)#4
	PLINE (71,80)  = AVGMUS,MASK
	IF (AVGMUS) PLINE (83,89) = (ONHCUR*100000 / AVGMUS)#1,'ZZ.XXXX' 
	CALL PRINT
	INCR KY1CNT
	INCR KY2CNT
	K1ONHC = K1ONHC + ONHCUR
	K1USLW = K1USLW + USGWKL
	K1USLY = K1USLY + TUSGLY
	K1USLQ = K1USLQ + TUSGLQ
	K1AVGM = K1AVGM + AVGMUS
	IF (TDESCR(1,9).NE.LSTDSC(1,9)) CALL SUBKY1
	IF (TDESCR(1,3).NE.LSTDSC(1,3)) CALL SUBKY2
FSTTIM,
	LSTDSC = TDESCR
	ACCUM =
	RETURN
SUBKY1,
	IF (KY1CNT.LE.1) GOTO SUBCL1
	PLINE (1,9) = LSTDSC(1,9)
	PLINE (10,20) = ' SUBTOTAL: '
	PLINE(23,32) = K1ONHC,MASK
	PLINE(35,44) = K1USLW,MASK
	PLINE(47,56) = K1USLY,MASK
	PLINE(59,68) = K1USLQ,MASK
	PLINE (71,80)  = K1AVGM,MASK
	IF (K1AVGM) PLINE (83,89) = (K1ONHC*100000 / K1AVGM)#1,'ZZ.XXXX' 
	CALL PRINT
SUBCL1,
	XCALL LINFD (1)
	INCR LINCNT
	K2ONHC = K2ONHC + K1ONHC
	K2USLW = K2USLW + K1USLW
	K2USLY = K2USLY + K1USLY
	K2USLQ = K2USLQ + K1USLQ
	K2AVGM = K2AVGM + K1AVGM
	KY1ACC =
	RETURN
SUBKY2,
	IF (KY2CNT.LE.1) GOTO SUBCL2
	PLINE (1,3) = LSTDSC(1,3)
	PLINE (10,20) = ' SUBTOTAL: '
	PLINE(22,32) = K2ONHC,MASK
	PLINE(34,44) = K2USLW,MASK
	PLINE(46,56) = K2USLY,MASK
	PLINE(58,68) = K2USLQ,MASK
	PLINE (70,80)  = K2AVGM,MASK
	IF (K2AVGM) PLINE (83,89) = (K2ONHC*100000 / K2AVGM)#1,'ZZ.XXXX' 
	CALL PRINT
SUBCL2,
	XCALL LINFD (1)
	INCR LINCNT
	GTONHC = GTONHC + K2ONHC
	GTUSLW = GTUSLW + K2USLW
	GTUSLY = GTUSLY + K2USLY
	GTUSLQ = GTUSLQ + K2USLQ
	GTAVGM = GTAVGM + K2AVGM
	KY2ACC =
	RETURN
EOF,
	CALL PRTLIN
	XCALL LINFD (2)
	PLINE (8,20) = 'GRAND TOTAL: '
	PLINE(22,32) = GTONHC,MASK
	PLINE(34,44) = GTUSLW,MASK
	PLINE(46,56) = GTUSLY,MASK
	PLINE(58,68) = GTUSLQ,MASK
	PLINE (70,80)  = GTAVGM,MASK
	IF (GTAVGM) PLINE (83,89) = (GTONHC*100000 / GTAVGM)#1,'ZZ.XXXX' 
	CALL PRINT
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
ENDOFF,
	CALL CLOSES
ABORT,
	XCALL PGCHN ('IM:IMMENU',1)
PRINT,
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,HDR3,
&		'NO LEGEND',' ',' ',0,132,PRTCTL,1,LPSW,RPTNUM,PRTTYP)
	RETURN
END
;                        ON HAND    STK USED     AVERAGE     AVERAGE     AVERAGE 
;INVENTORY                 AS OF      WK END   USE/MONTH   USE/MONTH   USE/MONTH    MONTHS
;DESCRIPTION            XX/XX/XX    XX/XX/XX        19XX   QTR-X  88    YTD 19XX   ON HAND
;XXX - XXX x XXX.XXX   X,XXX,XXX   X,XXX,XXX   X,XXX,XXX   X,XXX,XXX   X,XXX,XXX-  XX.XXXX
;
