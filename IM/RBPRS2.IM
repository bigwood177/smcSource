;  RBPRS2 / IM 
;
;
;		::PCPYIM.DEF::
;*****************************************************************************
;		INVENTORY MANAGEMENT  
;
;		RELEASED: AUGUST 1, 1984 (d70s10)
;*****************************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		2ND PART OF REBUILD OF PRODUCT STRUCTURE FILE AFTER A RE-ORG
;		OF THE INVENTORY MASTER FILE
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD PRDSTR		;
		.INCLUDE 'DEF:RD091A.DEF'
RECORD PRDCTL		;
		.INCLUDE 'DEF:RD091B.DEF'
RECORD PRDIDX		;
		.INCLUDE 'DEF:RD092A.DEF'
RECORD INVCTL		;
		.INCLUDE 'DEF:RD041B.DEF'
RECORD INVIDX		;
		.INCLUDE 'DEF:RD042A.DEF'
RECORD
	PIXCTR	,D5
	INXCTR	,D5
	SWITCH	,D1
	V	,D1
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
PROC
	XCALL TERID (V)
	XCALL WATE (4,V)
	XCALL OUTPT (2,01,1,'REBUILD WHERE USED',V)
	XCALL FILES (1,'U',91,5)		;FILE # 91 -- PRDSTR FILE
	XCALL FILES (2,'U',92,5)		;FILE # 92 -- PRDIDX FILE
	XCALL FILES (3,'I',41,5)		;FILE # 41 -- ITMMAS FILE
	XCALL FILES (4,'I',42,5)		;FILE # 42 -- ITMIDX FILE
	LOKCTL = 1
	XCALL IO (1,PRDCTL,1,READ,LOKCTL)
	LOKCTL = 1
	XCALL IO (3,INVCTL,1,READ,LOKCTL)
	PIXCTR = 2
	INXCTR = 2
FSTPDX,
	IF (PIXCTR.GT.MAX091) GO TO EOF
	LOKCTL = 1
	XCALL IO (2,PRDIDX,PIXCTR,READ,LOKCTL)
	IF (PRDIDX.EQ.']]]]]]]]]]]]]]]]]]]]') GO TO EOF
	IF (IRC091.NE.0) GO TO FSTIDX
	INCR PIXCTR
	GO TO FSTPDX
FSTIDX,
	IF (INXCTR.GT.MAX041) GO TO BADREC
	LOKCTL = 1
	XCALL IO (4,INVIDX,INXCTR,READ,LOKCTL)
	IF (IRC041.NE.0) GO TO COMPAR
	INCR INXCTR
	GO TO FSTIDX
COMPAR,
	IF (IITMNO.LT.CMPITM) GOTO RDIDX
	IF (IITMNO.GT.CMPITM) GOTO BADREC
	LOKCTL = 1
	XCALL IO (1,PRDSTR,IRC091,READ,LOKCTL)
	CRECNO = IRC041
	LOKCTL = 1
	XCALL IO (1,PRDSTR,IRC091,WRITE,LOKCTL)
	GOTO RDPIDX
BADREC,
	LOKCTL = 1
	XCALL IO (1,PRDSTR,IRC091,READ,LOKCTL)
	CITMNO = ']]]DEL'
	LOKCTL = 1
	XCALL IO (1,PRDSTR,IRC091,WRITE,LOKCTL)
	IRC091 = 0
	LOKCTL = 1
	XCALL IO (2,PRDIDX,PIXCTR,WRITE,LOKCTL)
	INCR DEL091
RDPIDX,
	INCR PIXCTR
	IF (PIXCTR.GT.MAX091) GOTO EOF
	LOKCTL = 1
	XCALL IO (2,PRDIDX,PIXCTR,READ,LOKCTL)
	IF (PRDIDX(1,6).EQ.']]]]]]') GOTO EOF
	GOTO COMPAR
RDIDX,
	INCR INXCTR
	IF (INXCTR.GT.ORG041) GOTO BADREC
	LOKCTL = 1
	XCALL IO (4,INVIDX,INXCTR,READ,LOKCTL)
	GOTO COMPAR
EOF,
	LOKCTL = 1
	XCALL IO (1,PRDCTL,1,WRITE,LOKCTL)
	XCALL FILES (1,'U',91,4)
	XCALL FILES (2,'U',92,4)
	XCALL FILES (3,'I',41,4)
	XCALL FILES (4,'I',42,4)
	XCALL PGCHN ('IM:IMMENU',1)
END
