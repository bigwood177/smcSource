;  USAGEX / IM 
;
;
;		::PCPYIM.DEF::
;*****************************************************************************
;		INVENTORY MANAGEMENT  
;
;		RELEASED: AUGUST 1, 1984 (d70s10)
;*****************************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;		PRINT INVENTORY USAGE EXCEPTIONS REPORT
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD ITMMAS		;
		.INCLUDE 'DEF:RD041A.DEF'
RECORD ,X		;
		.INCLUDE 'DEF:RD041B.DEF'
RECORD ITMIDX  		;
		.INCLUDE 'DEF:RD042A.DEF'
RECORD TITLE
		,A33,	'INVENTORY USAGE EXCEPTIONS REPORT'
RECORD LEGND1
		,A39,	'ALL ITEMS WITH SUM OF ERRORS EXCEEDING '
	APCT	,A3
		,A1,	'%'
		,A41,	' OF AVERAGE ERROR WITHIN INVENTORY CLASS '
	DSPCLS	,A3
RECORD HDR1
		,A49
		,A3,	'PRD'
		,A3
		,A3,	'AVG'
		,A4
		,A3,	'ERR'
		,A4
		,A3,	'AVG'
		,A4
		,A5,	'USAGE'
		,A1
		,A5,	'SAFTY'
		,A2
		,A5,	'SAFTY'
		,A2
		,A5,	'REORD'
		,A2
		,A4,	'LEAD'
		,A4
		,A3,	'QTY'
		,A6
		,A3,	'QTY'
RECORD HDR2
		,A11,	'ITEM NUMBER'
		,A6
		,A16,	'ITEM DESCRIPTION'
		,A16
		,A3,	'CAT'
		,A3
		,A3,	'ERR'
		,A4
		,A3,	'SUM'
		,A3
		,A5,	'USAGE'
		,A2
		,A6,	'WGTFCT'
		,A1
		,A5,	'STOCK'
		,A2
		,A5,	'FACTR'
		,A2
		,A5,	'LEVEL'
		,A2
		,A4,	'TIME'
		,A2
		,A6,	'SLDMTD'
		,A3
		,A6,	'SLDYTD'
RECORD
	SPLFIL	,A14
RECORD
	RECNO	,D5
	ITMCNT	,D5
	LINCNT	,D2,	60
	PGCNT	,D3
	PLINE	,A132
	PRTCTL	,D3
	ENTRY	,A3
	INXCTL	,D1
	STXCTL	,D1
	CTR	,D2
	LOCTNS	,D2
	DEFALT	,A2
	MASK	,A8,	'ZZZ,ZZX-'
	MSK	,A7,	'ZZZ,ZZX'
	WKPCT	,D3
	WKCLAS	,A1
	CNGCTL	,D1
	WHATNO	,D2
	DECMAL	,D8
	BLANKS	,A10
	SWITCH	,D1,	1
	V	,D1
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	LPSW	,D2
	LPARG	,D1
	RPTNUM	,D3
	PRTTYP	,A1
	FILFLG	,D1
	LPFLAG	,D1
PROC (1)
	XCALL TERID (V)
BEGIN,
	XCALL OUTPT (1,1,2,'PRINT USAGE EXCEPTIONS REPORT',V)

	IF (FILFLG.EQ.1) GO TO DISPLA
	XCALL WATE (4,V)
	SWITCH = 1
	XCALL FILES (2,'I',42,SWITCH)		;FILE # 42 -- ITMIDX FILE
	IF (SWITCH.EQ.9) GO TO END

	SWITCH = 1
	XCALL FILES (1,'I',41,SWITCH)		;FILE # 41 -- ITMMAS FILE
	IF (SWITCH.NE.9) FILFLG = 1
	IF (SWITCH.NE.9) GO TO START
	CALL CLOSE1
	GO TO END
START,
	LOKCTL = 1
	XCALL IO (1,ITMMAS,1,READ,LOKCTL)
	DEFALT = DFLTLO
	LOCTNS = NUMLOC
DISPLA,
	XCALL OUTPT (1,1,2,'PRINT USAGE EXCEPTIONS REPORT',V)
	XCALL OUTPT (6,6,0,'PRINT ALL ITEMS WITH SUM OF ERRORS',V)
	XCALL OUTPT (0,0,0,' EXCEEDING       OF AVERAGE ERROR',V)
	XCALL OUTPT (7,6,0,'WITHIN INVENTORY CLASS',V)

	XCALL INPUT (6,51,3,0,'#E',ENTRY,INXCTL,V)
	GO TO (DISPLA,END), INXCTL
	IF (ENTRY.EQ.'  ') GO TO DISPLA
	WKPCT = ENTRY(1,3)
	APCT(1,3) = WKPCT,MSK
	XCALL OUTPT (6,51,0,APCT,V)
	XCALL OUTPT (6,54,0,'%',V)

	XCALL INPUT (7,30,1,0,'A ',ENTRY,INXCTL,V)
	GO TO (DISPLA), INXCTL
	IF (ENTRY.EQ.' ') XCALL OUTPT (7,30,0,'ALL ',V)
	WKCLAS = ENTRY(1,1)
	DSPCLS = WKCLAS
	IF (WKCLAS.EQ.' ') DSPCLS = 'ALL'

	CNGCTL = 2
	XCALL ANYCN (CNGCTL,WHATNO)
	IF (CNGCTL) GO TO BEGIN
	XCALL WATE (4,V)
	ITMCNT =
	RECNO =
READ,
	INCR RECNO
	LOKCTL = 1
	XCALL IO (2,ITMIDX,RECNO,READ,LOKCTL)
	IF (IITMNO.EQ.']]]]]]') GO TO NOFIND
BUFF1,
	IF (IRC041.EQ.0) GO TO READ
	IF (LINCNT.GE.58) LINCNT = 60
	LOKCTL = 1
	XCALL IO (1,ITMMAS,IRC041,READ,LOKCTL)
	IF (AVGERR.EQ.0) GO TO READ
	DECMAL = (SUMERR*100)/AVGERR
	IF (DECMAL.LT.0) DECMAL = -1 * DECMAL
	IF (DECMAL.LE.WKPCT) GO TO READ
	IF (WKCLAS.NE.' '.AND.WKCLAS.NE.INVCLS) GO TO READ
BUFF,
	IF (LPFLAG.EQ.1) GO TO NOOPEN
	SPLFIL(5,6) = 'CE'
	LPSW = 1
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO END
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	LPFLAG = 1
NOOPEN,
	CTR = 1
	IF (LOC(CTR).NE.DEFALT) CALL FINDLO
	PLINE (1,15) = ITEMNO
	IF(JSTIFY) XCALL LEFTJ (PLINE(1,15),15)
	PLINE (18,47) = DESCR
	PLINE (50,51) = PRDCAT
	PLINE (54,58) = AVGERR,MSK
	PLINE (61,66) = SUMERR, MASK
	PLINE (68,74) = AVGUSE, MSK
	PLINE (82,87) = SAFSTK, MSK
	PLINE (91,93) = SAFFCT, 'X.X'
	PLINE (77,79) = USEWGT, '.XX'
	PLINE (96,101) = REOLVL(CTR),MSK
	PLINE (104,107) = LEADTM, 'ZX.X'
	PLINE (109,116) = QTYMTD, MASK
	PLINE (118,125) = QTYYTD, MASK
	CALL PRINT
	XCALL LINFD (1)
	INCR LINCNT
	INCR ITMCNT
	GO TO READ

;******************************************************************

CLOSE2,
	XCALL FILES (1,'I',41,4)
CLOSE1,
	XCALL FILES (2,'I',42,4)
	RETURN

;*****************************************************************

PRINT,
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,'NO HDR3',LEGND1,
&		'NO LEGEND2','  ',0,132,PRTCTL,1,LPSW,RPTNUM,PRTTYP)
	RETURN

;*****************************************************************

NOFIND,
	IF (ITMCNT.NE.0) GO TO END1
	XCALL MESAG ('NO ITEMS ON FILE MATCHING CRITERIA',2)
	GO TO RESET
FINDLO,
	INCR CTR
	IF (CTR.GT.LOCTNS.OR.LOC(CTR).EQ.'  ') GO TO READ
	IF (LOC(CTR).NE.DEFALT) GO TO FINDLO
	RETURN
END1,
	IF (ITMCNT.EQ.0) GO TO END
	XCALL LINFD (2)
	PLINE (2,7) = ITMCNT, MSK
	PLINE (9,22) = 'ITEMS REPORTED'
	LINCNT = LINCNT - 1
	CALL PRINT
RESET,
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	PGCNT =
	LINCNT = 60
	LPFLAG =
	GO TO DISPLA

;**********************************************************

END,
	XCALL WATE (4,V)
	IF (FILFLG.EQ.1) CALL CLOSE2
	XCALL PGCHN ('IM:IMMENU',1)

;**********************************************************
END
