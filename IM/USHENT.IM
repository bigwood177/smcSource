; USHENT / IM
;
;	ENTER USAGE HISTORY - USE THIS ONCE
;
RECORD USGHST
		.INCLUDE 'DEF:RD134A.DEF'
RECORD USHCTL
		.INCLUDE 'DEF:RD134B.DEF'
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD SRTMSG
		,A9 ,'IM:IMMENU'
	RCNT	,D5
	OCNT	,D5
	NXTPRG	,A9
RECORD
	MSGCTL	,D1
	V	,D1
	SWITCH	,D1
	DECMAL	,D10
	OPTION	,D1
	ENTRY	,A30
	INXCTL	,D1
	SELECT	,D1
	LOKCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	KMATTY	,A3
	KGAGE	,A3
	KWIDTH 	,D6
	KEY	,A12
	BSMID	,D5
	SRCCTL	,D1
	CNGCTL	,D1
	WHATNO	,D2
	TODAY	,D6
	BLANKS	,A10
	LSTDTE	,D6
PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'USAGE HISTORY INITIAL ENTRY',1)
	XCALL RDATE (TODAY)
 OPEN1,
	SWITCH = 1
	XCALL FILES (4,'U',134,SWITCH)		;FILE 134 - USGHST
	IF (SWITCH.NE.9) GOTO OPEN2
	GOTO ABORT
OPEN2,
	GOTO RDHDR
CLOSES,
	XCALL FILES (2,'I',132,4)
CLOSE1,
	XCALL FILES (3,'I',133,4)
	RETURN
RDHDR,
	LOKCTL = 1
	XCALL IO (4,USHCTL,1,READ,LOKCTL)

	UNLOCK 4
	XCALL MMENU ('USAGE HISTORY INITIAL ENTRY','USAGE',SELECT,V)
	GOTO (DISPLA,DISPLA,DISPLA,PRINT,PRINT), SELECT
ENDOFF,
	CALL CLOSES
	IF (ORG134.EQ.REC134) GOTO ABORT
	NXTPRG = 'IM:IMMENU'
	GOTO SORT
PRINT,
	CALL CLOSES
	XCALL PGCHN ('IM:IMMENU',1)
SORT,
	IF (SWITCH.EQ.9) GOTO ABORT
	RCNT = REC134
	OCNT = ORG134
	MSGCTL = 5
	XCALL SNMSG (SRTMSG,MSGCTL)
	MSGCTL = 2
	XCALL SNMSG (NXTPRG,MSGCTL)
	XCALL PGCHN ('AP:SRTUHS',1)
ABORT,
	XCALL PGCHN ('IM:IMMENU',1)
;
DISPLA,
	UNLOCK 4
	USGHST =
	INXCTL =
	CNGCTL =
	XCALL OUTPT ( 6,20,2,'1. MATL/GAUGE/SIZE ',1)
	XCALL OUTPT ( 8,20,0,'2. QUANTITY USED ',1)
	XCALL OUTPT (10,20,0,'3. UNIT OF MEASURE ',1)
	XCALL OUTPT (12,20,0,'4. UNIT COST ',1)
	XCALL OUTPT (14,20,0,'5. DATE USED ',1)
DESCR,
	IF (CNGCTL.AND.SELECT.GT.1)
	BEGIN
	  XCALL MESAG ('CANNOT CHANGE KEY',1)
	  GOTO ANYCNG
	END
	CTL = '06,40,03,01,AE'
	CALL INPUT
	GOTO (DISPLA,RDHDR), INXCTL
;;;	KMATTYP = ENTRY
	KMATTY = ENTRY
	XCALL OUTPT (06,44,0,'-',1)
	CTL = '06,46,03,01,A '
	CALL INPUT
	GOTO (DESCR), INXCTL
	KGAGE = ENTRY
	XCALL OUTPT (06,50,0,'x',1)
	CTL = '06,52,06,01,# '
	CALL INPUT
	GOTO (DESCR), INXCTL
	DECMAL = ENTRY
	CALL DSP3DP
	KWIDTH = ENTRY
	KEY(1,3) = KMATTY
	KEY(4,6) = KGAGE
	KEY(7,12) = KWIDTH,'XXXXXX'
	XCALL SERCH (4,USGHST,KEY,7,18,ORG134,BSMID,SRCCTL,1,49,54,0,0,0,0)
	GOTO (DSPREC,DSPREC), SELECT-1
;;;	IF (SRCCTL.EQ.0) GOTO NODUPS
	USGHST =
	HMATTY = KMATTY
	HGAGE = KGAGE
	HWIDTH = KWIDTH
	GOTO (ANYCNG), CNGCTL
QTYONH,
	CTL = '08,40,07,00,# '
	CALL INPUT
	GOTO (DISPLA), INXCTL
	HQTY = ENTRY
	GOTO (ANYCNG), CNGCTL
UOFM,
	CTL = '10,40,04,00,A '
	CALL INPUT
	GOTO (DISPLA), INXCTL
	IF (ENTRY.EQ.BLANKS)
	BEGIN
	  ENTRY (1,2) = 'LB'
	  XCALL OUTPT (ROW,COL,0,ENTRY(1,2),1)
	END
	HUOFM = ENTRY
	GOTO (ANYCNG), CNGCTL
COST,
	CTL = '12,40,08,00,# '
	CALL INPUT
	GOTO (DISPLA), INXCTL
	DECMAL = ENTRY
	CALL DSP4DP
	HCOST = ENTRY
	GOTO (ANYCNG), CNGCTL
DTRCVD,
	CTL = '14,40,06,00,D '
	CALL INPUT
	GOTO (DISPLA), INXCTL
	IF (ENTRY.EQ.BLANKS)
	BEGIN
	  ENTRY(1,6) = LSTDTE
	  DECMAL (1,6) = ENTRY
	  CALL DSPDTE
	END
	HDTUSE = ENTRY
	LSTDTE = HDTUSE 
	GOTO ANYCNG
DSPREC,
	IF (SRCCTL) GOTO NOFIND
;;;	XCALL OUTPT (06,40,0,HMATTYP,1)
	XCALL OUTPT (06,40,0,HMATTY,1)
	XCALL OUTPT (0,0,0,' - ',1)
	XCALL OUTPT (0,0,0,HGAGE,1)
	XCALL OUTPT (0,0,0,' x ',1)
	CTL = '06,52,06'
	DECMAL = HWIDTH
	CALL DSP3DP
	CTL = '08,40,06'
	DECMAL = HQTY
	CALL DSPNUM
	XCALL OUTPT (10,40,0,HUOFM,1)
	CTL = '12,40,08'
	DECMAL = HCOST
	CALL DSP4DP
	CTL = '14,40,06'
	DECMAL (1,6) = HDTUSE
	CALL DSPDTE
	XCALL OUTPT (24,1,0,'RIGHT TRANSACTION <N> ?',1)
	XCALL INPUT (24,25,01,00,'YN',ENTRY,INXCTL,1)
	IF (INXCTL.NE.1) GOTO GETNXT
	IF (SELECT.EQ.3) GOTO DELETE
	GOTO ANYCNG
GETNXT,
	INCR BSMID
	LOKCTL = 1
	XCALL IO (4,USGHST,BSMID,READ,LOKCTL)
	IF (USGHST(7,18).EQ.KEY) GOTO DSPREC
	XCALL MESAG ('NO MORE FOUND',2)
	GOTO DISPLA
	
ANYCNG,
	XCALL ANYCN (CNGCTL,WHATNO)
	IF (CNGCTL)
	BEGIN 
	  GOTO (QTYONH,UOFM,COST,DTRCVD), WHATNO-1
	  CNGCTL = 3
	  GOTO ANYCNG
	END
	IF (SELECT.EQ.2) GOTO CHANGE
ADD,
	LOKCTL = 1
	XCALL IO (4,USHCTL,1,READ,LOKCTL)
	INCR REC134
	IF (REC134.GE.MAX134) GOTO FULL
	LOKCTL = 1
	XCALL IO (4,USHCTL,1,WRITE,LOKCTL)
	LOKCTL = 1
	XCALL IO (4,USGHST,REC134,WRITE,LOKCTL)
	GOTO FREBUF
DELETE,
;;;	XCALL MESAG ('NO DELETE MODE',1)
	HDTUSE = 0
CHANGE,
	LOKCTL = 1
	XCALL IO (4,USGHST,BSMID,WRITE,LOKCTL)
	GOTO FREBUF
FREBUF,
	LOKCTL = 1
	XCALL IO (4,USGHST,MAX134,READ,LOKCTL)
	LOKCTL = 1
	XCALL IO (4,USHCTL,1,READ,LOKCTL)
	GOTO DISPLA
FULL,
	XCALL MESAG ('THE USGHST FILE IS NOW FULL AND NEEDS TO BE EXPANDED',2)
	CALL CLOSES
	GOTO ABORT
NOFIND,
	XCALL MESAG ('RECORD NOT FOUND',1)
	GO TO DISPLA
NODUPS,
	XCALL MESAG ('RECORD ALREADY ON FILE',1)
	GO TO DISPLA
INPUT,
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,1)
	RETURN
DSP4DP,
	OPTION = 7
	GO TO CALDSP
DSP3DP,
	OPTION = 4
	GO TO CALDSP
DSPDTE,
	OPTION = 2
	GOTO CALDSP
DSPNUM,
	OPTION = 1
CALDSP,
	XCALL DSPLY (MAX,ROW,COL,DECMAL,OPTION,1)
	RETURN
END

