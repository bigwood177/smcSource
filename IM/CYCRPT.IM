;  CYCRPT / IM 
;
;
;		::PCPYIM.DEF::
;*****************************************************************************
;		INVENTORY MANAGEMENT  
;
;		RELEASED: AUGUST 1, 1984 (d70s10)
;		MODIFIED: MAY 31, 1985 EAV-807
;*****************************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;		PRINTS CYCLE COUNT WORKSHEET
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'
RECORD CYCIDX		;
		.INCLUDE 'DEF:RD130A.DEF'
RECORD ITMMAS		;
		.INCLUDE 'DEF:RD041A.DEF'
RECORD INVCTL,X		;
		.INCLUDE 'DEF:RD041B.DEF'
RECORD ITMIDX		;
		.INCLUDE 'DEF:RD042A.DEF'
RECORD TITLE
		,A21,	'CYCLE COUNT WORKSHEET'
RECORD LEGND1
		,A24,	'ITEMS NOT COUNTED SINCE '
	LDATE	,A8
		,A21,	' -- CYCLE COUNT CODE '
	LCNTCD	,A3
RECORD HDR1
		,A1
		,A3,	'BIN'
		,A59
		,A3,	'TAG'
		,A8
		,A6,	'ACTUAL'
		,A19
	QTYFLD	,A8,	'QUANTITY'
		,A2
		,A5,	'CYCLE'
		,A3
		,A4,	'LAST'
RECORD HDR2
		,A6,	'NUMBER'
		,A2
		,A11,	'ITEM NUMBER'
		,A6
		,A11,	'DESCRIPTION'
		,A21
		,A3,	'U/M'
		,A3
		,A2,	'NO'
		,A9
		,A5,	'COUNT'
		,A7
		,A10,	'CHECKED BY'
		,A3
	ONHFLD	,A7,	'ON-HAND'
		,A3
		,A4,	'CODE'
		,A3
		,A7,	'COUNTED'
RECORD MESSAG
	TSTCDE	,A1
	LCDATE	,D6
	ONHFLG	,A1				; 'N' = DON'T SHOW OH-HAND
RECORD
	SPLFIL	,A14
	V	,D1
	JUSTIF	,D1
	LINCNT	,D2,	60
	PGCNT	,D3
	PLINE	,A132
	UNDLIN	,A36,	'---------  -----------  ------------'
	SWITCH	,D1
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	LPSW	,D2
	LPARG	,D1
	RPTNUM	,D3
	PRTTYP	,A1
	MSGSWT	,D1
	BLANKS	,A10
	LPFLAG	,D1
PROC (1)
	XCALL TERID (V)
	XCALL OUTPT (2,1,1,'PRINT',1)
	XCALL WATE (4,V)
	SWITCH = 5				; FILES PROTECTED IN <CYCSEL>
	XCALL FILES (1,'I',130,SWITCH)		; FILE # 130 - CYCLE REPORT
						; INDEX/CYCIDX
	SWITCH = 5
	XCALL FILES (2,'I',41,SWITCH)		; FILE # 41 - ITEM MASTER
	LOKCTL = 1
	XCALL IO (2,INVCTL,1,READ,LOKCTL)	; READ CONTROL RECORD
	JUSTIF = JSTIFY				; FOR VALUE OF JSTIFY
REDCTL,
	LOKCTL = 1
	XCALL IOS (1,CYCIDX,READ,LOKCTL)	;SKIP PAST BLANK CTL RECORD
	IF (LOKCTL.EQ.2) GO TO ENDOFF
	MSGSWT = 1
	XCALL SNMSG (MESSAG,MSGSWT)			;GET REPORT PARAMETERS
	IF (MSGSWT.EQ.9) GO TO ENDOFF
						; FROM CYCSEL PROGRAM
	LDATE = LCDATE, 'ZX/XX/XX'
	LCNTCD = TSTCDE
	IF (TSTCDE.EQ.' ') LCNTCD = 'ALL'		; <SPACE> INPLIES ALL
							; CLASSES CHOSEN
	IF (ONHFLG.EQ.'Y') GO TO IDXLOP
	QTYFLD =
	ONHFLD =
; --------------------------- MAIN PROGRAM LOOP ----------------------------
IDXLOP,
	IF (LPFLAG.EQ.1) GO TO SKIP
	SPLFIL(5,6) = 'CN'
	LPSW = 2		;PRINTED OR SPOOLED
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	LPFLAG = 1
SKIP,
	LOKCTL = 1
	XCALL IOS (1,CYCIDX,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO LASTRD
	IF (CYCIDX.EQ.']]]]]]]]') GO TO LASTRD
	LOKCTL = 1
	XCALL IO (2,ITMMAS,CYC041,READ,LOKCTL)
	PLINE  (2,4) = BINNUM
	PLINE (9,23) = ITEMNO
	IF (JUSTIF) XCALL LEFTJ (PLINE(9,23),15)
	PLINE (26,55) = DESCR
	PLINE (58,59) = SUOFM
	IF (ONHFLG.EQ.'Y') PLINE (100,107) = QTYONH, 'ZZZ,ZZX-'
	PLINE (112,112) = CYCTCD
	PLINE (117,124) = LSTCNT, 'ZX/XX/XX'
	CALL PRINT
	PLINE (62,97) = UNDLIN
	CALL PRINT
	XCALL LINFD (1)			;MAKES LARGER SPACE TO WRITE IN.
	INCR LINCNT
	IF (LINCNT.GE.58) LINCNT = 60
	GO TO IDXLOP
LASTRD,
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
ENDOFF,
	XCALL WATE (4,V)
	XCALL FILES (2,'I',41,4)		; CLOSE ITEM MASTER
	SWITCH = 7
	XCALL FILES (1,'I',130,SWITCH)		; DELETE CYCLE COUNT INDEX
	MSGSWT = 4
	XCALL SNMSG (BLANKS,MSGSWT)
	XCALL PGCHN ('IM:IMMENU',1)
; -------------------------------------------------------------------------
PRINT,
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,
&		'NO HDR',LEGND1,'NO LEGEND',' ',0,132,132,1,LPSW,RPTNUM,PRTTYP)
	RETURN
END
