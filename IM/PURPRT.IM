;  PURPRT / IM 
;
;
;		::PCPYIM.DEF::
;*****************************************************************************
;		INVENTORY MANAGEMENT  
;
;		RELEASED: AUGUST 1, 1984 (d70s10)
;*****************************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;		PRINT OUT ITEMS PURCHASED REPORT BY VENDOR
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD	PURCHS		;
		.INCLUDE 'DEF:RD053A.DEF'
RECORD	,X		;
		.INCLUDE 'DEF:RD053B.DEF'
RECORD ITMCTL		;
		.INCLUDE 'DEF:RD041B.DEF'
RECORD	HDR1
		,A79
		,A3,	'PRD'
		,A3
		,A4,	'LEAD'
		,A3
		,A8,	'EXP RCPT'
		,A7
		,A4,	'LAST'
		,A13
		,A8,	'QUANTITY'
RECORD	HDR2
		,A1
		,A6,	'VENDOR'
		,A1
		,A8,	'LOCATION'
		,A2
		,A9,	'PO NUMBER'
		,A4
		,A11,	'ITEM NUMBER'
		,A5
		,A16,	'ITEM DESCRIPTION'
		,A16
		,A3,	'CAT'
		,A3
		,A4,	'TIME'
		,A5
		,A4,	'DATE'
		,A9
		,A4,	'COST'
		,A5
		,A6,	'WEIGHT'
		,A2
		,A7,	'ORDERED'
		,A1
RECORD	CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2

RECORD
	SPLFIL	,A14
RECORD
	ENTRY	,A2
	INXCTL	,D1
	CNGCTL	,D1
	WHATNO	,D2
	STXCTL	,D1
	STRTNO	,A4
	ENDNO	,A4
	STVEND	,A4
	ENDVND	,A4
	LINCNT	,D2,	60
	PGCNT	,D3
	PLINE	,A132
	V	,D1
	SWITCH	,D1
	ENDPUR	,D1
	NWVEND	,D1
	FSTFLG	,D1,	1
	RPTLOC	,A2
	SAVPO	,A8
	SVEND	,A4
	POWGTT	,D8
	VNWGTT	,D8
	GRWGTT	,D8
	POACCM	,D6
	VNDACC	,D5
	GRDACC	,D5
	EXTWGT	,D8
	POQTY	,D6
	VQTY	,D6
	TQTY	,D6
	TITLE	,A35,	'ITEMS PURCHASED REPORT -- BY VENDOR'
	TOTID	,A13,	'ITEMS ORDERED'
	MASK	,A8,	'Z,ZZZ.XX'
	TMASK	,A10,	'ZZZ,ZZZ.XX'
	MASKN	,A8,	'ZZZ,ZZX-'
	RECNO	,D5,	00001
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	LPSW	,D2
	LPARG	,D1
	RPTNUM	,D3
	PRTTYP	,A1
	LPFLAG	,D1
PROC (1)
	XCALL TERID (V)
	XCALL OUTPT (2,1,0,'PRINT - BY VENDOR',1)
	XCALL FILES (1,'I',53,4)

	SWITCH = 1
	XCALL FILES (1,'I',53,SWITCH)		;FILE # 53 -- PURCHS FILE
	IF (SWITCH.EQ.9) GO TO GETOUT

	SWITCH = 1
	XCALL FILES (3,'I',041,SWITCH)		;OPENS ITMMAS TO GET VALUE OF
	IF (SWITCH.EQ.9) GO TO END		;JSTIFY.

	LOKCTL = 1
	XCALL IO (3,ITMCTL,1,READ,LOKCTL)
	XCALL FILES (3,'I',041,4)

GETNUM,
	LINCNT = 66
	FSTFLG = 1
	RECNO = 00001
	ENDPUR =
	XCALL OUTPT (1,1,2,'PURCHASING ENTRY & EDITING',V)
	XCALL OUTPT (2,1,0,'PRINT - BY VENDOR',1)

	STXCTL = 3
	XCALL STENO (STRTNO,ENDNO,'VENDOR #',4,STXCTL,V)
	IF (STXCTL.EQ.1) GO TO END

	IF (STRTNO.EQ.'    ') GO TO ALL
	STVEND = STRTNO
	IF (ENDNO.EQ.'    ') GO TO TEST
	ENDVND = ENDNO
	GO TO TEST
ALL,
	STVEND = '    '
	ENDVND = ']]]]'
TEST,
	IF (ENDNO.EQ.'    ') ENDVND = STVEND
	IF (STVEND.GT.ENDVND) XCALL MESAG
&		('STARTING VENDOR # IS GREATER THAN ENDING VENDOR #',1)
	IF (STVEND.GT.ENDVND) GO TO GETNUM
LOCATN,
	XCALL OUTPT (3,1,2,'\',1)
	XCALL OUTPT (6,24,0,'PLEASE INPUT LOCATION DESIRED ',V)
	CTL = '06,55,02,00,A '
	CALL INPUT
	GO TO (GETNUM) ,INXCTL
	RPTLOC = ENTRY(1,2)
	IF (RPTLOC.EQ.'  ') XCALL OUTPT (6,55,0,'ALL',V)

	CNGCTL = 2
	XCALL ANYCN (CNGCTL,WHATNO)
	GO TO (LOCATN) ,CNGCTL
	XCALL WATE (3,V)
NXTLIN,
	CALL RDPURC
	IF (ENDPUR) GO TO GRDTOT
;	IF (NWVEND) CALL VNDTOT
	IF (LPFLAG.EQ.0) CALL LPOPEN
	CALL PRTLIN
	GO TO NXTLIN
RDPURC,
	NWVEND =
	INCR RECNO
	LOKCTL = 1
	XCALL IO (1,PURCHS,RECNO,READ,LOKCTL)
	IF (PURCHS.EQ.']]]]]]') GO TO ENDPUR
	IF (PVEND.LT.STVEND.OR.PVEND.GT.ENDVND) GO TO RDPURC
	IF (PLOC.NE.RPTLOC.AND.RPTLOC.NE.'  ') GO TO RDPURC
	IF (FSTFLG) GO TO SETFST
	IF (SVEND.EQ.PVEND) RETURN
	NWVEND = 1
	RETURN
ENDPUR,
	ENDPUR = 1
	RETURN
SETFST,
	SAVPO = PPONUM
	SVEND = PVEND
	NWVEND = 1
	RETURN

;*************************************************************

PRTLIN,
	IF (SAVPO.NE.PPONUM.OR.NWVEND.EQ.1) CALL POTOT
	IF (NWVEND.EQ.1) CALL VNDTOT
	IF (NWVEND.EQ.1) PLINE (3,6) = PVEND
	IF (NWVEND.EQ.1) XCALL LEFTJ (PLINE(3,6),4)
	PLINE (12,13) = PLOC
	PLINE (19,26) = PPONUM
	PLINE (30,44) = PITEM
	IF (JSTIFY) XCALL LEFTJ (PLINE(30,44),15)
	PLINE (48,77) = PDESCR
	PLINE (81,82) = PPRDCT
	PLINE (86,89) = PLDTM,'ZZ.X'
	PLINE (93,100) = PRCTDT,'ZX-XX-XX'
	PLINE (103,111) = PLSTCT,TMASK
	EXTWGT = PWGT*PORDED
	PLINE (113,123) = EXTWGT,'ZZZ,ZZZ.XX-'
	PLINE (125,132) = PORDED,MASKN
	CALL PRINT
	INCR POACCM
	INCR VNDACC
	POQTY = POQTY + PORDED
	VQTY = VQTY + PORDED
	POWGTT = POWGTT + EXTWGT
	VNWGTT = VNWGTT + EXTWGT
	RETURN
POTOT,
	IF (FSTFLG.EQ.1) RETURN
	LINCNT = LINCNT - 1
	XCALL LINFD (1)
	PLINE (29,35) = POACCM,MASKN
	PLINE (37,49) = TOTID
	PLINE (51,64) = 'ON P.O. NUMBER'
	PLINE (66,73) = SAVPO
	PLINE (113,123) = POWGTT,'ZZZ,ZZZ.XX-'
	PLINE (124,132) = POQTY,MASKN
	CALL PRINT
	XCALL LINFD (1)
	LINCNT = LINCNT + 3
	SAVPO = PPONUM
	POACCM = 0
	POWGTT = 0
	POQTY = 0
	RETURN
VNDTOT,
	IF (FSTFLG) GO TO ENDVND
	XCALL LINFD (2)
	PLINE (29,35) = VNDACC,MASKN
	PLINE (37,49) = TOTID
	PLINE (51,62) = 'FROM VENDOR '
	PLINE (63,66) = SVEND
	XCALL LEFTJ (PLINE(63,66),4)
	PLINE (113,123) = VNWGTT,'ZZZ,ZZZ.XX-'
	PLINE (124,132) = VQTY,MASKN
	CALL PRINT
	GRDACC = GRDACC + VNDACC
	GRWGTT = GRWGTT + VNWGTT
	TQTY = TQTY + VQTY
	VQTY =
	VNDACC =
	VNWGTT =
	SVEND = PVEND
	XCALL LINFD (3)
	LINCNT = LINCNT + 5
ENDVND,
	FSTFLG =
	RETURN
GRDTOT,
	IF (FSTFLG.EQ.1) GO TO NONE
	CALL POTOT
	CALL VNDTOT
	IF (STVEND.EQ.ENDVND) GO TO CONTCK
	XCALL LINFD (1)
	INCR LINCNT
	PLINE (29,35) = GRDACC,MASKN
	PLINE (37,49) = TOTID
	PLINE (51,64) = 'FROM LOCATION '
	PLINE (65,66) = RPTLOC
	IF (RPTLOC.EQ.'  ') PLINE (56,68) = 'ALL LOCATIONS'
	PLINE (113,123) = GRWGTT,'ZZZ,ZZZ.XX-'
	PLINE (124,132) = TQTY,MASKN
	CALL PRINT
CONTCK,
	GRDACC =
	GRWGTT =
	TQTY =
	XCALL LINFD (3)
	LINCNT = LINCNT + 4
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	XCALL WATE (3,V)
	LPFLAG =
	PGCNT =
	IF (STVEND.EQ.ENDVND.OR.ENDNO.NE.'[[[') GO TO GETNUM
	IF (RPTLOC.EQ.'  '.AND.STRTNO.EQ.'   '.AND.ENDNO.EQ.'[[') GO TO END
	GO TO GETNUM

;*****************************************************************

END,
	XCALL FILES (1,'I',53,4)
GETOUT,
	XCALL WATE (4,V)
	XCALL PGCHN ('IM:PURCH',1)

;*****************************************************************

PRINT,
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,'NO HDR','NO LEGEND',
&		' ',' ',0,1,132,0,LPSW,RPTNUM,PRTTYP)
	RETURN

;*****************************************************************

INPUT,
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,V)
	RETURN

;*****************************************************************

NONE,
	XCALL MESAG ('NO ITEMS EXIST FOR THIS REPORT',1)
	GO TO GETNUM

;*****************************************************************

LPOPEN,
	SPLFIL(5,6) = 'CI'
	LPSW = 1
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO END
	XCALL OUTPT (3,1,2,'\',1)
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	LPFLAG = 1
	RETURN

;*****************************************************************
END
