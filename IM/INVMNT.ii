; INVMNT / IM
;
;	RAW MATERIALS INVENTORY MAINTENANCE
;
RECORD	D_STUFF
	D_IN	,D8		;DATE-IN, ANY FORMAT
	D_OUT	,D6		;RETURN VALUE MMDDYY
	D_OUTR	,D8		;RETURN VALUE CCYYMMDD
	D_FMT	,A10		;RETURN VALUE MM/DD/CCYY
	D_SW	,A2		;"99" = DATE CONVERSION ERROR



RECORD INVMAS
		.INCLUDE 'DEF:RD132A.DEF'
RECORD INVCTL
		.INCLUDE 'DEF:RD132B.DEF'
RECORD INVIDX
		.INCLUDE 'DEF:RD133A.DEF'
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD SRTMSG
		,A9 ,'IM:INVCNT'
	RCNT	,D5
	OCNT	,D5
	NXTPRG	,A9
RECORD
	MSGCTL	,D1
	V	,D1
	SWITCH	,D1
	DECMAL	,D10
	OPTION	,D1
	ENTRY	,A30
	INXCTL	,D1
	SELECT	,D1
	LOKCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	KTAGNO	,D6
	KEY	,A6
	BSMID	,D5
	SRCCTL	,D1
	CNGCTL	,D1
	WHATNO	,D2
	TODAA	,D6
	TODAY	,D8
	BLANKS	,A10
PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'RAW MATERIALS INVENTORY MAINTENANCE',1)
	XCALL RDATE (TODAA)
	XCALL DATE8(TODAA, D_OUT, TODAY, D_FMT, D_SW)
 OPEN1,
	SWITCH = 1
	XCALL FILES (3,'U',133,SWITCH)		;FILE 133 - RINVIX
	IF (SWITCH.NE.9) GOTO OPEN2
	GOTO ABORT
OPEN2,
	XCALL FILES (2,'U',132,SWITCH)		;FILE 132 - RINVMS
	IF (SWITCH.NE.9) GOTO RDHDR
	CALL CLOSE1
	GOTO ABORT
CLOSES,
	XCALL FILES (2,'I',132,4)
CLOSE1,
	XCALL FILES (3,'I',133,4)
	RETURN
RDHDR,
	LOKCTL = 1
	XCALL IO (2,INVCTL,1,READ,LOKCTL)
	UNLOCK 3
	UNLOCK 2
	XCALL MMENU ('RAW MATERIALS INVENTORY MAINTENANCE','ITEM',SELECT,V)
	GOTO (DISPLA,DISPLA,DISPLA,PRINT,PRINT), SELECT
ENDOFF,
	CALL CLOSES
	IF (DEL132.GT.20) GOTO REORG
	IF (ORG132.EQ.REC132) GOTO ABORT
	NXTPRG = 		;;;defaults to 'IM:IMMENU'
	GOTO SORT
PRINT,
	CALL CLOSES
	IF (SELECT.EQ.4) XCALL PGCHN ('IM:INVPRT',1)
	NXTPRG = 'IM:INVPRT'
	GOTO SORT
SORT,
	SWITCH = 3
	XCALL FILES (3,'I',133,SWITCH)
	IF (SWITCH.EQ.9) GOTO ABORT
	RCNT = REC132
	OCNT = ORG132
	MSGCTL = 5
	XCALL SNMSG (SRTMSG,MSGCTL)
	MSGCTL = 2
	XCALL SNMSG (NXTPRG,MSGCTL)
	XCALL PGCHN ('IM:SRTINV',1)
REORG,
	XCALL OUTPT (6,10,2,'The RINVMS file needs to be reorganized.  This',1)
	XCALL OUTPT (8,10,0,'process can take a long time depending on file',1)
	XCALL OUTPT (10,10,0,'size and number of deleted records.  You might',1)
	XCALL OUTPT (12,10,0,'consider running this process over lunch or at',1)
	XCALL OUTPT (14,10,0,'night.  DO YOU WISH TO REORGANIZE NOW <N> ?',1)
	CTL = '14,55,01,00,YN'
	CALL INPUT
	IF (INXCTL.EQ.1) XCALL PGCHN ('IM:ORGINV',1)
ABORT,
	XCALL PGCHN ('IM:IMMENU',1)
;
DISPLA,
	UNLOCK 3
	UNLOCK 2
	INVMAS =
	INVIDX =
	INXCTL =
	CNGCTL =
	XCALL OUTPT ( 4,20,2,'1. TAG NUMBER ',1)
	XCALL OUTPT ( 6,20,0,'2. MATL/GAUGE/SIZE ',1)
	XCALL OUTPT ( 8,20,0,'3. QUANTITY ON HAND ',1)
	XCALL OUTPT (10,20,0,'4. UNIT OF MEASURE ',1)
	XCALL OUTPT (12,20,0,'5. UNIT COST ',1)
	XCALL OUTPT (14,20,0,'6. DATE RECEIVED ',1)
	XCALL OUTPT (16,20,0,'7. SOURCE ',1)
	XCALL OUTPT (18,20,0,'8. NOTES ',1)
TAGNO,
	IF (CNGCTL.AND.SELECT.NE.1) GOTO ANYCNG
	CTL = '04,40,06,01,#E'
	CALL INPUT
	GOTO (DISPLA,RDHDR), INXCTL
	KTAGNO = ENTRY
	IF (KTAGNO.EQ.0) GOTO TAGNO
	KEY = KTAGNO,'XXXXXX'
	XCALL SERCH (3,INVIDX,KEY,1,6,ORG132,BSMID,SRCCTL,1,7,11,0,0,0,0)
	GOTO (DSPREC,DSPREC), SELECT - 1
	IF (SRCCTL.EQ.0) GOTO NODUPS
	TAGNO = KTAGNO
	GOTO (ANYCNG), CNGCTL
DESCR,
	CTL = '06,40,03,01,A '
	CALL INPUT
	GOTO (DISPLA), INXCTL
	MATTYP = ENTRY
	XCALL OUTPT (06,44,0,'-',1)
	CTL = '06,46,03,01,A '
	CALL INPUT
	GOTO (DESCR), INXCTL
	GAGE = ENTRY
	XCALL OUTPT (06,50,0,'x',1)
	CTL = '06,52,06,01,# '
	CALL INPUT
	GOTO (DESCR), INXCTL
	DECMAL = ENTRY
	CALL DSP3DP
	WIDTH = ENTRY
	GOTO (ANYCNG), CNGCTL
QTYONH,
	CTL = '08,40,06,00,# '
	CALL INPUT
	GOTO (DISPLA), INXCTL
	QTYONH = ENTRY
	GOTO (ANYCNG), CNGCTL
UOFM,
	CTL = '10,40,04,00,A '
	CALL INPUT
	GOTO (DISPLA), INXCTL
	IF (ENTRY.EQ.BLANKS)
	BEGIN
	  ENTRY (1,2) = 'LB'
	  XCALL OUTPT (ROW,COL,0,ENTRY(1,2),1)
	END
	UOFM = ENTRY
	GOTO (ANYCNG), CNGCTL
COST,
	CTL = '12,40,08,00,# '
	CALL INPUT
	GOTO (DISPLA), INXCTL
	COST = ENTRY
	DECMAL = COST
	CALL DSP4DP
	GOTO (ANYCNG), CNGCTL
DTRCVD,
	CTL = '14,40,08,00,D '
	CALL INPUT
	GOTO (DISPLA), INXCTL
	IF (ENTRY.EQ.BLANKS)
	BEGIN
	  ENTRY(1,8) = TODAY
	  XCALL DATE8(TODAY, D_OUT, D_OUTR, D_FMT, D_SW)
	  XCALL OUTPT (ROW, COL, 1, D_FMT, V)
	END
	DTRCVD = ENTRY
	GOTO (ANYCNG), CNGCTL
SOURCE,
	CTL = '16,40,05,00,A '
	CALL INPUT
	GOTO (DISPLA), INXCTL
	SOURCE = ENTRY	
	GOTO (ANYCNG), CNGCTL
NOTES,
	CTL = '18,40,30,00,A '
	CALL INPUT
	GOTO (DISPLA), INXCTL
	NOTES = ENTRY
	GOTO ANYCNG
DSPREC,
	IF (SRCCTL) GOTO NOFIND
	LOKCTL = 1
	XCALL IO (2,INVMAS,IRC132,READ,LOKCTL)
	CTL = '04,40,06'
	DECMAL = TAGNO
	CALL DSPNUM
	XCALL OUTPT (06,40,0,MATTYP,1)
	XCALL OUTPT (0,0,0,' - ',1)
	XCALL OUTPT (0,0,0,GAGE,1)
	XCALL OUTPT (0,0,0,' x ',1)
	CTL = '06,52,06'
	DECMAL = WIDTH
	CALL DSP3DP
	CTL = '08,40,06'
	DECMAL = QTYONH
	CALL DSPNUM
	XCALL OUTPT (10,40,0,UOFM,1)
	CTL = '12,40,08'
	DECMAL = COST
	CALL DSP4DP

	CTL = '14,40,06'
	XCALL DATE8(DTRCVD, D_OUT, D_OUTR, D_FMT, D_SW)
	XCALL OUTPT (ROW, COL, 0, D_FMT, V)
;;	DECMAL (1,6) = DTRCVD
;;	CALL DSPDTE

	XCALL OUTPT (16,40,0,SOURCE,1)
	XCALL OUTPT (18,40,0,NOTES,1)
	IF (SELECT.EQ.3) GOTO DELETE
ANYCNG,
	XCALL ANYCN (CNGCTL,WHATNO)
	IF (CNGCTL)
	BEGIN 
	  GOTO (TAGNO,DESCR,QTYONH,UOFM,COST,DTRCVD,SOURCE,NOTES), WHATNO
	  CNGCTL = 3
	  GOTO ANYCNG
	END
	IF (SELECT.EQ.2) GOTO CHANGE
ADD,
	LOKCTL = 1
	XCALL IO (2,INVCTL,1,READ,LOKCTL)
	INCR REC132
	IF (REC132.GE.MAX132) GOTO FULL
	LOKCTL = 1
	XCALL IO (2,INVCTL,1,WRITE,LOKCTL)
	LOKCTL = 1
	XCALL IO (2,INVMAS,REC132,WRITE,LOKCTL)
	ITAGNO = TAGNO
	IRC132 = REC132
	LOKCTL = 1
	XCALL IO (3,INVIDX,REC132,WRITE,LOKCTL)
	GOTO FREBUF
CHANGE,
	LOKCTL = 1
	XCALL IO (2,INVMAS,IRC132,WRITE,LOKCTL)
	GOTO FREBUF
DELETE,
	XCALL OUTPT (24,1,0,'DELETE REQUIRES PASSWORD: ',1)
	CTL = '24,30,08,01,# '
	CALL INPUT
	DECMAL = ENTRY
	IF (DECMAL.NE.TODAY)
	BEGIN
	  XCALL MESAG ('INVALID PASSWORD - SWITCHING TO CHANGE MODE',1)
	  SELECT = 2
	  XCALL OUTPT (2,1,0,'CHANGE/INQUIRE',1)
	  GOTO ANYCNG
	END
	XCALL OUTPT (24,1,1,'OK TO DELETE <N> ?',1)
	CTL = '24,20,01,00,YN'
	CALL INPUT
	IF (INXCTL.NE.1) GOTO DISPLA
	NOTES = ']]]DEL'
	LOKCTL = 1
	XCALL IO (2,INVMAS,IRC132,WRITE,LOKCTL)
	IRC132 =
	LOKCTL = 1
	XCALL IO (3,INVIDX,BSMID,WRITE,LOKCTL)
	LOKCTL = 1
	XCALL IO (2,INVCTL,1,READ,LOKCTL)
	INCR DEL132
	LOKCTL = 1
	XCALL IO (2,INVCTL,1,WRITE,LOKCTL)
	XCALL MESAG ('TAG NUMBER DELETED',2)
FREBUF,
	LOKCTL = 1
	XCALL IO (3,INVIDX,MAX132,READ,LOKCTL)
	LOKCTL = 1
	XCALL IO (2,INVMAS,MAX132,READ,LOKCTL)
	LOKCTL = 1
	XCALL IO (3,INVIDX,1,READ,LOKCTL)
	LOKCTL = 1
	XCALL IO (2,INVCTL,1,READ,LOKCTL)
	GOTO DISPLA
FULL,
	XCALL MESAG ('THE RINVMS FILE IS NOW FULL AND NEEDS TO BE EXPANDED',2)
	CALL CLOSES
	GOTO ABORT
NOFIND,
	XCALL MESAG ('TAG NUMBER NOT FOUND',1)
	GO TO DISPLA
NODUPS,
	XCALL MESAG ('TAG NUMBER ALREADY ON FILE',1)
	GO TO DISPLA
INPUT,
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,1)
	RETURN
DSP4DP,
	ENTRY = DECMAL,	'ZZZ,ZZX.XXXX' [LEFT]
	XCALL OUTPT (ROW,COL,0,ENTRY,1)
	RETURN
	OPTION = 7
	GO TO CALDSP
DSP3DP,
	OPTION = 4
	GO TO CALDSP
DSPDTE,
	OPTION = 2
	GOTO CALDSP
DSPNUM,
	OPTION = 1
CALDSP,
	XCALL DSPLY (MAX,ROW,COL,DECMAL,OPTION,1)
	RETURN
END
