;  PURRPT / IM 
;
;
;		::PCPYIM.DEF::
;*****************************************************************************
;		INVENTORY MANAGEMENT  
;
;		RELEASED: AUGUST 1, 1984 (d70s10)
;*****************************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;		PRINT OUT ITEMS PURCHASED REPORT BY ITEM
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD	PURCHS		;
		.INCLUDE 'DEF:RD053A.DEF'
RECORD	,X		;
		.INCLUDE 'DEF:RD053B.DEF'
RECORD ITMCTL		;
		.INCLUDE 'DEF:RD041B.DEF'
RECORD	HDR1
		,A50
		,A3,	'PRD'
		,A3
		,A8,	'EXP RCPT'
		,A30
		,A4,	'LEAD'
		,A8
		,A4,	'LAST'
		,A13
		,A8,	'QUANTITY'
		,A1
RECORD	HDR2
		,A11,	'ITEM NUMBER'
		,A7
		,A16,	'ITEM DESCRIPTION'
		,A16
		,A3,	'CAT'
		,A5
		,A4,	'DATE'
		,A4
		,A6,	'VENDOR'
		,A1
		,A8,	'LOCATION'
		,A2
		,A9,	'PO NUMBER'
		,A2
		,A4,	'TIME'
		,A8
		,A4,	'COST'
		,A4
		,A6,	'WEIGHT'
		,A3
		,A7,	'ORDERED'
		,A2
RECORD	CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD
	SPLFIL	,A14

RECORD
	ENTRY	,A2
	INXCTL	,D1
	CNGCTL	,D1
	WHATNO	,D2
	STXCTL	,D1
	STRTNO	,A15
	ENDNO	,A15
	LINCNT	,D2,	60
	PGCNT	,D3
	PLINE	,A132
	V	,D1
	SWITCH	,D1
	BLANKS	,A15
	ENDPUR	,D1
	NWITEM	,D1
	FSTFLG	,D1,	1
	RPTLOC	,A2
	SITEM	,A15
	ITMWGT	,D8
	GRWGTT	,D8
	ITMACC	,D7
	GRDACC	,D7
	EXTWGT	,D8
	TITLE	,A33,	'ITEMS PURCHASED REPORT -- BY ITEM'
	TOTID	,A11,	'ITEM TOTALS'
	MASK	,A8,	'Z,ZZZ.XX'
	TMASK	,A10,	'ZZZ,ZZZ.XX'
	MASKO	,A7,	'ZZZ,ZZX'
	MASKN	,A8,	'ZZZ,ZZX-'
	MASKP	,A9,	'ZZZZ,ZZX-'
	RECNO	,D5,	00001
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	LPSW	,D2
	LPARG	,D1
	RPTNUM	,D3
	PRTTYP	,A1
	LPFLAG	,D1
PROC (1)
	XCALL TERID (V)
	XCALL OUTPT (2,1,2,'PRINT - BY ITEM',1)

	XCALL FILES (1,'U',53,4)
	SWITCH = 1
	XCALL FILES (1,'I',53,SWITCH)		;FILE # 53 -- PURCHS FILE
	IF (SWITCH.EQ.9) GO TO GETOUT

	SWITCH = 1
	XCALL FILES (2,'I',41,SWITCH)
	IF (SWITCH.EQ.9) GO TO END

	LOKCTL = 1
	XCALL IO (2,ITMCTL,1,READ,LOKCTL)
	XCALL FILES (2,'I',41,4)
GETNUM,
	XCALL OUTPT (1,1,2,'PURCHASING ENTRY & EDITING',V)
	XCALL OUTPT (2,1,1,'PRINT - BY ITEM',1)
	LINCNT = 66
	ENDPUR = 0
	FSTFLG = 1
	RECNO = 00001
	STXCTL = 0

	IF (JSTIFY.EQ.1) STXCTL = 3
	XCALL STENO (STRTNO,ENDNO,'ITEM #',15,STXCTL,V)
	GO TO (END) ,STXCTL
	IF (ENDNO.EQ.BLANKS) ENDNO = STRTNO
	IF (STRTNO.GT.ENDNO) GO TO GETNUM

LOCATN,
	XCALL OUTPT (3,1,2,'\',1)
	XCALL OUTPT (6,24,0,'PLEASE INPUT LOCATION DESIRED ',V)
	CTL = '06,55,02,00,AE'
	CALL INPUT
	GO TO (END,END) ,INXCTL
	RPTLOC = ENTRY(1,2)
	IF (RPTLOC.EQ.'  ') XCALL OUTPT (6,55,0,'ALL',V)

	CNGCTL = 2
	XCALL ANYCN (CNGCTL,WHATNO)
	GO TO (LOCATN) ,CNGCTL
	XCALL WATE (4,V)
NXTLIN,
	CALL RDPURC
	IF (ENDPUR) GO TO GRDTOT
	IF (NWITEM) CALL ITMTOT
	IF (LPFLAG.EQ.0) CALL LPOPEN
	CALL PRTLIN
	GO TO NXTLIN
RDPURC,
	NWITEM =
	INCR RECNO
	LOKCTL = 1
	XCALL IO (1,PURCHS,RECNO,READ,LOKCTL)
	IF (PURCHS.EQ.']]]]]]') GO TO ENDPUR
	IF (PITEM.LT.STRTNO.OR.PITEM.GT.ENDNO) GO TO RDPURC
	IF (PLOC.NE.RPTLOC.AND.RPTLOC.NE.'  ') GO TO RDPURC
	IF (FSTFLG) GO TO SETFST
	IF (SITEM.EQ.PITEM) RETURN
	NWITEM = 1
	RETURN
ENDPUR,
	ENDPUR = 1
	RETURN
SETFST,
	SITEM = PITEM
	NWITEM = 1
	RETURN
PRTLIN,
	IF (NWITEM.EQ.1) PLINE (1,15) = PITEM
	IF (NWITEM.EQ.1.AND.JSTIFY.EQ.1) XCALL LEFTJ (PLINE(1,15),15)
	IF (NWITEM.EQ.1) PLINE (19,48) = PDESCR
	PLINE (52,53) = PPRDCT
	PLINE (57,64) = PRCTDT,'ZX-XX-XX'
	PLINE (68,71) = PVEND
	XCALL LEFTJ (PLINE(68,71),4)
	PLINE (77,78) = PLOC
	PLINE (84,91) = PPONUM
	PLINE (95,98) = PLDTM,'ZZ.X'
	PLINE (102,110) = PLSTCT,TMASK
	EXTWGT = PWGT*PORDED
	PLINE (113,123) = EXTWGT,'ZZZ,ZZZ.XX-'
	PLINE (124,131) = PORDED,MASKN
	CALL PRINT
	ITMACC = ITMACC + PORDED
	ITMWGT = ITMWGT + EXTWGT
	RETURN
ITMTOT,
	IF (FSTFLG) GO TO ENDITM
	LINCNT = LINCNT - 1
	XCALL LINFD (2)
	PLINE (97,107) = TOTID
	PLINE (111,120) = ITMWGT, TMASK
	PLINE (123,131) = ITMACC,MASKP
	CALL PRINT
	GRDACC = GRDACC + ITMACC
	GRWGTT = GRWGTT + ITMWGT
	ITMACC =
	ITMWGT =
	SITEM = PITEM
	XCALL LINFD (3)
	LINCNT = LINCNT + 6
ENDITM,
	FSTFLG =
	RETURN
GRDTOT,
	IF (FSTFLG.EQ.1) GO TO NONE
	CALL ITMTOT
	IF (STRTNO.EQ.ENDNO) GO TO CONTCK
	LINCNT = LINCNT - 1
	XCALL LINFD (1)
	PLINE (78,88) = TOTID
	PLINE (90,102) = 'FOR LOCATION '
	PLINE (103,104) = RPTLOC
	IF (RPTLOC.EQ.'  ') PLINE (94,106) = 'ALL LOCATIONS'
	PLINE (111,120) = GRWGTT, TMASK
	PLINE (123,131) = GRDACC,MASKP
	CALL PRINT
	XCALL LINFD (3)
	LINCNT = LINCNT + 5
CONTCK,
	GRWGTT =
	GRDACC =
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	XCALL WATE (3,V)
	LPFLAG =
	PGCNT =
	IF (STRTNO.EQ.ENDNO.OR.ENDNO.NE.'[[[') GO TO GETNUM
	IF (RPTLOC.EQ.'  '.AND.STRTNO.EQ.'   '.AND.ENDNO.EQ.'[[') GO TO END
	GO TO GETNUM

;**********************************************************************

END,
	XCALL FILES (1,'I',53,4)
GETOUT,
	XCALL WATE (4,V)
	XCALL PGCHN ('IM:PURCH',1)

;**********************************************************************

PRINT,
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,'NO HDR','NO LEGEND',
&		' ',' ',0,1,132,0,LPSW,RPTNUM,PRTTYP)
	RETURN

;**********************************************************************

INPUT,
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,V)
	RETURN

;**********************************************************************

NONE,
	XCALL MESAG ('NO ITEMS EXIST FOR THIS REPORT',1)
	GO TO GETNUM

;**********************************************************************

LPOPEN,
	SPLFIL(5,6) = 'CJ'
	LPSW = 1
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO END
	XCALL OUTPT (3,1,2,'\',1)
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	LPFLAG = 1
	RETURN

;**********************************************************************
END
