;  ABCSEL / IM 
;
;		::PCPYIM.DEF::
;*****************************************************************************
;		INVENTORY MANAGEMENT  
;
;		RELEASED: AUGUST 1, 1984 (d70s10)
;*****************************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		SELECTS CRITERIA, BUILDS INDEX FOR THE
;		ABC INVENTORY CLASS ANALYSIS REPORT
;
;		NOTE:
;		THE FIRST FOUR RECORDS ARE USED TO PASS CONTROL DATA; THE FIRST
;		CONTAINING THE TOTAL VALUE OF INVENTORY ON HAND, AND THE SECOND,
;		THIRD, AND FOURTH CONTAINING THE DESIRED PERCENTAGES OF TOTAL
;		VALUE TO BE PLACED IN CLASSES A, B, AND C RESPECTIVELY.
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD ITMMAS	
		.INCLUDE 'DEF:RD041A.DEF'
RECORD INVCTL	
		.INCLUDE 'DEF:RD041B.DEF'
RECORD ITMIDX	
		.INCLUDE 'DEF:RD042A.DEF'
RECORD ABCIDX	
		.INCLUDE 'DEF:RD131A.DEF'
RECORD ABCCTL,X	
		.INCLUDE 'DEF:RD131C.DEF'
RECORD ABCCT2,X	
		.INCLUDE 'DEF:RD131B.DEF'
RECORD
		.INCLUDE 'DEF:RD131S.DEF'
RECORD SNDMSG

	FILNAM	,A9,	'IM:ABCRPT'
	RCNT	,D5
	OCNT	,D5
		,D3,	131
RECORD
	WTCNT	,D5
	APERCT	,D3
	BPERCT	,D3
	CPERCT	,D3
	ENTRY	,A3
	INXCTL	,D1
	V	,D1
	WHATNO	,D1
	CNGCTL	,D1
	SIZE	,D5
	SWITCH	,D1
	ROW	,D2
	TTLVAL	,D15
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
PROC (1)
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'PRINT ABC ANALYSIS REPORT',V)
	XCALL OUTPT (2,1,1,'SELECT CRITERIA',1)
; *********************** OPEN AND PROTECT FILES *************************
	SWITCH = 3
	XCALL FILES (2,'I',41,SWITCH)		; FILE # 41 - ITEM MASTER FILE
	IF (SWITCH.EQ.9) GO TO INUSE

	SWITCH = 5
	XCALL FILES (2,'I',41,SWITCH)
	LOKCTL = 1				; READ ITMMAS CONTROL RECORD
	XCALL IO (2,INVCTL,1,READ,LOKCTL)	; TO GET VALUE OF JSTIFY

	SWITCH = 3
	XCALL FILES (1,'O',131,SWITCH)		; FILE # 131 - ABC ANALYSIS INDEX
	IF (SWITCH.EQ.9) GO TO INU041


	SIZE = ((REC041+5)* (SIZ131+2) / 512) + 1
	XCALL OFILE (1,131,SIZE,SIZ131,SWITCH)
	IF (SWITCH.EQ.1) GO TO INU131

	GO TO DISPLA
INU131,
	XCALL FILES (1,'O',131,4)
INU041,
	XCALL FILES (2,'I',041,4)
INUSE,
	XCALL PGCHN ('IM:IMMENU',1)
; ************************* DISPLAY SCREEN -- GET CRITERIA ******************
DISPLA,
	XCALL OUTPT (3,1,2,'\',1)
	XCALL OUTPT (5,7,0,'PLEASE ENTER % OF TOTAL INVENTORY VALUE',V)
	XCALL OUTPT (0,0,0,' TO BE REPRESENTED IN CLASS:',V)
	XCALL OUTPT (6,37,0,'A-',V)
	XCALL OUTPT (7,37,0,'B-',V)
	XCALL OUTPT (8,37,0,'C-',V)
INPPCA,
	ROW = 06
	CALL INPPER
	GO TO (DISPLA, ABTRUN, DEFPCA), INXCTL
	IF (ENTRY(1,3).EQ.'   ') GO TO DEFPCA
	APERCT = ENTRY(1,3)
	ENTRY(1,3) = APERCT, 'ZXX'
	XCALL OUTPT (6,40,1,ENTRY(1,3),V)
	GO TO INPBCB
DEFPCA,
	APERCT = 80
	XCALL OUTPT (6,40,1,' 80',V)
INPBCB,
	ROW = 07
	CALL INPPER
	GO TO (DISPLA, ABTRUN, DEFPCB), INXCTL
	IF (ENTRY(1,3).EQ.'   ') GO TO DEFPCB
	BPERCT = ENTRY(1,3)
	ENTRY(1,3) = BPERCT, 'ZXX'
	XCALL OUTPT (7,40,1,ENTRY(1,3),V)
	IF ((APERCT + BPERCT).GT.100) GO TO INVALD
	GO TO CALPCC
DEFPCB,
	BPERCT = 15
	IF ((APERCT + BPERCT).GT.100) GO TO INVALD
	XCALL OUTPT (7,40,1,' 15',V)
CALPCC,
	CPERCT = 100 - (APERCT + BPERCT)
	ENTRY(1,3) = CPERCT, 'ZZX'
	XCALL OUTPT (8,40,0,ENTRY(1,3),V)
	GO TO ANYCNG
; *********
INVALD,
	XCALL MESAG ('PERCENTAGES MUST ADD UP TO 100',2)
	GO TO DISPLA
; *********
INPPER,
	XCALL INPUT (ROW,40,03,00,'#E',ENTRY,INXCTL,V)
	RETURN
; **********
ABTRUN,
	XCALL FILES (2,'I',41,4)		;FILE # 41 - INVENTORY MASTER
	SWITCH = 7
	XCALL FILES (1,'O',131,SWITCH)		;FILE #131 - ABC INDEX FILE.
	XCALL PGCHN ('IM:IMMENU',1)
; **********
ANYCNG,
	CNGCTL = 2
	XCALL ANYCN (CNGCTL,WHATNO)
	GO TO (DISPLA), CNGCTL
; ********************** WRITE DUMMY RECORDS ********************************
	XCALL WATE (4,V)
	XCALL OUTPT (2,1,1,'BUILD INDEX',1)
	ABCCTL =
	LOKCTL = 1
	XCALL IOS (1,ABCCTL,WRITE,LOKCTL)
	IF (LOKCTL.EQ.3) GO TO INU131
	ABCCTL(1,17) = '99999999999999999'	; TO KEEP THEM BUBBLED UP
						; AFTER THE SORT.
	ABCPCT = APERCT
	LOKCTL = 1
	XCALL IOS (1,ABCCT2,WRITE,LOKCTL)
	IF (LOKCTL.EQ.3) GO TO INU131
	ABCPCT = BPERCT
	LOKCTL = 1
	XCALL IOS (1,ABCCT2,WRITE,LOKCTL)
	IF (LOKCTL.EQ.3) GO TO INU131
	ABCPCT = CPERCT
	LOKCTL = 1
	XCALL IOS (1,ABCCT2,WRITE,LOKCTL)
	IF (LOKCTL.EQ.3) GO TO INU131

; ******** OUTPUT INDEX RECORDS - COMPUTE VALUE OF INVENTORY ON HAND ******
; **** NOTE:  ITEMS WITH NEGATIVE QUANTITY ON HAND WILL BE CONSIDERED  TO *
; **** HAVE ZERO VALUE -- NOT A NEGATIVE ONE. *****************************


	LOKCTL = 1
	XCALL IO (2,INVCTL,1,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO ABTRUN
	ABC041 = 1
IDXLOP,
	INCR ABC041
	LOKCTL = 1
	XCALL IOS (2,ITMMAS,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO WRTTOT
	IF (ITMMAS(1,6).EQ.']]]]]]') GO TO WRTTOT
	IF (DESCR(1,6).EQ.']]]DEL') GO TO IDXLOP
	IF (OBSFLG.NE.'A') GO TO IDXLOP
	VALAVU = USEYTD * AVGCST	;UNITS USED YTD TIMES AVERAGE COST
	INCR WTCNT
	IF (VALAVU.LT.0) VALAVU = 0
	TTLVAL = TTLVAL + VALAVU
	LOKCTL = 1
	XCALL IOS (1,ABCIDX,WRITE,LOKCTL)
	IF (LOKCTL.EQ.3) GO TO INU131
	GO TO IDXLOP
WRTTOT,
	ABCIDX = ']]]]]]]]]]]]]]]]]]]]'
	LOKCTL = 1
	XCALL IOS (1,ABCIDX,WRITE,LOKCTL)
	IF (LOKCTL.EQ.3) GO TO INU131
	ABCCTL =
	TOTVAL = TTLVAL, 'XXXXXXXXXXXXXXX'
	CLOSE 1
	SWITCH = 5
	XCALL FILES (1,'U',131,SWITCH)			; FILE # 131 - ABCIDX
	LOKCTL = 1
	XCALL IO (1,ABCCTL,1,WRITE,LOKCTL)
; ****************** CLOSE FILES, PREP AND STOP TO SORT. ******************
	CLOSE 1
	CLOSE 2
	RCNT = WTCNT + 5
	OCNT = 00004					;SKIP CONTROL RECORDS
	SWITCH = 5
	XCALL SNMSG (SNDMSG,SWITCH)
	XCALL PGCHN ('IM:SRTABC',0)

END

