SUBROUTINE ADVEN
;
;  ADVEN / IM 
;
;
;		::PCPYIM.DEF::
;*****************************************************************************
;		INVENTORY MANAGEMENT  
;
;		RELEASED: AUGUST 1, 1984 (d70s10)
;*****************************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;		MENU SELECTION FOR REORDERING ADVICE REPORT BY VENDOR
;
;	SELECTION DATA FOR SUBROUTINE ADVEN:
;		ONLY ONE VENDOR CAN BE SELECTED.
;
;		IF KLOC = DEFAULT LOCATION, CALCULATIONS WILL BE BASED ON THE
;		   SUM OF ALL LOCATIONS - AND REPORT WILL PRINT THIS LOC ONLY.
;
;		IF KLOC = 'ALL', CALCULATIONS WILL BE BASED ON THE SUM OF ALL
;		   LOCATIONS - BUT THE REPORT WILL PRINT LOCATION DETAIL.
;
;		IF KLOC = ONE LOC, ONLY THIS LOCATION WILL PRINT.
;		AVFLAG IS SET IF QTY AVAILABLE SHOULD INCLUDE QTY ON ORDER.
;		MPFLAG IS SET FOR ITEMS PURCHASED(P), MANUFACTURED(M) OR
;		   BOTH(B) TO BE INCLUDED IN REPORT.
;
	MENUID	,A
	KVEND	,A
	KLOC	,A
	KPRCNT	,D
	KNUMB	,D
	VNDOPT	,D
	AVFLAG	,D
	MPFLAG	,A
	SELECT	,D
	V	,D
RECORD	CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD
	ENTRY	,A5
	INXCTL	,D1
	CNGCTL	,D1
	WHATNO	,D1
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
PROC (1)
	XCALL OUTPT (2,1,2,MENUID,1)
	XCALL OUTPT (3,10,0,'1. VENDOR NUMBER',V)
	XCALL OUTPT (4,10,0,'2. LOCATION',V)
	XCALL OUTPT (5,10,0,'3. PERCENT FROM REORDER LEVEL',V)
	XCALL OUTPT (6,10,0,'   NUMBER FROM REORDER LEVEL',V)
	XCALL OUTPT (7,10,0,'4. INCLUDE MULTIPLE VENDORS ?',V)
	XCALL OUTPT (8,10,0,
&	'5. SHOULD QUANTITY AVAILABLE INCLUDE ON-ORDER ?',V)
	XCALL OUTPT (9,10,0,
&	'6. FOR ITEMS MANUFACTURED(M), PURCHASED(P) OR BOTH(B) ?',V)
	CNGCTL =
VEND,
	KVEND =
	SELECT =
	CTL = '03,68,04,01,AE'
	CALL INPUT
	GO TO (VEND,END), INXCTL
	XCALL FRMAT (ENTRY,4)
	XCALL OUTPT (3,68,0,ENTRY(1,4),V)
	KVEND = ENTRY(1,4)
	GO TO (ANYCNG) ,CNGCTL
LOC,
	KLOC =
	CTL = '04,68,02,00,A '
	CALL INPUT
	GO TO (VEND),INXCTL
	KLOC = ENTRY(1,2)
	IF (KLOC.EQ.'  ') XCALL OUTPT (4,68,0,'ALL',V)
	GO TO (ANYCNG), CNGCTL
PRCNT,
	XCALL OUTPT (6,68,0,'    ',V)
	KPRCNT =
	KNUMB =
	CTL = '05,68,02,00,#- '
	CALL INPUT
	GO TO (VEND), INXCTL
	KPRCNT = ENTRY (1,2)
	ENTRY (1,4) = KPRCNT, '-ZX%'
	IF (KPRCNT.NE.0) XCALL OUTPT (5,68,0,ENTRY(1,4),V)
	IF (KPRCNT.NE.0.AND.CNGCTL.EQ.1) GO TO ANYCNG
	IF (KPRCNT.NE.0) GO TO MLTVND
NUMB,
	CTL = '06,68,03,00,#- '
	CALL INPUT
	GO TO (VEND), INXCTL
	KNUMB = ENTRY (1,3)
	GO TO (ANYCNG), CNGCTL
MLTVND,
	CTL = '07,68,01,01,YN'
	CALL INPUT
	VNDOPT =
	IF (INXCTL.NE.1.AND.INXCTL.NE.2) GO TO MLTVND
	GO TO (NOMULT), INXCTL - 1
	VNDOPT = 1
NOMULT,
	GO TO (ANYCNG) , CNGCTL
INCONO,
	CTL = '08,68,01,01,YN'
	CALL INPUT
	AVFLAG = INXCTL
	GO TO (ANYCNG) ,CNGCTL
MFGPUR,
	CTL = '09,68,01,00,A '
	CALL INPUT
	IF (ENTRY(1,1).EQ.' ') ENTRY(1,1) = 'B'
	XCALL OUTPT (9,68,0,ENTRY(1,1),V)
	IF (ENTRY(1,1).NE.'M'.AND.ENTRY(1,1).NE.'P'.AND.ENTRY(1,1).NE.'B')
&		GO TO MFGPUR
	MPFLAG = ENTRY(1,1)
	GO TO ANYCNG
END,
	SELECT = 1
ENDMNU,
	RETURN
ANYCNG,
	XCALL ANYCN (CNGCTL,WHATNO)
	GO TO (ENDMNU,CNGBR), CNGCTL+1
BADCNG,
	CNGCTL = 3
	GO TO ANYCNG
CNGBR,
	GO TO (VEND,LOC,PRCNT,MLTVND,INCONO,MFGPUR), WHATNO
	GO TO ANYCNG
INPUT,
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,V)
	RETURN
END
