;  TAGEDT / IM 
;
;
;		::PCPYIM.DEF::
;*****************************************************************************
;		INVENTORY MANAGEMENT  
;
;		RELEASED: AUGUST 1, 1984 (d70s10)
;*****************************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;		PROGRAM FOR PRODUCING AN EDIT LIST OF "INVTAG"
;		(INVENTORY COUNT TAGS).
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD INVTAG		;
		.INCLUDE 'DEF:RD040A.DEF'
RECORD DUMTAG		;
		.INCLUDE 'DEF:RD040B.DEF'
RECORD ITMMAS		;
		.INCLUDE 'DEF:RD041A.DEF'
RECORD DUMINV		;
		.INCLUDE 'DEF:RD041B.DEF'
RECORD ITMIDX		;
		.INCLUDE 'DEF:RD042A.DEF'
RECORD TITLE
		,A20,	'COUNT TAGS EDIT LIST'
RECORD MENUID,X
		,A20
RECORD HDR1
		,A8
		,A10,	'TAG NUMBER'
		,A3
		,A11,	'ITEM NUMBER'
		,A7
		,A11,	'DESCRIPTION'
		,A17
		,A8,	'LOCATION'
		,A3
		,A15,	'UNIT-OF-MEASURE'
		,A5
		,A16,	'QUANTITY COUNTED'
RECORD PLINE
		,A132
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD
	SPLFIL	,A14
RECORD
	V	,D1
	FIRST	,D1
	LASTNO	,D6
	DUMMY	,D5
	SRCCTL	,D1
	SWITCH	,D1
	BADOPN	,D1
	COUNT	,D5,	00001
	STRTNO	,A6
	ENDNO	,A6
	SSTRNO	,D6
	SENDNO	,D6
	STXCTL	,D1
	LINCNT	,D2
	PGCNT	,D3
	TAGS	,D6
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	LPSW	,D2
	LPARG	,D1
	RPTNUM	,D3
	PRTTYP	,A1
PROC (1)
	XCALL TERID (V)
	CALL OPENS			;OPEN ALL NECESSARY FILES
	IF (BADOPN) XCALL PGCHN ('IM:IMMENU',1)
MENU,
	XCALL OUTPT (1,1,2,'PHYSICAL COUNT ENTRY & EDITING',1)
	XCALL OUTPT (2,1,0,MENUID,1)
	STXCTL = 1			;ALLOW ONLY NUMERIC STENO ENTRY
	XCALL STENO (STRTNO,ENDNO,'TAG NUMBER',6,STXCTL,V)
	GO TO (END), STXCTL		;"END" TYPED
	SSTRNO = STRTNO
	IF (ENDNO.EQ.'[[[   ') SENDNO = 999999
	IF (ENDNO.NE.'[[[   ') SENDNO = ENDNO
	CALL REPORT
	IF (ENDNO.EQ.'[[[   ') GO TO END
	GO TO MENU
END,
	XCALL WATE (3,V)
	CALL CLOSES			;CLOSE ALL FILES
	XCALL PGCHN ('IM:TAGMNT',1)		;RETURN TO MAINTENANCE PROGRAM

;************************************************************************

REPORT,
	XCALL OUTPT (2,1,1,'PRINT-OUT',1)
	SPLFIL(5,6) = 'CR'
	LPSW = 1
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) ENDNO = '[[[   '
	IF (LPSW.EQ.0) RETURN
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)		;PROCESSING MESSAGE

	LINCNT = 60			;MUST BE 60 FOR 1ST TIME THRU
	XCALL LINFD (1)
	PGCNT = 0
	TAGS = 0			;NUMBER OF TAGS COUNTED FOR REPORT
	COUNT = 1			;RECORD NUMBER
NXTREC,
	INCR COUNT
READ,
	LOKCTL = 1
	XCALL IO (1,INVTAG,COUNT,READ,LOKCTL)
	IF (TAGITM.EQ.']]]DEL') GO TO NXTREC	;RECORD FLAGGED FOR DELETION
	IF (TAGITM.EQ.']]]]]]') GO TO EOF	;END OF FILE CONDITION
	IF (TAGNO.LT.SSTRNO) GO TO NXTREC	;LOCATE FIRST RECORD FOR
						;PRINTING
	IF (TAGNO.GT.SENDNO) GO TO EOF		;END OF FILE CONDITION

	IF (FIRST) LASTNO = TAGNO - 1
	FIRST =
	IF (TAGNO.NE.LASTNO + 1) CALL SEQOUT	;TAG NUMBERS
								;OUT OF SEQUENCE
	LASTNO = TAGNO
	PLINE (11,16) = TAGNO, 'XXXXXX'
	PLINE (22,36) = TAGITM
	IF (JSTIFY) XCALL LEFTJ (PLINE(22,36),15)
	DESCR = '** ITEM NO LONGER ON FILE **'
	LOC (1) = '??'
	XCALL SERCH (2,ITMIDX,TAGITM,1,15,ORG041,DUMMY,SRCCTL,4,16,20,0,0,0,0)
	IF (SRCCTL.EQ.1.OR.IRC041.EQ.0) GO TO ITEM
	LOKCTL = 1
	XCALL IO (3,ITMMAS,IRC041,READ,LOKCTL)
ITEM,
	PLINE (40,68) = DESCR
	PLINE (71,72) = TLOC
	PLINE (85,86) = TUOM
	PLINE (103,109) = TQTYOH, 'ZZZ,ZZX'
	CALL PRINT
	XCALL LINFD (1)
	INCR LINCNT
	INCR TAGS			;COUNT ANOTHER TAG PRINTED
	GO TO NXTREC			;GO BACK AND PRINT ANOTHER
EOF,
	PLINE (10,38) = 'TOTAL NUMBER OF TAGS THIS RUN'
	PLINE (40,46) = TAGS, 'ZZZ,ZZX'
	CALL PRINT
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	PGCNT =
	RETURN

;************************************************************************

OPENS,
	SWITCH = 1
	XCALL FILES (1,'I',40,SWITCH)		;FILE #40 "INVTAG"
	IF (SWITCH.EQ.9) GO TO INUSE1
	SWITCH = 1
	XCALL FILES (2,'I',42,SWITCH)		;FILE #42 "ITMIDX"
	IF (SWITCH.EQ.9) GO TO INUSE2
	SWITCH = 1
	XCALL FILES (3,'I',41,SWITCH)		;FILE #41 "ITMMAS"
	IF (SWITCH.EQ.9) GO TO INUSE3
	LOKCTL = 1
	XCALL IO (3,DUMINV,1,READ,LOKCTL)			;RETRIEVE ITMMAS CONTROL RECORD
	BADOPN = 0
	RETURN
INUSE3,
	XCALL FILES (2,'I',42,4)
INUSE2,
	XCALL FILES (1,'I',40,4)
INUSE1,
	BADOPN = 1
	RETURN

;************************************************************************

CLOSES,
	XCALL FILES (3,'I',41,4)				;CLOSE "ITMMAS"
	XCALL FILES (2,'I',42,4)				;  "   "ITMIDX"
	XCALL FILES (1,'I',40,4)				;  "   "INVTAG"
	RETURN

;************************************************************************

PRINT,
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,'NO HDR','NO HDR',
&	 'NO LEGEND','NO LEGEND','NO LEGEND',0,132,132,0,LPSW,RPTNUM,PRTTYP)
	RETURN

;************************************************************************

SEQOUT,
	PLINE (11,16) = '******'
	PLINE (22,40) = 'TAG SEQUENCE BREAK'
	CALL PRINT
	XCALL LINFD (1)
	INCR LINCNT
	RETURN
END
