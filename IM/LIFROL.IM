;  LIFROL / IM 
;
;
;		::PCPYIM.DEF::
;*****************************************************************************
;		INVENTORY MANAGEMENT  
;
;		RELEASED: AUGUST 1, 1984 (d70s10)
;*****************************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;		PROGRAM TO "ROLL-OVER" CURRENT INVENTORY COSTS & QUANTITIES
;		TO THE "LIFO" HOLDING FIELDS
;
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD ITMMAS		;
		.INCLUDE 'DEF:RD041A.DEF'
RECORD DUMINV		;
		.INCLUDE 'DEF:RD041B.DEF'
RECORD ITMIDX		;
		.INCLUDE 'DEF:RD042A.DEF'
RECORD
	V	,D1
	BRACKS	,A15	,']]]]]]]]]]]]]]]'
	BADOPN	,D1
	LOCCTR	,D2
	SWITCH	,D1
	QTY	,D6
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
PROC (1)
	XCALL TERID (V)
	XCALL WATE (4,V)		;PROCESSING MESSAGE
	XCALL OUTPT (2,1,1,'ROLL-OVER LIFO QUANTITIES & COSTS',1)
	CALL OPENS			;OPEN ALL NECESSARY FILES
	IF (BADOPN) XCALL PGCHN ('UT:CLROF2',1)	;GET OTHER USERS OFF!
	LOKCTL = 1
	XCALL IOS (1,ITMIDX,READ,LOKCTL)	;SKIP PAST 1ST ITMIDX RECORD
	IF (LOKCTL.EQ.2) GO TO EOF
NXTIDX,
	LOKCTL = 1
	XCALL IOS (1,ITMIDX,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOF
	IF (IITMNO .EQ. BRACKS) GO TO EOF	;END OF FILE CONDITION
	IF (IRC041 .EQ. 0) GO TO NXTIDX		;DELETED RECORD
	LOKCTL = 1
	XCALL IO (2,ITMMAS,IRC041,READ,LOKCTL)
	IF (DESCR .EQ. ']]]DEL') GO TO NXTIDX	;DELETED RECORD
	XCALL OUTPT (11,20,0,'UPDATING ITEM # ',V)
	XCALL OUTPT (0,0,1,ITEMNO,V)
	CALL ROLL			;PERFORM "ROLL-OVER"
	GO TO NXTIDX
EOF,
	XCALL MESAG ('UPDATE COMPLETED',2)
	XCALL WATE (4,V)
	CALL CLOSES
	XCALL PGCHN ('IM:IMSFMN',1)
;************************************************************************
ROLL,
	LOCCTR =
	QTY =
MLTLOC,
	INCR LOCCTR
	IF (LOCCTR.GT.NUMLOC.OR.LOC(LOCCTR).EQ.'  ') GO TO TESTQ
	QTY = QTY + QTYONH(LOCCTR)
	GO TO MLTLOC
TESTQ,
					;LIFOBQ IS THE
					;BASE QUANTITY ON HAND
	IF (QTY .LE. LIFOBQ .OR. QTY .EQ. 0) GO TO DECRES
INCRES,
	LIFOBP = (((QTY-LIFOBQ) * AVGCST)#1 + (LIFOBQ * LIFOBP)) / QTY
						;COMPUTE NEW LIFO BASE COST
	LIFOBQ = QTY				;USE NEW QUANTITY ON HAND
	GO TO WRITIT
DECRES,
	LIFOBQ = QTY			;USE NEW QUANTITY ON HAND
	LIFOBP = LIFOBP			;NO CHANGE IN LIFO BASE COST
WRITIT,
	LOKCTL = 1
	XCALL IO (2,ITMMAS,IRC041,WRITE,LOKCTL)
	RETURN
;************************************************************************
OPENS,
	SWITCH = 2				;OPEN AND PROTECT
	XCALL FILES (1,'I',42,SWITCH)		;ITMIDX FILE #42
	IF (SWITCH .EQ. 9) GO TO INUSE1
	SWITCH = 2				;OPEN AND PROTECT
	XCALL FILES (2,'U',41,SWITCH)		;ITMMAS FILE #41
	IF (SWITCH .EQ. 9) GO TO INUSE2
	LOKCTL = 1
	XCALL IO (2,DUMINV,1,READ,LOKCTL)			;READ CONTROL RECORD
	BADOPN =
	RETURN
INUSE2,
	XCALL FILES (1,'I',42,4)		;CLOSE ITMIDX #42
	SWITCH = 2
	XCALL SNMSG ('041,IM:LIFROL',SWITCH)	;SEND MESSAGE TO CLROFF
						;TO GET USERS OFF THE ITMMAS
	BADOPN = 1				;FLAG UNSUCCESSFUL
	RETURN
INUSE1,
	SWITCH = 2
	XCALL SNMSG ('042,IM:LIFROL',SWITCH)	;SEND MESSAGE TO CLROFF
						;TO GET USERS OFF THE ITMIDX
	BADOPN = 1				;FLAG UNSUCCESSFUL
	RETURN
;************************************************************************
CLOSES,
	XCALL FILES (2,'U',41,4)		;CLOSE ITMMAS FILE #41
	XCALL FILES (1,'I',42,4)		;CLOSE ITMIDX FILE #42
	RETURN
END
