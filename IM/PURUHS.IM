; PURUHS / IM
;
;	PURGE USAGE HISTORY
;
;
RECORD	D_STUFF
	D_IN	,D8		;DATE-IN, ANY FORMAT
	D_OUT	,D6		;RETURN VALUE MMDDYY
	D_OUTR	,D8		;RETURN VALUE CCYYMMDD
	D_FMT	,A10		;RETURN VALUE MM/DD/CCYY
	D_SW	,A2		;"99" = DATE CONVERSION ERROR

RECORD USGHST
		.INCLUDE 'DEF:RD134A.DEF'
;
RECORD USGCTL
		.INCLUDE 'DEF:RD134B.DEF'
;
RECORD
	PCNT	,D5
	SCNT	,D5
	RECNO	,D5
	SWITCH	,D1	,1
	PURDAT	,D8
	PDATE	,D8
	TDATE	,D6
	ENTRY	,A10
	ALPHA	,A10
	INXCTL	,D1
	V	,D1
	LOKCTL	,D1
	READ	,D1	,0
	WRITE	,D1	,1
	WRCNT	,D5
RECORD	,X
	APCNT	,A5
	ASCNT	,A5
PROC 
	XCALL TERID (V)
	XCALL OUTPT (2,1,2,'PURGE USAGE HISTORY',1)
	XCALL OUTPT (12,20,0,'ARE YOU SURE YOU WANT TO RUN THIS PROGRAM <N> ?',1)
	XCALL INPUT (12,68,01,00,'YN',ENTRY,INXCTL,1)
	IF (INXCTL.NE.1) XCALL PGCHN ('IM:IMMENU',1)
ASK1,
	XCALL RDATE (TDATE)
	PURDAT = ( 123100 + TDATE(5,6) - 2 )
	XCALL DATE8(TDATE, D_OUT, PURDAT, ALPHA, D_SW)

;;;	ALPHA = PURDAT,'<XX/XX/XX>'
	XCALL OUTPT (14,20,0,'PURGE THROUGH DATE ',1)
	XCALL OUTPT (0,0,0,ALPHA,1)
	XCALL INPUT (14,68,08,00,'DE',ENTRY,INXCTL,1)
	GOTO (ASK1,ABORT), INXCTL
	IF (ENTRY.EQ.'      ') ENTRY(1,8) = PURDAT,'XXXXXXXX'
	PURDAT = ENTRY
	XCALL DATE8(PURDAT, D_OUT, D_OUTR, ALPHA, D_SW)
;;;	ALPHA = PURDAT,'XX/XX/XX'
	XCALL OUTPT (14,68,0,ALPHA,1)
;;;	PDATE(1,2) = PURDAT(5,6)
;;;	PDATE(3,6) = PURDAT(1,4)
	PDATE = PURDAT
	XCALL OUTPT (16,20,0,'ARE YOU SURE YOU ABSOLUTELY CERTAIN THIS IS',1)
	XCALL OUTPT (17,20,0,'WHAT YOU WANT TO DO <N> ?',1)
	XCALL INPUT (17,48,01,00,'YN',ENTRY,INXCTL,1)
	IF (INXCTL.NE.1) GOTO ASK1
OPEN1,
	XCALL FILES (4,'U',134,SWITCH)		;FILE 134 - USGHST
	IF (SWITCH.NE.9) GOTO RDHDR
	GOTO ABORT
CLOSES,
	XCALL FILES (4,'I',134,4)
	RETURN
RDHDR,
	XCALL WATE (2,2)
 	RECNO = 1
	WRCNT = 1
	LOKCTL = 1
	XCALL IO (4,USGCTL,RECNO,READ,LOKCTL)	;REC 1
	XCALL OUTPT (23,01,1,'\',1)
	PCNT =
	SCNT =
RDLOOP,
	DISPLAY (15,13,'PURGED = ',APCNT,9,'SAVED = ',ASCNT)
	INCR RECNO
	LOKCTL = 1
	XCALL IO (4,USGHST,RECNO,READ,LOKCTL)
	IF (USGHST.EQ.']]]]]]]]]]') GOTO BRACK
;;;	TDATE(1,2) = HDTUSE(5,6)
;;;	TDATE(3,6) = HDTUSE(1,4)
	TDATE = HDTUSE
	IF (TDATE.LE.PDATE.OR.HDTUSE.EQ.0) INCR PCNT
	IF (TDATE.LE.PDATE.OR.HDTUSE.EQ.0) GOTO RDLOOP
	INCR SCNT
	INCR WRCNT
	IF (WRCNT.EQ.RECNO) GOTO RDLOOP
	LOKCTL = 1
	XCALL IO (4,USGHST,WRCNT,WRITE,LOKCTL)
	HDTUSE =
	LOKCTL = 1
	XCALL IO (4,USGHST,RECNO,WRITE,LOKCTL)
	GOTO RDLOOP
BRACK,
	XCALL OUTPT (24,01,1,'\',1)
	RECNO = WRCNT
BR2,
	INCR RECNO
	IF (RECNO.GT.REC134) GOTO EOF
	XCALL  IO (4,USGHST,RECNO,WRITE,LOKCTL)
	GOTO BR2
EOF,
	ORG134 = WRCNT
	REC134 = WRCNT
	LOKCTL = 1
	XCALL IO (4,USGCTL,1,WRITE,LOKCTL)
	CALL CLOSES
	XCALL MESAG ('USGHST FILE SUCCESSFULY PURGED',2)
ABORT,
	XCALL PGCHN ('IM:IMMENU',1)
END
