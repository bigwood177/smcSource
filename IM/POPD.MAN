;POPD.MAN
;		AUTHOR: SHERWOOD S. QUIRING
;		POPUP WINDOW ROUTINE

;		11-dec-02 ssq: 	added palet option
;		10-25-17: THIS VERSION JUST DISPLAYS

SUBROUTINE	POPD
	POPAR	,A		;POP UP WINDOW DATA
	DR	,D		;1=DISPLAY, 2=REMOVE
	PALET	,D		;OPTIONAL PALLET
	
	.INCLUDE 'DEF:WINDOWS.DEF'

RECORD	POPARA
	.INCLUDE 'DEF:POP1.DEF'

RECORD	FUNKEY
	.INCLUDE 'DEF:FUNKEY.DEF'

RECORD	WRKLIN
	ARO	,A2
		,A1,'.'
		,A1
	REST	,A80

RECORD	VARS
	STAT	,D1
	STAT_KEY	,D3
	DEC	,D6
	ALPHA	,A3
	WHDR	,A80
	F_KEY	,D3
	I	,D5
	VAL	,D1
	WROW	,D2
	WCOL	,D2
	FIRST_ROW	,D2
	LAST_ROW	,D2
	SAVE_ROW	,D2
	X	,D2
	Y	,D2
	HT	,D2
	LN	,D2
	MAXROW	,D2
	WLEN	,D2
	TITLEN	,D2
PROC
	POPARA = POPAR		;POP UP DATA

	USING DR SELECT
	(1),	CALL DSPWND		;DISPLAY WINDOW
	(2),	CALL REMOVE
	ENDUSING

	XRETURN
;==================================================================

REMOVE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; REMOVE WINDOW
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	XCALL W_PROC(WP_REMOVE,POP_WND)
;;;	XCALL W_PROC(WP_DELETE,POP_WND)
	XCALL W_UPDT
	RETURN
;------------------------------------------

DSPWND,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; DISPLAY WINDOW, FILL W/ DATA
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; determine height & length of window, x & y coordinates
;;; to center window on screen.
	LAST_ROW = NUMROW + 1
	IF (NUMROW .GT. NUMARA) LAST_ROW = NUMARA + 1
	INCR LAST_ROW		;SSQ 6-17-98

	HT = LAST_ROW		;HEIGHT OF WINDOW, LEAVE ROOM FOR HEADER
	LN = PLEN + 2		;LENGHT OF WINDOW
	
	LN = LN + 1		;FOR "*"

	WHDR = DLINE

	IF (WX .LE. 0)
	THEN	X = (24-HT)/2		;X CO-ORDINATE
	ELSE	X = WX
	IF (WY .LE. 0)
	THEN	Y = (80-LN)/2		;Y CO-ORDINATE
	ELSE 	Y = WY

;;; place window...
	XCALL W_PROC(WP_FIND,POP_WND,POP_WID)
	IF (.NOT. POP_WND) XCALL W_PROC(WP_CREATE,POP_WND,POP_WID,HT,LN)

	TITLEN = %TRIM(POP_TITLE)
	IF (TITLEN .GT.0)
&	 XCALL W_BRDR(POP_WND,WB_TITLE,POP_TITLE(1,TITLEN),WB_TPOS,WBT_TOP,WBT_CENTER)
	XCALL W_PROC(WP_PLACE,POP_WND,X,Y)
	XCALL W_DISP(POP_WND,WD_CLEAR)
	XCALL W_DISP (POP_WND,WD_ATTR,ATTR_CLR+ATTR_RVRS)
;;;	XCALL W_DISP(POP_WND,WD_POS,1,1,WHDR(1,PLEN) )
;;;	XCALL W_DISP(POP_WND,WD_HLINE,2,1,PLEN+2)
;;; fill window w/ data...
	CLEAR ARO
	WLEN = LN
;;;	WROW = 2
	WROW = 0
	FOR I FROM 1 THRU LAST_ROW-2
		BEGIN
		INCR WROW
		DLINE = PARRY(I)
		ARO = I, 'ZX'
		REST = DLINE(1,PLEN)
		XCALL W_DISP(POP_WND,WD_POS,WROW,1,WRKLIN(1,WLEN) )
		END
	IF (%PASSED(PALET)) XCALL W_AREA (POP_WND, WA_COLOR, PALET)	;ssq 12/11

	POPAR = POPARA		;POP UP DATA

	RETURN
;---------------------------------------------------------------------

