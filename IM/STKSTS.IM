; STKSTS / IM
;
;	STOCK STATUS PRINTOUT
;
;
RECORD	D_STUFF
	D_IN	,D8		;DATE-IN, ANY FORMAT
	D_OUT	,D6		;RETURN VALUE MMDDYY
	D_OUTR	,D8		;RETURN VALUE CCYYMMDD
	D_FMT	,A10		;RETURN VALUE MM/DD/CCYY
	D_SW	,A2		;"99" = DATE CONVERSION ERROR

RECORD INVMAS
		.INCLUDE 'DEF:RD131A.DEF'
;;;RECORD INVCTL
;;;		.INCLUDE 'DEF:RD132B.DEF'
RECORD INVTAG
		.INCLUDE 'DEF:RD040A.DEF'
;:
RECORD HDR1
		,A50,'--------- TAG ----------------    UNIT         QTY'
		,A50,'        UNIT        DOLLARS       DATE            '
RECORD HDR2
		,A50,'NUMBER    DESCRIPTION             MEAS      ON HND'
		,A50,'        COST        ON HAND     RECEIVED    NOTES '

RECORD LEG1
		,A19,'SELECTION CRITERA: '
	KMATTY	,A3
		,A3,' - '
	KGAGE1	,A3
		,A3,' x '
	KWID1	,A7
RECORD LEG2
		,A25
	KGAGE2	,A3
		,A3,' x '
	KWID2	,A7
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD
	TITLE	,A30,'STOCK STATUS PRINT OUT        '
	V	,D1
	SWITCH	,D1
	KEY	,A15
	BSMID	,D5
	SRCCTL	,D1
	LOKCTL	,D1
	READ	,D1	,0
	PLINE	,A132
	PGCNT	,D2
	LINCNT	,D2	,62
	PRTTYP	,A1
	RPTNUM	,D3
	PRTCTL	,D1
	LPSW	,D1
	SPLFIL	,A14
	MASK	,A11	,'ZZ,ZZZ,ZZX-'
	DTMASK	,A8	,'XX/XX/XX'
	DP4MSK	,A15	,'Z,ZZZ,ZZZ.XXXX-'
	RECNO	,D5
	PSTMSG	,A9
	POST	,D1
	MSGCTL	,D1
	TAGCNT	,D6
	TOTQTY 	,D8
	TOTCST	,D12
	EXTCST	,D10
	ENTRY	,A10
	INXCTL	,D1
	BLANKS	,A10
	DECMAL	,D10
	OPTION	,D1
;
PROC
	XCALL TERID (V)
	XCALL OUTPT (2,1,2,TITLE,1)
OPEN1,
	SWITCH = 5
	XCALL FILES (3,'I',040,SWITCH)		;FILE 040 - INVTAG
	IF (SWITCH.NE.9) GOTO OPEN2
	GOTO ABORT
OPEN2,
	XCALL FILES (2,'SI',132,SWITCH)		;FILE 132 - RINVMS
	IF (SWITCH.NE.9) GOTO SELECT
	CALL CLOSE1
	GOTO ABORT
SELECT,
	XCALL OUTPT (3,1,2,'         SELECT DESCRIPTION',1)
DESCR,
	CTL = '03,30,03,00,AE'
	CALL INPUT
	GOTO (SELECT,ENDOFF), INXCTL
	IF (ENTRY.EQ.BLANKS) 
	BEGIN
	  ENTRY(1,3) = '***'
	  XCALL OUTPT (ROW,COL,0,ENTRY(1,3),1)
	END
	KMATTY = ENTRY
	XCALL OUTPT (03,34,0,'-',1)
	CTL = '03,36,03,00,A '
	CALL INPUT
	GOTO (DESCR), INXCTL
	KGAGE1 = ENTRY
	XCALL OUTPT (04,34,0,'-',1)
	CTL = '04,36,03,00,A '
	CALL INPUT
	GOTO (DESCR), INXCTL
	IF (ENTRY.EQ.BLANKS)
	BEGIN
	  ENTRY(1,3) = KGAGE1
	  XCALL OUTPT (ROW,COL,0,ENTRY(1,3),1)
	END
	KGAGE2 = ENTRY
	XCALL OUTPT (03,40,0,'x',1)
	CTL = '03,42,06,00,# '
	CALL INPUT
	GOTO (DESCR), INXCTL
	DECMAL = ENTRY
	CALL DSP3DP
	KWID1 = DECMAL,'ZZZ.XXX'
	XCALL OUTPT (04,40,0,'x',1)
	CTL = '04,42,06,00,# '
	CALL INPUT
	GOTO (DESCR), INXCTL
	IF (ENTRY.EQ.BLANKS) ENTRY(1,6) = KWID1
	DECMAL = ENTRY
	CALL DSP3DP
	KWID2 = DECMAL,'ZZZ.XXX'
OPENLP,
	LPSW = 1
	SPLFIL (5,6) = 'FS'
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.NE.0) GOTO RDHDR
	CALL CLOSES
	GOTO ABORT
CLOSES,
	XCALL FILES (2,'SI',132,4)
CLOSE1,
	XCALL FILES (3,'I',040,4)
	RETURN
RDHDR,
	XCALL WATE (2,2)
;;;	LOKCTL = 1
;;;	XCALL IO (2,INVCTL,1,READ,LOKCTL)
 	RECNO = 1
RDLOOP,
	INCR RECNO
	LOKCTL = 1
	XCALL IO (3,INVTAG,RECNO,READ,LOKCTL)
	IF (INVTAG.EQ.']]]]]]]]]]') GOTO EOF
;;;	IF (TRECNO.EQ.0) GOTO RDLOOP
	IF (KMATTY.NE.'***'.AND.TDESCR(1,3).NE.KMATTY) GOTO RDLOOP
	IF (TDESCR(7,9).LT.KGAGE1.OR.TDESCR(7,9).GT.KGAGE2) GOTO RDLOOP
	IF (TDESCR(13,19).LT.KWID1.OR.TDESCR(13,19).GT.KWID2) GOTO RDLOOP
	LOKCTL = 1
	XCALL ISIO (2,INVMAS,TTAGNO,READ,LOKCTL)
	PLINE (1,6) = RMTAG,'ZZZZZX'
	PLINE (11,13) = RMMAT
	PLINE (14,16) = ' - '
	PLINE (17,19) = RMGA
	PLINE (20,22) = ' x '
	PLINE (23,29) = RMWID,'ZZZ.XXX'
	PLINE (36,39) = 'LBS'
	PLINE (44,51) = RMWGT,MASK
	PLINE (55,63) = RMCOST,DP4MSK
	EXTCST = RMWGT * RMCOST
	PLINE (67,78) = EXTCST,DP4MSK

	XCALL DATE8(RMDAT, D_OUT, D_OUTR, D_FMT, D_SW)
	PLINE (81,90) = D_FMT

	PLINE (95,129) = RMNOTE
	CALL PRINT
	INCR TAGCNT
	TOTQTY = TOTQTY + RMWGT
	TOTCST = TOTCST + EXTCST
	GOTO RDLOOP
EOF,
	XCALL LINFD (2)
	PLINE (1,7) = TAGCNT,MASK
	PLINE (11,36) = 'TAGS SELECTED - SUBTOTAL: '
	PLINE (41,51) = TOTQTY,MASK
	PLINE (64,78) = TOTCST,DP4MSK
	CALL PRINT
	PLINE (18,36) = 'AVERAGE UNIT COST: '
	IF (TOTQTY) PLINE (55,63) = (TOTCST/TOTQTY),DP4MSK
	CALL PRINT
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	GOTO SELECT
ENDOFF,
	CALL CLOSES
ABORT,
	XCALL PGCHN ('IM:IMMENU',1)
PRINT,
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,'NO HDR3',
&		LEG1,LEG2,'NO LEGEND',0,132,PRTCTL,1,LPSW,RPTNUM,PRTTYP)
	RETURN
DSP3DP,
	OPTION = 4
	GO TO CALDSP
DSPNUM,
	OPTION = 1
CALDSP,
	XCALL DSPLY (MAX,ROW,COL,DECMAL,OPTION,1)
	RETURN
INPUT,
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,1)
	RETURN
END
;--------- TAG ----------------    UNIT         QTY        UNIT        DOLLARS       DATE
;NUMBER    DESCRIPTION             MEAS      ON HND        COST        ON HAND     RECEIVED    NOTES
;XXXXXX-   XXXXXXXXXXXXXXXXXXXX    XXXX      XXXXXX-   XXXX.XXXX    XX,XXX.XXXX    XX/XX/XX    XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
;XXXXXX-   XXXXXXXXXXXXXXXXXXXX    XXXX      XXXXXX-   XXXX.XXXX    XX,XXX.XXXX    XX/XX/XX    XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
