;  LOCSTS / IM 
;
;
;		::PCPYIM.DEF::
;*****************************************************************************
;		INVENTORY MANAGEMENT  
;
;		RELEASED: AUGUST 1, 1984 (d70s10)
;*****************************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;		PROGRAM TO PRINT INVENTORY STOCK STATUS REPORT BY LOCATION
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD ITMMAS		;
		.INCLUDE 'DEF:RD041A.DEF'
RECORD DUMINV ,X		;
		.INCLUDE 'DEF:RD041B.DEF'
RECORD ITMIDX		;
		.INCLUDE 'DEF:RD042A.DEF'
RECORD TITLE
		,A31,	'STOCK STATUS REPORT BY LOCATION'
RECORD LEGND1
		,A19,	'   BACK ORDER CODE:'
		,A36,	'   NO = ITEM MAY NOT BE BACK ORDERED'
		,A41,	'   (BLANK MEANS ITEM MAY BE BACK ORDERED)'
RECORD LEGND2
		,A16,	'   STOCK STATUS:'
		,A34,	'   REO = AT OR BELOW REORDER LEVEL'
		,A21,	'   O/S = OUT OF STOCK'
RECORD HDR1
		,A58
		,A3,	'PRD'
		,A3
		,A3,	'QTY'
		,A5
		,A3,	'QTY'
		,A5
		,A3,	'QTY'
		,A3
		,A5,	'REORD'
		,A5
		,A7,	'AVERAGE'
		,A5
		,A8,	'VALUE OF'
		,A3
		,A5,	'BKORD'
		,A2
		,A3,	'STK'
RECORD HDR2
		,A8,	'LOCATION'
		,A4
		,A7,	'ITEM NO'
		,A7
		,A16,	'ITEM DESCRIPTION'
		,A16
		,A3,	'CAT'
		,A2
		,A5,	'ONHND'
		,A3
		,A5,	'ALLOC'
		,A3
		,A5,	'ONORD'
		,A2
		,A5,	'LEVEL'
		,A6
		,A4,	'COST'
		,A7
		,A9,	'INVENTORY'
		,A3
		,A4,	'CODE'
		,A2
		,A3,	'STS'
RECORD RCVMSG
	STRTLO	,A3
RECORD
	SPLFIL	,A14
	ITMCNT	,D5
	ITMVAL	,D9
	JUSTIF	,D1
	TOTVAL	,D10
	MASK1	,A10,	'ZZ,ZZZ.XXX'
	MASK2	,A14,	'ZZ,ZZZ,ZZZ.XX-'
	MASK3	,A7,	'ZZ,ZZX-'
	MASK6	,A8,	'ZZZ,ZZX-'
	LINCNT	,D2,	60
	PGCNT	,D3
	PLINE	,A132
	PRTCTL	,D3
	ENTRY	,A2
	INXCTL	,D1
	RECNO	,D5
	SWITCH	,D1
	CTR	,D2
	LOCTNS	,D2
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	V	,D1
	LPSW	,D2
	LPARG	,D1
	RPTNUM	,D3
	PRTTYP	,A1
	BLANKS	,A10
	MSGSWT	,D1
PROC (1)
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'PRINT STOCK STATUS REPORT BY LOCATION',V)
	MSGSWT = 1
	XCALL SNMSG (RCVMSG,MSGSWT)
	MSGSWT = 3
	XCALL SNMSG (BLANKS,MSGSWT)	;PURGE MESSAGE STACK
BEGIN,
	SWITCH = 1
	XCALL FILES (2,'I',42,SWITCH)		;FILE # 42 -- ITMIDX FILE
	IF (SWITCH.EQ.9) GO TO INU042
	SWITCH = 1
	XCALL FILES (1,'I',41,SWITCH)		;FILE # 41 -- ITMMAS FILE
	IF (SWITCH.EQ.9) GO TO INU041
	LOKCTL = 1
	XCALL IO (1,DUMINV,1,READ,LOKCTL)
	JUSTIF = JSTIFY
	LOCTNS = NUMLOC
	RECNO =
	SPLFIL(5,6) = 'CL'
	LPSW = 1
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO CLOSE
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
READ,
	INCR RECNO
	LOKCTL = 1
	XCALL IO (2,ITMIDX,RECNO,READ,LOKCTL)
	IF (IITMNO.EQ.']]]]]]') GO TO TOTAL
	IF (IRC041.EQ.0) GO TO READ
	LOKCTL = 1
	XCALL IO (1,ITMMAS,IRC041,READ,LOKCTL)
	CTR =
MULTLO,
	INCR CTR
	IF (CTR.GT.LOCTNS) GO TO READ
	IF (LOC(CTR).NE.STRTLO) GO TO MULTLO
	PLINE (4,5) = LOC(CTR)
	PLINE (10,24) = ITEMNO
	IF (JUSTIF) XCALL LEFTJ (PLINE(10,24),15)
	PLINE (27,56) = DESCR
	PLINE (59,60) = PRDCAT
	PLINE (62,69) = QTYONH(CTR), MASK6
	PLINE (70,77) = QTYCOM(CTR), MASK6
	PLINE (78,85) = QTYONO(CTR), MASK6
	PLINE (87,93) = REOLVL(CTR),MASK3
	PLINE (94,103) = AVGCST,MASK1
	IF (QTYONH(CTR).LT.0) QTYONH(CTR) = 0
	ITMVAL = (QTYONH(CTR)*AVGCST) #1
	TOTVAL = TOTVAL + ITMVAL
	PLINE (106,118) = ITMVAL, MASK2
	IF (BOCODE.EQ.1) PLINE (122,123) = 'NO'
	IF ((QTYONH(CTR)-QTYCOM(CTR)).LE.REOLVL(CTR)) PLINE (127,129) = 'REO'
	IF ((QTYONH(CTR)-QTYCOM(CTR)).LE.0) PLINE (127,129) = 'O/S'
	INCR ITMCNT
	CALL PRINT
	XCALL LINFD (1)
	INCR LINCNT
	GO TO READ
PRINT,
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,
&	'NO HDR3',LEGND1,LEGND2,'NO LEGEND3',0,132,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
TOTAL,
	PLINE (2,7) = ITMCNT, MASK3
	PLINE (8,12) = 'ITEMS'
	PLINE (92,103) = 'TOTAL VALUE:'
	PLINE (105,118) = TOTVAL, MASK2
	XCALL LINFD (1)
	CALL PRINT
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	XCALL FILES (1,'I',41,4)
	XCALL FILES (2,'I',42,4)
	XCALL PGCHN ('IM:PSTKST',1)
CLOSE,
	XCALL FILES (1,'I',41,4)
INU041,
	XCALL FILES (1,'I',42,4)
INU042,
	XCALL PGCHN ('IM:IMMENU',1)
END
