;  ADVLOC / IM 
;
;
;		::PCPYIM.DEF::
;*****************************************************************************
;		INVENTORY MANAGEMENT  
;
;		RELEASED: AUGUST 1, 1984 (d70s10)
;*****************************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;		PROGRAM TO PRINT REORDERING ADVICE REPORT BY LOCATION
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD ITMMAS		;
		.INCLUDE 'DEF:RD041A.DEF'
RECORD DUMINV ,X		;
		.INCLUDE 'DEF:RD041B.DEF'
RECORD	SELINV		;
		.INCLUDE 'DEF:RD056A.DEF'
RECORD	SPECS
	KVEND	,A4
	KLOC	,A2
	KPRCNT	,D2
	KNUMB	,D3
	VNDOPT	,D1
	AVFLAG	,D1
	MPFLAG	,A1
RECORD  LEGND1
		,A51,	'FOR ITEMS MANUFACTURED(M), PURCHASED(P), OR BOTH(B)'
		,A2
	MPBFLG	,A1
		,A5
	STAT	,A4
		,A1
		,A30,	'FROM REORDER LEVEL AT LOCATION'
		,A2
	LOCAT	,A3
		,A2
	LOCNOT	,A18
RECORD  LEGND2
		,A40,	'QUANTITY AVAILABLE INCLUDES ON-ORDER ?  '
	AVFLG	,A1
		,A65
		,A26,	'R = REORDER   O = STOCKOUT'
RECORD	HDR1
		,A53
		,A3,	'PRD'
		,A3
		,A3,	'QTY'
		,A6
		,A3,	'QTY'
		,A6
		,A3,	'QTY'
		,A4
		,A5,	'REORD'
		,A3
		,A5,	'RECOM'
		,A3
		,A5,	'ORDER'
		,A13
		,A5,	'TOTAL'
		,A1
		,A3,	'PUR'
RECORD	HDR2
		,A2
		,A3,	'LOC'
		,A3
		,A11,	'ITEM NUMBER'
		,A3
		,A16,	'ITEM DESCRIPTION'
		,A15
		,A3,	'CAT'
		,A2
		,A5,	'ONHND'
		,A4
		,A5,	'ALLOC'
		,A4
		,A5,	'ONORD'
		,A3
		,A5,	'LEVEL'
		,A3
		,A5,	'ORDER'
		,A3
		,A5,	'UP-TO'
		,A4
		,A6,	'WEIGHT'
		,A2
		,A6,	'WEIGHT'
		,A1
		,A2,	'UM'
		,A1
		,A5,	'REORD'
RECORD
	SPLFIL	,A14
RECORD
	V	,D1
	SWITCH	,D1,	1
	BLANKS	,A2,	'  '
	PTR	,D2,	01
	LOCTNS	,D2,	05		;LOCATIONS MAXIMUM ARRAY
	DEFALT	,A2
	LOCONH	,D6
	LOCCOM	,D6
	LOCONO	,D6
	LOCRCM	,D6
	LOCWGT	,D6
	SAVLOC	,A2
	TOTWT	,D6
	WGHT	,D6
	MASK	,A8,	'ZZZ,ZZX-'
	MASK2D	,A9,	'ZZ,ZZZ.XX'
	MASKT	,A9,	'ZZZZ,ZZX-'
	MASKP	,A8,	'ZZZZ,ZZX'
	LINCNT	,D2,	60
	PGCNT	,D3
	PLINE	,A132
	RECODR	,D6
	TITLE	,A36,	'REORDERING ADVICE REPORT BY LOCATION'
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	LPSW	,D2
	LPARG	,D1
	RPTNUM	,D3
	PRTTYP	,A1
	MSGSWT	,D1
	JUSTIF	,D1
PROC (1)
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'REORDERING ADVICE REPORT BY LOCATION',V)
BEGIN,
	SPLFIL(5,6) = 'CP'
	LPSW = 1
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO EXIT
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)

	SWITCH = 1
	XCALL FILES (1,'I',41,SWITCH)		;FILE #41 -- ITMMAS FILE
	IF (SWITCH.EQ.9) GO TO CLOSE

	SWITCH = 5
	XCALL FILES (3,'I',56,SWITCH)		;FILE #56 -- SELINV FILE
	IF (SWITCH.EQ.9) GO TO CLOSE1

	LOKCTL = 1
	XCALL IO (1,DUMINV,1,READ,LOKCTL)
	JUSTIF = JSTIFY
	LOCTNS = NUMLOC
	DEFALT = DFLTLO
	MSGSWT = 1
	XCALL SNMSG (SPECS,MSGSWT)
	IF (SWITCH.EQ.9) GO TO NONE
FLAGS,
	LOCAT = KLOC
	IF (KLOC.EQ.'  ') LOCAT = 'ALL'
	IF (KLOC.EQ.DEFALT) LOCNOT = '(DEFAULT LOCATION)'
	AVFLG = 'N'
	IF (AVFLAG.EQ.1) AVFLG = 'Y'
	MPBFLG = MPFLAG
STATS,
	IF (KPRCNT.NE.0) GO TO PCENT
	STAT = KNUMB,'-ZZX'
	GO TO NXTITM
PCENT,
	STAT(4,4) = '%'
	STAT(1,3) = KPRCNT,'-ZX'
NXTITM,
	LOKCTL = 1
	XCALL IOS (3,SELINV,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO END
	IF (SELINV.EQ.']]]]]]') GO TO END
	IF (SEL041.EQ.0) GO TO NXTITM
	LOKCTL = 1
	XCALL IO (1,ITMMAS,SEL041,READ,LOKCTL)
	IF (SAVLOC.NE.SRTLOC) CALL LOCTOT
	PTR = 0
NXTLOC,
	INCR PTR
	IF (LOC(PTR).EQ.BLANKS) GO TO NOFIND
	IF (LOC(PTR).EQ.SRTLOC) GO TO PRTLIN
	IF (KLOC.EQ.DEFALT.AND.LOC(PTR).EQ.DEFALT) GO TO PRTLIN
	IF (PTR.LT.LOCTNS) GO TO NXTLOC
NOFIND,
	XCALL MESAG ('LOCATION NOT FOUND',1)
	GO TO NXTITM
PRTLIN,
	PLINE (3,4) = LOC(PTR)
	PLINE (7,21) = ITEMNO
	IF (JUSTIF) XCALL LEFTJ (PLINE(7,21),15)
	PLINE (23,52) = DESCR
	PLINE (54,55) = PRDCAT
	PLINE (57,64) = QTYONH(PTR),MASK
	PLINE (66,73) = QTYCOM(PTR),MASK
	PLINE (75,82) = QTYONO(PTR),MASK
	PLINE (84,89) = REOLVL(PTR),MASKP
	RECODR = RECMIN
	IF (AVFLAG.EQ.2 .AND. REOLVL(PTR).GT.(QTYOH-QTYCM))
&	RECODR = RECMIN + REOLVL (PTR) - (QTYOH-QTYCM)
	IF (AVFLAG.EQ.1 .AND. REOLVL(PTR).GT.(QTYOH-QTYCM+QTYOO))
&	RECODR = RECMIN + REOLVL (PTR) - (QTYOH-QTYCM+QTYOO)
	PLINE (91,97) = RECODR, MASKP
	PLINE (100,105) = ORDUPT(PTR), MASKP
;
;
;	PSRAT IS SUBSCRIPTED TO USE THE FIRST FOUR DIGITS OF THE D8 FIELD
;	IN ANTICIPATION OF SUPPORTING 4 DECIMAL PLACES AT A LATER DATE.
;
;	PSRAT WILL BE INCORPORATED INTO THE PURCHASE ORDER PACKAGE
;
;;;	WGHT = (WEIGHT*PSRAT(1,4))
	WGHT = WEIGHT
	PLINE (108,115) = WGHT,MASK2D
;;;	TOTWT = (WEIGHT*PSRAT(1,4)*RECODR) #2
	TOTWT = (WEIGHT*RECODR) #2		;TOTAL WEIGHT IS FOR THE
	PLINE (117,123) = TOTWT, MASKP		;   RECOMMENDED ORDER
	PLINE (125,126) = PUOFM
	PLINE (129,129) = R
	PLINE (130,130) = O
	CALL PRINT
	IF (LOC(PTR).EQ.DEFALT) CALL ITMTOT
	XCALL LINFD (1)
	INCR LINCNT
	LOCONH = LOCONH + QTYOH
	LOCCOM = LOCCOM + QTYCM
	LOCONO = LOCONO + QTYOO
	LOCRCM = LOCRCM + RECODR
	LOCWGT = LOCWGT + TOTWT
	PTR = 1
	GO TO NXTITM
ITMTOT,					;ONLY USED TO PRINT DEFAULT LOC TOTALS
	PLINE (7,33) = 'ITEM TOTAL -- ALL LOCATIONS'
	PLINE (56,64) = QTYOH,MASKT
	PLINE (65,73) = QTYCM,MASKT
	PLINE (75,82) = QTYOO,MASKT
	CALL PRINT
	RETURN
LOCTOT,
	IF (SAVLOC.EQ.'  ') SAVLOC = SRTLOC
	IF (SAVLOC.EQ.SRTLOC) RETURN
	XCALL LINFD (1)
	INCR LINCNT
	PLINE (61,68) = 'LOCATION'
	PLINE (70,71) = SAVLOC
	PLINE (73,79) = 'TOTALS:'
	PLINE (91,97) = LOCRCM,MASKP
	PLINE (117,123) = LOCWGT,MASKP
	CALL PRINT
	SAVLOC = SRTLOC
	LOCONH =
	LOCCOM =
	LOCONO =
	LOCRCM =
	LOCWGT =
	XCALL LINFD (3)
	LINCNT = LINCNT + 3
	RETURN
PRINT,
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,
&	'NO HDR',LEGND1,LEGND2,'NO LEGEND',0,1,132,1,LPSW,RPTNUM,PRTTYP)
	RETURN
END,
	CALL LOCTOT
	XCALL FILES (1,'I',41,4)
	SWITCH = 7
	XCALL FILES (3,'I',56,SWITCH)	;DELETE SELINV FILE WHEN DONE
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	MSGSWT = 4
	XCALL SNMSG (BLANKS,MSGSWT)
	XCALL PGCHN ('IM:ADVICE',1)
NONE,
	XCALL MESAG ('DATA SPECS NOT RECEIVED',1)
	XCALL FILES (3,'I',56,4)
CLOSE1,
	XCALL FILES (1,'I',41,4)
CLOSE,
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	XCALL PGCHN ('IM:IMMENU',1)
EXIT,
	XCALL FILES (3,'I',56,4)
	XCALL PGCHN ('IM:ADVICE',1)
END
