;  CYCSEL / IM 
;
;		::PCPYIM.DEF::
;*****************************************************************************
;		INVENTORY MANAGEMENT  
;
;		RELEASED: AUGUST 1, 1984 (d70s10)
;*****************************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;		INPUTS CRITERIA, GENERATES INDEX FOR CYCLE COUNT WORKSHEET
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD CYCIDX
		.INCLUDE 'DEF:RD130A.DEF'
RECORD ITMMAS
		.INCLUDE 'DEF:RD041A.DEF'
RECORD INVCTL
		.INCLUDE 'DEF:RD041B.DEF'
RECORD ITMIDX
		.INCLUDE 'DEF:RD042A.DEF'
RECORD
		.INCLUDE 'DEF:RD130S.DEF'
RECORD SNDMSG
		,A3	,'IM:'
	FILNAM	,A6,	'CYCRPT'
	RCNT	,D5,	00001
	OCNT	,D5,	00002
RECORD NXTMSG
	MSGPRG	,A10
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD MESSAG
	TSTCDE	,A1		; CYCLE COUNT CODE -- <SPACE> MEANS ALL
	LCNTDT	,D6
	ONHFLG	,A1		; 'N' MEANS DON'T SHOW ON-HAND ON REPORT
RECORD
	V	,D1
	ENTRY	,A8
	INXCTL	,D1
	LCTDAT	,D6
	TCNTDT	,D6		; YYMMDD VERSION OF LAST COUNT DATE IN ITMMAS
	TCNTCD	,D1		; CYCLE COUNT CODE REQ'D
	ZNGFLG	,D1		; 1 IF ZERO OR NEGATIVE ON HAND ITEMS ONLY.
	CNGCTL	,D1
	WHATNO	,D1
	SIZE	,D5
	SWITCH	,D1
	LOCSBC	,D2
	ALL	,D1
	ALPHA	,A8
	TODAY	,D6
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
PROC (1)
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'PRINT CYCLE COUNT WORKSHEET',V)
	XCALL OUTPT (2,1,0,'SELECT CRITERIA',1)
; --------------------- OPEN, PROTECT FILES -------------------------------
OPNFIL,
	SWITCH = 3
	XCALL FILES (2,'I',41,SWITCH)		; FILE # 41 - ITEM MASTER
	IF (SWITCH.EQ.9) GO TO INU041
	SWITCH = 5
	XCALL FILES (2,'I',41,SWITCH)
	IF (SWITCH.EQ.9) GO TO CLOSE1
OPNIDX,
	SWITCH = 3
	XCALL FILES (1,'O',130,SWITCH)		; FILE # 130 CYCLE COUNT INDEX
	IF (SWITCH.EQ.9) GO TO CLOSE1
	LOKCTL = 1
	XCALL IOS (2,INVCTL,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO CLOSE1
	SIZE = (((REC041+1)*(SIZ130+2)+2)/512) + 1
	XCALL OFILE (1,130,SIZE,SIZ130,SWITCH)
	IF (SWITCH.NE.1) GO TO DISPLA
CLOSE,
	XCALL FILES (1,'O',130,4)
CLOSE1,
	XCALL FILES (2,'I',41,4)
INU041,
	XCALL PGCHN ('IM:IMMENU',1)
; ------------------------ INPUT, CHECK REPORT CRITERIA -------------------
DISPLA,
	XCALL OUTPT (3,1,2,'\',1)
	XCALL OUTPT (4,15,0,'PLEASE ENTER CRITERIA:',V)
	XCALL OUTPT (5,19,0,'1. LAST COUNTED ON OR BEFORE',V)
	XCALL OUTPT (6,19,0,'2. WITH CYCLE COUNT CODE OF',V)
	XCALL OUTPT (7,19,0,'3. ZERO OR NEGATIVE ON-HAND ONLY ?',V)
	XCALL OUTPT (8,19,0,'4. SHOW COMPUTED QUANTITY-ON-HAND ?',V)
INPLCD,
	CTL = '05,56,06,00,DE'
	CALL INPUT
	GO TO (DISPLA, ABTRUN), INXCTL
	IF (ENTRY(1,6).EQ.'      ') CALL GETDAT	;DEFAULT TO SYSTEM DATE
						;TO GET ALL ON REPORT
	LCTDAT = ENTRY(1,6)
	ENTRY(1,6) = LCTDAT, 'XXXXXX'		;CHANGE TO YYMMDD FORMAT
	LCTDAT(1,2) = ENTRY(5,6)
	LCTDAT(3,6) = ENTRY(1,4)
	GO TO (ANYCNG), CNGCTL
INPCLS,
	ALL =
	CTL = '06,56,01,00,#E'
	CALL INPUT
	GO TO (DISPLA, ABTRUN), INXCTL
	TCNTCD = ENTRY(1,1)
	IF (ENTRY(1,1).NE.' ') GO TO NOBLNC
	XCALL OUTPT (6,56,0,'ALL',V)
	ALL = 1
NOBLNC,
	GO TO (ANYCNG), CNGCTL
INPZNG,
	CTL = '07,56,01,00,YN'
	CALL INPUT
	IF (INXCTL.EQ.1) ZNGFLG = 1
	GO TO (ANYCNG), CNGCTL
INPONH,
	CTL = '08,56,01,00,YN'
	CALL INPUT
	ONHFLG = ENTRY (1,1)
	GO TO ANYCNG
GETDAT,
	XCALL RDATE (TODAY)
	IF (TODAY.EQ.0) GO TO GETDAT
	ALPHA = TODAY, 'XX/XX/XX'
	XCALL OUTPT (5,56,0,ALPHA,V)
	ENTRY(1,6) = TODAY
	RETURN
INPUT,
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,V)
	RETURN
ANYCNG,
	CNGCTL = 6
ANCN2,
	XCALL ANYCN (CNGCTL,WHATNO)
	GO TO (ABTRUN,PROCES), CNGCTL + 2
	GO TO (INPLCD,INPCLS,INPZNG,INPONH), WHATNO
	GO TO ANYCNG
ABTRUN,
	XCALL FILES (2,'I',41,4)
	SWITCH = 7
	XCALL FILES (1,'O',130,SWITCH)
	XCALL PGCHN ('IM:IMMENU',1)
; ---------------------- BUILD CYCLE COUNT INDEX ----------------------------
PROCES,
	XCALL WATE (4,V)
READI1,
	TSTCDE = TCNTCD
	IF (ALL.EQ.1) TSTCDE =
	ENTRY(1,6) = LCTDAT, 'XXXXXX'	; BACK TO MMDDYY FORMAT FOR THE REPORT
	LCNTDT(5,6) = ENTRY(1,2)
	LCNTDT(1,4) = ENTRY(3,6)
	CYCIDX =
	LOKCTL = 1
	XCALL IOS (1,CYCIDX,WRITE,LOKCTL)	; CONTROL RECORD
	IF (LOKCTL.EQ.3) GO TO ABTRUN
	CYC041 = 1
IDXLOP,
	INCR CYC041
	LOKCTL = 1
	XCALL IOS (2,ITMMAS,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO LASTRD
	IF (ITMMAS(1,6).EQ.']]]]]]') GO TO LASTRD
	IF (DESCR(1,6).EQ.']]]DEL') GO TO IDXLOP
	IF (OBSFLG.NE.'A') GO TO IDXLOP
	IF ((ALL.NE.1).AND.(TCNTCD.NE.CYCTCD)) GO TO IDXLOP
	IF ((ZNGFLG.EQ.1).AND.(QTYONH.GT.0)) GO TO IDXLOP
	ENTRY(1,6) = LSTCNT, 'XXXXXX'			;GET YYMMDD FORMAT
	TCNTDT(1,2) = ENTRY(5,6) 			;FOR COMPARISON
	TCNTDT(3,6) = ENTRY(1,4)
	IF (TCNTDT.GT.LCTDAT) GO TO IDXLOP
	LOCSBC =
BINLOC,
	INCR LOCSBC
	IF (LOCSBC.GT.NUMLOC) GO TO IDXLOP
	IF (LOC(LOCSBC).EQ.DFLTLO) GO TO LOCEND
	GO TO BINLOC
LOCEND,
	BINNUM = PIKSEQ(LOCSBC)
	INCR RCNT
	LOKCTL = 1
	XCALL IOS (1,CYCIDX,WRITE,LOKCTL)
	IF (LOKCTL.EQ.3) GO TO ABTRUN
	GO TO IDXLOP
LASTRD,
	LOKCTL = 1
	XCALL IOS (1,']]]]]]]]',WRITE,LOKCTL)
	IF (LOKCTL.EQ.3) GO TO ABTRUN
	CLOSE 1
	SWITCH = 5
	XCALL FILES (1,'I',130,SWITCH)
	LOKCTL = 1
	XCALL IO (1,CYCIDX,2,READ,LOKCTL)
	IF (CYCIDX.EQ.']]]]]]]]') GO TO NOITEM
	CLOSE 1
	CLOSE 2
	MSGPRG = MESSAG
	SWITCH = 5
	XCALL SNMSG (SNDMSG,SWITCH)
	SWITCH = 2
	XCALL SNMSG (NXTMSG,SWITCH)
	XCALL PGCHN ('IM:SRTCYC',0)
NOITEM,
	XCALL MESAG ('NO ITEMS ON FILE MATCHING CRITERIA',2)
	SWITCH = 7
	XCALL FILES (1,'I',130,SWITCH)			;DELETE CYCIDX FILE
	XCALL FILES (2,'I',41,4)			;CLOSE ITEM MASTER
	GO TO OPNFIL					;START AGAIN

END
