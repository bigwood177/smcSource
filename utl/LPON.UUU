SUBROUTINE LPON; (LPSW,SPLFIL)
;
;   LPON / UTL 
;
;
;
;		SUBROUTINE TO OPEN OUTPUT DEVICE FOR REPORT PRINT/DISPLAY
;
			; VALUE OF "LPSW" INCOMING
			; ========================
			; 1 = OUTPUT MAY BE PRINTED, SPOOLED OR DISPLAYED
			; 2 = OUTPUT MAY BE PRINTED OR SPOOLED, BUT NOT 
			;     DISPLAYED
			; 3 = OUTPUT MAY BE PRINTED ONLY
			; 4 = OUTPUT MAY BE AUTO-SPOOLED
			; 5 = OUTPUT IS SPECIAL FORMS-SEND TO NON-QUEUED
			;	DEVICE (COULD BE LOCAL PRINTER)
			;
			; VALUE OF "LPSW" OUTGOING
			; ========================
			; 0 = THE DEVICE IS NOT READY OR USER WANTS TO END
			; 1 = OUTPUT IS TO BE PRINTED USING A SYSTEM 
			;     PRINTER (1 - 4)
			; 2 = OUTPUT IS TO BE SPOOLED
			; 3 = OUTPUT IS TO BE PRINTED LOCALLY
			; 4 = OUTPUT IS TO BE DISPLAYED
			; 5 = OUTPUT IS PRINTED LOCALLY WITH NO FORM FEED 
			;     AT END
			; 6 = OUTPUT IS GOING TO NON-QUEUED DEVICE
			;
			; VALUE OF "TERMID" INDICATES TERMINAL TYPE
			; ========================================
			; 0 = NO TERMINAL
			; 1 = VT52 WITH NO OPTIONS
			; 2 = VT52 WITH PRINTER PORT OPTION
			; 3 = VT100 WITH NO OPTIONS - VT52 MODE
			; 4 = VT100 WITH ADVANCED VIDEO OPTION - VT52 MODE
			; 5 = VT100 WITH PRINTER PORT OPTION - VT52 MODE
			; 6 = VT100 WITH NO OPTIONS - ANSI MODE
			; 7 = VT100 WITH ADVANCED VIDEO OPTION - ANSI MODE
			; 8 = VT100 WITH PRINTER PORT OPTION - ANSI MODE
			;
	LPSW	,D
	SPLFIL	,A
COMMON
			.INCLUDE 'DEF:COM001.DEF'
RECORD MESARA
			.INCLUDE 'DEF:MES001.DEF'
RECORD CONAM2
			.INCLUDE 'DEF:RD099B.DEF'
RECORD DEVICE
			.INCLUDE 'DEF:DEV001.DEF'
RECORD FILBL
	FILDEV	,A3
		,A1,	':'
	FILCOD	,A2
	FILTRM	,A2
	FILSEQ	,A2
		,A1,	'.'
	FILCMP	,A3
		,A1,	'['
	SIZE	,D5
		,A1,	']'
RECORD FILB1, X
		,A14
RECORD,X
		,A4
	NAME	,A6
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A4
		,A1
	FCTYPE	,A6
RECORD AERR
	ERR	,D3
RECORD
	BLANK	,A3
	CNGCTL	,D1
	CNT1	,D2
	CNT2	,D2
	LOCAL	,D1
	PTRDEV	,A5
	RPDEST	,D2
	ENTRY	,A4
	ERL	,D5
	FILSPC	,A21
	FILSP1	,A14
	INXCTL	,D1
	J	,D2
	MASK	,A3,'XXX'
	MESAGE	,A60
	PRNTRS	,7A5,	'LP:','LQ:','LR:','LS:','LPA0:','LPA1:','LPA2:'
	PRTNUM	,D1
	SUB	,D1
	SVLPSW	,D1
	WHATNO	,D1
PROC
	ERR =
	SVLPSW = LPSW
	FILCOD = SPLFIL (5,6)
	OPEN (11,U,'UT:MESARA.DDF')
	ON ERROR RDERR
RDERR,
	READ (11,MESARA,TNMBR+1)
	OFF ERROR
	PRTNUM = DEFPTR
	CURPTR = DEFPTR
	SIZE = SPLSZE
	DELRPT = 'Y'
	NUMCPY = 1
	QUEDPT = 0
	LPSW = 1		;DEFAULT VALUE OF LPSW
	IF (SVLPSW.EQ.4 .AND. SPOOLS.EQ.2) GO TO SPOOL
	IF (CMPCOD.NE.BLANK) CALL GETPTT
	LOCAL =
	IF (TERMID.EQ.2 .OR. TERMID.EQ.5 .OR. TERMID.EQ.8) LOCAL = 1
BEGIN,
	QUEDPT = 0
	IF (SVLPSW.EQ.5) GO TO SPCFRM
	XCALL OUTPT (3,1,2,'\')
	XCALL OUTPT (9,20,0,'REPORT DESTINATION')
	XCALL OUTPT (10,25,0,'1. LP')
	XCALL OUTPT (11,25,0,'2. LQ')
	XCALL OUTPT (12,25,0,'3. LR')
	XCALL OUTPT (13,25,0,'4. LS')
	XCALL OUTPT (14,25,0,'5. LPA0')
	XCALL OUTPT (15,25,0,'6. LPA1')
	XCALL OUTPT (16,25,0,'7. LPA2')
	XCALL OUTPT (17,25,0,'8. SPOOL FILE')
	XCALL OUTPT (18,25,0,'9. VIDEO TERMINAL')
	XCALL OUTPT (19,24,0,'10. LOCAL PRINTER')
	IF (DEFPTR.EQ.0) GO TO REPDES
	IF (PRTTYP(DEFPTR).EQ.'X') GO TO REPDES
	XCALL DSPLY (9,44,1,DEFPTR,1,0)
	RPDEST = DEFPTR
	IF (SVLPSW.EQ.3 .OR. QUEPTR(RPDEST).NE.'Y') GO TO ANYCN1
	QUEDPT = 1
	XCALL OUTPT (18,20,0,'NUMBER OF COPIES')
	XCALL OUTPT (18,44,0,'1')
	XCALL OUTPT (20,20,0,'DELETE AFTER PRINTING?')
	XCALL OUTPT (20,44,0,'Y')
ANYCN1,
	CNGCTL = 6
	XCALL ANYCN (CNGCTL,WHATNO,0,0,0,0)
	GO TO (END,PROCES,REPDES), CNGCTL + 2
REPDES,
	ENTRY = RPDEST
	CTL = '09,44,01,01,N 00,E  '
	CALL INPUT
	GO TO (BEGIN,END), INXCTL
	RPDEST = ENTRY
	IF (RPDEST.LE.0 .OR. RPDEST.GE.8) GO TO REPDES
	CALL TRMTST
	IF (RPDEST.EQ.0) GO TO REPDES
	IF (RPDEST.GT.7) GO TO (SPOOL,DISPLA,LOCAL), RPDEST - 7
	IF (PRTTYP(RPDEST).EQ.'X') GO TO INVPTR
	PRTNUM = RPDEST
	CURPTR = RPDEST
	LPSW = 1
	QUEDPT = 0
	IF (QUEPTR(RPDEST).EQ.'Y') QUEDPT = 1
	IF (SVLPSW.NE.3 .AND. QUEPTR(RPDEST).EQ.'Y') GO TO NUMCPY
	XCALL OUTPT (18,1,1,'\')
	XCALL OUTPT (20,1,1,'\')
	GO TO ANYCN1
NUMCPY,
	XCALL OUTPT (18,20,0,'NUMBER OF COPIES')
	XCALL OUTPT (20,20,0,'DELETE AFTER PRINTING?')
	CTL = '18,44,01,00,# 00,  '
	ENTRY = 1
	CALL INPUT
	GO TO (BEGIN), INXCTL
	NUMCPY = ENTRY
	IF (NUMCPY.LE.0) GO TO NUMCPY
DELFIL,
	CTL = '20,44,01,00,YY  '
	CALL INPUT
	DELRPT = 'Y'
	IF (INXCTL.EQ.2) DELRPT = 'N'
	IF (DELRPT.NE.'Y' .AND. DELRPT.NE.'N') GO TO DELFIL
	GO TO ANYCN1
TRMTST,
	IF (RPDEST.EQ.8 .AND. SVLPSW.EQ.3) GO TO BADPRT
	IF (RPDEST.EQ.9 .AND. SVLPSW.GT.1) GO TO BADPRT
	IF (RPDEST.EQ.8 .AND. SPOOLS.LE.0) RPDEST = 0
	IF (RPDEST.EQ.9 .AND. (TERMID.LT.3.OR.TERMID.GT.8)) RPDEST = 0
	IF (RPDEST.EQ.10 .AND. LOCAL.EQ.0) RPDEST = 0
	IF (RPDEST.EQ.0) XCALL MESAG
&	('?UT-REPORT DESTINATION INVALID FOR THIS TERMINAL',1)
	RETURN
GETPTT,
	ON ERROR GETPTT
	READ (14,DEVICE,99)
	UNLOCK 14
	FILDEV = DEVNAM (CNPOS)
	NAME = 'CONAME'
	FILCMP = CMNME
	CALL SQUEZ
	ON ERROR NOCMP
	CLOSE 13
	OPEN (13,I,FILSP1)			;CONAME MANUALLY OPENED TO
						;AVOID USE OF FILES SUBROUTINE
	READ (13,CONAM2,2)
	CLOSE 13
NOCMP,
	NAME =
	FILCOD = SPLFIL (5,6)
	OFF ERROR
	RETURN
BADPRT,
	RPDEST = 0
	XCALL MESAG ('?UT-REPORT DESTINATION INVALID FOR THIS REPORT',1)
	RETURN
INVPTR,
	RPDEST = 0
	XCALL MESAG 
&		('?UT-INVALID PRINTER SELECTION - PRINTER NOT INSTALLED',1)
	GO TO BEGIN
BADDEV,
	OFF ERROR
	XCALL ERROR (ERR,ERL)
	IF (ERR.EQ.17) XCALL MESAG ('?UT-ERROR - INVALID PRINTER',1)
	IF (ERR.EQ.18) XCALL MESAG ('?UT-ERROR - DEVICE NOT AVAILABLE',1)
	IF (ERR.NE.17 .AND. ERR.NE.18) GO TO BADDV1
SPCFRM,
	XCALL OUTPT (3,1,2,'\')
	XCALL OUTPT (10,20,2,'SPECIAL FORMS REPORT DESTINATION')
	XCALL OUTPT (11,25,0,'1. LP')
	XCALL OUTPT (12,25,0,'2. LQ')
	XCALL OUTPT (13,25,0,'3. LR')
	XCALL OUTPT (14,25,0,'4. LS')
	XCALL OUTPT (15,25,0,'5. LPA0')
	XCALL OUTPT (16,25,0,'6. LPA1')
	XCALL OUTPT (17,25,0,'7. LPA2')

;;;	IF (LOCAL) XCALL OUTPT (15,25,0,'5. LOCAL PRINTER')
	CTL = '10,54,01,01,N 00,FE '
	CALL INPUT
	GO TO (SPCFRM,END), INXCTL
	RPDEST = ENTRY
;;;	IF (RPDEST.LT.1 .OR. RPDEST.GT.5) GO TO SPCFRM
;;;	IF (LOCAL.EQ.0 .AND. RPDEST.EQ.5) GO TO SPCFRM
;;;	IF (RPDEST.EQ.5) GO TO LOCAL
;;;	IF (QUEPTR(RPDEST).EQ.'Y') GO TO NOTQUE
;;;	IF (PRTTYP(RPDEST).EQ.'X') GO TO INVPTR
	ON ERROR BADDEV
	CLOSE 13
	OPEN (13,O,PRNTRS(RPDEST))
	OFF ERROR
	LPSW = 6
	CURPTR = 0
	GO TO RETURN
NOTQUE,
	XCALL MESAG ('?UT-CANNOT PRINT SPECIAL FORMS ON QUEUED PRINTER',1)
	GO TO SPCFRM
OFFLIN,
	OFF ERROR
	XCALL ERROR (ERR,ERL)
	IF (ERR.EQ.18) XCALL MESAG ('?UT-PRINTER OFF LINE OR UNAVAILABLE',1)
	IF (ERR.NE.18) GO TO BADDV1
	GO TO BEGIN
BADDV1,
	MESAGE = '?UT-ERROR #     OCCURRED WHILE ATTEMPTING TO OPEN PRINTER'
	MESAGE (13,15) = AERR
	XCALL MESAG (MESAGE,1)
	GO TO BEGIN
PROCES,
	IF (QUEPTR(RPDEST).EQ.'Y') GO TO SPOOL
	ON ERROR OFFLIN
	CLOSE 13
	OPEN (13,O,PRNTRS(RPDEST))
	DISPLAY (13,13)
	OFF ERROR
	GO TO RETURN
SPOOL,
	XCALL OUTPT (24,1,1,'PLEASE WAIT ...')
	FILDEV = SPOLDV
	FILTRM = TNMBR - 1,MASK
	FILCMP = CMPCOD
	ON ERROR GOOD
	CLOSE 13
	ERR = 1
	J =
OPENSP,
	INCR J
	IF (J.GT.20) GO TO SPLERR
	FILSEQ = J, MASK
	CALL SQUEZ
	OPEN (13,I,FILSP1)
	CLOSE 13
	GO TO OPENSP
GOOD,
	OFF ERROR
	CLOSE 13
	XCALL ERROR (ERR,ERL)
	IF (ERR.NE.18) GO TO SPLERR
	ON ERROR SPLERR
	ERR = 2
	CALL SQUEZ
	OPEN (13,O,FILSPC)
;;;	IF (RPDEST.LE.4 .AND. RPDEST.GE.1) LPSW = 1
;;;	IF ((SVLPSW.EQ.4.AND.SPOOLS.EQ.2) .OR. RPDEST.EQ.5) LPSW = 2
	IF (RPDEST.LE.7 .AND. RPDEST.GE.1) LPSW = 1
	IF ((SVLPSW.EQ.8.AND.SPOOLS.EQ.2) .OR. RPDEST.EQ.9) LPSW = 2
					;SPOOLED AND NOT PRINTED
	IF (LPSW.EQ.2) CURPTR = 0	;SCAN OFF SPACES FOR SPOOLING
	SPLFIL = FILB1			;SEND UNSQUEEZED FILESPEC TO LPOFF
	GO TO RETURN
DISPLA,
	CLOSE 13
	OPEN (13,I,'TT:')
	LPSW = 4
	GO TO RETURN
LOCAL,
	CLOSE 13
	OPEN (13,I,'TT:')
	CURPTR = 0
	LPSW = 3
	IF (SVLPSW.EQ.5) LPSW = 5	;NO FORM FEED AFTER PRINTING
	GO TO RETURN

;***********************SQUEEZE SPACES OUT OF FILE SPECIFICATION**************

SQUEZ,
	FILSPC =
	FILSP1 =
	CNT1 =
	CNT2 =
SQZLUP,
	IF (CNT1.GE.21) RETURN
	INCR CNT1
	IF (FILBL(CNT1,CNT1).EQ.BLANK) GO TO SQZLUP
	INCR CNT2
	FILSPC (CNT2,CNT2) = FILBL (CNT1,CNT1)
	IF (CNT1.LE.14) FILSP1 (CNT2,CNT2) = FILBL (CNT1,CNT1)
	GO TO SQZLUP
;*****************************************************************************
RETURN,
	OFF ERROR
	WRITE (11,MESARA,TNMBR+1)
	CLOSE 11
	XCALL OUTPT (3,1,2,'\')
	RETURN
END,
	LPSW = 0
	GO TO RETURN
INPUT,
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,FCTYPE,ENTRY,INXCTL)
	RETURN
SPLERR,
	OFF ERROR
	MESAGE = '?UT-SPOOLER ERROR '
	IF (ERR.EQ.1)
&		MESAGE (19,57) = '- TOO MANY SPOOLED REPORTS OF THIS TYPE'
	IF (ERR.EQ.2)
&		MESAGE (19,58) = '- CANNOT OPEN SPOOL FILE ON SPOOL DEVICE'
	IF (ERR.NE.2 .AND. ERR.NE.1) 
&		MESAGE (19,41) = '- #     ON OPENING FILE'
	IF (ERR.NE.2 .AND. ERR.NE.1) MESAGE (23,25) = ERR,'ZZX'
	XCALL MESAG (MESAGE,1)
	GO TO BEGIN
END
