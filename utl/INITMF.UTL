;  INITMF  /  UTL 
;
;
;		::PCPYUTL.DEF::
;*****************************************************************************
;		SECURITY SYSTEM UTILITIES AND SUBROUTINES 
;		DIBOL FOR RT-11
;		
;		RELEASED: JANUARY 1, 1985
;*****************************************************************************
;
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984,
;		1985, MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		PROGRAM TO INITIALIZE THE "MESARA" FILE PRIOR
;		TO RUNNING ANY APPLICATIONS.
;
;		THIS PROGRAM USES "STREAM" I/O TO THE TERMINAL SO IT
;		MAY INITIALIZE A FILE WITH ANY TERMINAL CONFIGURATION.
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD FILE
	DEV	,A3
		,A1,	':'
		,A10,	'MESARA.DDF'
		,A1,	'['
	SIZE	,D3
		,A1,	']'
RECORD MESARA
			.INCLUDE 'DEF:MES001.DEF'
RECORD DUMMES
			.INCLUDE 'DEF:MES002.DEF'
RECORD
		,A29,	'1 = DEC VT52                 '
		,A29,	'2 = DEC VT52  with printer   '
		,A29,	'3 = DEC VT100 (in VT52 mode) '
		,A29,	'4 = DEC VT100 (in VT52 mode  '
		,A29,	'     with AVO)               '
		,A29,	'5 = DEC VT100 (in VT52 mode  '
		,A29,	'    with AVO & local printer)'
		,A29,	'6 = DEC VT100 (in ANSI mode) '
		,A29,	'7 = DEC VT100 (in ANSI mode  '
		,A29,	'     with AVO)               '
		,A29,	'8 = DEC VT100 (in ANSI mode  '
		,A29,	'    with AVO & local printer)'
	TXTLMT	,D2	,12
RECORD,X
	TEXT	,12A29
RECORD
	ENTRY	,A10
	BLANKS	,A10
	ERR	,D3
	ERL	,D4
	ERROR	,D3
	SUB	,D3
	PTR	,D3
PROC
	OPEN (15,I,'TT:')

;****************************************
; ACCEPT DEVICE NAME FOR INITIALIZATION

BEGIN,
	XCALL FLAGS (01000100)		;SUPPRESS "STOP" AND ENABLE FILE PROT.
	DISPLAY (15,13,10,'Initialization of Message File "MESARA.DDF"')
GETDEV,
	DISPLAY (15,13,10,
&	 'On what device do you wish to initialize the file? [UT] ')
	READS (15,ENTRY)
	IF (ENTRY(1,3).EQ.BLANKS) GO TO USEUT
	IF (ENTRY(4,10).NE.BLANKS) GO TO TOOMNY
	SUB = 1
	IF (ENTRY(SUB,SUB).NE.BLANKS) GO TO DEV1
TOOMNY,
	DISPLAY (15,13,10,'?UT-PLEASE ENTER 3 CHARACTERS OR LESS FOR DEVICE',7)
	GO TO GETDEV
DEV1,
	IF (ENTRY(SUB+1,SUB+1).EQ.BLANKS .OR. ENTRY(SUB+1,SUB+1).EQ.':')
&		GO TO DEVIC
	IF (SUB.GE.3) GO TO DEVIC
	INCR SUB
	GO TO DEV1

USEUT,
	DEV = ' UT'
	GO TO GETSIZ
					; CONSTRUCT FILE SPECIFICATION
DEVIC,
	PTR = 1
	IF (SUB.LT.3) PTR = 2
	IF (SUB.EQ.1) PTR = 3
	DEV (PTR,3) = ENTRY (1,SUB)

GETSIZ,
	DISPLAY (15,13,10,
& 'What is the maximum number of terminals you will use on the system? [16] ')
	READS (15,ENTRY)
	IF (ENTRY(4,4).NE.BLANKS) GO TO TOOBIG
	IF (ENTRY(1,3).EQ.BLANKS) ENTRY = '16'
	ON ERROR NOTNUM
	SIZE = ENTRY(1,3)
	IF (SIZE.LE.0) GO TO ONEMIN
	INCR SIZE
	IF (SIZE.GE.100) GO TO TOOBIG
	MAXTER = SIZE			;ESTABLISH NUMBER OF RECORDS AND STORE
					;IN FIRST MESARA RECORD
	INCR SIZE
	OFF ERROR
	GO TO DEFTRM
NOTNUM,
	DISPLAY (15,13,10,'?UT-MUST BE NUMERIC',7)
	GO TO GETSIZ
ONEMIN,
	DISPLAY (15,13,10,'?UT-MUST BE AT LEAST ONE TERMINAL',7)
	GO TO GETSIZ
TOOBIG,
	DISPLAY (15,13,10,'?UT-MUST BE LESS THAN 99',7)
	GO TO GETSIZ
;****************************************
; DEFAULT TERMINAL ASSIGNMENT

DEFTRM,
	DISPLAY (15,13,10,10,
& 'The following questions deal with default values which will be placed',13,10,
& 'in each MESARA record. These values may later be changed for each',13,10,
& 'terminal via MESSAGE FILE MAINTENANCE.')
	DISPLAY (15,13,10,10,
&	 'Please select the default terminal type. The following',13,10,
&	'terminal types are valid:',
&	 13,10,13,10)
	SUB =
TXTLUP,
	INCR SUB
	IF (SUB.GT.TXTLMT) GO TO TTYTYP
	DISPLAY (15,'      ',TEXT(SUB),13,10)
	GO TO TXTLUP
TTYTYP,
	DISPLAY (15,13,10,
&	 '   Please enter default terminal type [6] ')
	ENTRY =
	READS (15,ENTRY)
	IF (ENTRY(2,10).NE.BLANKS) GO TO TTYTYP
	IF (ENTRY.EQ.BLANKS) ENTRY(1,1) = '6'
	ON ERROR BADTER
	TERMID = ENTRY
	IF (TERMID.LT.1 .OR. TERMID.GT.8) GO TO TTYTYP
	OFF ERROR
	GO TO CHAIN
BADTER,
	DISPLAY (15,13,10,'?UT-MUST BE NUMERIC',7)
	GO TO TTYTYP
;****************************************
CHAIN,
	DISPLAY (15,13,10,10,'DISPLAY "Chain to ..." MESSAGE? [N]')
	READS (15,ENTRY)
	IF (ENTRY(1,1).EQ.BLANKS) ENTRY (1,1) = 'N'
	IF (ENTRY(1,1).NE.'Y' .AND. ENTRY(1,1).NE.'N') GO TO CHAIN
	DEVLOP =
	IF (ENTRY(1,1).EQ.'Y') DEVLOP = 1

;****************************************
CTRLC,
	DISPLAY (15,13,10,10,
& 'Users may optionally be prevented from aborting program execution',13,10,
& 'by trapping the use of CONTROL/C (^C).')
CTCQ,
	DISPLAY (15,13,10,10,'Trap Control/C? [Y] ')
	READS (15,ENTRY)
	IF (ENTRY(1,1).EQ.BLANKS) ENTRY (1,1) = 'Y'
	IF (ENTRY(1,1).NE.'Y' .AND. ENTRY(1,1).NE.'N') GO TO CTCQ
	CNTRLC =
	IF (ENTRY(1,1).EQ.'Y') CNTRLC = 1

INIT,
	DISPLAY (15,13,10,10)
	DISPLAY (15,13,10,'   ',FILE,13,10)
	DISPLAY (15,'OK to initialize above file? [N] ')
	READS (15,ENTRY(1,1))
	IF (ENTRY(1,1).NE.'Y') STOP

;****************************************
; OPEN OUTPUT FILE

	XCALL OPENF (1,'U',FILE(1,14),ERROR,0,ERR)
	CLOSE 1
        IF (ERR.EQ.0) GO TO EXISTS
OPENFL,
	ERROR = SIZE
	XCALL OPENF (1,'O',FILE(1,14),ERROR,510,ERR)
	IF (ERR) GO TO ERROR
	GO TO WRITIT

;****************************************
; OPEN ERROR ENCOUNTERED

ERROR,
	IF (ERR.EQ.2) GO TO NOROOM
	IF (ERR.EQ.3 .AND. ERROR.EQ.32) GO TO EXISTS
	ENTRY (1,3) = ERROR, 'ZZX'
	DISPLAY (15,13,10,
&	'?UT-FATAL DIBOL ERROR # ',ENTRY(1,3),' ON FILE ',FILE,7,7)
	STOP

;****************************************
; REPLACE EXISTING FILE LOGIC

EXISTS,
	DISPLAY (15,13,10,'WARNING - "UT:MESARA.DDF" ALREADY EXISTS',13,10,7,
&	'REPLACE IT? [N] ')
	READS (15,ENTRY(1,1))
	IF (ENTRY(1,1).EQ.'N'.OR.ENTRY(1,1).EQ.' ') STOP
	IF (ENTRY(1,1).NE.'Y') GO TO EXISTS
	XCALL FLAGS (01000000)
	GO TO OPENFL

;****************************************
; INSUFFICIENT SPACE ERROR

NOROOM,
	DISPLAY (15,13,10,
&	'?UT-NOT ENOUGH ROOM ON THE DEVICE SPECIFIED FOR "MESARA.DDF"',13,10,7)
	STOP
;****************************************
; INITIALIZE FILE

WRITIT,
	SUB = 1
        SIZE=SIZE-1
	WRITES (1,DUMMES)
LOOP,
	INCR SUB
        IF (SUB.GT.SIZE) GO TO END
	WRITES (1,MESARA)
	GO TO LOOP
END,
	CLOSE 1
	DISPLAY (15,13,10,'INITIALIZATION COMPLETE.')
	DISPLAY (15,13,10,13,10)
	CLOSE 15
	STOP
END
