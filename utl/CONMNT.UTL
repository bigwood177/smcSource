;  CONMNT / UTL 
;
;
;		::PCPYUTL.DEF::
;*****************************************************************************
;		SECURITY SYSTEM UTILITIES AND SUBROUTINES 
;		DIBOL FOR RT-11
;		
;		RELEASED: JANUARY 1, 1985
;*****************************************************************************
;
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984,
;		1985, MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		BUILDS AND MAINTAINS THE "CONAME" FILE FOR THE CURRENT COMPANY
;
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD CTL
	ROW	,22D2
	COL	,22D2
	MAX	,22D2
	MIN	,22D2
	INTYPE	,22A4
	FCTYPE	,22A4
	SPECIL	,22D1
COMMON
		.INCLUDE 'DEF:COM001.DEF'
RECORD CONAME
		.INCLUDE 'DEF:RD099A.DEF'
RECORD CONAM2
		.INCLUDE 'DEF:RD099B.DEF'
RECORD CONAM3
		.INCLUDE 'DEF:RD099D.DEF'
RECORD,X
		.INCLUDE 'DEF:RD099E.DEF'
RECORD
		.INCLUDE 'DEF:RD099S.DEF'
RECORD PRTCTL			;INPUT PARAMETERS FOR NAME/PRINTER SCREEN
		,22D2,	05,08,08,09,09,10,10,11,11,14,15,18,19,20
		,22D2,	22,26,44,26,44,26,44,26,44,44,44,44,44,44
		,22D2,	50,01,01,01,01,01,01,01,01,01,03,01,03,05
		,22D2,	01,00,01,00,01,00,01,00,01,00,00,00,01,01
		,22A4,	'A   ','A   ','A   ','A   ','A   ','A   ','A   ',
&			'A   ','A   ','# 00','# 00','# 00','A   ','# 00'
		,22A4,	'ED  ','U   ','U   ','U   ','U   ','U   ','U   ',
&			'U   ','U   ','U   ','U   ','U   ','U   ','U   '
		,22D1
RECORD PKGCTL
		,22D2,	05,06,07,08,09,10,11,12,13,14,15,16,17,18,19,20,21
		,22D2,	56,56,56,56,56,56,56,56,56,56,56,56,56,56,56,56,56
		,22D2,	01,01,01,01,01,01,01,01,01,01,01,01,01,01,01,01,01
		,22D2,	00,00,00,00,00,00,00,00,00,00,00,00,00,00,00,00,00
		,22A4,	'Y   ','Y   ','Y   ','Y   ','Y   ','Y   ','Y   ',
&			'Y   ','Y   ','Y   ','Y   ','Y   ','Y   ','Y   ',
&			'Y   ','Y   ','Y   '
		,22A4,	'ED1 ','UD  ','UD  ','UD  ','UD  ','UD  ','UD  ',
&			'UD  ','UD  ','UD  ','UD  ','UD  ','UD  ','UD  ',
&			'UD  ','UD  ','U   '
		,22D1
RECORD
	ANSWER	,D1
	BAD	,D1				;ERROR ON ENTRY FLAG
	BLANKS	,A50
	BUILD	,D1
	CARATS	,A25,'^^^^^^^^^^^^^^^^^^^^^^^^^'
	CNGCTL	,D1
	CNT	,D1
	DSPNUM	,A1
	DSPGL	,A24
	ENDPNT	,D2
	ENTRY	,A50
	ERROR	,A3
	INXCTL	,D1
	LEFTBL	,D2
	LEN	,D2
	LOKCTL	,D1
	MAXNUM	,D2
	N	,D2
	NUM	,D2
	READ	,D1,	0
	REDFIL	,A14
	SAV1	,A50
	SAV2	,A50
	SAVCON	,5D1
	SIZE	,D2
	STRPNT	,D2
	SUB	,D2
	SWITCH	,D1
	WHATNO	,D2
	WORK	,A50
	WRITE	,D1,	1
PROC
	XCALL TERID
	IF (ACCES(99).NE.'U') GO TO UNAVL	;DPSECU SECURITY ACCESS
	SWITCH = 3				;PROTECT CONAME
	XCALL FILES (1,'U',99,SWITCH)
	IF (SWITCH.EQ.9) GO TO INUSE
	XCALL FFILE (99,REDFIL,SWITCH)
	IF (SWITCH.EQ.1) GO TO INUSE
	XCALL OPENF (1,'U',REDFIL,ERROR,0,SWITCH)
	IF (SWITCH) GO TO NOFILE
BEGIN,
	BUILD =
	ON ERROR NOFILE
	READ (1,CONAME,1)
	SAV1 = CONAME
	READ (1,CONAM2,2)
	SAV2 = CONAM2
	READ (1,CONAM3,3)
	OFF ERROR
	GO TO DISPLA
NOFILE,
	OFF ERROR
	CLOSE 1
	SIZE = 1
	XCALL OFILE (1,99,SIZE,SIZ099,SWITCH)
	IF (SWITCH) GO TO EXIT
	BUILD = 1
DISPLA,
	CONAME = SAV1
	CONAM2 = SAV2
	XCALL OUTPT (01,01,2,'COMPANY FILE MAINTENANCE')
	XCALL OUTPT (02,01,0,'COMPANY NAME AND PRINTER/SPOOLER SPECIFICATIONS')
	XCALL OUTPT (05,05,0,'1. COMPANY NAME')
	XCALL OUTPT (07,05,0,
&	                'PRINTERS:           TYPE             QUEUED?')
	XCALL OUTPT (08,10,0,'2. "LP" (=1)                  3.')
	XCALL OUTPT (09,10,0,'4. "LQ" (=2)                  5.')
	XCALL OUTPT (10,10,0,'6. "LR" (=3)                  7.')
	XCALL OUTPT (11,10,0,'8. "LS" (=4)                  9.')
	XCALL OUTPT (13,05,0,'PRINT-OUT DEFAULTS:')
	XCALL OUTPT (14,09,0,'10. DEFAULT PRINTER DESTINATION')
	XCALL OUTPT (15,09,0,'11. NEXT REPORT SEQUENCE NUMBER')
	XCALL OUTPT (17,05,0,'SPOOL FILE PARAMETERS:')
	XCALL OUTPT (18,09,0,'12. SPOOLING OPTION')
	XCALL OUTPT (19,09,0,'13. SPOOL FILE DEVICE')
	XCALL OUTPT (20,09,0,'14. SPOOL FILE BLOCK SIZE')
	CTL = PRTCTL
	IF (BUILD.EQ.0) GO TO DSPCMP
	CNGCTL =
CONAME,
	ENTRY = CONAME
	NUM = 1
	CALL INPUT
	GO TO (DISPLA,EXIT,DISPLA,DISPLA,PRTENT), INXCTL
	CONAME = ENTRY
	IF (CONAME.EQ.BLANKS) GO TO BADNAM
	CALL CENTER
	GO TO (ANYCNG), CNGCTL
PRTENT,
	NUM = 1
NXTPRT,
	IF (NUM.GE.9) GO TO DEFPRT
	INCR NUM
GETPRT,
	CALL PRMENU
	CNT = NUM/2
	ENTRY = PRTTYP (CNT)
	IF (ENTRY.EQ.BLANKS) ENTRY = 'X'
	CALL INPUT
	GO TO (DISPLA,EXIT,PRTENT,UPPRT), INXCTL
	IF (ENTRY.NE.'C' .AND. ENTRY.NE.'L' .AND. ENTRY.NE.'X') GO TO GETPRT
	PRTTYP (CNT) = ENTRY
	CALL CLEAR
	GO TO (ANYCNG), CNGCTL
	INCR NUM
QUEPRT,
	CNT = NUM/2
	CALL QUMENU
	ENTRY = QUEPTR (CNT)
	IF (ENTRY.EQ.BLANKS) ENTRY = 'N'
	CALL INPUT
	GO TO (DISPLA,EXIT,PRTENT,UPPRT), INXCTL
	IF (ENTRY.NE.'N' .AND. ENTRY.NE.'Y') GO TO QUEPRT
	QUEPTR (CNT) = ENTRY
	CALL CLEAR
	GO TO (ANYCNG), CNGCTL
	GO TO NXTPRT
UPPRT,
	NUM = NUM - 1
	IF (NUM.LE.1) GO TO CONAME
	IF ((NUM/2)*2.EQ.NUM) GO TO GETPRT
	GO TO QUEPRT
UPDEF,
	NUM = NUM - 1
	GO TO GETPRT
DEFPRT,
	NUM = 10
	XCALL OUTPT (23,1,1,'A value of 0 means no default ')
	ENTRY = DEFPRT
	CALL INPUT
	GO TO (DISPLA,DISPLA,DISPLA,UPDEF), INXCTL
	DEFPRT = ENTRY
	IF (DEFPRT.GT.4) GO TO DEFPRT
	IF (DEFPRT.EQ.0) GO TO NOCHK
	IF (PRTTYP(DEFPRT).EQ.'X') GO TO DEFPRT
NOCHK,
	CALL CLEAR
	GO TO (ANYCNG), CNGCTL
SEQNO,
	NUM = 11
	ENTRY =
	ENTRY (1,3) = NXTRPT
	IF (NXTRPT.EQ.0 .OR. NXTRPT.EQ.999) ENTRY = '001'
	CALL INPUT
	GO TO (DISPLA,DISPLA,DISPLA,DEFPRT,SPSW), INXCTL
	NXTRPT = ENTRY
	IF (NXTRPT.EQ.0) NXTRPT = 1
	XCALL DSPLY (ROW(NUM),COL(NUM),MAX(NUM),NXTRPT,5,0)
	GO TO (ANYCNG), CNGCTL
SPSW,
	NUM = 12
	XCALL OUTPT (23,1,1,
&'0=no spooling, 1=optional spooling only, 2=auto-spool okay')
	ENTRY = SPLFLG
	CALL INPUT
	GO TO (DISPLA,DISPLA,DISPLA,SEQNO,SPWRK), INXCTL
	SPLFLG = ENTRY
	IF (SPLFLG.GT.2) GO TO SPSW
	CALL CLEAR
	GO TO (ANYCNG), CNGCTL
SPWRK,
	IF (SPLFLG.EQ.0 .AND. CNGCTL.EQ.0) GO TO ANYCNG
	ENTRY = SPLDEV
	NUM = 13
	CALL INPUT
	GO TO (DISPLA,DISPLA,DISPLA,SPSW), INXCTL
	SPLDEV = ENTRY
	N =
SPWRK2,
	INCR N
	IF (N.GT.3) GO TO SPWRK3
	IF ((ENTRY(N,N).EQ.BLANKS.AND.N.LE.2) .OR. ENTRY(N,N).EQ.':')
&		GO TO SPWBAD
	GO TO SPWRK2
SPWBAD,
	XCALL MESAG
&       ('?UT-ENTER 2-3 CHARACTER DEVICE NAME WITHOUT BLANKS OR ":"',1)
	GO TO SPWRK
SPWRK3,
	GO TO (ANYCNG), CNGCTL
SPSIZE,
	NUM = 14
	ENTRY = SPLSIZ
	CALL INPUT
	GO TO (DISPLA,DISPLA,DISPLA,SPWRK), INXCTL
	SPLSIZ = ENTRY
	GO TO ANYCNG
CNGBR,
	NUM = WHATNO
	GO TO (CONAME,GETPRT,QUEPRT,GETPRT,QUEPRT,GETPRT,QUEPRT,GETPRT,QUEPRT,
&				DEFPRT,SEQNO,SPSW,SPWRK,SPSIZE), WHATNO
	WHATNO = -1
ANYCNG,
	CNGCTL = 2
	XCALL ANYCN (CNGCTL,WHATNO,1,14,0,0)
	GO TO (CNGBR,DISPLA), CNGCTL
PROCES,
	LOKCTL = 1
	XCALL IO (1,CONAME,1,WRITE,LOKCTL)
	LOKCTL = 1
	XCALL IO (1,CONAM2,2,WRITE,LOKCTL)
	SAV1 = CONAME
	SAV2 = CONAM2
	GO TO (EXIT,SCREN2,SCREN2,DISPLA), CNGCTL + 2
	GO TO SCREN2
DSPCMP,
	CALL CENTER
	XCALL OUTPT (8,26,0,PRTTYP(1))
	XCALL OUTPT (8,44,0,QUEPTR(1))
	XCALL OUTPT (9,26,0,PRTTYP(2))
	XCALL OUTPT (9,44,0,QUEPTR(2))
	XCALL OUTPT (10,26,0,PRTTYP(3))
	XCALL OUTPT (10,44,0,QUEPTR(3))
	XCALL OUTPT (11,26,0,PRTTYP(4))
	XCALL OUTPT (11,44,0,QUEPTR(4))
	XCALL DSPLY (14,44,1,DEFPRT,1,0)
	XCALL DSPLY (15,44,3,NXTRPT,5,0)
	XCALL DSPLY (18,44,1,SPLFLG,5,0)
	XCALL OUTPT (19,44,0,SPLDEV)
	XCALL DSPLY (20,44,5,SPLSIZ,5,0)
	GO TO ANYCNG
PRMENU,
	XCALL OUTPT (23,1,1,
&'"C"=Character Printer "L"=Line Printer "X"=No Printer Installed')
	RETURN
QUMENU,
	XCALL OUTPT (23,1,1,
&'"Y"=Printer Queue set up for this printer "N"=No Printer Queue set up')
	RETURN
CLEAR,
	XCALL OUTPT (23,1,1,'\')
	RETURN
CENTER,
	N =
CENT2,
	IF (N.GE.50) RETURN
	INCR N
	IF (CONAME(N,N).EQ.BLANKS) GO TO CENT2
	STRPNT = N
	N = 51
CENT3,
	N = N - 1
	IF (CONAME(N,N).EQ.BLANKS) GO TO CENT3
	ENDPNT = N
	LEN = ENDPNT - STRPNT + 1
	LEFTBL = (50 - LEN) / 2
	WORK =
	WORK (LEFTBL+1,LEFTBL+LEN) = CONAME (STRPNT,ENDPNT)
	CONAME = WORK
	CALL REDISP
	RETURN
REDISP,
	WORK = CONAME
	IF (LEFTBL.EQ.0) GO TO REDSP2
	WORK (1,LEFTBL) = CARATS
REDSP2,
	IF (LEFTBL+LEN.EQ.50) GO TO ENDDSP
	WORK (LEFTBL+LEN+1,50) = CARATS
ENDDSP,
	XCALL OUTPT (5,22,0,WORK)
	RETURN
BADNAM,
	XCALL MESAG ('?UT-COMPANY NAME CANNOT BE BLANK',1)
	GO TO CONAME
;*********************************************************************
;                         PACKAGES INSTALLED
;***************************************************************************
SCREN2,
	XCALL OUTPT (01,01,2,'COMPANY FILE MAINTENANCE')
	XCALL OUTPT (02,01,0,'PACKAGES INSTALLED FOR COMPANY ')
	XCALL OUTPT (0,0,0,CMNME)
	XCALL OUTPT (05,20,0,'1. CUSTOMER ORDER PROCESSING')
	XCALL OUTPT (06,20,0,'2. INVENTORY MANAGEMENT')
	XCALL OUTPT (07,20,0,'3. BILL OF MATERIAL PROCESSOR')
	XCALL OUTPT (08,20,0,'4. SHOP FLOOR CONTROL')
	XCALL OUTPT (09,20,0,'5. JOB COSTING')
	XCALL OUTPT (10,20,0,'6. LABOR PERFORMANCE')
	XCALL OUTPT (11,20,0,'7. STANDARD PRODUCT COSTING')
	XCALL OUTPT (12,20,0,'8. STANDARD PRODUCT ROUTING')
	XCALL OUTPT (13,20,0,'9. MATERIAL REQUIREMENTS PLANNING')
	XCALL OUTPT (14,19,0,'10. ACCOUNTS RECEIVABLE')
	XCALL OUTPT (15,19,0,'11. ACCOUNTS PAYABLE')
	XCALL OUTPT (16,19,0,'12. PAYROLL')
	XCALL OUTPT (17,19,0,'13. GENERAL LEDGER')
	XCALL OUTPT (18,19,0,'14. FIXED ASSETS AND DEPRECIATION')
	XCALL OUTPT (19,19,0,'15. PURCHASE ORDER AND RECEIVING')
	XCALL OUTPT (20,19,0,'16. REPORT WRITER')
	CTL = PKGCTL
	CNGCTL =
	LOKCTL = 1
	IF (BUILD.EQ.0) XCALL IO (1,CONAM3,3,READ,LOKCTL)
	IF (BUILD.EQ.0) GO TO DSPPKG
PKGSTR,
	NUM =
INPPKG,
	IF (NUM.GE.16) GO TO ANYCN2
	INCR NUM
PKGINP,
	ENTRY = 'N'
	IF (PAKFLG(NUM)) ENTRY = 'Y'
	CALL INPUT
	GO TO (SCREN2,EXIT,SCREN2,UPPKG,DWNPKG,DISPLA), INXCTL
	PAKFLG (NUM) =
	IF (ENTRY.EQ.'Y') PAKFLG (NUM) = 1
	GO TO (INPPKG,ANYCN2), CNGCTL + 1
ANYCN2,
	CNGCTL = 4
	XCALL ANYCN (CNGCTL,WHATNO,1,16,0,0)
	GO TO (WRTPKG,WRTPKG,CNGB2,SCREN2,WRTPKG), CNGCTL + 2
CNGB2,
	NUM = WHATNO
	GO TO PKGINP
WRTPKG,
	LOKCTL = 1
	XCALL IO (1,CONAM3,3,WRITE,LOKCTL)
	GO TO (EXIT,EXIT,EXIT,EXIT,DISPLA), CNGCTL + 2
	GO TO EXIT
UPPKG,
	NUM = NUM - 1
	GO TO PKGINP
DWNPKG,
	INCR NUM
	GO TO PKGINP
DSPPKG,
	NUM =
PKGLUP,
	IF (NUM.GE.16) GO TO ANYCN2
	INCR NUM
	ENTRY = 'N'
	IF (PAKFLG(NUM)) ENTRY = 'Y'
	XCALL OUTPT (ROW(NUM),COL(NUM),0,ENTRY(1,1))
	GO TO PKGLUP
;**************************INPUT OF ALL SCREENS***********************
INPUT,
	XCALL INPUT (ROW(NUM),COL(NUM),MAX(NUM),MIN(NUM),INTYPE(NUM),
&	FCTYPE(NUM),ENTRY,INXCTL)
	RETURN
UNAVL,
	XCALL MESAG ('?UT-CONAME FILE UNAVAILABLE',1)
	GO TO INUSE
EXIT,
	SWITCH = 4
	XCALL FILES (1,'U',99,SWITCH)
INUSE,
	XCALL PGCHN ('UT:MSMENU',2)
END
