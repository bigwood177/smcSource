SUBROUTINE LPOFF ;(LPSW,SLPFIL,PGCNT)
;
;  LPOFF / UTL 
;
;
;		::PCPYUTL.DEF::
;*****************************************************************************
;		SECURITY SYSTEM UTILITIES AND SUBROUTINES 
;		DIBOL FOR RT-11
;		
;		RELEASED: JANUARY 1, 1985
;*****************************************************************************
;
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984,
;		1985, MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		TURNS OFF PRINTER CHANNEL AFTER PRINTING
;		CREATES SPOOL DIRECTORY ENTRY WHEN FILE IS SPOOLED TO DISK
;
	LPSW	,D
	SPLFIL	,A
	PGCNT	,D
;
;		SUBROUTINE TO CLOSE OUTPUT DEVICE AND RESET FOR REPORT/DISPLAY
;
;		USES CHANNEL 11 FOR READING OF MESARA AND SPLDIR
;
			; VALUE OF "LPSW" INCOMING
			; ========================
			;  1 = OUTPUT HAS BEEN PRINTED USING SYSTEM PRINTER
			;  2 = OUTPUT HAS BEEN SPOOLED
			; -3 = OUTPUT HAS BEEN PRINTED LOCALLY
			; -4 = OUTPUT HAS BEEN DISPLAYED
			; -5 = OUTPUT HAS BEEN PRINTED LOCALLY (NO FF AT END)
			;  6 = OUTPUT HAS BEEN PRINTED ON NON-QUEUED DEVICE
			;
			; VALUE OF "LPSW" OUTGOING
			; ========================
			; 0 = CHANNEL 13 CLOSED
			; VALUE OF "TRMID" INDICATES TERMINAL TYPE
			; ========================================
			; 1 = VT52 WITH NO OPTIONS
			; 2 = VT52 WITH PRINTER PORT OPTION
			; 3 = VT100 WITH NO OPTIONS - VT52 MODE
			; 4 = VT100 WITH ADVANCED VIDEO OPTION - VT52 MODE
			; 5 = VT100 WITH PRINTER PORT OPTION - VT52 MODE
			; 6 = VT100 WITH NO OPTIONS - ANSI MODE
			; 7 = VT100 WITH ADVANCED VIDEO OPTION - ANSI MODE
			; 8 = VT100 WITH PRINTER PORT OPTION - ANSI MODE
COMMON
	SYSTM	,D1
	TNMBR	,D2
RECORD MESARA
			.INCLUDE 'DEF:MES001.DEF'
RECORD SPLDIR	,X
			.INCLUDE 'DEF:SPL001.DEF'
RECORD	,X
			.INCLUDE 'DEF:SPL002.DEF'
RECORD
	BLANK	,A1
	CNT1	,D2
	CNT2	,D2
	DATE	,A9
	ENDRPT	,A30	,'END OF REPORT     PRESS RETURN'
	ENTRY	,A2
	INXCTL	,D1
	PRNTRS	,4A2,	'LP','LQ','LR','LS'
	SAVFIL	,A14
	SPLCNT	,D3
	TCHAR	,D3
	TIME	,D6
PROC
	IF (LPSW.EQ.0) RETURN
	IF (LPSW.LT.0) LPSW = -LPSW
	OPEN (11,I,'UT:MESARA.DDF')
	ON ERROR READ1
READ1,
	READ (11,MESARA,TNMBR+1)
	OFF ERROR
	CLOSE 11
	GO TO (SPOOL,SPOOL,LOCAL,DIS100,LOCAL,LPDONE), LPSW
SPOOL,
	IF (QUEDPT.NE.1) CALL FORMFD	;NOT NEEDED FOR SYSTEM QUEUE (AUTOMATIC FF)
	IF (LPSW.EQ.2 .OR. DELRPT.EQ.'N') CALL SPLDIR
	IF (SPLCNT.GT.200) GO TO SPLERR
	IF (LPSW.EQ.2) GO TO LPDONE	;NO PRINTING, JUST SPOOLING
	IF (QUEDPT.NE.1) GO TO LPDONE	;NON QUEUED PRINTER
	CALL SQUEZ			;SQUEEZE FILESPEC AFTER WRITING TO
					;SPLDIR AND BEFORE USING IN LPQUE
	CLOSE 13
	XCALL OUTPT (24,1,1,'SUBMITTING REPORT ')
	DISPLAY (15,SPLFIL,' TO PRINTER QUEUE ',PRNTRS(CURPTR))
	IF (DELRPT.NE.'N') LPQUE (SPLFIL,LPNUM:CURPTR,COPIES:NUMCPY,DELETE)
	IF (DELRPT.EQ.'N') LPQUE (SPLFIL,LPNUM:CURPTR,COPIES:NUMCPY)
	GO TO LPDONE
;***********************SQUEEZE SPACES OUT OF FILE SPECIFICATION***************
SQUEZ,
	CNT1 =
	CNT2 =
	SAVFIL =
SQZLUP,
	IF (CNT1.GE.14) GO TO SQZDUN
	INCR CNT1
	IF (SPLFIL(CNT1,CNT1).EQ.BLANK) GO TO SQZLUP
	INCR CNT2
	SAVFIL (CNT2,CNT2) = SPLFIL (CNT1,CNT1)
	GO TO SQZLUP
SQZDUN,
	SPLFIL = SAVFIL
	RETURN
;*****************************************************************************
SPLDIR,
	XCALL DATE (DATE)
	XCALL TIME (TIME)
	OPEN (11,U,'UT:SPLDIR.DDF')
	ON ERROR LOCKED
LOCKED,
	READ (11,SPLDIR,1)
	OFF ERROR
	SPLCNT = SPLREC
	INCR SPLCNT
	IF (SPLCNT.GT.200) RETURN
	SPLREC = SPLCNT
	WRITE (11,SPLDIR,1)
	SPLNAM = SPLFIL			;UNSQUEEZED FILESPEC - REQUIRED FOR
					;PROPER IDENTIFICATION IN XXSPOL PRGS.
	SPDATE = DATE
	SPTIME = TIME (1,4), 'XX:XX'
	SPPAGS = PGCNT
	ON ERROR WRTERR
WRTERR,
	WRITE (11,SPLDIR,SPLCNT)
	OFF ERROR
	CLOSE 11
	RETURN
LOCAL,
	IF (LPSW.EQ.3) CALL FORMFD
	IF (TERMID.NE.2) GO TO LOC100
	DISPLAY (13,27,88)		;(ESC X) TURN VT52 PRINTER CONTROLLER OFF
	GO TO LPDONE
LOC100,
	DISPLAY (13,27,91,52,105)	;(ESC [ 4 i) TURN VT100 PRINTER CONTROLLER OFF
	IF (TERMID.EQ.5)
&		DISPLAY (13,27,91,63,50,108)	;(ESC [ ? 2 l) ENTER VT52 MODE
	GO TO LPDONE
DIS100,
	DISPLAY (13,ENDRPT)
GETCHR,
	IF (SYSTM.EQ.1) DISPLAY (15,29,'F',29,'Q',1)
						;UNDER TSX, TURN OFF CHARACTER
						;ECHO AND ACTIVATE ON FIRST
						;CHARACTER TYPED
	ACCEPT (13,TCHAR)
	IF (TCHAR.NE.13) GO TO GETCHR
	IF (SYSTM.NE.1) ACCEPT (13,TCHAR)
	DISPLAY (13,27,91,63,51,108)	;(ESC [ ? 3 l) SET SCREEN TO 80 COLUMNS
	DISPLAY (13,27,91,49,59,50,52,114)	;(ESC [ 1 ; 2 4 r) SET FULL SCREEN SCROLL
	DISPLAY (13,27,91,63,52,108)		;JUMP SCROLL MODE
	DISPLAY (13,27,91,48,109)	;(ESC [ 0 m) SET CHARACTER ATTRIBUTES OFF
	IF (TERMID.EQ.3.OR.TERMID.EQ.4.OR.TERMID.EQ.5)
&		DISPLAY (13,27,91,63,50,108)	;(ESC [ ? 2 l) ENTER VT52 MODE
	IF (SYSTM.EQ.1) DISPLAY (13,29,'E')
						;UNDER TSX, TURN ON CHARACTER
						;ECHO
LPDONE,
	LPSW =
	XCALL OUTPT (24,1,1,'\')
	CLOSE 13
	RETURN
SPLERR,
	XCALL OUTPT (23,1,2,'\')
	XCALL OUTPT (23,1,4,'?UT-SPOOLER DIRECTORY FULL - FILE ')
	XCALL OUTPT (0,0,4,SPLFIL)
	XCALL OUTPT (0,0,4,' MUST BE PRINTED MANUALLY')
	XCALL MESAG ('PLEASE MAKE A NOTE OF THE SPOOL FILE NAME',2)
	GO TO LPDONE
FORMFD,
	ON ERROR OFFLIN
	FORMS (13,0)
	OFF ERROR
	RETURN
OFFLIN,
	XCALL OUTPT (24,1,1,'?UT-PRINTER OFF LINE')
	SLEEP 5
	XCALL OUTPT (24,1,1,'\')
	GO TO FORMFD
END
