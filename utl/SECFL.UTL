SUBROUTINE SECFL	;(COMPN,DECMAL)
;
;  SECFL / UTL 
;
;
;		::PCPYUTL.DEF::
;*****************************************************************************
;		SECURITY SYSTEM UTILITIES AND SUBROUTINES 
;		DIBOL FOR RT-11
;		
;		RELEASED: JANUARY 1, 1985
;*****************************************************************************
;
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984,
;		1985, MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		MAINTAIN FILENAMES SUBROUTINE FOR SECURITY MAINTENANCE
;
	COMPN	,A
	DECMAL	,D
RECORD COMPNY
			.INCLUDE 'DEF:CMP001.DEF'
RECORD SECURE
			.INCLUDE 'DEF:SEC004.DEF'
RECORD DEVICE
			.INCLUDE 'DEF:DEV001.DEF'
RECORD,X
			.INCLUDE 'DEF:DEV002.DEF'
RECORD S16CTL
		,11D2,	04,04,06,08,10,12,14,16,18,20,22
		,11D2,	24,49,24,41,41,41,41,41,41,41,41
		,11D2,	06,03,03,03,03,03,03,03,03,03,03
		,11D2,	04,01,02,00,00,00,00,00,00,00,00
		,11A4,	'A   ','N 00','A   ','A   ','A   ','A   ','A   ',
&			'A   ','A   ','A   ','A   '
		,11A4,	'E   ','EU  ','U   ','D   ','UD  ','UD  ','UD  ',
&			'UD  ','UD  ','UD  ','U   '
		,11D1
		,D2,	01
		,D2,	11
RECORD,X
	ROW16	,A22
	COL16	,A22
	MAX16	,A22
	MIN16	,A22
	TYP16	,A44
	FCT16	,A44
	SPC16	,A11
	STN16	,A2
	END16	,A2
RECORD S17CTL
		,2D2,	11,11
		,2D2,	30,52
		,2D2,	06,03
		,2D2,	00,00
		,2A4,	'A   ','N 00'
		,2A4,	'ED  ','U   '
		,2D1
RECORD,X
	ROW17	,A4
	COL17	,A4
	MAX17	,A4
	MIN17	,A4
	TYP17	,A8
	FCT17	,A8
	SPC17	,A2
COMMON CTL
	ROW	,19D2
	COL	,19D2
	MAX	,19D2
	MIN	,19D2
	INTYP	,19A4
	FCTYP	,19A4
	STRNM	,2D2
	ENDNM	,2D2
COMMON,X
	ROW0	,A38
	COL0	,A38
	MAX0	,A38
	MIN0	,A38
	TYP0	,A76
	FCT0	,A76
	STN0	,A4
	END0	,A4
;*****************************COMMON DATA SECTION***********************
COMMON
	PKCOD	,20A1,	'E','C','D','I','H','O','L','K','M','A','F','G','B',
&			'J','N','P','Q','R','S','T'
					;PACKAGE CODES IN REC #2 OF SECURE
					;SORTED BY ORDER OF DISPLAY (PACKAGE
					;CODES COME FROM SPOOL CODES.
	PCKGS	,20A3,	'COP','IM ','BMP','SFC','JC ','LP ','SPC','SPR','MRP',
&			'AR ','AP ','PR ','GL ','AD ','POR','UTL','   ','GEN',
&			'   ','   '
					;PACKAGES CORRESPONDING TO THE ABOVE
					;PACKAGE CODES
	SVPAK	,200A1			;STORES THE PACKAGE CODES FROM REC #2
	MAXPK	,D2			;MAXIMUM NUMBER OF ALLOWED PACKAGES
					;IN PACKAGE CODE ARRAY (PKCOD, PCKGS)
RECORD
	BLANKS	,A3
	BSMID	,D3
	CNGCTL	,D1
	CNGFCT	,19A4,	'E   '
	ENTRY	,A6
	FILCNT	,D3
	SAVFIL	,A6
	INXCTL	,D1
	LOKCTL	,D1
	NUM	,D2
	READ	,D1,	0
	SAVCOD	,A3
	SRCCTL	,D1
	SUB	,D3
	TSTCOD	,A3
	WHATNO	,D2
	WRITE	,D1,	1
PROC
	COMPNY = COMPN
	GO TO (ADDDFL,DELDFL), DECMAL - 9
ADDDFL,
	CTL =
	CNGCTL =
	ROW0 = ROW16
	COL0 = COL16
	MAX0 = MAX16
	MIN0 = MIN16
	TYP0 = TYP16
	FCT0 = FCT16
	STN0 = STN16
	END0 = END16
	XCALL OUTPT (02,01,2,'ADD FILENAMES TO DEVICE FILE')
	XCALL OUTPT (04,10,0,'1. FILE NAME')
	XCALL OUTPT (04,33,0,'2. FILE NUMBER')
	XCALL OUTPT (06,10,0,'3. PACKAGE')
	XCALL OUTPT (08,10,0,'4. COMPANY CODE')
	XCALL OUTPT (08,33,0,'DEVICE')
	XCALL OUTPT (10,10,0,'5. COMPANY CODE')
	XCALL OUTPT (10,33,0,'DEVICE')
	XCALL OUTPT (12,10,0,'6. COMPANY CODE')
	XCALL OUTPT (12,33,0,'DEVICE')
	XCALL OUTPT (14,10,0,'7. COMPANY CODE')
	XCALL OUTPT (14,33,0,'DEVICE')
	XCALL OUTPT (16,10,0,'8. COMPANY CODE')
	XCALL OUTPT (16,33,0,'DEVICE')
	XCALL OUTPT (18,10,0,'9. COMPANY CODE')
	XCALL OUTPT (18,33,0,'DEVICE')
	XCALL OUTPT (20,09,0,'10. COMPANY CODE')
	XCALL OUTPT (20,33,0,'DEVICE')
	XCALL OUTPT (22,09,0,'11. COMPANY CODE')
	XCALL OUTPT (22,33,0,'DEVICE')
INPS16,
	NUM = 1
	ENTRY =
	CALL INPUT
	GO TO (ADDDFL,RETURN), INXCTL
	SAVFIL = ENTRY
	BSMID = 1
	SRCCTL =
	XCALL SERCH (14,DEVICE,SAVFIL,1,6,200,BSMID,SRCCTL,7,1,6,0,0,0,0)
	GO TO (BADD16), SRCCTL + 1
	GO TO (CNGS16), CNGCTL
IN1S16,
	NUM = 2
	ENTRY =
	CALL INPUT
	GO TO (ADDDFL,RETURN,RETURN,INPS16), INXCTL
	FILCNT = ENTRY
	IF (FILCNT.LE.0) GO TO IN1S16
	IF (FILCNT.GT.200) GO TO TOBG16
	LOKCTL = 1
	XCALL IO (14,DEVICE,FILCNT,READ,LOKCTL)
	IF (FILNAM.NE.BLANKS) GO TO BADD16
	GO TO (CNGS16), CNGCTL
IN2S16,
	NUM = 3
	ENTRY =
	CALL INPUT
	GO TO (ADDDFL,RETURN,RETURN,IN1S16,IN1S16), INXCTL
	SAVCOD = ENTRY
	CALL GETPKC
	IF (SRCCTL) GO TO IN2S16
	GO TO (CNGS16), CNGCTL
DSPD16,
	NUM = 3
DSPL16,
	IF (NUM.GE.ENDNM(1)) GO TO INPD16
	INCR NUM
	XCALL OUTPT (ROW(NUM),27,0,COMCOD(NUM-3))
	GO TO DSPL16
CNGB16,
	NUM = WHATNO
	GO TO (INPS16,IN1S16,IN2S16), NUM
	GO TO IN1L16
BADC16,
	WHATNO = -1
CNGS16,
	FCT0 = CNGFCT
	CNGCTL = 2
	CALL ANYCNG
	GO TO (PRCC16,PRCC16,CNGB16,ADDDFL), CNGCTL + 2
	GO TO CNGS16
INPD16,
	NUM = 3
INPL16,
	IF (NUM.GE.ENDNM(1)) GO TO CNGS16
	INCR NUM
IN1L16,
	IF (COMCOD(NUM-3).EQ.'XXX') GO TO (INPL16,BADC16), CNGCTL + 1
	ENTRY = DEVNAM (NUM-3)
	CALL INPUT
	GO TO (ADDDFL), INXCTL
	GO TO (UPFL16,INPL16), INXCTL - 3
	DEVNAM (NUM-3) = ENTRY
	GO TO (INPL16,CNGS16), CNGCTL + 1
	GO TO CNGS16
UPFL16,
	NUM = NUM - 1
	IF (COMCOD(NUM-3).EQ.'XXX') GO TO UPFL16
	GO TO IN1L16
PRCC16,
	FILNAM = SAVFIL
	WSTATS =
	LOKCTL = 1
	XCALL IO (14,DEVICE,FILCNT,WRITE,LOKCTL)
	LOKCTL = 1
	XCALL IO (1,SECURE,2,READ,LOKCTL)
	FILPAK (FILCNT) = PKCOD (SUB)
	SVPAK (FILCNT) = PKCOD (SUB)
	LOKCTL = 1
	XCALL IO (1,SECURE,2,WRITE,LOKCTL)
	GO TO (RETURN), CNGCTL + 2
	GO TO ADDDFL
BADD16,
	XCALL MESAG ('?UT-FILENAME/FILE NUMBER ALREADY IN USE',1)
	GO TO (INPS16,IN1S16), NUM
TOBG16,
	XCALL MESAG ('?UT-DEVICE FILE NUMBER CANNOT BE LARGER THAN 200',1)
	GO TO IN1S16
GETPKC,
	SUB =
	SRCCTL =
PKCLUP,
	IF (SUB.GE.MAXPK) GO TO NOTF16
	INCR SUB
	IF (SAVCOD.NE.PCKGS(SUB)) GO TO PKCLUP
	RETURN
NOTF16,
	XCALL MESAG ('?UT-INVALID PACKAGE CODE',1)
	SRCCTL = 1
	RETURN
;****************************************************************************
;                       DELETE FILENAMES FROM DEVICE FILE
;****************************************************************************
DELDFL,
	CTL =
	CNGCTL =
	ROW0 = ROW17
	COL0 = COL17
	MAX0 = MAX17
	MIN0 = MIN17
	TYP0 = TYP17
	FCT0 = FCT17
	XCALL OUTPT (02,01,2,'DELETE FILENAMES FROM DEVICE FILE')
	XCALL OUTPT (11,20,0,'FILENAME')
	XCALL OUTPT (11,39,0,'FILE NUMBER')
	CALL INPSC7
	GO TO (DELDFL,RETURN), INXCTL
	SUB = 7
	XCALL MESAG ('OK TO DELETE THIS FILENAME FROM DEVICE?',SUB)
	GO TO (DELDFL), SUB
	FILNAM =
	WDVNAM =
	WSTATS =
	LOKCTL = 1
	XCALL IO (14,DEVICE,FILCNT,WRITE,LOKCTL)
	LOKCTL = 1
	XCALL IO (1,SECURE,2,READ,LOKCTL)
	FILPAK (FILCNT) =
	SVPAK (FILCNT) =
	LOKCTL = 1
	XCALL IO (1,SECURE,2,WRITE,LOKCTL)
	XCALL MESAG ('FILENAME DELETED',1)
	GO TO DELDFL
INPSC7,		;MODULARIZE THIS ENTRY SECTION FOR USE BY OTHER SECTIONS
	NUM = 1
	ENTRY =
	CALL INPUT
	IF (INXCTL) RETURN
	IF (ENTRY.NE.BLANKS) GO TO DVSRCH
IN1SC7,
	NUM = 2
	ENTRY =
	CALL INPUT
	IF (INXCTL) RETURN
	IF (ENTRY.EQ.BLANKS) GO TO INPSC7
	FILCNT = ENTRY
	IF (FILCNT.LE.0 .OR. FILCNT.GT.200) GO TO IN1SC7
	LOKCTL = 1
	XCALL IO (14,DEVICE,FILCNT,READ,LOKCTL)
	IF (FILNAM.EQ.BLANKS) GO TO BADDV7
	XCALL OUTPT (ROW(1),COL(1),0,FILNAM)
	RETURN
BADDV7,
	XCALL MESAG ('?UT-INVALID FILE NAME/NUMBER',1)
	INXCTL = 1
	RETURN
DVSRCH,
	FILCNT = 1
	SRCCTL =
	XCALL SERCH (14,DEVICE,ENTRY(1,6),1,6,200,FILCNT,SRCCTL,7,1,6,0,0,0,0)
	GO TO (BADDV7), SRCCTL
	LOKCTL = 1
	XCALL IO (14,DEVICE,FILCNT,READ,LOKCTL)	;READ AND LOCK RECORD
	ENTRY (1,3) = FILCNT
	XCALL OUTPT (ROW(2),COL(2),0,ENTRY(1,3))
	RETURN
INPUT,
	XCALL INPUT (ROW(NUM),COL(NUM),MAX(NUM),MIN(NUM),INTYP(NUM),
&	FCTYP(NUM),ENTRY,INXCTL)
	RETURN
ANYCNG,
	XCALL ANYCN (CNGCTL,WHATNO,STRNM(1),ENDNM(1),0,0)
	RETURN
RETURN,
	COMPN = COMPNY
	RETURN
END
