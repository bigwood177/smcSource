;  ORGSEC / UTL 
;
;
;		::PCPYUTL.DEF::
;*****************************************************************************
;		SECURITY SYSTEM UTILITIES AND SUBROUTINES 
;		DIBOL FOR RT-11
;		
;		RELEASED: JANUARY 1, 1985
;*****************************************************************************
;
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984,
;		1985, MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		REORGANIZES THE UT:SECURE.DDF FILE
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD
	SECARY  ,20A212
COMMON
			.INCLUDE 'DEF:COM001.DEF'
RECORD SECURE
			.INCLUDE 'DEF:SEC001.DEF'
RECORD ,X
			.INCLUDE 'DEF:SEC005.DEF'
RECORD SECCTL ,X
			.INCLUDE 'DEF:SEC003.DEF'
RECORD BRACKS
			.INCLUDE 'DEF:SEC006.DEF'
RECORD
	CNT	,D3
	DELCNT  ,D3
	ERROR   ,D3
	LOKCTL	,D1
	N	,D3
	RDPNTR	,D5
	READ	,D1
	SWITCH	,D1
	WRITE	,D1,	1
	WRPNTR  ,D5
PROC
	CNPOS = -2
	XCALL TERID
	XCALL OUTPT (2,1,1,'REORGANIZE SECURITY FILE')
	XCALL WATE (4)
SETUP,
	RDPNTR = 3
	WRPNTR = 3
	XCALL OPENF (11,'U','UT:SECURE.DDF',ERROR,0,SWITCH)
	IF (SWITCH) GO TO NOFILE
READ,
	LOKCTL = 1
	XCALL IO (11,SECURE,RDPNTR,READ,LOKCTL)
	IF (SECURE.EQ.BRACKS) GO TO EOF
	IF (DFLSEC.EQ.']]]DEL') GO TO DELREC
	INCR CNT
	SECARY(CNT) = SECURE
	IF (CNT.EQ.20) CALL WRITE
	INCR RDPNTR
	GO TO READ
DELREC,
	INCR DELCNT
	INCR RDPNTR
	GO TO READ
WRITE,
	IF (N.EQ.CNT) GO TO OUT
	INCR N
	LOKCTL = 1
	XCALL IO (11,SECARY(N),WRPNTR,WRITE,LOKCTL)
	INCR WRPNTR
	GO TO WRITE
OUT,
	CNT =
	N =
	RETURN
EOF,
	IF (CNT.NE.0) CALL WRITE
	IF (RDPNTR.EQ.WRPNTR) GO TO DONE
	LOKCTL = 0
	XCALL IO (11,BRACKS,WRPNTR,WRITE,LOKCTL)
	INCR WRPNTR
	GO TO EOF
DONE,
	LOKCTL = 1
	XCALL IO (11,SECCTL,1,READ,LOKCTL)
	RECSEC = RECSEC - DELCNT
	ORGSEC = RECSEC
	DELSEC =
	LOKCTL = 1
	XCALL IO (11,SECCTL,1,WRITE,LOKCTL)
END,
	CLOSE 11
	XCALL PGCHN ('UT:CLRSEC',2)
NOFILE,
	XCALL MESAG ('UT:SECURE.DDF FILE NOT FOUND',1)
	XCALL PGCHN ('UT:SECMNT',2)
END
