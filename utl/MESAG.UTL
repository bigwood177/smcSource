SUBROUTINE MESAG ; (MSG,MSXCTL)
;
;  MESAG / UTL 
;
;
;		::PCPYUTL.DEF::
;*****************************************************************************
;		SECURITY SYSTEM UTILITIES AND SUBROUTINES 
;		DIBOL FOR RT-11
;		
;		RELEASED: JANUARY 1, 1985
;*****************************************************************************
;
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984,
;		1985, MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		ROUTINE TO DISPLAY STANDARD ERROR MESSAGES
;
	MSG	,A			; MESSAGE TO BE DISPLAYED
	MSXCTL	,D			; OPTION SWITCH (SEE BELOW)
COMMON
		.INCLUDE 'DEF:COM001.DEF'
RECORD MESAGE
	INMSG	,A66			; WORK FIELDS
	RTMSG	,A13
RECORD
	TCHAR	,D3
	RETMSG	,A13,	' PRESS RETURN'

			; ***** INCOMING OPTION CODES *****
			;     1,2 = DISPLAY MSG ON LINE 24
			;		WITH "PRESS "RETURN""
			;	3 = DISPLAY "INVALID ENTRY" ON LINE 24
			;		WITH "PRESS "RETURN""
			;	4 = DISPLAY MSG ONLY ON LINE 24
			;	5 = DISPLAY
			;	"XXXXXX FILE NOW FULL - SEE INSTRUCTIONS"
			;	& STOP (XXXXXX IS FILE NAME PASSED IN MSG)
			;	6 = DISPLAY "INVALID SELECTION" ON LINE 12 OR
			;		24 WITH "PRESS "RETURN""
			;	7 = DISPLAY MSG ON LINE 24 AND
			;		ACCEPT A "Y" OR "N" ANSWER.
			;
			;***OUTGOING OPTION CODES
			;	0 = "Y" WAS ANSWERED IN THE Y/N ANSWER OPTION
			;	1 = "N" WAS ANSWERED IN THE Y/N ANSWER OPTION
			;
PROC
	INMSG = MSG
	RTMSG = RETMSG
	IF (SYSTM.EQ.1) DISPLAY (15,29,'F')
	GO TO (DSPMSG,DSPMSG,INVALD,MSONLY,STOPMS,INVSEL,MSGANS), MSXCTL
INVALD,
	INMSG = 'INVALID ENTRY'
	GO TO DSPMSG
MSONLY,
	MESAGE = MSG
DSPMSG,
	DISPLAY (15,7)
	XCALL OUTPT (24,1,4,MESAGE)
	IF (MSXCTL.EQ.4) GO TO RETURN
	CALL ACPTCR
	XCALL OUTPT (24,1,1,'\')
	GO TO RETURN
MSGANS,
	XCALL OUTPT (24,1,4,MSG)
	DISPLAY (15,'  .',8,7)
	IF (SYSTM.EQ.1) DISPLAY (15,29,'Q',1)
	ACCEPT (15,TCHAR)
	IF (TCHAR.NE.89 .AND. TCHAR.NE.78) GO TO MSGANS
	DISPLAY (15,' ',8,TCHAR)
	IF (TCHAR.EQ.89) MSXCTL = 0
	IF (TCHAR.EQ.78) MSXCTL = 1
MSGRCV,
	ACCEPT (15,TCHAR)
	IF (TCHAR.EQ.127) GO TO MSGANS
	IF (TCHAR.NE.13) GO TO MSGRCV
	IF (SYSTM.NE.1) ACCEPT (15,TCHAR)
	XCALL OUTPT (24,1,1,'\')
	GO TO RETURN
STOPMS,
	INMSG(8,39) = 'FILE NOW FULL - SEE INSTRUCTIONS'
	XCALL OUTPT (20,20,2,INMSG)
	STOP
INVSEL,
	INMSG = 'INVALID SELECTION'
	GO TO DSPMSG
ACPTCR,
	ACCEPT (15,TCHAR)
	IF (TCHAR.NE.13) GO TO ACPTCR
	IF (SYSTM.NE.1) ACCEPT (15,TCHAR)
	RETURN
RETURN,
	IF (SYSTM.EQ.1) DISPLAY (15,29,'E')
	RETURN
END
