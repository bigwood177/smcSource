;  LKSEE / UTL 
;
;
;		::PCPYUTL.DEF::
;*****************************************************************************
;		SECURITY SYSTEM UTILITIES AND SUBROUTINES 
;		DIBOL FOR RT-11
;		
;		RELEASED: JANUARY 1, 1985
;*****************************************************************************
;
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984,
;		1985, MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;	
;	ALLOWS INQUIRIES AND CHANGES TO FILE CONTROL RECORDS
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD IN
	DATA	,A1536
COMMON
			.INCLUDE 'DEF:COM001.DEF'
RECORD DEVICE
			.INCLUDE 'DEF:DEV001.DEF'
RECORD CTLVAR
	DELFLG	,D1
	SRTFLG	,D1
	ORGCNT	,D5
	RECCNT	,D5
	MAXCNT	,D5
	DELCNT	,D3
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
		,A1
	FCTYPE	,A2
RECORD
	COL2	,D2
	ROW2	,D2
	SZ	,D4
	DCHAR	,D3
	ENTRY	,A30
	DFILNO	,A3
	DFILNM	,A14
	CLCTL	,D1
	INXCTL	,D1
	CNGCTL	,D1
	WHATNO	,D2
	SWITCH	,D1
	INQUIR	,D1
	CHANGE	,D1
	BLANKS	,A10
	FILNUM	,D5
	READ	,D1,0
	WRITE	,D1,1
	LOKCTL	,D1
	MSGSWT	,D1
	ERROR	,D3
PROC
	XCALL TERID
	CHANGE =
	INQUIR =
	XCALL OUTPT (1,1,2,'FILE CONTROL RECORD INQUIRY - ')
	IF (ACCES(100).NE.'U') GO TO INQMOD
CHGMOD,
	CHANGE = 1
	XCALL OUTPT (1,31,0,'CHANGE/INQUIRE MODE')
	GO TO GETNAM
INQMOD,
	INQUIR = 1
	XCALL OUTPT (1,31,0,'INQUIRE MODE')
GETNAM,
	XCALL OUTPT (3,01,2,'NOTE: The selection of the device name and')
	XCALL OUTPT (4,01,0,'      company code for files selected')
	XCALL OUTPT (5,01,0,'      are derived from the currently active')
	XCALL OUTPT (6,01,0,'      password.')
	XCALL OUTPT (10,01,0,'FILE NAME: ')
	XCALL OUTPT (10,29,0,'DEVICE#: ')
INPFIL,
	CTL = '10,12,06,00,A ,FE'
	CALL INPUT
	GO TO (GETNAM,ENDOFF), INXCTL
	IF (ENTRY.EQ.BLANKS) GO TO GETDEV
	IF (ENTRY(1,6).EQ.'ORDHDR' .OR. ENTRY(1,6).EQ.'ORDLIN') GO TO BADFIL
							;ISAM FILES
	XCALL WATE (3)
	FILNUM = 0
RDDEV,
	INCR FILNUM
	IF (FILNUM.GT.200) GO TO EOF
	LOKCTL = 0
	XCALL IO (14,DEVICE,FILNUM,READ,LOKCTL)
	IF (LOKCTL) GO TO ENDOFF
	IF (FILNAM.NE.ENTRY) GO TO RDDEV
	GO TO GOTIT
GETDEV,
	CTL = '10,38,03,00,# ,FE'
	CALL INPUT
	GO TO (GETNAM,ENDOFF), INXCTL
	IF (ENTRY.EQ.BLANKS) GO TO INPFIL
	FILNUM = ENTRY
	IF (FILNUM.LE.0 .OR. FILNUM.GT.200) GO TO BADNUM
	IF (FILNUM.EQ.44 .OR. FILNUM.EQ.45) GO TO BADFIL
	LOKCTL = 0
	XCALL IO (14,DEVICE,FILNUM,READ,LOKCTL)
	IF (LOKCTL) GO TO ENDOFF
	IF (FILNAM.NE.BLANKS) GO TO GOTIT
BADNUM,
	XCALL MESAG ('?UT-INVALID DEVICE NUMBER',1)
	GO TO GETDEV
GOTIT,
	XCALL OUTPT (24,01,1,'\')
	XCALL FFILE (FILNUM,DFILNM,CLCTL)
	IF (CLCTL.EQ.0) GO TO GOTIT2
	XCALL MESAG ('Error in resolving fully qualified filename',2)
	XCALL MESAG ('Please check your password',2)
	GO TO GETNAM
GOTIT2,
	DFILNO = FILNUM,'ZZX'
	XCALL OUTPT (10,01,0,'FILE NAME: ')
	XCALL OUTPT (0,0,0,DFILNM)
	XCALL OUTPT (10,29,0,'DEVICE#: ')
	XCALL OUTPT (0,0,0,DFILNO)
	XCALL OPENF (1,'I',DFILNM,ERROR,0,SWITCH)
	IF (SWITCH) GO TO NOFILE
	READS (1,IN,EMPTY)
	XCALL RSTAT (SZ,DCHAR)
	CLOSE 1
	IF ((SZ-19).LE.0) 
&		XCALL MESAG('File has a non-standard control record',2)
	IF ((SZ-19).LE.0) GO TO GETNAM
	CTLVAR = IN(SZ-19,SZ)
	CALL DISPLA
	CLOSE 1
	IF (INQUIR) XCALL MESAG(' ',1)
	IF (INQUIR) GO TO GETNAM
	SWITCH = 2
	XCALL FILES (1,'U',FILNUM,SWITCH)
	IF (SWITCH.EQ.9)
&	   XCALL MESAG('CHANGE/INQUIRE requires exclusive access',2)
	IF (SWITCH.EQ.9) GO TO GETNAM
	CALL CHGCTL
	XCALL FILES (1,'U',FILNUM,4)
	GO TO GETNAM
BADFIL,
	XCALL MESAG ('?UT-No Control record for this file',1)
	GO TO GETNAM
EOF,
	CLOSE 1
	XCALL MESAG ('?UT-INVALID FILE-NAME (not in the DEVICE table)',1)
	GO TO GETNAM
ENDOFF,
	XCALL PGCHN ('UT:MSMENU',2)
NOFILE,
	XCALL MESAG ('?UT-FILE OPEN ERROR',1)
	GO TO GETNAM
EMPTY,
	XCALL MESAG ('?UT-FILE IS EMPTY',1)
	CLOSE 1
	GO TO GETNAM
;*************************************************************************
DISPLA,
	XCALL OUTPT (12,1,0,'CONTROL RECORD FORMAT:')
	ENTRY(1,1) = DELFLG,'X'
	XCALL OUTPT (14,10,0,'1. DELFLG = ')
	XCALL OUTPT (0,0,0,ENTRY(1,1))
	ENTRY(1,1) = SRTFLG,'X'
	XCALL OUTPT (15,10,0,'2. SRTFLG = ')
	XCALL OUTPT (0,0,0,ENTRY(1,1))
	ENTRY(1,5) = ORGCNT,'ZZZZX'
	XCALL OUTPT (16,10,0,'3. ORGCNT = ')
	XCALL OUTPT (0,0,0,ENTRY(1,5))
	ENTRY(1,5) = RECCNT,'ZZZZX'
	XCALL OUTPT (17,10,0,'4. RECCNT = ')
	XCALL OUTPT (0,0,0,ENTRY(1,5))
	ENTRY(1,5) = MAXCNT,'ZZZZX'
	XCALL OUTPT (18,10,0,'5. MAXCNT = ')
	XCALL OUTPT (0,0,0,ENTRY(1,5))
	ENTRY(1,3) = DELCNT,'ZZX'
	XCALL OUTPT (19,10,0,'6. DELCNT =   ')
	XCALL OUTPT (0,0,0,ENTRY(1,3))
	ENTRY(1,5) = SZ, 'ZZZZX'
	XCALL OUTPT (20,10,0,'   RECSIZ = ')
	XCALL OUTPT (0,0,0,ENTRY(1,5))

	RETURN
;*************************************************************************
CHGCTL,
	GO TO ANYCNG
DELFLG,
	ENTRY =
	CTL = '14,22,01,00,# , '
	CALL INPUT
	GO TO (CHANGX), INXCTL
	IF (ENTRY(1,1).EQ.' ') ENTRY(1,1) = DELFLG,'X'
	DELFLG = ENTRY(1,1)
	XCALL OUTPT (ROW,COL,0,ENTRY(1,1))
	GO TO ANYCNG
SRTFLG,
	ENTRY =
	CTL = '15,22,01,00,# , '
	CALL INPUT
	GO TO (CHANGX), INXCTL
	IF (ENTRY(1,1).EQ.' ') ENTRY(1,1) = SRTFLG,'X'
	SRTFLG = ENTRY(1,1)
	XCALL OUTPT (ROW,COL,0,ENTRY(1,1))
	GO TO ANYCNG
ORGCNT,
	ENTRY =
	CTL = '16,22,05,00,# , '
	CALL INPUT
	GO TO (CHANGX), INXCTL
	IF (ENTRY(1,5).EQ.'     ') ENTRY(1,5) = ORGCNT,'XXXXX'
	ORGCNT = ENTRY(1,5)
	ENTRY(1,5) = ORGCNT,'ZZZZX'
	XCALL OUTPT (16,10,0,'3. ORGCNT = ')
	XCALL OUTPT (0,0,0,ENTRY(1,5))
	GO TO ANYCNG
RECCNT,
	ENTRY =
	CTL = '17,22,05,00,# , '
	CALL INPUT
	GO TO (CHANGX), INXCTL
	IF (ENTRY(1,5).EQ.'     ') ENTRY(1,5) = RECCNT,'XXXXX'
	RECCNT = ENTRY(1,5)
	ENTRY(1,5) = RECCNT,'ZZZZX'
	XCALL OUTPT (17,10,0,'4. RECCNT = ')
	XCALL OUTPT (0,0,0,ENTRY(1,5))
	GO TO ANYCNG
MAXCNT,
	ENTRY =
	CTL = '18,22,05,01,# , '
	CALL INPUT
	GO TO (CHANGX), INXCTL
	IF (ENTRY(1,5).EQ.'     ') ENTRY(1,5) = MAXCNT,'XXXXX'
	MAXCNT = ENTRY(1,5)
	ENTRY(1,5) = MAXCNT,'ZZZZX'
	XCALL OUTPT (18,10,0,'5. MAXCNT = ')
	XCALL OUTPT (0,0,0,ENTRY(1,5))
	GO TO ANYCNG
DELCNT,
	ENTRY =
	XCALL OUTPT (19,22,0,'     ')
	CTL = '19,22,03,00,# , '
	CALL INPUT
	GO TO (CHANGX), INXCTL
	IF(ENTRY(1,3).EQ.'   ') ENTRY(1,3) = DELCNT,'XXX'
	DELCNT = ENTRY(1,3)
	ENTRY(1,3) = DELCNT,'ZZX'
	XCALL OUTPT (19,10,0,'6. DELCNT =   ')
	XCALL OUTPT (0,0,0,ENTRY(1,3))
ANYCNG,
	CNGCTL = 1
	XCALL ANYCN(CNGCTL,WHATNO,1,6,0,0)
	GO TO (REWRIT,CHNG,CHANGX), CNGCTL + 1
CHNG,
;	GO TO (DELFLG,SRTFLG,ORGCNT,BADCNG,BADCNG,DELCNT), WHATNO
	GO TO (DELFLG,SRTFLG,ORGCNT,RECCNT,MAXCNT,DELCNT), WHATNO
BADCNG,
	CNGCTL = 3
	GO TO ANYCNG
REWRIT,
	IN(SZ-19,SZ) = CTLVAR
	LOKCTL = 1
	XCALL IO (1,IN(1,SZ),1,WRITE,LOKCTL)
CHANGX,
	RETURN
;*************************************************************************
INPUT,
	XCALL INPUT(ROW,COL,MAX,MIN,TYPE,FCTYPE,ENTRY,INXCTL)
	RETURN
;*************************************************************************
END
