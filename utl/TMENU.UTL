SUBROUTINE TMENU; (MENUID,SELECT)
;
;  TMENU / UTL 
;
;
;		::PCPYUTL.DEF::
;*****************************************************************************
;		SECURITY SYSTEM UTILITIES AND SUBROUTINES 
;		DIBOL FOR RT-11
;		
;		RELEASED: JANUARY 1, 1985
;*****************************************************************************
;
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984,
;		1985, MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		ROUTINE TO DISPLAY STANDARD TRANSACTION FILE
;		MAINTENANCE MENU & PASS BACK OPTION SELECTED
;
	MENUID	,A	; TITLE TO DISPLAY AT TOP OF SCREEN
	SELECT	,D	; VALUE OF OPTION PASSED BACK (SELECT = 0-5)
			; OR INCOMING OPTION
			;
			;	INCOMING VALUE OF SELECT
			; "-1" = DON'T DISPLAY "POST" OR ALLOW POST QUESTION
RECORD
	INXCTL	,D1	; PARAMETERS
	ENTRY	,A3	; FOR INPUT
	SELIN	,D1	; SAVE THE INCOMING VALUE OF SELECT
	MODEID	,5A9,	'ADD      ','CHANGE   ','DELETE   ','EDIT LIST',
&			'POST     '
PROC
BEGIN,
	SELIN = SELECT
	XCALL OUTPT (1,1,2,MENUID)
	XCALL OUTPT (7,20,0,'PLEASE SELECT')
	XCALL OUTPT (9,25,0,'1. ENTER NEW TRANSACTIONS')
	XCALL OUTPT (11,25,0,'2. CHANGE EXISTING TRANSACTIONS')
	XCALL OUTPT (13,25,0,'3. DELETE EXISTING TRANSACTIONS')
	XCALL OUTPT (15,25,0,'4. PRINT TRANSACTION EDIT LIST')
	IF (SELIN.NE.-1)
&	XCALL OUTPT (17,25,0,'5. POST TRANSACTIONS TO MAIN FILES')
MNUSEL,
	XCALL INPUT (7,35,1,0,'# ','EF',ENTRY,INXCTL)	;INPUT SELECTED OPTION
	GO TO (MNUSEL,SELEND), INXCTL
	SELECT = ENTRY
	IF (SELECT.GE.1.AND.SELECT.LE.5) GO TO DSPMOD
	GO TO MNUSEL
DSPMOD,
	IF (SELIN.EQ.-1 .AND. SELECT.EQ.5) GO TO MNUSEL
	XCALL OUTPT (2,1,0,MODEID(SELECT))
	IF (SELECT.EQ.5) GO TO POST
	RETURN
POST,
	XCALL OUTPT (7,20,2,'\')
	XCALL OUTPT (12,20,0,'ARE TRANSACTIONS OK TO POST ?')
	XCALL INPUT (12,51,1,1,'YN','E',ENTRY,INXCTL)
	GO TO (BEGIN,SELEND), INXCTL
	IF (ENTRY.EQ.'N') GO TO BEGIN
	XCALL WATE (4)
	RETURN
SELEND,
	SELECT =			; PASS BACK SELECT = 0 FOR END OF MENU
	RETURN
END
