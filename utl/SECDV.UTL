
SUBROUTINE SECDV	;(COMPN,DECMAL)
;
;  SECDV / UTL 
;
;
;		::PCPYUTL.DEF::
;*****************************************************************************
;		SECURITY SYSTEM UTILITIES AND SUBROUTINES 
;		DIBOL FOR RT-11
;		
;		RELEASED: JANUARY 1, 1985
;*****************************************************************************
;
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984,
;		1985, MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		SUBROUTINE OF SECMNT TO INPUT DEVICE ASSIGNMENT SCREENS
	COMPN	,A
	DECMAL	,D	;NUMBER OF OPTION SELECTED
RECORD SECURE
			.INCLUDE 'DEF:SEC001.DEF'
RECORD,X
			.INCLUDE 'DEF:SEC002.DEF'
RECORD,X
			.INCLUDE 'DEF:SEC003.DEF'
RECORD DEVFIL
			.INCLUDE 'DEF:DEV001.DEF'
RECORD COMPNY
			.INCLUDE 'DEF:CMP001.DEF'
RECORD S4CTL
		,19D2,	04,06,07,08,09,10,11,12,13,14,15,16,17,18,19,20,21,22
		,19D2,	39,56,56,56,56,56,56,56,56,56,56,56,56,56,56,56,56,56
		,19D2,	03,03,03,03,03,03,03,03,03,03,03,03,03,03,03,03,03,03
		,19D2,	03,00,00,00,00,00,00,00,00,00,00,00,00,00,00,00,00,00
		,19A4,	'A   ','A   ','A   ','A   ','A   ','A   ','A   ',
&			'A   ','A   ','A   ','A   ','A   ','A   ','A   ',
&			'A   ','A   ','A   ','A   ','A   '
		,19A4,	'E   ','D   ','UD  ','UD  ','UD  ','UD  ','UD  ',
&			'UD  ','UD  ','UD  ','UD  ','UD  ','UD  ','UD  ',
&			'UD  ','U   '
		,D2,	01
		,D2,	16
RECORD,X
	ROW4	,A38
	COL4	,A38
	MAX4	,A38
	MIN4	,A38
	TYP4	,A76
	FCT4	,A76
	STN4	,A2
	END4	,A2
RECORD S5CTL
		,1D2,	10
		,1D2,	30
		,1D2,	01
		,1D2,	01
		,1A4,	'N 00'
		,1A4,	'FE  '
RECORD,X
	ROW5	,A2
	COL5	,A2
	MAX5	,A2
	MIN5	,A2
	TYP5	,A4
	FCT5	,A4
RECORD S7CTL
		,10D2,	05,05,07,09,11,13,15,17,19,21
		,10D2,	25,47,47,47,47,47,47,47,47,47
		,10D2,	06,03,03,03,03,03,03,03,03,03
		,10D2,	00,00,00,00,00,00,00,00,00,00
		,10A4,	'A   ','N 00','A   ','A   ','A   ','A   ','A   ',
&			'A   ','A   ','A   '
		,10A4,	'E   '
		,D2,	01
		,D2,	08
RECORD,X
	ROW7	,A20
	COL7	,A20
	MAX7	,A20
	MIN7	,A20
	TYP7	,A40
	FCT7	,A40
	STN7	,A2
	END7	,A2
COMMON CTL
	ROW	,19D2
	COL	,19D2
	MAX	,19D2
	MIN	,19D2
	INTYP	,19A4
	FCTYP	,19A4
	STRNM	,2D2
	ENDNM	,2D2
COMMON,X
	ROW0	,A38
	COL0	,A38
	MAX0	,A38
	MIN0	,A38
	TYP0	,A76
	FCT0	,A76
	STN0	,A4
	END0	,A4
;*****************************COMMON DATA SECTION***********************
COMMON
	PKARY	,400D3	;PAKAGE ARRAY LOADED FROM REC #2 OF SECURE.
			;ACCESSED BY THE ARYCNT COUNTER (=(PAKAGE # *20)-20)
			;=20 PACKAGES X 20 FILES MAX PER PACKAGE
	MAXPK	,D2			;MAXIMUM NUMBER OF ALLOWED PACKAGES
					;IN PACKAGE CODE ARRAY (PKCOD, PCKGS)
COMMON			;PACKAGE COUNTER - KEEPS TRACK OF THE NUMBER OF FILES
	CNTS	,20D2	;CONTAINED WITHIN EACH PACKAGE ARRAY (PKARY)
;*****************************************************************************
RECORD
	ARYCNT	,D3			;STORES THE RELATIVE ARRAY NUMBER
					;BEING ACCESSED IN PKARY
	BLANKS	,A10
	CNGFCT	,19A4,	'E   '
	DEVICS	,20A3			;LIST OF DEVICE NAMES BY PACKAGE TO
					;LOAD INTO DEVICE FILE
	ENTRY	,A10
	INXCTL	,D1
	CNGCTL	,D1
	FILCNT	,D3
	LOKCTL	,D1
	NUM	,D2
	READ	,D1,	0
	SAVCOD	,A3
	SAVSUB	,D2
	SELECT	,D1
	SRCCTL	,D1
	SUB	,D2
	WHATNO	,D2
	WRITE	,D1,	1
PROC
	COMPNY = COMPN
	GO TO (ADDDEV,CNGDEV), DECMAL - 1
CNGDPK,
	CNGCTL = 1
	SELECT = 2
	XCALL OUTPT (2,1,2,'CHANGE DEVICE ASSIGMENTS BY PACKAGE')
	GO TO CNTIN4
ADDDEV,
	CNGCTL =
	SELECT = 1
	XCALL OUTPT (02,01,2,'ADD DEVICE ASSIGNMENTS')
CNTIN4,
	DEVICS (1,60) =
	CTL =
	ROW0 = ROW4
	COL0 = COL4
	MAX0 = MAX4
	MIN0 = MIN4
	TYP0 = TYP4
	FCT0 = FCT4
	STN0 = STN4
	END0 = END4
	CALL DSPSC4
SC4COD,
	NUM = 1
	ENTRY =
	CALL INPUT
	GO TO (ADDDEV,RETURN), INXCTL
	SAVCOD = ENTRY
	IF (SAVCOD.EQ.'XXX') GO TO BADCD4
	SUB =
CODLP4,
	IF (SUB.GE.8) GO TO BADCD4
	INCR SUB
	IF (COMCOD(SUB).NE.SAVCOD) GO TO CODLP4
	SAVSUB = SUB			;SAVE POSITION FOR LATER USE IN DEVFIL
	SAVCOD =
	GO TO SC4DEV
BADCD4,
	XCALL MESAG ('?UT-INVALID COMPANY CODE' ,1)
	GO TO SC4COD
SC4DEV,
	GO TO (SC4CNG), CNGCTL
	IF (NUM.GE.17) GO TO SC4CNG
	INCR NUM
INPDV4,
	ENTRY = DEVICS (NUM-1)
	IF (ENTRY.EQ.BLANKS) ENTRY = SAVCOD
	CALL INPUT
	GO TO (ADDDEV,RETURN,INPDV4,UP4,DN4), INXCTL
	DEVICS (NUM-1) = ENTRY
	SAVCOD = DEVICS (NUM-1)
	GO TO SC4DEV
UP4,
	XCALL OUTPT (ROW(NUM),COL(NUM),0,DEVICS(NUM-1))
	NUM = NUM - 1
	GO TO INPDV4
DN4,
	XCALL OUTPT (ROW(NUM),COL(NUM),0,DEVICS(NUM-1))
	GO TO SC4DEV
CNGB4,
	NUM = WHATNO + 1
	GO TO INPDV4
SC4CNG,
	FCT0 = CNGFCT
	CNGCTL = 2
	CALL ANYCNG
	GO TO (CNGB4,CNTIN4), CNGCTL
PRCC4,
	XCALL WATE (4)
	SUB =
PRC4LP,
	IF (SUB.GE.MAXPK) GO TO (ADDDEV,CNGDPK), SELECT
	INCR SUB
	IF (DEVICS(SUB).EQ.BLANKS .AND. SELECT.EQ.2) GO TO PRC4LP
				;DON'T CHANGE DEVICE NAME TO BLANKS 
				;FOR UNENTERED PACKAGES IN CHANGE MODE
	ARYCNT = (SUB-1)*20
	NUM =
PRC4L1,
	IF (NUM.GE.CNTS(SUB)) GO TO PRC4LP
	INCR NUM
	FILCNT = PKARY (ARYCNT+NUM)
	LOKCTL = 1
	XCALL IO (14,DEVFIL,FILCNT,READ,LOKCTL)
	DEVNAM (SAVSUB) = DEVICS (SUB)
	LOKCTL = 1
	XCALL IO (14,DEVFIL,FILCNT,WRITE,LOKCTL)
	GO TO (RETURN), CNGCTL + 2
	GO TO PRC4L1
DSPSC4,
	XCALL OUTPT (04,25,2,'COMPANY CODE')
	XCALL OUTPT (05,55,0,'DEVICE')
	XCALL OUTPT (06,10,0,'1. CUSTOMER ORDER PROCESSING')
	XCALL OUTPT (07,10,0,'2. INVENTORY MANAGEMENT')
	XCALL OUTPT (08,10,0,'3. BILL OF MATERIALS PROCESSOR')
	XCALL OUTPT (09,10,0,'4. SHOP FLOOR CONTROL')
	XCALL OUTPT (10,10,0,'5. JOB COSTING')
	XCALL OUTPT (11,10,0,'6. LABOR PERFORMANCE')
	XCALL OUTPT (12,10,0,'7. STANDARD PRODUCT COSTING')
	XCALL OUTPT (13,10,0,'8. STANDARD PRODUCT ROUTING')
	XCALL OUTPT (14,10,0,'9. MATERIAL REQUIREMENTS PLANNING')
	XCALL OUTPT (15,09,0,'10. ACCOUNTS RECEIVABLE')
	XCALL OUTPT (16,09,0,'11. ACCOUNTS PAYABLE')
	XCALL OUTPT (17,09,0,'12. PAYROLL')
	XCALL OUTPT (18,09,0,'13. GENERAL LEDGER')
	XCALL OUTPT (19,09,0,'14. FIXED ASSETS AND DEPRECIATION')
	XCALL OUTPT (20,09,0,'15. PURCHASE ORDER AND RECEIVING')
	XCALL OUTPT (21,09,0,'16. SECURITY/COMPANY FILES')
	RETURN
;********************************************************************
;                     CHANGE DEVICE ASSIGNMENTS
;********************************************************************
CNGDEV,
	CTL =
	ROW0 = ROW5
	COL0 = COL5
	MAX0 = MAX5
	MIN0 = MIN5
	TYP0 = TYP5
	FCT0 = FCT5
	XCALL OUTPT (01,01,2,'SECURITY SYSTEM MAINTENANCE')
	XCALL OUTPT (02,01,0,'CHANGE DEVICE ASSIGNMENTS')
	XCALL OUTPT (10,15,0,'PLEASE SELECT')
	XCALL OUTPT (12,20,0,'1. CHANGE DEVICE ASSIGNMENTS BY PACKAGE')
	XCALL OUTPT (14,20,0,'2. CHANGE DEVICE ASSIGNMENTS BY FILE')
	NUM = 1
	CALL INPUT
	GO TO (CNGDEV,RETURN), INXCTL
	IF (ENTRY.EQ.'1') GO TO CNGDPK
	IF (ENTRY.EQ.'2') GO TO CNGDFL
	GO TO CNGDEV
;**********************************************************************
;                  CHANGE DEVICE ASSIGNMENTS BY FILE
;**********************************************************************
CNGDFL,
	CTL =
	ROW0 = ROW7
	COL0 = COL7
	MAX0 = MAX7
	MIN0 = MIN7
	TYP0 = TYP7
	FCT0 = FCT7
	STN0 = STN7
	END0 = END7
	XCALL OUTPT (02,01,2,'CHANGE DEVICE ASSIGNMENTS BY FILE')
	XCALL OUTPT (05,15,0,'FILENAME ')
	XCALL OUTPT (05,34,0,'FILE NUMBER')
	XCALL OUTPT (07,15,0,'1. COMPANY CODE ')
	XCALL OUTPT (07,39,0,'DEVICE')
	XCALL OUTPT (09,15,0,'2. COMPANY CODE ')
	XCALL OUTPT (09,39,0,'DEVICE')
	XCALL OUTPT (11,15,0,'3. COMPANY CODE ')
	XCALL OUTPT (11,39,0,'DEVICE')
	XCALL OUTPT (13,15,0,'4. COMPANY CODE ')
	XCALL OUTPT (13,39,0,'DEVICE')
	XCALL OUTPT (15,15,0,'5. COMPANY CODE ')
	XCALL OUTPT (15,39,0,'DEVICE')
	XCALL OUTPT (17,15,0,'6. COMPANY CODE ')
	XCALL OUTPT (17,39,0,'DEVICE')
	XCALL OUTPT (19,15,0,'7. COMPANY CODE ')
	XCALL OUTPT (19,39,0,'DEVICE')
	XCALL OUTPT (21,15,0,'8. COMPANY CODE ')
	XCALL OUTPT (21,39,0,'DEVICE')
	CALL INPSC7
	GO TO (CNGDFL,RETURN), INXCTL
	GO TO DSPDV7
INPSC7,		;MODULARIZE THIS ENTRY SECTION FOR USE BY OTHER SECTIONS
	NUM = 1
	ENTRY =
	CALL INPUT
	IF (INXCTL) RETURN
	IF (ENTRY.NE.BLANKS) GO TO DVSRCH
	NUM = 2
	ENTRY =
	CALL INPUT
	IF (INXCTL) RETURN
	IF (ENTRY.EQ.BLANKS) GO TO INPSC7
	FILCNT = ENTRY
	IF (FILCNT.LE.0 .OR. FILCNT.GT.200) GO TO BADDV7
	LOKCTL = 1
	XCALL IO (14,DEVFIL,FILCNT,READ,LOKCTL)
	IF (FILNAM.EQ.BLANKS) GO TO BADDV7
	XCALL OUTPT (ROW(1),COL(1),0,FILNAM)
	RETURN
BADDV7,
	XCALL MESAG ('?UT-INVALID FILE NAME/NUMBER',1)
	INXCTL = 1
	RETURN
DVSRCH,
	SRCCTL =
	FILCNT = 1
	XCALL SERCH (14,DEVFIL,ENTRY(1,6),1,6,200,FILCNT,SRCCTL,5,1,6,0,0,0,0)
	GO TO (BADDV7), SRCCTL
	LOKCTL = 1
	XCALL IO (14,DEVFIL,FILCNT,READ,LOKCTL)	;READ AND LOCK RECORD
	ENTRY (1,3) = FILCNT
	XCALL OUTPT (ROW(2),COL(2),0,ENTRY(1,3))
	RETURN
DSPDV7,
	SUB =
DSPLP7,
	IF (SUB.GE.8) GO TO CNGSC7
	INCR SUB
	XCALL OUTPT (ROW(SUB+2),32,0,COMCOD(SUB))
	XCALL OUTPT (ROW(SUB+2),COL(SUB+2),0,DEVNAM(SUB))
	GO TO DSPLP7
CNGB7,
	NUM = WHATNO + 2
	GO TO INPDV7
BADCG7,
	WHATNO = -1
CNGSC7,
	CNGCTL = 2
	CALL ANYCNG
	GO TO (PRCC7,PRCC7,CNGB7,CNGDFL), CNGCTL + 2
INPDV7,
	IF (COMCOD(WHATNO).EQ.'XXX') GO TO BADCG7
	ENTRY = DEVNAM (WHATNO)
	CALL INPUT
	GO TO (CNGDFL), INXCTL
	DEVNAM (WHATNO) = ENTRY
	GO TO CNGSC7
PRCC7,
	LOKCTL = 1
	XCALL IO (14,DEVFIL,FILCNT,WRITE,LOKCTL)
	GO TO (RETURN), CNGCTL + 2
	GO TO CNGDFL
INPUT,
	XCALL INPUT (ROW(NUM),COL(NUM),MAX(NUM),MIN(NUM),INTYP(NUM),
&	FCTYP(NUM),ENTRY,INXCTL)
	RETURN
ANYCNG,
	XCALL ANYCN (CNGCTL,WHATNO,STRNM(1),ENDNM(1),0,0)
	RETURN
RETURN,
	RETURN
END
