SUBROUTINE DSPLY ; (ROW,COL,MAX,DECMAL,OPTION,NUMDC)
;
;  DSPLY / UTL 
;
;
;		::PCPYUTL.DEF::
;*****************************************************************************
;		SECURITY SYSTEM UTILITIES AND SUBROUTINES 
;		DIBOL FOR RT-11
;		
;		RELEASED: JANUARY 1, 1985
;*****************************************************************************
;
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984,
;		1985, MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		ROUTINE TO FORMAT & DISPLAY A NUMBER,
;		DATE OR DECIMAL FIELD ON THE SCREEN
;
	ROW     ,D	; SCREEN ROW
	COL	,D	; SCREEN COLUMN
	MAX	,D	; MAX # OF DIGITS IN NUMBER FIELD
	DECMAL	,D	; INCOMING DECIMAL FIELD
	OPTION	,D	; OPTION SWITCH
			;	1 = NUMBER, MASK WITH ALL XS
			;	2 = DATE, CONVERTS YYMMDD TO MMDDYY AND FORMAT
			;		(or DDMMYY format, depending upon
			;		the option defined below)
			;	3 = DATE, PASSED IN MMDDYY (or DDMMYY) FORMAT, 
			;		DON'T CONVERT DATE BEFORE DISPLAYING
			;	4 = DECIMAL NUMBER, MASK WITH COMMAS
			;	5 = DECIMAL NUMBER, MASK WITH NO COMMAS
			;		(SAME AS OPTION #1 MASKED WITH Z'S 
			;		WHEN YOU PASS NUMDC=0)
	NUMDC	,D	; NUMBER OF DECIMAL PLACES TO MASK

;*****************************************************************************
;			DATE FORMAT OPTION
;*****************************************************************************
;RECORD DATE		;This record format allows you to use DDMMYY date 
;	DD	,D2	;formatting. 
;	MM	,D2
;	YY	,D2
			;To change from one format to the other, remove the
			;semi-colons from in front of the format you wish to
			;use and place semi-colons in front of the corresponding
			;names of the other date format. As initially set up,
			;MMDDYY format is selected for use.
			;If any changes are made, they must also be made in the
			;INPUT.UTL subroutine in order to be properly handled.
			;
RECORD DATE		;This record format allows you to use MMDDYY formatting
	MM	,D2
	DD	,D2
	YY	,D2
;******************************************************************************
RECORD,X
	MMDDYY	,D6
RECORD INDATE
	Y	,D2
	M	,D2
	D	,D2
RECORD,X
	YYMMDD	,D6
RECORD
	ALPHA	,A24	; WORK
	COL2	,D2
	CHAR	,D2	; FIELDS
	DOT	,A1,	'.'
	MASK	,A24
	ROW2	,D2
	SPACES	,D2
	COMMAS	,A24,	'ZZZ,ZZZ,ZZZ,ZZZ,ZZZ,ZZZ-'
	XS	,A23,	'XXXXXXXXXXXXXXXXXXXXXXX'
	ZEES	,A24,	'ZZZZZZZZZZZZZZZZZZZZZZZ-'
PROC
	CHAR = 0
	CALL SPCALC
	GO TO (DSPNUM,DSPDTE,DSPDTE,DSPDEC,DSPDEC), OPTION
	RETURN
SPCALC,
	SPACES =
	IF (OPTION.EQ.2 .OR. OPTION.EQ.3) SPACES = 2	;2 SPACES FOR DATES
	IF (DECMAL.LT.0) INCR SPACES			;ADD SPACE FOR "-"
	IF (NUMDC.GT.0) INCR SPACES			;ADD SPACE FOR "."
	IF (OPTION.EQ.4) SPACES = (MAX-(NUMDC+1))/3 + SPACES
							;ADD SPACES FOR ","'S.
	RETURN
DSPNUM,				; CONVERT TO ALPHA
	CALL SETNUM
	ALPHA = DECMAL, MASK
	GO TO JUSTFY
DSPDTE,				; FORMAT & DISPLAY DATE
	MMDDYY = DECMAL
	IF (OPTION.EQ.2) CALL DATCNV
	ALPHA (1,8) = MMDDYY, 'XX/XX/XX'
	XCALL OUTPT (ROW,COL,0,ALPHA(1,8))
	RETURN
DSPDEC,
	IF (OPTION.EQ.4) CALL SETCMA
	IF (OPTION.EQ.5) CALL SETMSK
	ALPHA = DECMAL, MASK
JUSTFY,						; LEFT JUSTIFY ROUTINE
	INCR CHAR
	IF (ALPHA(CHAR,CHAR).EQ.' '.OR.ALPHA(CHAR,CHAR).EQ.',') GO TO JUSTFY
	XCALL OUTPT (ROW,COL,0,ALPHA(CHAR,24))
	IF (MAX+SPACES.LE.25-CHAR) RETURN
	ALPHA =
	SPACES = (MAX+SPACES)-(25-CHAR)		;CALCULATE THE NUMBER OF SPACES
						;NECESSARY TO BLANKS THE FIELD
	COL2 = COL+25-CHAR
	XCALL OUTPT (ROW,COL2,0,ALPHA(1,SPACES))
	RETURN
SETNUM,
	MASK = ZEES
	MASK (24-MAX,23) = XS
	RETURN
SETMSK,						;SETUP INITIAL DISPLAY MASK
	MASK = ZEES
	MASK (23,23) = XS
	IF (NUMDC.EQ.0) RETURN
	MASK (23-NUMDC,23) = XS
	MASK (23-NUMDC,23-NUMDC) = DOT
	RETURN
SETCMA,						;SETUP REDISPLAY MASK
	MASK = COMMAS
	IF (NUMDC.EQ.0) RETURN
	MASK (1,21-NUMDC) = MASK (NUMDC+2,23)
	IF (MASK(1,1).EQ.',') MASK (1,1) =
	MASK (22-NUMDC,23) = XS
	MASK (23-NUMDC,23-NUMDC) = DOT
	RETURN
DATCNV,
	YYMMDD = DECMAL				;CONVERT INCOMING DATE
	MM = M					;FROM YYMMDD FORMAT
	DD = D					;TO MMDDYY OR DDMMYY
	YY = Y
	RETURN
END
