SUBROUTINE SECCM	;(COMPN,INXCTL)
;
;  SECCM / UTL 
;
;
;		::PCPYUTL.DEF::
;*****************************************************************************
;		SECURITY SYSTEM UTILITIES AND SUBROUTINES 
;		DIBOL FOR RT-11
;		
;		RELEASED: JANUARY 1, 1985
;*****************************************************************************
;
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984,
;		1985, MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		SUBROUTINE OF SECMNT FOR INPUT OF COMPANY CODE SCREEN
;
	COMPN	,A	;COMPNY.DDF
	INXCTL	,D
RECORD COMPNY
			.INCLUDE 'DEF:CMP001.DEF'
COMMON CTL
	ROW	,19D2
	COL	,19D2
	MAX	,19D2
	MIN	,19D2
	INTYP	,19A4
	FCTYP	,19A4
	STRNM	,2D2
	ENDNM	,2D2
COMMON,X
	ROW0	,A38
	COL0	,A38
	MAX0	,A38
	MIN0	,A38
	TYP0	,A76
	FCT0	,A76
	STN0	,A4
	END0	,A4
RECORD S3CTL
		,8D2,	06,08,10,12,14,16,18,20
		,8D2,	37,37,37,37,37,37,37,37
		,8D2,	03,03,03,03,03,03,03,03
		,8D2,	03,03,03,03,03,03,03,03
		,8A4,	'A   ','A   ','A   ','A   ','A   ','A   ','A   ','A   '
		,8A4,	'ED  ','EUD ','EUD ','EUD ','EUD ','EUD ','EUD ','EU  '
		,D2,	01
		,D2,	08
RECORD,X
	ROW3	,A16
	COL3	,A16
	MAX3	,A16
	MIN3	,A16
	TYP3	,A32
	FCT3	,A32
	STN3	,A2
	END3	,A2
RECORD
	CMPCNT	,D1
	CNGCTL	,D1
	ENTRY	,A3
	LOKCTL	,D1
	NUM	,D2
	READ	,D1,	0
	SAVCOD	,A3
	SUB	,D3
	TSTCOD	,A3
	WHATNO	,D2
	WRITE	,D1,	1
PROC
ADDCMP,
	CTL =
	ROW0 = ROW3
	COL0 = COL3
	MAX0 = MAX3
	MIN0 = MIN3
	TYP0 = TYP3
	FCT0 = FCT3
	STN0 = STN3
	END0 = END3
CMPCDS,
	CNGCTL =
	COMPNY = COMPN
	XCALL OUTPT (01,01,2,'SECURITY SYSTEM MAINTENANCE')
	XCALL OUTPT (02,01,0,'MAINTAIN COMPANY CODES')
	XCALL OUTPT (06,20,0,'1. COMPANY CODE')
	XCALL OUTPT (08,20,0,'2. COMPANY CODE')
	XCALL OUTPT (10,20,0,'3. COMPANY CODE')
	XCALL OUTPT (12,20,0,'4. COMPANY CODE')
	XCALL OUTPT (14,20,0,'5. COMPANY CODE')
	XCALL OUTPT (16,20,0,'6. COMPANY CODE')
	XCALL OUTPT (18,20,0,'7. COMPANY CODE')
	XCALL OUTPT (20,20,0,'8. COMPANY CODE')
	NUM =
DSPLUP,
	IF (NUM.GE.8) GO TO INPSC3
	INCR NUM
	XCALL OUTPT (ROW(NUM),COL(NUM),1,COMCOD(NUM))
	GO TO DSPLUP
INPSC3,
	CNGCTL =
	SUB = 1
	CALL ANYCNG
	GO TO (CNG3,CMPCDS,CMPCDS), CNGCTL
	LOKCTL = 1
	XCALL IO (2,COMPNY,1,WRITE,LOKCTL)
	LOKCTL = 0
	XCALL IO (2,COMPNY,1,READ,LOKCTL)	;RELOCK RECORD
	IF (LOKCTL) GO TO EXIT			;IF UNSUCCESSFUL, EXIT (SINGLE
						;USER SECURITY SYSTEM).
	GO TO RETURN
CNG3,
	NUM = WHATNO
	ENTRY = COMCOD (NUM)
	CALL INPUT
	GO TO (CMPCDS), INXCTL
	IF (INXCTL) GO TO AROW3
CHKCOD,
	SUB =
	SAVCOD = ENTRY
	IF (SAVCOD(3,3).LE.'9' .AND. SAVCOD(3,3).GE.'0') GO TO BAD3A
CODLUP,
	IF (SUB.GE.8) GO TO CODOK
	INCR SUB
	TSTCOD = COMCOD (SUB)
	IF (TSTCOD.EQ.'XXX') GO TO CODLUP	;'XXX' IS OK TO MATCH
	IF (SUB.NE.NUM .AND. TSTCOD (1,2).EQ.SAVCOD(1,2)) GO TO BAD3B
	GO TO CODLUP
CODOK,
	COMCOD (NUM) = ENTRY
	GO TO INPSC3
AROW3,
	IF (INXCTL.EQ.4) NUM = NUM - 1
	IF (INXCTL.EQ.5) INCR NUM
	GO TO CNG3
BAD3A,
	XCALL MESAG ('?UT-LAST CHARACTER OF COMPANY CODE MUST NOT BE NUMERIC',1)
	GO TO CNG3
BAD3B,
	XCALL MESAG
&	('?UT-FIRST 2 CHARACTERS OF ALL COMPANY CODES MUST BE UNIQUE',1)
	GO TO CNG3
RETURN,
	COMPN = COMPNY
	INXCTL = 2
	RETURN
INPUT,
	XCALL INPUT (ROW(NUM),COL(NUM),MAX(NUM),MIN(NUM),INTYP(NUM),
&	FCTYP(NUM),ENTRY,INXCTL)
	RETURN
ANYCNG,
	XCALL ANYCN (CNGCTL,WHATNO,STRNM(1),ENDNM(1),0,0)
	RETURN
EXIT,
	COMPN = COMPNY
	INXCTL = 3
	RETURN
END
