;  MSMENU / UTL 
;
;	9-30-98 ssq: security is in logon.utl, logon.wsc, backing
;	out of this menu does not remove security
;
;		OVERALL MASTER MENU FOR APPLICATION SELECTION

;	5-aug-03 ssq: 	display company name
;	6-mar-19 ssq: add START MENU & END MENU labels

EXTERNAL FUNCTION
	CHK_SEC	,D

RECORD
	AUTH,D1	;1=OK, 0=NOT AUTHORIZED		;3-11-19

COMMON
			.INCLUDE 'DEF:COM001.DEF'
RECORD SECURE
			.INCLUDE 'DEF:SEC001.DEF'
RECORD WSECUR ,X
			.INCLUDE 'DEF:SEC002.DEF'
RECORD SECCTL
			.INCLUDE 'DEF:SEC003.DEF'
RECORD DEVICE
			.INCLUDE 'DEF:DEV001.DEF'
RECORD MESARA
			.INCLUDE 'DEF:MES001.DEF'
RECORD,X
			.INCLUDE 'DEF:MES002.DEF'
RECORD	CONAM1
			.INCLUDE 'DEF:RD099A.DEF'	;SSQ 8-5-03
RECORD CONAM2
			.INCLUDE 'DEF:RD099B.DEF'
RECORD CONAM3
			.INCLUDE 'DEF:RD099D.DEF'
RECORD ,X
			.INCLUDE 'DEF:RD099E.DEF'
RECORD COMPNY
			.INCLUDE 'DEF:CMP001.DEF'
RECORD SPWORD
	WPWORD	,9A1
RECORD EXE
	EXEPGM	,A9
	EXTENT	,A4
RECORD REDFIL
		,A14
RECORD CMPMSG
		,A6,	'?UT - '
	CMPFIL	,A14
		,A43,	' MUST BE CREATED. DO YOU WISH TO CONTINUE? '
RECORD
	ROW	,4D2,	12,12,03,04
	COL	,4D2,	43,70,35,35
	MAX	,4D2,	01,03,02,01
	MIN	,4D2,	00,03,01,01
	TYPE	,4A4,	'YN  ','A   ','N 00','N 00'
	FCTYPE	,4A3,	'E  ','E  ','FE1','FE1'
RECORD
		,A30,	' !"#$%&''()*+,-./0123456789:;<='
		,A35,	'>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[\]^_`'
RECORD,X
	DECODE	,65A1
RECORD
		,A30,	'VHA. WIB/!XJC0"YKD1#ZLE2$[MF3%'
		,A35,	'\NG4&]O5''^P6(_Q7)`R8*S9+T:,U;-<=>?@'
RECORD,X
	CODE	,65A1

RECORD	VARS
	XNAME	,A50
	I	,D5
	ASTRIX	,A32,'********************************'
	BSMID	,D5
	COMCTR	,D1
	CTL	,D1
	DPNTR	,D3
	DSPCOM	,A3		;DISPLAY OF COMPANY CODE
	ENTRY	,A10
	ERROR	,A3		;RETURNING POSSIBLE ERROR MESSAGE (OPENF)
	FCMENU	,D1		;0-FRONT MENU 1-BACK MENU
	HPWORD	,4A1
	INXCTL	,D1
	KEYA	,A12
	LOKCTL	,D1
	PGM	,D2

	MAXFIL	,D2,	19
	PROGNM	,19A9,	'CP:CPMENU','IM:IMMENU','BM:BMMENU','SF:SFMENU',
&			'JC:JCMENU','LB:LPMENU','SC:SCMENU','SR:SRMENU',
&			'MR:MRMENU','AR:ARMENU','AP:APMENU','PR:PRMENU',
&			'GL:GLMENU','AD:ADMENU','PO:POMENU','RW:RPG   ',
&			'@SATURN  ','@DAILY   ','@MONEND  '
	STROW	,D2,	03

	BAKPGM	,12A9,	'UT:SECMNT','UT:CONMNT','UT:MSGMNT','UT:RSTART',
&			'UT:XPAND ','UT:CLRFIL','UT:LKSEE ','UT:WRKRES',
&			'         ','         ','         ','         '
	ORGCNT	,D5
	READ	,D1	,0
	RECCNT	,D5
	REENTR	,D1
	SCMPNY	,A3
	SRCCTL	,D1
	SWITCH	,D1
	SECCTR	,D1	,0
	SUB	,D2
	TERM	,D2		;SAVES THE VALUE OF TNMBR + 1
	TODAY	,D6
	WRITE	,D1	,1
PROC

	CNPOS = -1
	XCALL TERID
	IF (TERID.GT.0) GO TO CONTNU
	XCALL FLAGS (01000000)
	STOP
CONTNU,
	TERM = TNMBR + 1
	CALL FRSDSP
	XCALL RDATE (TODAY)
	IF (TODAY.EQ.0) GO TO NODATE
	EXE = 'UT:SECURE.DDF'
	XCALL OPENF (1,'I',EXE,ERROR,0,SWITCH)
	IF (SWITCH) GO TO BADOPN
REDCTL,
	XCALL IO (1,SECCTL,1,READ,LOKCTL)
	ORGCNT = ORGSEC
	RECCNT = RECSEC
	EXE = 'UT:MESARA.DDF'
	CLOSE 13
	XCALL OPENF (13,'U',EXE,ERROR,0,SWITCH)
	IF (SWITCH) GO TO BADOPN
	LOKCTL = 1
	XCALL IO (13,MESARA,TERM,READ,LOKCTL)
	IF (CNPOS.EQ.-1) GO TO GETPAS
	UNLOCK 13
	REENTR = 1
	GO TO CONMCK
;***********************FIRST SCREEN DISPLAY***********************
FRSDSP,

;;;	XCALL OUTPT (1,1,2,'MCBA SYSTEM MASTER MENU')
;;;	XCALL OUTPT (1,1,2,'Sheet Metal Connectors, Inc.')

	XCALL OUTPT (1,1,2,XNAME)
	ROW (3) = 3			;SET ROW FOR INPUT

	RETURN
;**********************LOG INTO/OUT OF SYSTEM*****************
LOGOUT,
	goto endoff	;ssq 9-30-98
	CALL FRSDSP
	XCALL OUTPT (12,17,0,'DO YOU WISH TO LOG OUT ?')
	XCALL OUTPT (12,48,0,'COMPANY CODE')
	XCALL OUTPT (12,62,0,CMNME)
	CTL = 1
	CALL INPUT
	GO TO (MSMENU,ENDOFF), INXCTL
	IF (ENTRY(1,1).EQ.'Y') GO TO ENDOFF
GETPAS,
	REENTR =
	CALL FRSDSP
	XCALL OUTPT (12,8,0,'PLEASE ENTER YOUR SYSTEM PASSWORD')
	XCALL OUTPT (12,56,0,'COMPANY CODE')
INPTPW,
	XCALL INPT3 (12,43,9,9,'AE',ENTRY,INXCTL)
	GO TO (GETPAS,ENDOFF), INXCTL
	SPWORD = ENTRY(1,9)
	CALL SCRMBL
	CALL CODE
INPTCC,
	CTL = 2
	CALL INPUT
	GO TO (GETPAS,ENDOFF), INXCTL
	SCMPNY = ENTRY(1,3)
	KEYA(1,9) = SPWORD
	KEYA(10,12) = SCMPNY
READSF,
	SRCCTL = 2
	XCALL SERCH (1,SECURE,KEYA,1,12,ORGCNT,BSMID,SRCCTL,1,13,18,0,0,0,0)
	IF (SRCCTL.EQ.0) GO TO PFOUND
NOTFND,
	INCR SECCTR
	IF (SECCTR.GT.5) GO TO CLOSE1
	XCALL MESAG ('?UT-PASSWORD/COMPANY COMBINATION INVALID',1)
	GO TO GETPAS
PFOUND,
	XCALL WATE (4)
	SECREC = BSMID
	ACCES (1,200) = ACCESS (1,200)
	EXE = 'UT:COMPNY.DDF'
	XCALL OPENF (3,'I',EXE,ERROR,0,SWITCH)
	IF (SWITCH) GO TO BADOPN
	LOKCTL = 1
	XCALL IO (3,COMPNY,1,READ,LOKCTL)
	CLOSE 3
	OFF ERROR
	COMCTR = 0
FINDCO,
	INCR COMCTR
	IF (COMCTR.GT.8) GO TO NOTFND
	IF (COMCOD(COMCTR).NE.COMPNM) GO TO FINDCO
	CNPOS = COMCTR
	CMPPOS = COMCTR
	CMNME = COMCOD (COMCTR)
	CMPCOD = COMCOD (COMCTR)
	LOKCTL = 1
	XCALL IO (13,MESARA,TERM,WRITE,LOKCTL)
CONMCK,
	XCALL FFILE (99,REDFIL,SWITCH)
	IF (SWITCH.EQ.1) GO TO ABORT
	XCALL OPENF (4,'I',REDFIL,ERROR,0,SWITCH)
	IF (SWITCH) GO TO NOCMP
	ON ERROR NOCMP
	READ (4,CONAM1,1)		;SSQ 8-5-03
;	READ (4,CONAM2,3)
	READ (4,CONAM3,3)		;5/09/86 PATCH UT7  J.T.
	READ (4,CONAM2,2)
	OFF ERROR

	clear xname			;SSQ 8-21-03
	do	incr i
	until (conam1(i,i) .ne. ' ')
	if (i.lt. 50) xname = conam1(i,50)
	CLOSE 4
;*************************************************************
;                      DISPLAY MAIN MENU
;************************************************************
MSMENU,
	DSPCOM = CMNME
	FCMENU =

;;;	XCALL OUTPT (01,01,2,'MCBA SYSTEM MASTER MENU')
;;;	XCALL OUTPT (1,1,2,'Sheet Metal Connectors, Inc.')

NORECV,
	XCALL OUTPT (1,1,2,XNAME)
	XCALL OUTPT (1,64,0,'COMPANY CODE ')
	XCALL OUTPT (0,0,0,DSPCOM)
	XCALL OUTPT (03,20,0,'PLEASE SELECT')
	ROW(3) = 4
;~START MENU
			INCR ROW(3)
			XCALL OUTPT (ROW(3),25,0,'1. CUSTOMER ORDER PROCESSING')
			INCR ROW(3)
			XCALL OUTPT (ROW(3),25,0,'2. INVENTORY MANAGEMENT')
			INCR ROW(3)
			XCALL OUTPT (ROW(3),24,0,'10. ACCOUNTS RECEIVABLE')
			INCR ROW(3)
			XCALL OUTPT (ROW(3),24,0,'11. ACCOUNTS PAYABLE')
			INCR ROW(3)
			XCALL OUTPT (ROW(3),24,0,'12. PAYROLL')
			INCR ROW(3)
			XCALL OUTPT (ROW(3),24,0,'13. GENERAL LEDGER')
			INCR ROW(3)
			XCALL OUTPT (ROW(3),24,0,'15. PURCHASE ORDER AND RECEIVING')
;~END MENU
			PAKFLG(19) = 1
			INCR ROW(3)
			XCALL OUTPT (ROW(3),22,0,'<PF1> SYSTEM FUNCTIONS MENU')

PAKAGE,
	CTL = 3
	ROW (3) = STROW
	CALL INPUT
	GO TO (PAKAGE,LOGOUT,PAKAGE,PAKAGE,PAKAGE,DPMENU), INXCTL
	PGM = ENTRY
	IF (PGM.LT.1.OR.PGM.GT.MAXFIL) GO TO PAKAGE
	IF (PAKFLG(PGM).NE.1) GOTO NOPGM	;5/09/86 PATCH UT7  J.T.

	AUTH = %CHK_SEC ('UT','MSMENU', PGM)
	IF (.NOT. AUTH) 
		BEGIN
		XCALL MESAG ('NOT AUTHORIZED FOR THIS SELECTION',1)
		GOTO NORECV
		END

	XCALL WATE (3)
	IF (PGM.NE.17.AND.PGM.NE.18.AND.PGM.NE.19)
	BEGIN
	  SWITCH = 3
	  XCALL PGCHN (PROGNM(PGM),SWITCH)
	  IF (SWITCH.EQ.1) GO TO NOPGM
	END
	CLOSE 1
	CLOSE 2
	CLOSE 3
	GO TO CHAIN
NOPGM,
	XCALL MESAG ('?UT-PACKAGE NOT INSTALLED OR PACKAGE LOGICAL NOT SET',1)
	GO TO (PAKAGE,SYSFCN), FCMENU + 1
DPMENU,
	IF (ACCES(99).EQ.'U') GO TO DSPDPM	;SECURITY ON DPSECU FILE
	XCALL MESAG ('?UT-SYSTEM FUNCTIONS NOT AVAILABLE',1)
	GO TO PAKAGE
DSPDPM,
	FCMENU = 1
;;;	XCALL OUTPT (01,01,2,'MCBA SYSTEM MASTER MENU')
;;;	XCALL OUTPT (1,1,2,'Sheet Metal Connectors, Inc.')
	XCALL OUTPT (1,1,2,XNAME)
	XCALL OUTPT (02,01,0,'SYSTEM FUNCTIONS')
	XCALL OUTPT (04,20,0,'PLEASE SELECT')
	XCALL OUTPT (06,25,0,'1. SECURITY SYSTEM')
	XCALL OUTPT (08,25,0,'2. COMPANY FILE MAINTENANCE')
	XCALL OUTPT (10,25,0,'3. MESSAGE FILE MAINTENANCE')
	XCALL OUTPT (12,25,0,'4. PROGRAM RESTART')
	XCALL OUTPT (14,25,0,'5. EXPAND OR REDUCE FILE SIZE')
	XCALL OUTPT (16,25,0,'6. CLEAR FILE STATUS FLAGS')
	XCALL OUTPT (18,25,0,'7. FILE CONTROL RECORD INQUIRY')
	XCALL OUTPT (20,25,0,'8. RESTART WORK ORDER PRINT')
SYSFCN,
	CTL = 4
	CALL INPUT
	GO TO (DSPDPM,MSMENU,DPMENU,DPMENU,DPMENU,MSMENU), INXCTL
	PGM = ENTRY
	IF (PGM.LT.1 .OR. PGM.GT.8) GO TO SYSFCN
	XCALL WATE (3)
	SWITCH = 3
	XCALL PGCHN (BAKPGM(PGM),SWITCH)
	IF (SWITCH.EQ.1) GO TO NOPGM
	CLOSE 1
	CLOSE 2
	CLOSE 3
	GO TO CHAIN
;************************INPUT**************************
INPUT,
	ENTRY =
	XCALL INPUT (ROW(CTL),COL(CTL),MAX(CTL),MIN(CTL),TYPE(CTL),FCTYPE(CTL),
&	ENTRY, INXCTL)
	RETURN
;*************************LOGOFF***********************
ENDOFF,
	XCALL WATE (3)

;;; remove 9-30-98 ssq
;;;	LOKCTL = 1
;;;	XCALL IO (13,MESARA,TERM,READ,LOKCTL)
;;;	CMPCOD =
;;;	CMPPOS =
;;;	SECREC =
;;;	SPOLDV =
;;;	LOKCTL = 1
;;;	XCALL IO (13,MESARA,TERM,WRITE,LOKCTL)
;;; remove 9-30-98 ssq
CLOSE1,
	CLOSE 1
	CLOSE 2
	CLOSE 13
	CLOSE 14
	XCALL FLAGS (01000000)
	XCALL OUTPT (1,1,2,'\')
	XCALL OUTPT (12,1,0,ASTRIX)
	DISPLAY (15,' END OF PROGRAM ',ASTRIX)
	XCALL OUTPT (1,1,0,'\')
	CLOSE 15
	STOP	'UT:SYSLOG'
	STOP
;****************UNSCRAMBLE PASSWORD*********
SCRMBL,
	HPWORD(1) = WPWORD(1)
	HPWORD(2) = WPWORD(3)
	HPWORD(3) = WPWORD(5)
	HPWORD(4) = WPWORD(7)
	WPWORD(1) = WPWORD(2)
	WPWORD(3) = WPWORD(4)
	WPWORD(5) = WPWORD(6)
	WPWORD(7) = WPWORD(8)
	WPWORD(2) = HPWORD(1)
	WPWORD(4) = HPWORD(2)
	WPWORD(6) = HPWORD(3)
	WPWORD(8) = HPWORD(4)
	HPWORD(1) = WPWORD(1)
	HPWORD(2) = WPWORD(2)
	WPWORD(1) = WPWORD(8)
	WPWORD(2) = WPWORD(9)
	WPWORD(8) = HPWORD(1)
	WPWORD(9) = HPWORD(2)
	RETURN
CODE,
	SUB =
CODELP,
	IF (SUB.GE.9) RETURN
	INCR SUB
	XCALL DECML (WPWORD(SUB),DPNTR)
	DPNTR = DPNTR - 31
	WPWORD (SUB) = CODE (DPNTR)
	GO TO CODELP
;*******************ERROR MESSAGES************************
NODATE,
	XCALL OUTPT (1,1,2,'\')
	XCALL OUTPT (12,16,0,
&		'?UT-NO SYSTEM DATE - PLEASE SET THE SYSTEM DATE AND TIME')
	GO TO ABORT
BADOPN,
	XCALL OUTPT (2,1,2,'\')
        XCALL OUTPT (12,16,0,'?UT-')
	IF (SWITCH.NE.1) GO TO BADERR
	DISPLAY (15,EXE,' FILE NOT FOUND ON DEVICE ASSIGNED UT:',7)
	GO TO ABORT
BADERR,
	DISPLAY (15,'FATAL DIBOL ERROR # ',ERROR,' ON FILE ',EXE,7)
	GO TO ABORT
NOCMP,
	CLOSE 4
	INXCTL = 7
	CMPFIL = REDFIL
	XCALL MESAG
&	(CMPMSG,INXCTL)
	IF (INXCTL) GO TO GETPAS
	SWITCH = 9
ABORT,
	CLOSE 13
	CLOSE 14
	CLOSE 3
	CLOSE 2
	CLOSE 1
	IF (SWITCH.EQ.9) XCALL PGCHN ('UT:CONMNT',2)
	STOP
;****************************************
CHAIN,
	LOKCTL = 1
	XCALL IO (13,MESARA,TERM,READ,LOKCTL)
	IF (FCMENU) GO TO CLRMSG
	DPNTR =
LOOP,
	INCR DPNTR
	IF (DPNTR.GT.4) GOTO CLRMSG
	MESSGE (DPNTR) =
	GOTO LOOP
CLRMSG,
	IF (REENTR) GO TO NOUPD
	CMPCOD = COMPNM
	DEFPTR = DEFPRT
	SPLSZE = SPLSIZ
	SPOOLS = SPLFLG
	SPOLDV = SPLDEV
NOUPD,
	LOKCTL = 1
	XCALL IO (13,MESARA,TERM,WRITE,LOKCTL)
	CLOSE 13
	SWITCH = 1
	IF (FCMENU) SWITCH = 2
	IF (FCMENU.EQ.0) XCALL PGCHN (PROGNM(PGM),SWITCH)
	IF (FCMENU) XCALL PGCHN (BAKPGM(PGM),SWITCH)
END
