SUBROUTINE OPENF	;(CHANEL,MODE,FLSPEC,BLOCKS,RECSIZ,SWITCH)
;
;  OPENF / UTL 
;
;
;		::PCPYUTL.DEF::
;*****************************************************************************
;		SECURITY SYSTEM UTILITIES AND SUBROUTINES 
;		DIBOL FOR RT-11
;		
;		RELEASED: JANUARY 1, 1985
;*****************************************************************************
;
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984,
;		1985, MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		SUBROUTINE TO HANDLE ALL NON-DEVICE OPENING OF FILES WITHIN
;		PACKAGES
;
	CHANEL	,D	;CHANNEL ON WHICH TO PERFORM OPERATION
	MODE	,A	; MODE IN WHICH OPERATION IS MADE
			; 	I = OPEN IN INPUT
			;	U = OPEN IN UPDATE
			;	O = OPEN IN OUTPUT USING BLOCKS AS SIZE
			;	D = DELETE SPECIFIED FILE
	FLSPEC	,A	; FILE SPECIFICATION (UP TO 14 CHARACTERS LONG)
	BLOCKS	,D	; INCOMING:
			; NUMBER OF BLOCKS TO ALLOCATE FOR OPENING A FILE IN
			; OUTPUT MODE (IGNORED FOR OTHER MODES).
			; IF BLOCKS IS ZERO, LET THE OPERATING SYSTEM HANDLE
			; BLOCK SIZE ALLOCATION
			;
			; OUTGOING:
			; CONTAINS ERROR MESSAGE FOR SWITCH = 3
			;
	RECSIZ	,D	; RECORD LENGTH (NOT INCLUDING LINE TERMINATOR)
			; (IGNORED EXCEPT FOR OUTPUT MODE)
			; NOT USED IN RT11/TSX+ BUT REQUIRED FOR COMMON SOURCE
	SWITCH	,D	; STATUS SWITCH
			;	0 = OPEN/DELETE PERFORMED SUCCESSFULLY
			;	1 = FILE NOT FOUND (INPUT AND UPDATE)
			;	2 = NOT ENOUGH ROOM ON DEVICE (OUTPUT)
			;	3 = SOME OTHER DIBOL ERROR (RETURNED IN 
			;	    "BLOCKS")

RECORD REDFIL		;WORKING STORAGE FOR INCOMING FILE SPEC
	SAVFIL	,A14
		,A1,	'['
	SIZE	,D5
		,A1,	']'
RECORD
	ERROR	,D3
	ERLINE	,D5
	FILBL	,A21
	FILB1	,A14
	LNGTH	,D2	;LENGTH COUNTER FOR DESTINATION FILE SPEC
	SUB	,D2	;LENGTH COUNTER FOR SOURCE FILE SPEC
PROC
	SWITCH =
	SAVFIL = FLSPEC
	SIZE = BLOCKS
	CALL SQUEZ
	ON ERROR ERROR
	IF (MODE.EQ.'I') OPEN (CHANEL,I,FILB1)
	IF (MODE.EQ.'U') OPEN (CHANEL,U,FILB1)
	IF (MODE.EQ.'O') OPEN (CHANEL,O,FILBL)
	IF (MODE.EQ.'D') XCALL DELET (CHANEL,FILB1)
	OFF ERROR
GOBACK,
	RETURN
;*******************SQUEEZE OUT SPACES*************************
SQUEZ,
	SUB =
	LNGTH =
	FILBL =
	FILB1 =
LOOP,
	IF (SUB.GE.21) GO TO CLRLBL
	INCR SUB
	IF (REDFIL(SUB,SUB).EQ.' ') GO TO LOOP
	INCR LNGTH
	FILBL (LNGTH,LNGTH) = REDFIL (SUB,SUB)
	IF (SUB.LE.14) FILB1 (LNGTH,LNGTH) = REDFIL (SUB,SUB)
	GO TO LOOP
CLRLBL,
	RETURN
;**********************ERROR ANALYSIS*********************************
ERROR,
	OFF ERROR
	XCALL ERROR (ERROR,ERLINE)
	IF (ERROR.EQ.18) SWITCH = 1	;FILE NOT FOUND
	IF (ERROR.EQ.24) SWITCH = 2
	IF (ERROR.NE.18 .AND. ERROR.NE.24) GO TO OTHER
	GO TO GOBACK
OTHER,
	BLOCKS = ERROR			;SET "BLOCKS" FIELD TO ERROR NUMBER
	SWITCH = 3
	GO TO GOBACK
END
