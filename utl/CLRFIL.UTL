;  CLRFIL / UTL 
;
;
;		::PCPYUTL.DEF::
;*****************************************************************************
;		SECURITY SYSTEM UTILITIES AND SUBROUTINES 
;		DIBOL FOR RT-11
;		
;		RELEASED: JANUARY 1, 1985
;*****************************************************************************
;
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984,
;		1985, MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		PROGRAM TO CLEAR STATUS FLAGS IN THE DEVICE FILE
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD
	CNTS	,20D2
RECORD,X
	CNT1	,D18
	CNT2	,D18
	CNT3	,D4
COMMON
			.INCLUDE 'DEF:COM001.DEF'
RECORD DEVICE
			.INCLUDE 'DEF:DEV001.DEF'
RECORD COMPAN
			.INCLUDE 'DEF:CMP001.DEF'
RECORD SECURE
			.INCLUDE 'DEF:SEC004.DEF'
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
		,A1
	FCTYPE	,A3
RECORD
	PKARY	,400D3	;PACKAGE ARRAY LOADED FROM REC #2 OF SECURE.
			;ACCESSED BY THE ARYCNT COUNTER (=(PAKAGE # *20)-20)
			;=20 PACKAGES X 20 FILES MAX PER PACKAGE

	PKCOD	,20A1,	'E','C','D','I','H','O','L','K','M','A','F','G','B',
&			'J','N','P','Q','R','S','T'
					;PACKAGE CODES IN REC #2 OF SECURE
					;SORTED BY ORDER OF DISPLAY (PACKAGE
					;CODES COME FROM SPOOL CODES.
	PCKGS	,20A3,	'COP','IM ','BMP','SFC','JC ','LP ','SPC','SPR','MRP',
&			'AR ','AP ','PR ','GL ','AD ','POR','UTL','   ','   ',
&			'   ','   '
					;PACKAGES CORRESPONDING TO THE ABOVE
					;PACKAGE CODES
	MAXPK	,D2,	18		;MAXIMUM NUMBER OF ALLOWED PACKAGES
	ARYCNT	,D3
	RECNO	,D3
	BLANKS	,A6
	ENTRY	,A4
	FILCNT	,D3
	INXCTL	,D1
	CNGCTL	,D1
	WHATNO	,D2
	CPNTR	,D1
	SPNTR	,D2
	FILFLG	,D1
	STATLT	,A9
	SCMPNY	,A3
	SSYST	,A3
	SFILNM	,D3
	SSTAT	,D2
	SUB	,D3
	V	,D1
	ALPHA	,A2
	N	,D2
PROC
	XCALL TERID
	XCALL OUTPT (1,1,2,'CLEAR FILE STATUS FLAGS')
	XCALL WATE (4)
	OPEN (13,I,'UT:COMPNY.DDF')
	READ (13,COMPAN,1)
	CLOSE 13
	OPEN (13,I,'UT:SECURE.DDF')
	READ (13,SECURE,2)
	CLOSE 13
LODFIL,
	FILCNT =
	CNT1 =
	CNT2 =
	CNT3 =
LDLUP1,
	IF (FILCNT.GE.200) GO TO LODDUN
	INCR FILCNT
	SUB =
LDLUP2,
	IF (SUB.GE.MAXPK) GO TO LDLUP1
	INCR SUB
	IF (FILPAK(FILCNT).EQ.PKCOD(SUB)) GO TO SETARY
	GO TO LDLUP2
SETARY,
	ARYCNT = (SUB*20)-20		;20 FILES PER PACKAGE MAX
	IF (CNTS(SUB).GE.20) GO TO LDLUP1
	INCR CNTS (SUB)
	PKARY (ARYCNT+CNTS(SUB)) = FILCNT
	GO TO LDLUP1
LODDUN,
	GO TO DISPLA
DISPLA,
	CNGCTL =
	XCALL OUTPT (1,1,2,'CLEAR FILE STATUS FLAGS')
	XCALL OUTPT (10,20,0,'1. COMPANY CODE')
	XCALL OUTPT (12,20,0,'2. PACKAGE')
	XCALL OUTPT (14,20,0,'3. FILE NUMBER')
	XCALL OUTPT (16,20,0,'4. STATUS')
CMPCOD,
	CPNTR =
	CTL = '10,37,03,00,A ,FE '
	CALL INPUT
	SCMPNY = ENTRY
	GO TO (DISPLA,END), INXCTL
	IF (SCMPNY.EQ.BLANKS) XCALL OUTPT (10,37,0,'ALL')
	IF (SCMPNY.EQ.BLANKS) GO TO CMPCD2
	CALL FINDCO
	IF (CPNTR.GT.8) GO TO CMPCOD
CMPCD2,
	GO TO (ANYCNG), CNGCTL
SYST,
	SPNTR =
	CTL = '12,37,03,00,A ,F '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	SSYST = ENTRY
	IF (SSYST.EQ.BLANKS) XCALL OUTPT (12,37,0,'ALL')
	IF (SSYST.EQ.BLANKS) GO TO SYST2
	CALL FINDSY
	IF (SPNTR.GT.MAXPK) GO TO SYST
SYST2,
	GO TO (ANYCNG), CNGCTL
FILNUM,
	XCALL OUTPT (14,37,1,'\')
	CTL = '14,37,03,00,# ,F  '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	SFILNM = ENTRY
	IF (SFILNM.EQ.0) GO TO ALLFLS
	IF (SFILNM.GT.200) GO TO INVFIL
	CALL FINDFL
	IF (FILFLG.EQ.1) GO TO INVFIL
	IF (SFILNM.EQ.0.OR.SCMPNY.EQ.BLANKS) GO TO FILNM2
	XCALL OUTPT (14,51,0,'CURRENT STATUS = ')
	ALPHA = STATUS (CPNTR)
	DISPLAY (15,ALPHA)
FILNM2,
	GO TO (ANYCNG), CNGCTL
STAT,
	CTL = '16,37,02,00,# ,F  '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	SSTAT = ENTRY
	STATLT = 'CLEAR'
	IF (SSTAT.GT.0) STATLT = 'IN USE'
	IF (SSTAT.EQ.99) STATLT = 'PROTECTED'
	XCALL OUTPT (16,42,1,STATLT)
	GO TO ANYCNG
ALLFLS,
	XCALL OUTPT (14,37,1,'ALL')
	XCALL MESAG ('WARNING - THIS OPTION MAY CAUSE USAGE CONFLICTS',2)
	GO TO FILNM2
PROCES,
	XCALL WATE (4)
	IF (SFILNM.NE.0) GO TO ONLY1
	IF (SSYST.NE.BLANKS) GO TO ONESYS
	RECNO =
LOOP,
	INCR RECNO
	IF (RECNO.GT.200) GO TO PRCDUN
	GO TO SETST
ONESYS,
	SUB =
	ARYCNT = (SPNTR*20)-20		;START OF PARTICULAR COMPANY ARRAY
					;SPNTR POINTS TO PCKGS VALUE
LOOP2,
	INCR SUB
	IF (SUB.GT.20) GO TO PRCDUN
	IF (PKARY(ARYCNT+SUB).EQ.0) GO TO PRCDUN
	RECNO = PKARY (ARYCNT+SUB)
SETST,
	ONERROR SETST
	READ (14,DEVICE,RECNO)
	IF (SCMPNY.NE.BLANKS) STATUS (CPNTR) = SSTAT
	IF (SCMPNY.EQ.BLANKS) CALL ALLSTT
	WRITE (14,DEVICE,RECNO)
	OFFERROR
	IF (SSYST.NE.BLANKS) GO TO LOOP2
	GO TO LOOP
ONLY1,
	ONERROR ONLY1
	READ (14,DEVICE,SFILNM)
	IF (SCMPNY.NE.BLANKS) STATUS (CPNTR) = SSTAT
	IF (SCMPNY.EQ.BLANKS) CALL ALLSTT
	WRITE (14,DEVICE,SFILNM)
	OFFERROR
PRCDUN,
	GO TO (END), CNGCTL + 2
	GO TO DISPLA
ALLSTT,
	N =
ALLST2,
	INCR N
	IF (N.GT.8) RETURN
	STATUS (N) = SSTAT
	GO TO ALLST2
FINDCO,
	INCR CPNTR
	IF (CPNTR.GT.8) GO TO NOTFND
	IF (COMCOD(CPNTR).EQ.SCMPNY) RETURN
	GO TO FINDCO
NOTFND,
	XCALL MESAG ('?UT-INVALID COMPANY CODE',1)
	RETURN
FINDSY,
	INCR SPNTR
	IF (SPNTR.GT.MAXPK) GO TO NOTFN2
	IF (PCKGS(SPNTR).EQ.SSYST) RETURN
	GO TO FINDSY
NOTFN2,
	XCALL MESAG ('?UT-INVALID PACKAGE CODE',1)
	RETURN
FINDFL,
	FILFLG =
	ONERROR FINDFL
	READ (14,DEVICE,SFILNM)
	OFFERROR
	UNLOCK 14
	XCALL OUTPT (14,42,0,FILNAM)
	IF (FILNAM.EQ.BLANKS) FILFLG = 1
	RETURN
INVFIL,
	XCALL MESAG ('?UT-INVALID FILE NUMBER',1)
	GO TO FILNUM
END,
	CLOSE 14
	XCALL PGCHN('UT:MSMENU',2)
INPUT,
	XCALL INPUT(ROW,COL,MAX,MIN,TYPE,FCTYPE,ENTRY,INXCTL)
	RETURN
ANYCNG,
	CNGCTL = 2
	XCALL ANYCN (CNGCTL,WHATNO,1,4,0,0)
	GO TO (PROCES,PROCES,CNGBR,DISPLA), CNGCTL + 2
CNGBR,
	GO TO (CMPCOD,SYST,FILNUM,STAT), WHATNO
	WHATNO = -1
	GO TO ANYCNG
END
