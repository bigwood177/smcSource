;  PRTPAS / UTL 
;
;
;		::PCPYUTL.DEF::
;*****************************************************************************
;		SECURITY SYSTEM UTILITIES AND SUBROUTINES 
;		DIBOL FOR RT-11
;		
;		RELEASED: JANUARY 1, 1985
;*****************************************************************************
;
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984,
;		1985, MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		PRINT PASSWORDS IN SECURITY FILE
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD HDR
		,A16,	'PASSWORDS AS OF '
	DATE	,A8
		,A18,	' FOR COMPANY CODE '
	CMCODE	,A3
COMMON
			.INCLUDE 'DEF:COM001.DEF'
RECORD COMPNY
			.INCLUDE 'DEF:CMP001.DEF'
RECORD SECURE
			.INCLUDE 'DEF:SEC001.DEF'
RECORD ,X
			.INCLUDE 'DEF:SEC005.DEF'
RECORD BRACKS
			.INCLUDE 'DEF:SEC006.DEF'
RECORD
		,A30,	' !"#$%&''()*+,-./0123456789:;<='
		,A35,	'>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[\]^_`'
RECORD,X
	DECODE	,65A1
RECORD
		,A30,	'VHA. WIB/!XJC0"YKD1#ZLE2$[MF3%'
		,A35,	'\NG4&]O5''^P6(_Q7)`R8*S9+T:,U;-<=>?@'
RECORD,X
	CODE	,65A1
RECORD SPWORD
	WPWORD	,9A1
RECORD
	HPWORD	,9A1
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	BLANKS	,A3
	CNGCTL	,D1
	CNT	,D1
	ENTRY	,A3
	INXCTL	,D1
	LPSW	,D1
	LINCNT	,D3
	LOKCTL	,D1
	MSG	,A6
	MSGCTL	,D1
	PGCNT	,D3	,000
	PRTCTL	,D3
	PLINE	,A132
	COUNT   ,D2
	SUB	,D3
	READ	,D1,	0
	RECCNT  ,D3
	TODAY	,D6
	WHATNO	,D1
PROC
	CNPOS = -1
	XCALL TERID
	MSGCTL = 1
	XCALL SNMSG (MSG,MSGCTL)
	IF (MSGCTL.EQ.9) GO TO NOTRUN
	IF (MSG.NE.'PRTSEC') GO TO NOTRUN
	MSGCTL = 4
	XCALL SNMSG (MSG,MSGCTL)
	OPEN (1,I,'UT:SECURE.DDF')
	OPEN (2,I,'UT:COMPNY.DDF')
	LOKCTL = 1
	XCALL IO (2,COMPNY,1,READ,LOKCTL)
	XCALL RDATE (TODAY)
	DATE = TODAY, 'XX/XX/XX'
BEGIN,
	XCALL OUTPT (1,1,2,'SECURITY SYSTEM MAINTENANCE')
	XCALL OUTPT (2,1,0,'PRINT PASSWORDS')
	XCALL OUTPT (12,20,0,'PLEASE ENTER COMPANY CODE')
CMPCOD,
	XCALL INPUT (12,47,03,00,'A ','FE',ENTRY,INXCTL)
	GO TO (BEGIN,END), INXCTL
	IF (ENTRY.EQ.BLANKS) GO TO ALLCMP
	CMCODE = ENTRY
	CNT =
CHKLUP,
	IF (CNT.GE.8) GO TO NOTFND
	INCR CNT
	IF (CMCODE.NE.COMCOD(CNT)) GO TO CHKLUP
	GO TO ANYCNG
NOTFND,
	XCALL MESAG ('?UT-INVALID COMPANY CODE',1)
	GO TO BEGIN
ALLCMP,
	XCALL OUTPT (12,47,0,'ALL')
	CMCODE = 'ALL'
ANYCNG,
	CNGCTL = 6
	XCALL ANYCN (CNGCTL,WHATNO,0,0,0,0)
	GO TO (END,LPON,CMPCOD,BEGIN), CNGCTL + 2
LPON,
	SPLFIL (5,6) = 'UT'		;FOR SYSTEM QUEUE ONLY - NO SPOOLING
	LPSW = 3			;PRINTED ONLY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO END
	XCALL WATE (2)
	PLINE = HDR
	CALL PRINT
	XCALL LINFD (1)
	PLINE (1,8) = 'PASSWORD'
	PLINE (12,15) = 'CODE'
	CALL PRINT
	PLINE (1,9) = '========='
	PLINE (12,15) = '===='
	CALL PRINT
	XCALL LINFD (1)
	RECCNT = 2
READ,
	INCR RECCNT
	LOKCTL = 1
	XCALL IO (1,SECURE,RECCNT,READ,LOKCTL)
	IF (DFLSEC.EQ.']]]DEL') GO TO READ
	IF (SECURE.EQ.BRACKS) GO TO EOF
	IF (ENTRY.NE.BLANKS .AND. COMPNM.NE.CMCODE) GO TO READ
	SPWORD = PASWRD
	HPWORD(1) = WPWORD(8)
	HPWORD(2) = WPWORD(9)
	HPWORD(3) = WPWORD(3)
	HPWORD(4) = WPWORD(4)
	HPWORD(5) = WPWORD(5)
	HPWORD(6) = WPWORD(6)
	HPWORD(7) = WPWORD(7)
	HPWORD(8) = WPWORD(1)
	HPWORD(9) = WPWORD(2)
	WPWORD(1) = HPWORD(2)
	WPWORD(2) = HPWORD(1)
	WPWORD(3) = HPWORD(4)
	WPWORD(4) = HPWORD(3)
	WPWORD(5) = HPWORD(6)
	WPWORD(6) = HPWORD(5)
	WPWORD(7) = HPWORD(8)
	WPWORD(8) = HPWORD(7)
	WPWORD(9) = HPWORD(9)
	COUNT = 
	CALL LTRLUP
	PLINE (1,9) = SPWORD
	PLINE (12,14) = COMPNM
	LINCNT =
	CALL PRINT
	GO TO READ
LTRLUP,
	IF (COUNT.GE.9) RETURN
	INCR COUNT
	SUB =
DECLUP,	
	IF (SUB.GE.65) RETURN
	INCR SUB
	IF (WPWORD(COUNT).EQ.CODE(SUB)) GO TO GOTLTR
	GO TO DECLUP
GOTLTR,
	WPWORD (COUNT) = DECODE (SUB)
	GO TO LTRLUP
EOF,
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	PGCNT =
	LINCNT = 0
	GO TO BEGIN
END,
	CLOSE 1
	CLOSE 2
	XCALL PGCHN ('UT:SECMNT',2)
NOTRUN,
	XCALL MESAG ('?UT-MUST RUN THIS PROGRAM FROM SECURITY SYSTEM',1)
	XCALL PGCHN ('UT:SECMNT',2)
PRINT,
	XCALL LPOUT (LINCNT,PGCNT,PLINE,'NO TITLE','NO HDR',' ',' ',
&		'NO LEGEND',' ',' ',0,132,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
END
