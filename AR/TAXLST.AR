;TAXLST.AR
;
;	CUSTOMER LIST BY TAX CODE
;
; 	5-23-18: ssq convert cusmas to isam

RECORD CUSMAS		; 
		.INCLUDE 'DEF:RD001A.DEF'

RECORD	EMAILC
	.INCLUDE 'DEF:RD084A.DEF'

RECORD TMPIDX		; 
	T_TAX	,A3
	T_NAME	,A25
	T_CUSNO	,D6
	
;;;RECORD TMPIDX		; 
;;;	T_TAX	,A3
;;;	T_NAME	,A25
;;;	T_REC	,D5

RECORD DEVICE
	PHYDEV	,A3
		,A8
RECORD TITLE
		,A26,	'ALPHABETICAL CUSTOMER LIST'
RECORD HDR1
		,A1
		,A4,	'CUST'
		,A119
		,A3	,'TAX'
		,A2
		,A3
RECORD HDR2
		,A2
		,A2,	'NO'
		,A3
		,A4,	'NAME'
		,A22
		,A11,	'ADDRESS - 1'
		,A15
		,A11,	'ADDRESS - 2'
		,A11
		,A4,	'CITY'
		,A12
		,A2,	'ST'
		,A2
		,A3,	'ZIP'
		,A10
		,A5,	'PHONE'
		,A5
		,A3,	'CDE'
		,A2
		,A3
record	data
		,a1024	

RECORD	VARS
	OPNOK	,D1
	CHN084	,D2
	tabfil	,a*,	'c:\smc\spool\taxlst.xls'
	tab	,a1
	len	,d8,	00001024
	field	,a512
	ln	,d6
	STCOD	,A3
	ENCOD	,A3
	A3	,A3
	CUSCNT	,D5
	ENTRY	,A30
	INXCTL	,D1
	CNGCTL	,D1
	WHATNO	,D2
	PLINE	,A132
	LINCNT	,D2,	60
	LPARG	,D1
	LPSW	,D1
	PGCNT	,D3
	RPTNUM	,D3
	PRTTYP	,A1
	SPLFIL	,A14
	SWITCH	,D1,	1
	V	,D1
	READ	,D1,	0
	WRITE	,D1,	1
	LOKCTL	,D1
PROC
	XCALL TERID (V)
	xcall ascii(9,tab)
DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,'CUSTOMER LIST BY TAX CODE',1)
	XCALL OUTPT (4,4,1,'1. START TAX CODE',1)
	XCALL OUTPT (6,4,1,'2. END   TAX CODE',1)
STCOD,
	XCALL INPUT (4, 23, 03, 00, 'AE', ENTRY, INXCTL, 1)
	GOTO (DISPLA,ENDOFF),INXCTL
	STCOD = ENTRY(1,3)
	IF (STCOD .EQ. A3)
		BEGIN
		ENCOD = 'ZZZ'
		XCALL OUTPT (4,23,1,'ALL',1)
		XCALL OUTPT (6,23,1,A3,1)
		GOTO ANYCNG
		END
	GOTO (ANYCNG), CNGCTL
ENCOD,
	XCALL INPUT (6,23,03,00,'AE', ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	ENCOD = ENTRY (1,3)
	IF (ENCOD .EQ. A3) ENCOD = STCOD
	XCALL OUTPT (6,23,1,ENCOD,1)
ANYCNG,
	XCALL ANYCN (CNGCTL, WHATNO)
	GOTO (PROCES, CNGBR), CNGCTL +1
CNGBR,
	GOTO (STCOD, ENCOD),WHATNO
	GOTO ANYCNG

PROCES,
	IF (STCOD .GT. ENCOD) GOTO ANYCNG
	XCALL OUTPT (2,1,0,'BUILD INDEX',1)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO ENDOFF

	READ (1, CUSMAS, ^FIRST) [ERR=TLOOP]	;HEADER
;;;	T_REC = 1
TLOOP,
;;;	INCR T_REC
	LOKCTL = 
	XCALL IOS (1,CUSMAS,READ,LOKCTL)
	IF (LOKCTL .NE. 0) GOTO T_EOF
;;;	IF (CUSMAS .EQ. ']]]]]]') GOTO T_EOF
	IF (NAME .EQ. ']]]DEL') GOTO TLOOP
	IF (TAXFLG.LT.STCOD .OR. TAXFLG.GT.ENCOD) GOTO TLOOP

	T_CUSNO = CUSNO
	T_TAX = TAXFLG
	T_NAME = NAME
	WRITES (8,TMPIDX)
	GOTO TLOOP
T_EOF,
	CLOSE 8
	SORT (IN='SPL:TAXLST.DAT', RECORD=TMPIDX, KEY=(T_TAX,T_NAME))
	OPEN(8,I,'SPL:TAXLST.DAT')

	SPLFIL = 'SPL:TAXLST.SPL'
	LPSW = 1
	XCALL AR_LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO END1
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)

	open (24,o,'spl:TAXLST.XLS')
	DATA = 'CUST #' +TAB+ 'NAME' +TAB+ 'ADD1' +TAB+ 'ADD2' +TAB+ 'CITY' +TAB+
&		'STATE' +TAB+ 'ZIP' +TAB+ 'PHONE' +TAB+ 'TAXFLG' +TAB
	LN = %TRIM (data)
	WRITES (24, DATA(1,LN) )
	XCALL OUTPT (2,1,60,'SPL:TAXLST.XLS',1)
LOOP,
	LOKCTL = 1
	XCALL IOS (8,TMPIDX,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOF
	LOKCTL = 1
	XCALL ISIO (1,CUSMAS,T_CUSNO,READ,LOKCTL)
	call get_email

	PLINE (1,6) = CUSNO
	PLINE (8,32) = NAME
	PLINE (34,58) = ADD1
	PLINE (60,80) = ADD2
	PLINE (82,96) = CITY
	PLINE (98,99) = STATE
	PLINE (101,110) = ZIP
	PLINE (112,123) = PHONE, 'ZZZ-ZZZ-ZZZZ'
	PLINE (126,128) = TAXFLG
	CALL PRINT
	PLINE (8,48) = E_MAIL
	CALL PRINT
	CALL PRINT
	INCR CUSCNT

	clear data
	field = cusno,	'ZZZZZZ' [left]
	call tab_d
	field = name
	call tab_d
	field = add1
	call tab_d
	field = add2
	call tab_d
	field = city
	call tab_d
	field = state
	call tab_d
	field = zip
	call tab_d
	field = e_mail
	call tab_d
	field = phone, 'ZZZ ZZZ ZZZZ' [left]
	call tab_d
	field = taxflg
	call tab_d

	ln = %trim(data)
	writes (24, data(1,ln) )

	GO TO LOOP
PRINT,
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,'NO HDR',
&		'NO LEGEND',' ',' ',0,132,132,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;--------------------------------------------------------
get_email,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	clear emailc
	e_cust = cusno
	find (chn084, emailc, e_key) [err=ge_loop]
ge_loop,
	xcall ios (chn084, emailc, read, lokctl)
	if (lokctl .ne. 0) goto ge_eof
	if (e_cust .ne. cusno) goto ge_eof
	if (e_dflt .ne. 9) goto ge_loop
ge_eof,
	if (e_cust .ne. cusno) e_mail = 'no email'

	return
;------------------------------------------------
tab_d,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	xcall tabd (data, len, field, tab)
	return
;------------------------------------------------
EOF,
	XCALL LINFD (1)
	LINCNT = LINCNT - 1
	PLINE (1,6) = CUSCNT, 'ZZ,ZZX'
	PLINE (8,24) = 'CUSTOMERS ON FILE'
	CALL PRINT
	XCALL AR_LPOFF (LPSW,SPLFIL,PGCNT)
	XCALL WATE (3,V)
	close 24
	if (cuscnt .gt. 0) xcall spawn (tabfil)

ENDOFF,

END1,
	CLOSE 1
	CLOSE CHN084
INU001,
;;;	XCALL FILES (2,'I',02,4)
END,
	CLOSE 8		;DELETE THE WORK FILE
	XCALL PGCHN ('AR:CUSMNU',1)
OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (1,'SI',01,SWITCH)		;FILE # 01 -- CUSMAS FILE
	IF (SWITCH.EQ.9) RETURN

	SWITCH = 5
	XCALL FILES (2,'SI',084, SWITCH)	;FILE # 84 -- EMAIL.ISM
	IF (SWITCH .EQ. 9) RETURN
	CHN084 = 2

	OPEN(8,O,'SPL:TAXLST.DAT')
	OPNOK = 1
	RETURN

END

