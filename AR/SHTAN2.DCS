; SHTAN2.AR - sales analysis by day
;
;	10/26/93 DS
;
RECORD ORDHST
		;  CUSTOMER ORDER HEADER HISTORY FILE -  RD176A.DEF
		;  RECORD SIZE = 425	FILE # 176
	OORDNO	,D6	; ORDER NUMBER
	OLOC	,A2	; ORDER LOCATION
	OCUSCD	,A2	; CUSTOMER TYPE CODE
	OORDDT	,D6	; ORDER DATE (MM/DD/YY)
	OCUSNO	,D6	; CUSTOMER NUMBER
	OCUSNM	,A25	; CUSTOMER NAME
	OSHPTO	,D4	; SHIP-TO NUMBER
	OSHPNM	,A30	; SHIP-TO-NAME
	OSHAD1	,A30	; SHIP-TO ADDRESS LINE 1
	OSHAD2	,A30	; SHIP-TO ADDRESS LINE 2
	OSHAD3	,A30	; SHIP-TO ADDRESS LINE 3
	OPONO	,A10	; CUSTOMER'S PURCHASE ORDER NUMBER
	OJOBNO	,A10	; JOB NUMBER
	OCLPPD	,A1	; COLLECT OR PREPAID CODE
	OTERMS	,A1	; TERMS OF PAYMENT CODE
	OTRMSD	,D4	; TERMS DISCOUNT PERCENTAGE (XX.XX%)
	ODSCAL	,D6	; TERMS DISCOUNT ALLOWED ($X,XXX.XX)
	OSHVIA	,A1	; SHIP VIA CODE
	OINVNO	,D6	; INVOICE NUMBER
	OINVDT	,D6	; INVOICE DATE (MM/DD/YY)
	OSALE	,D8	; AMOUNT OF SALE ($XXX,XXX.XX)
	OARACT	,D7	; A/R ACCOUNT NUMBER (XXXX-XXX)
	OMISC	,D6	; MISCELLANEOUS CHARGE AMOUNT ($X,XXX.XX)
	OMSACT	,D7	; MISCELLANEOUS CHARGES ACCOUNT NUMBER (XXXX-XXX)
	OTAX	,3D7	; SALES TAX AMOUNTS ($XX,XXX.XX)
	OTXACT	,3D7	; SALES TAX ACCOUNT NUMBERS (XXXX-XXX)
	OFRGHT	,D6	; FREIGHT CHARGES AMOUNT ($X,XXX.XX)
	OFRACT	,D7	; FREIGHT CHARGES ACCOUNT NUMBER (XXXX-XXX)
	ODISC	,D2	; ORDER DISCOUNT PERCENTAGE (XX%)
	OSLMAN	,D2	; SALESMAN NUMBER
	OCOMNT	,2A35	; ORDER COMMENTS
	OCOST	,D8	; COST OF ORDER ($XXX,XXX.XX)
	OCOMDU	,D7	; COMMISSION DUE ($XX,XXX.XX)
	OFLAG	,D1	; SELECTED FOR BILLING FLAG (0=NOT SELECTED,1=SELECTED
			;                            2=INVOICE PRINTED)
	ORDTYP	,A1	; ORDER TYPE
	ORDSEQ	,D3	; ORDER SEQUENCE
	OPROMD	,D6	; PROMISED DATE		;;;
	OSHDAT	,D6	; SHIP DATE		;;;
	OTAXFL	,A3	; TAX FLAG		;;; 5/6/93
		,A17	; UNUSED
;:
RECORD ARRAYS
	AOTAXF	,75A4	; TAX FLAG
	AOSALE	,75D9	; AMOUNT OF SALE ($XXX,XXX.XX)
	AOMISC	,75D8	; MISCELLANEOUS CHARGE AMOUNT ($X,XXX.XX)
	AOTAX1	,75D8	; SALES TAX AMOUNTS ($XX,XXX.XX)
	AOTAX2	,75D8	; SALES TAX AMOUNTS ($XX,XXX.XX)
	AOTAX3	,75D8	; SALES TAX AMOUNTS ($XX,XXX.XX)
	AOFRGHT	,75D8	; FREIGHT CHARGES AMOUNT ($X,XXX.XX)
	AINVCT	,75D6	; INVOICE COUNT
RECORD SUBTO
	STFL	,A4	; TAX FLAG
	SSL	,D9	; AMOUNT OF SALE ($XXX,XXX.XX)
	SMS	,D8	; MISCELLANEOUS CHARGE AMOUNT ($X,XXX.XX)
	STX1	,D8	; SALES TAX AMOUNTS ($XX,XXX.XX)
	STX2	,D8	; SALES TAX AMOUNTS ($XX,XXX.XX)
	STX3	,D8	; SALES TAX AMOUNTS ($XX,XXX.XX)
	SFR	,D8	; FREIGHT CHARGES AMOUNT ($X,XXX.XX)
	STL	,D9	; TOTAL
	SCNT	,D6	; INVOICE COUNT
RECORD TOTTO
	TTFL	,A4	; TAX FLAG
	TSL	,D9	; AMOUNT OF SALE ($XXX,XXX.XX)
	TMS	,D8	; MISCELLANEOUS CHARGE AMOUNT ($X,XXX.XX)
	TTX1	,D8	; SALES TAX AMOUNTS ($XX,XXX.XX)
	TTX2	,D8	; SALES TAX AMOUNTS ($XX,XXX.XX)
	TTX3	,D8	; SALES TAX AMOUNTS ($XX,XXX.XX)
	TFR	,D8	; FREIGHT CHARGES AMOUNT ($X,XXX.XX)
	TTL	,D9	; TOTAL
	TCNT	,D6	; INVOICE COUNT
RECORD TMPIDX
	TFL	,A5
	SL	,D12
	MS	,D10
	TX1	,D10
	TX2	,D10
	TX3	,D10
	FR	,D10
	TL	,D12
	CNT	,D6
	PFLAG	,D1
RECORD ALINE
	ATFL	,A5
	ASL	,A15
	AMS	,A12
	ATX1	,A12
	ATX2	,A12
	ATX3	,A12
	AFR	,A12
	ATL	,A15
	ACNT	,A6
RECORD LINES
		,A5
		,A15	,' ------------- '
		,A12	,' ---------- '
		,A12	,' ---------- '
		,A12	,' ---------- '
		,A12	,' ---------- '
		,A12	,' ---------- '
		,A15	,' ------------- '
		,A6	,' -----'
RECORD HDR1
		,A5	,' TFL '
		,A15	,'          SALE '
		,A12	,'       MISC '
		,A12	,'       TAX1 '
		,A12	,'       TAX2 '
		,A12	,'       TAX3 '
		,A12	,'        FRT '
		,A15	,'           TTL '
		,A6	,'   CNT'
RECORD LEG1
		,A12
		,A14,'REPORT MONTH: '
	RPTMON	,D2
RECORD	,X
		,A14
	ARPTMON	,A2
RECORD
	NUMASK	,A6,'ZZ,ZZX'
	DLMASK	,A15,'ZZZ,ZZZ,ZZZ.XX-'
	MON	,D2
	DFLD	,D6
	ALPHA	,A4
	I	,D2
	TODAY	,D6
	FILNAM	,A14
	V	,D1
	SWITCH	,D1
	INXCTL	,D1
	ENTRY	,A2
	PLINE	,A132
	LINCNT	,D2	,62
	PGCNT	,D3
	PRNTYP	,A1
	RPTNUM	,D3
	LPSW	,D1
	SPLFIL	,A14
	TITLE	,A31	,'SHIP-TO SALES TAX CODE ANALYSIS'	
	RECNO	,D5
	LSTTFL	,A4
	LOKCTL	,D1
	READ	,D1	,0
	WRITE	,D1	,1
	PASS	,D1	,0
	PCNT	,D1	,0
	DAY	,D2
PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'SHIP TO SALES AND TAX ANALYSIS - BY STATE',1)
	SWITCH = 1
	XCALL FILES (4,'I',176,SWITCH)		;FILE 176 - ORDHST FILE
	IF (SWITCH.EQ.9)
	BEGIN
	  XCALL MESAG ('ORDHST FILE PROTECTED - CANNOT CONTINUE',1)
	  GOTO CLOSE0
	END
	SWITCH = 2
;;;	XCALL FILES (8,'O',68,SWITCH)		;FILE 68 - TMPIDX FILE
		OPEN (8,O,'ORDHS2.DDF')
		OPEN (9,O,'A.A')
		TMPIDX =
	        FOR I FROM 1 THRU 32 WRITES (8,TMPIDX)
		CLOSE 8
		OPEN (8,U,'ORDHS2.DDF')
	IF (SWITCH.EQ.9)
	BEGIN
	  XCALL MESAG ('FILE IN USE - CANNOT CONTINUE',1)
	  GOTO CLOSE1
	END
	LOKCTL = 1
	XCALL IOS (4,ORDHST,READ,LOKCTL)
	IF (LOKCTL) GOTO EOF
	ARRAYS =
	XCALL RDATE (TODAY)
ASK1,
	XCALL OUTPT (12,20,1,'SELECT REPORT MONTH: ',1)
	XCALL INPUT (12,45,02,00,'#E',ENTRY,INXCTL,1)
	IF (INXCTL.EQ.2) GOTO CLOSE1
	RPTMON = ENTRY
	IF (RPTMON.EQ.0) 
	BEGIN
	  RPTMON = TODAY(1,2)
	  XCALL OUTPT (12,45,0,ARPTMON,1)
	END
	IF (RPTMON.LT.1.OR.RPTMON.GT.12) GOTO ASK1
	XCALL WATE (4,2)	
	XCALL OUTPT (2,1,0,LEG1,1)
RDLOOP,
	LOKCTL = 1
	XCALL IOS (4,ORDHST,READ,LOKCTL)
	IF (LOKCTL) GOTO EOF
	IF (ORDHST.EQ.']]]]]]]]]]') GOTO EOF
	MON = OINVDT(1,2)
	IF (MON.EQ.0) 
	BEGIN
	  DISPLAY (15,13,10,'NO INVOICE DATE: ',ORDHST(1,40))
	  GOTO RDLOOP
	END
	IF (MON.NE.RPTMON) GOTO RDLOOP
	DAY = OINVDT(3,4)
	READ (8,TMPIDX,DAY)
	SL = SL + OSALE
	MS = MS + OMISC
	TX1 = TX1 + OTAX(1)
	TX2 = TX2 + OTAX(2)
	TX3 = TX3 + OTAX(3)
	FR = FR + OFRGHT
	TL = TL + OSALE + OMISC + OTAX(1) + OTAX(2) + OTAX(3) + OFRGHT
	CNT = CNT + 1
	WRITE (8,TMPIDX,DAY)
	GOTO RDLOOP
EOF,
	TMPIDX = ']]]]]]]]]]]]]]]]]]]]'
	WRITE (8,TMPIDX,32)
	CLOSE 8
	OPEN (8,I,'ORDHS2.DDF')

;; PRINT
	LPSW = 1
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO CLOSE2
	XCALL OUTPT (2,1,0,'PRINTING ..',1)
	RECNO =
	TOTTO =
RDLP2,
	INCR RECNO
	LOKCTL = 1
	XCALL IO (8,TMPIDX,RECNO,READ,LOKCTL)
	IF (LOKCTL) GOTO EOF2
	IF (TMPIDX.EQ.']]]]]]]]]]') GOTO EOF2
	IF (CNT.LE.0) GOTO RDLP2
	ATFL(1,2) = RPTMON,'XX'
	ATFL(3,3) = '/'
	ATFL(4,5) = RECNO,'XX'
	ASL = SL	,DLMASK
	AMS = MS	,DLMASK
	ATX1 = TX1	,DLMASK
	ATX2 = TX2	,DLMASK
	ATX3 = TX3	,DLMASK
	AFR = FR	,DLMASK
	ATL = TL	,DLMASK
	ACNT = CNT	,NUMASK
	PLINE = ALINE
	CALL PRINT
	ALINE =
	TSL = TSL + SL
	TMS = TMS + MS
	TTX1 = TTX1 + TX1
	TTX2 = TTX2 + TX2
	TTX3 = TTX3 + TX3
	TFR = TFR + FR
	TTL = TTL + TL
	TCNT = TCNT + CNT
	GOTO RDLP2

EOF2,
	INCR LINCNT
	XCALL LINFD (1)
	LINCNT = LINCNT - 2
	PLINE = LINES
	CALL PRINT
	ATFL = 'TTL:'
	ASL = TSL	,DLMASK
	AMS = TMS	,DLMASK
	ATX1 = TTX1	,DLMASK
	ATX2 = TTX2	,DLMASK
	ATX3 = TTX3	,DLMASK
	AFR = TFR	,DLMASK
	ATL = TTL	,DLMASK
	ACNT = TCNT	,NUMASK
	PLINE = ALINE
	CALL PRINT
CLOSE3,
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
CLOSE2,
	XCALL FILES (8,'I',68,4)
CLOSE1,
	XCALL FILES (4,'I',176,4)
CLOSE0,
	XCALL PGCHN ('AR:ARMENU',1)
PRINT,
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,'NO HDR',' ',LEG1,' ',' ',
&			0,132,132,1,LPSW,RPTNUM,PRNTYP)
	RETURN
END
