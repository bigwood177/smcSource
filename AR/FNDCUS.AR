;FNDCUS.AR
;
; 5-16-18: ssq convert cusmas to isam
;
RECORD	CUSMAS
	.INCLUDE 'DEF:RD001X.DEF'

RECORD	KEYS
	K_CUSNO	,D6
	K_NAME	,A25
	K_ADDR	,A25
	K_CITY	,A15
	K_STATE	,A2
	K_ZIP	,A10
	K_PHONE	,A10
	K_FAX	,A10

RECORD	TOTTIM
	STARTT	,D6
		,A1
	STOPT	,D6
		,A2
	ELAPS	,D6

RECORD	VARS
	BLANKS	,A46
	OPNOK	,D1
	DFON	,D10
	CHN001	,D2
	I	,D5
	J	,D5
	ENTRY	,A30
	INXCTL	,D1
	CNGCTL	,D1
	WHATNO	,D2
	LOKCTL	,D1
	READ	,D1,0
	SWITCH	,D1
	V	,D1

PROC 
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'FIND CUSTOMER',1)
	
	CALL OPENS
	IF (.NOT. OPNOK) GOTO ENDOFF

DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,'FIND CUSTOMER',1)
	XCALL OUTPT (4,5,0,'1. NAME',1)
	XCALL OUTPT (5,5,0,'2. ADDRESS',1)
	XCALL OUTPT (6,5,0,'3. CITY',1)
	XCALL OUTPT (7,5,0,'4. STATE',1)
	XCALL OUTPT (8,5,0,'5. ZIP',1)
	XCALL OUTPT (9,5,0,'6. PHONE',1)
	XCALL OUTPT(10,5,0,'7. FAX',1)

NAME,
	XCALL INPUT (4,18,25,00,'AE',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	K_NAME = ENTRY(1,25)
	GOTO (ANYCNG),CNGCTL
ADDR,
	XCALL INPUT (5,18,25,00,'AE',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	K_ADDR = ENTRY(1,25)
	GOTO (ANYCNG),CNGCTL
CITY,
	XCALL INPUT (6,18,15,00,'AE',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	K_CITY = ENTRY(1,15)
	GOTO (ANYCNG),CNGCTL
STATE,
	XCALL INPUT (7,18,02,00,'AE',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	K_STATE = ENTRY(1,2)
	GOTO (ANYCNG),CNGCTL
ZIP,
	XCALL INPUT (8,18,10,00,'AE',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	K_ZIP = ENTRY(1,10)
	GOTO (ANYCNG),CNGCTL
PHONE,
	XCALL INPUT (9,18,10,00,'#E',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	K_PHONE = ENTRY(1,10)
FAX,
	XCALL INPUT (10,18,10,00,'#E',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	K_FAX = ENTRY(1,10)

ANYCNG,
	XCALL ANYCN (CNGCTL,WHATNO)
	GOTO (PROCES,CNGBR),CNGCTL+1
CNGBR,
	GOTO (NAME,ADDR,CITY,STATE,ZIP,PHONE,FAX),WHATNO
	GOTO ANYCNG

PROCES,
	I = 0
	XCALL TIME(STARTT)

	READ (CHN001,CUSMAS,^FIRST)[ERR=LOOP]
LOOP,
	READS (CHN001,CUSMAS,EOF)
	IF (CUSMAS .EQ. ']]]]]]') GOTO EOF

	IF (K_NAME.NE.BLANKS .AND. .NOT.%INSTR(1,NAME,K_NAME(1,%TRIM(K_NAME))))
&		 GOTO LOOP

	IF (K_ADDR.NE.BLANKS .AND. .NOT.%INSTR(1,ADDR,K_ADDR(1,%TRIM(K_ADDR))))
&		 GOTO LOOP

	IF (K_CITY.NE.BLANKS .AND. .NOT.%INSTR(1,CITY,K_CITY(1,%TRIM(K_CITY))))
&		 GOTO LOOP

	IF (K_STATE.NE.BLANKS .AND. K_STATE.NE.STATE) GOTO LOOP

	IF (K_ZIP.NE.BLANKS .AND. .NOT.%INSTR(1,ZIP,K_ZIP(1,%TRIM(K_ZIP))))
&		 GOTO LOOP

	IF (K_PHONE.NE.BLANKS .AND. .NOT.%INSTR(1,PHONE,K_PHONE(1,%TRIM(K_PHONE))))
&		 GOTO LOOP

	IF (K_FAX.NE.BLANKS .AND. .NOT.%INSTR(1,EFAX,K_FAX(1,%TRIM(K_FAX))))
&		 GOTO LOOP


	XCALL OUTPT (12,19,1,CUSNO,1)
	IF (NAME .EQ. ']]]DEL') 
		THEN	BEGIN
			XCALL OUTPT (13,19,1,ESORNA,1)
			XCALL OUTPT (13,2,0,'Deleted!!',1)
			END
		ELSE	BEGIN
			XCALL OUTPT (13,1,1,' ',1)
			XCALL OUTPT (13,19,1,NAME,1)
			END

	XCALL OUTPT (14,19,1,ADD1,1)
	XCALL OUTPT (15,19,1,ADD2,1)
	XCALL OUTPT (16,19,1,CITY,1)
	XCALL OUTPT (16,35,0,STATE,1)
	XCALL OUTPT (16,38,0,ZIP,1)
	DFON = PHONE
	ENTRY(1,12) = DFON,	'ZZZ ZZZ ZZZZ'
	XCALL OUTPT (17,19,1,ENTRY(1,12),1)

	DFON = EFAX
	ENTRY(1,12) = DFON,	'ZZZ ZZZ ZZZZ'
	XCALL OUTPT (18,19,1,ENTRY(1,12),1)


	XCALL TIME (STOPT)
;;;	ELAPS = STOPT - STARTT
;;;	INCR I
;;;	XCALL OUTPT (I,58,1,TOTTIM,1)

	XCALL OUTPT (23,1,1,'RIGHT CUSTOMER ?',1)
	XCALL INPUT (23,19,01,00,'YN',ENTRY,INXCTL,1)
	GOTO (LOOP),INXCTL-1

EOF,
	GOTO DISPLA
ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('AR:ARMENU',1)

OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	OPNOK = $FALSE

	SWITCH = 5
	XCALL FILES (1,'SI',001,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN001 = 1
	OPNOK = $TRUE

	RETURN
;---------------------------------------------

CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLOSE CHN001
	RETURN
;---------------------------------------------

END

