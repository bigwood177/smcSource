;  ORGATX / AR 
;
;
;		::PCPYAR.DEF::
;******************************************************************************
;		ACCOUNTS RECEIVABLE 
;
;		RELEASED: AUGUST 1, 1984 (d70s10)
;******************************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;
;	RE-ORGANIZATION OF THE ARTCDE FILE.  THIS FILE HAS NO INDEX.
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD ARTCDE		
		.INCLUDE 'DEF:RD169A.DEF'

RECORD,X		
		.INCLUDE 'DEF:RD169C.DEF'

RECORD ARTCTL		
		.INCLUDE 'DEF:RD169B.DEF'

RECORD ARTARY	
		.INCLUDE 'DEF:RD169D.DEF'

RECORD BRACKS
		,A22,	']]]]]]]]]]]]]]]]]]]]]]'
RECORD SRTMSG
	PRGNAM	,A9,	'AR:ATXCNT'
	RCNT	,D5
	OCNT	,D5
RECORD
	CNT	,D3
	DELCNT  ,D3
	LOKCTL	,D1
	N	,D3
	NXTPGM	,A9
	ORGCNT  ,D5
	ORGDEL	,D3
	RDPNTR	,D5
	READ	,D1
	RECCNT  ,D5
	SWITCH	,D1
	V	,D1
	WRITE	,D1,	1
	WRPNTR  ,D5
PROC
	XCALL TERID (V)
	XCALL OUTPT (2,1,1,'REORGANIZE TAX CODE FILE',1)
	XCALL WATE (4,V)
SETUP,
	RDPNTR = 2
	WRPNTR = 2
	SWITCH = 5
	XCALL FILES (1,'U',169,SWITCH)
	IF (SWITCH.EQ.9) GO TO NOMSG
	LOKCTL = 1
	XCALL IO (1,ARTCTL,1,READ,LOKCTL)
	ORGCNT = ORG169
	RECCNT = REC169
READ,
	LOKCTL = 1
	XCALL IO (1,ARTCDE,RDPNTR,READ,LOKCTL)
	IF (ARTCDE.EQ.BRACKS) GO TO EOF
	IF (DELFLD.EQ.']]]DEL') GO TO DELREC
	INCR CNT
	ARTARR(CNT) = ARTCDE
	IF (CNT.EQ.100) CALL WRITE
	INCR RDPNTR
	GO TO READ
DELREC,
	IF (RDPNTR.LE.ORGCNT) INCR ORGDEL
	INCR DELCNT
	INCR RDPNTR
	GO TO READ
WRITE,
	IF (N.EQ.CNT) GO TO OUT
	INCR N
	LOKCTL = 1
	XCALL IO (1,ARTARR(N),WRPNTR,WRITE,LOKCTL)
	INCR WRPNTR
	GO TO WRITE
OUT,
	CNT =
	N =
	RETURN
EOF,
	IF (CNT.NE.0) CALL WRITE
	IF (RDPNTR.EQ.WRPNTR) GO TO DONE
	ARTCDE = BRACKS
	LOKCTL = 0
	XCALL IO (1,ARTCDE,WRPNTR,WRITE,LOKCTL)
	INCR WRPNTR
	GO TO EOF
DONE,
	LOKCTL = 1
	XCALL IO (1,ARTCTL,1,READ,LOKCTL)
	ORG169 = ORG169 - ORGDEL
	REC169 = REC169 - DELCNT
	DEL169 =
	RCNT = REC169
	OCNT = ORG169
	LOKCTL = 1
	XCALL IO (1,ARTCTL,1,WRITE,LOKCTL)
END,
	SWITCH = 3
	XCALL SNMSG (' ',SWITCH)
	IF (OCNT.NE.RCNT) XCALL SNMSG (SRTMSG,2)
	IF (OCNT.NE.RCNT) XCALL PGCHN ('AR:SRTATX',0)
	XCALL FILES (1,'U',169,4)
NOMSG,
	XCALL PGCHN ('AR:ARSFMN',1)
END

