;ROLMNT.AR
;

	.DEFINE POOLSIZE	,25000
	.DEFINE WNDCHNL		,15
	.DEFINE MAXWINS		,10
	.INCLUDE 'DEF:WINDOWS.DEF'

RECORD	CUSMAS
	.INCLUDE 'DEF:RD001A.DEF'
RECORD,X
	.INCLUDE 'DEF:RD001B.DEF'

RECORD	CUSIDX
	.INCLUDE 'DEF:RD002A.DEF'

RECORD	POPAR
	.INCLUDE 'DEF:RXPOP.DEF'

RECORD	CUSALP
		.INCLUDE 'DEF:RD166A.DEF'

RECORD	CHANNEL
	CHN001	,D2
	CHN002	,D2
	CHN166	,D2

RECORD	WN_NAME
		,A5,	'ROLMN'
	WN_TNMBR,D4

RECORD	FUNKEY
	.INCLUDE 'DEF:FUNKEY.DEF'

RECORD	TMPLT
	T_NO	,A1
		,A*,	'. '
	T_DESC,	A8

RECORD
	VALTYP	,5A8,	'SALES   ','SHIPPING','GENERAL ','CREDIT  ','ALL     '
	SELTYP	,D2		;TYPE OF NOTES SELECTED
	MAXTYP	,D1,	5	;MAX NUMBER OF TYPES OF NOTES

RECORD	VARS
	UPDATE	,A1
	TSTAT	,D1
	START_NAME	,A25
	F_KEY	,D3
	I	,D5
	OPNOK	,D1
	ENTRY	,A30
	INXCTL	,D1
	CNGCTL	,D1
	WHATNO	,D1
	NUMRWW	,D2,	12
	SRCCTL	,D1
	KEY	,A6
	BSEND	,D5
	BSMID	,D5
	READ	,D1,0
	WRITE	,D1,1
	LOKCTL	,D1
	ACUST	,A6
	DCUST	,D6
	DTYPE	,D2,	02		;ROLODEX NOTES
	WND_1	,D4
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
XCALL MESAG ('THIS PROGRAM NO LONGER IN USE !!',1)
XCALL PGCHN ('AR:ARMENU',1)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO ENDOFF

	CALL INIT_WINDOW

;====================================================
DISPLA,
	XCALL W_DISP(WND_1,WD_POS,1,62,"<Insert>= ADD")
	XCALL W_DISP(WND_1,WD_POS,2,62,"<Delete> = DELETE")
	XCALL W_DISP(WND_1,WD_POS,23,2,WD_CLR,WDC_EOL,'<PF1> = ALPHA LOOK-UP')
	XCALL W_DISP(WND_1,WD_POS,3,1,"CUST #",WD_CLR,WDC_EOL)
	XCALL W_DISP(WND_1,WD_POS,4,1,'TYPE:')
	XCALL W_UPDT

	XCALL WINPT (WND_1,3,8,06,00,'#E',ENTRY,INXCTL,F_KEY)
;-
	GOTO (DISPLA,ENDOFF),INXCTL
	IF (F_KEY .EQ. F_01)
		BEGIN
		CALL ALPHA_LOOKUP
		XCALL W_DISP(WND_1,WD_POS,21,1,WD_CLR,WDC_EOL)
		GOTO (DISPLA,DISPLA),INXCTL
		XCALL W_DISP(WND_1,WD_POS,3,8,ENTRY(1,6) )
		END

	XCALL W_DISP(WND_1,WD_POS,21,1,WD_CLR,WDC_EOL)
;-
	DCUST = ENTRY(1,6)
	KEY = DCUST,'XXXXXX'
;;;	SRCCTL = 3
	XCALL SERCH (2,CUSIDX,KEY,1,6,BSEND,BSMID,SRCCTL,4,7,11,0,0,0,0)
	IF (SRCCTL .OR. IRC001.LE.0)
		BEGIN
		XCALL MESAG ('CUSTOMER NOT ON FILE',1)
		GOTO DISPLA
		END
	XCALL IO (CHN001,CUSMAS,IRC001,READ,LOKCTL)
	XCALL W_DISP(WND_1,WD_POS,3,16,NAME)
SELTYP,
	FOR I FROM 1 THRU MAXTYP
		BEGIN
		T_NO = I,	'X'
		T_DESC = VALTYP(I)
		XCALL W_DISP(WND_1,WD_POS,I+3,16,TMPLT)
		END
	XCALL WINPT (WND_1,4,8,01,00,'#E',ENTRY,INXCTL)
	GOTO (DISPLA,DISPLA),INXCTL
	SELTYP = ENTRY(1,1)
	IF (SELTYP.EQ.0 .OR. SELTYP.GT.MAXTYP) GOTO SELTYP
	IF (SELTYP .EQ. 4) 	;CREDIT
		BEGIN
		GOTO SELTYP
		END

	DTYPE = SELTYP
	CLEAR TMPLT
	FOR I FROM 1 THRU MAXTYP
		BEGIN
		XCALL W_DISP(WND_1,WD_POS,I+3,16,TMPLT)
		END
	XCALL W_DISP(WND_1,WD_POS,4,16,VALTYP(SELTYP))

	IF (DTYPE .EQ. MAXTYP) 
	THEN	UPDATE = 'N'
	ELSE	UPDATE = 'Y'

;;;	XCALL ROLDX (DCUST,DTYPE,UPDATE,7,10,NUMRWW)
	XCALL ROLDX (DCUST,DTYPE,UPDATE,7,10,NUMRWW,CUSMAS)
	GOTO DISPLA


ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('AR:ARMENU',1)
;====================================================
;====================================================

ALPHA_LOOKUP,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		;;; ALPHA CUSTOMER LOOK-UP
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL W_DISP(WND_1,WD_POS,21,2,WD_CLR,WDC_EOL,'CUST NAME:')
	XCALL WINPT(WND_1,21,13,25,00,'AE',ENTRY,INXCTL)
;;	CTL = '23,13,25,00,AE'
;;	CALL INPUT
	GOTO (ALPHA_RETURN,ALPHA_RETURN),INXCTL
	START_NAME = ENTRY(1,25)
	FIND (CHN166,CUSALP,START_NAME) [ERR=MORE_NAMES]
	CLEAR LOKCTL
MORE_NAMES,
	CALL GET_NAMES
	XCALL TTSTS(TSTAT)
	IF (TSTAT) XCALL W_DISP(WND_1,WD_READS,ENTRY)
	IF (NUMARA .GT. 0)
	THEN	BEGIN
		DLINE = '  CUST #  NAME'
		XCALL RXPOP (POPAR)
		END
	ELSE	BEGIN
		XCALL MESAG ('NO MORE NAMES',1)
		INXCTL = 2
		RETURN
		END

	CASE P_ACTION OF
	BEGINCASE
	0:	INXCTL = 2		;<END>
	1:	BEGIN
		DLINE = PARRY(PI)
		ENTRY = DLINE(1,6)
		CLEAR INXCTL		
		END
	4:	GOTO MORE_NAMES
	ENDCASE
ALPHA_RETURN,
	RETURN
;------------------------------------------------

GET_NAMES,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		;;; GET NEXT 20 NAMES FROM CUSALP
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR I
	XCALL IOS (CHN166,CUSALP,READ,LOKCTL)
	WHILE (LOKCTL.EQ.0 .AND. I.LT.MAXARA)
		BEGIN	
		CLEAR DLINE
		DLINE(1,6) = A_CUSNO,	'ZZZZZZ' [LEFT]
		DLINE(8,32) = A_NAME
		INCR I
		PARRY(I) = DLINE
	
		DLINE (1,25) = A_ADD1
		DLINE (26,46) = A_ADD2
		DLINE (47,61) = A_CITY
		DLINE (63,64) = A_STATE
		DLINE (66,75) = A_ZIP
		P_ADD(I) = DLINE
		XCALL IOS (CHN166,CUSALP,READ,LOKCTL)
		END

	NUMARA = I
	IF (I.EQ.0 .OR. I.EQ.MAXARA) RETURN
	
	FOR I FROM NUMARA+1 THRU MAXARA	CLEAR PARRY(I)
	RETURN
;------------------------------------------------

OPENS,
	CLEAR OPNOK
	SWITCH = 1
	XCALL FILES (1,'I',001,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN001 = 1

	XCALL FILES (2,'I',002,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN002 = 2

	XCALL FILES (3,'SI',166,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN166 = 3

	LOKCTL = 1
	XCALL IO (CHN001,CUSMAS,1,READ,LOKCTL)
	BSEND = ORG001

	OPNOK = 1
	RETURN
;------------------------------------------------------

CLOSE,
	IF (CHN001) XCALL FILES (CHN001,'I',001,4)
	IF (CHN002) XCALL FILES (CHN002,'I',002,4)
	IF (CHN166) XCALL FILES (CHN166,'SI',166,4)
	RETURN
;------------------------------------------------------


INIT_WINDOW,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL TNMBR (WN_TNMBR)
	XCALL W_INIT(POOLSIZE,WNDCHNL,MAXWINS)
	XCALL W_PROC(WP_FIND,WND_1,WN_NAME)
	IF (WND_1.EQ.0)
		BEGIN
		XCALL W_PROC(WP_CREATE,WND_1,WN_NAME,22,78)
		END
	XCALL W_BRDR(WND_1,WB_TITLE,'ROLODEX MAINTENANCE',
&			WB_TPOS,WBT_TOP,WBT_CENTER)
	XCALL W_PROC(WP_PLACE,WND_1,2,2)	
	XCALL W_DISP(WND_1,WD_CLEAR)

;; POP info...

	MAXARA = 20
	PLEN = 34
	NUMROW = 10
	WX = 8
	WY = 7
	POP_WID(1,5) = "RXPOP"
	POP_WID(6,8) = WN_TNMBR,	'XXX'
	POP_TITLE = "CUSTOMER NAMES"
	RETURN
;-----------------------------------------------------------------
