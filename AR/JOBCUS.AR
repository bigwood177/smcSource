;JOBCUS.AR
; 5-16-18: ssq convert cusmas to isam
;
	.DEFINE POOLSIZE	,25000
	.DEFINE WNDCHNL		,15
	.DEFINE MAXWINS		,10
	.INCLUDE 'DEF:WINDOWS.DEF'

RECORD	SLHHDR
	.INCLUDE 'DEF:RD044A.DEF'

RECORD	CUSMAS
	.INCLUDE 'DEF:RD001A.DEF'
RECORD,X
	.INCLUDE 'DEF:RD001B.DEF'

;;;RECORD	CUSIDX
;;;	.INCLUDE 'DEF:RD002A.DEF'

RECORD	POP
	.INCLUDE 'def:POP.DEF'

RECORD	CHANNEL
	CHN184	,D2
	CHN001	,D2
	CHN002	,D2

RECORD	FUNKEY
	.INCLUDE 'DEF:FUNKEY.DEF'

RECORD	WN_NAME
		,A5,	'JOBCS'
	WN_TNMBR,D4

RECORD	W_TITLE
	WT1	,A8,	'TOTAL:  '
	WT2	,A12

RECORD	CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2

RECORD	VARS
	OPNOK	,D1
	TOTSAL	,D10
	SAVCUS	,D6
	F_KEY	,D3
	KEY	,A6
	BSEND	,D5
	BSMID	,D5
	SRCCTL	,D1
	I	,D5
	IS	,D5
	ALPHA	,A30
	BLANKS	,A10
	CUST	,D6
	JOB	,A10
	PO	,A10
	SHIPTO	,A30
	INVTOT	,D12
	WND_1	,D4
	W_ID	,D4
	ENTRY	,A30
	INXCTL	,D1
	CNGCTL	,D1
	WHATNO	,D2
	READ	,D1,0
	WRITE	,D1,1
	LOKCTL	,D1
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	CALL OPENS
	IF (.NOT. OPNOK) GOTO ENDOFF

	CALL INIT_WINDOW

DISPLA,
	CLEAR CNGCTL
	XCALL W_DISP(WND_1,WD_CLEAR)
	XCALL W_DISP(WND_1,WD_POS,21,2,WD_CLR,WDC_EOL,'<PF1> = ALPHA LOOK-UP')
	XCALL W_DISP(WND_1,WD_POS,2,5,'1. CUST #')
	XCALL W_DISP(WND_1,WD_POS,3,5,'2. JOB  #')
	XCALL W_DISP(WND_1,WD_POS,4,5,'3. PO   #')
	XCALL W_DISP(WND_1,WD_POS,5,5,'4. SHIP-TO')
	XCALL W_UPDT
CUST,
	XCALL WINPT (WND_1,2,17,06,00,'#E',ENTRY,INXCTL,F_KEY)
	GOTO (DISPLA,ENDOFF),INXCTL
	IF (F_KEY .EQ. F_01)
	THEN	BEGIN
		XCALL CUSAL (WND_1,CUST,CUSMAS)
		IF (CUST .EQ. 0) GOTO DISPLA
		ENTRY(1,6) = CUST,'ZZZZZZ' [LEFT]
		XCALL W_DISP(WND_1,WD_POS,21,1,WD_CLR,WDC_EOL)
		XCALL W_DISP(WND_1,WD_POS,2,17,ENTRY(1,6) )
		XCALL W_DISP(WND_1,WD_POS,2,25,NAME)
		END
	ELSE	BEGIN
		CUST = ENTRY(1,6)
		IF (CUST .EQ. 0)
		    BEGIN
		    CUST = SAVCUS
		    ENTRY(1,6) = CUST,'ZZZZZZ' [LEFT]
		    XCALL W_DISP(WND_1,WD_POS,21,1,WD_CLR,WDC_EOL)
		    XCALL W_DISP(WND_1,WD_POS,2,17,ENTRY(1,6) )
		    END

		XCALL ISIO (CHN001, CUSMAS, CUST, READ, LOKCTL)
		IF (LOKCTL .NE. 0) NAME = '** NOT ON FILE **'
		
	;;;	KEY = CUST,	'XXXXXX'
	;;;	XCALL SERCH (CHN002,CUSIDX,KEY,1,6,BSEND,BSMID,SRCCTL,1,7,11,0,0,0,0)
	;;;	IF (SRCCTL.EQ.0 .AND. IRC001.NE.0) 
	;;;	    THEN  XCALL IO (CHN001,CUSMAS,IRC001,READ,LOKCTL)
	;;;	    ELSE  NAME = '** NOT ON FILE **'
		END
		    
	SAVCUS = CUST
	XCALL W_DISP(WND_1,WD_POS,2,25,NAME)
	GOTO (ANYCNG),CNGCTL
JOB,
	CTL = '03,17,10,00,A '
	CALL INPUT
	GOTO (DISPLA),INXCTL
	IF (ENTRY(1,10) .EQ. BLANKS) XCALL W_DISP(WND_1,WD_POS,ROW,COL,'ALL')
	JOB = ENTRY(1,10)
	GOTO (ANYCNG),CNGCTL
PO,
	CTL = '04,17,10,00,A '
	CALL INPUT
	GOTO (DISPLA),INXCTL
	IF (ENTRY(1,10) .EQ. BLANKS) XCALL W_DISP(WND_1,WD_POS,ROW,COL,'ALL')
	PO = ENTRY(1,10)
	GOTO (ANYCNG),CNGCTL
SHIPTO,
	CTL = '05,17,30,00,A '
	CALL INPUT
	GOTO (DISPLA),INXCTL
	IF (ENTRY(1,10) .EQ. BLANKS) XCALL W_DISP(WND_1,WD_POS,ROW,COL,'ALL')
	SHIPTO = ENTRY(1,30)
	GOTO (ANYCNG),CNGCTL
	
ANYCNG,
	XCALL WANCN(W_ID,22,CNGCTL,WHATNO)
	GOTO (PROCES,CNGBR),CNGCTL+1
CNGBR,
	GOTO (CUST,JOB,PO,SHIPTO),WHATNO
	GOTO ANYCNG
PROCES,
; process all matching records to get total amount billed...
	FIND (CHN184,SLHHDR,CUST,KRF=1)[ERR=FA_RETURN]
	MAXARA = 999
	INVTOT = 0
	CALL GET_MORE
	IF (INVTOT .EQ. 0) GOTO FA_RETURN
	WT2 = INVTOT,	'ZZZZ,ZZX.XX-'
	POP_TITLE = W_TITLE
	XCALL W_DISP(WND_1,WD_POS,8,10,WD_READS,ENTRY)
; display matching records...
	FIND (CHN184,SLHHDR,CUST,KRF=1)[ERR=FA_RETURN]
	MAXARA = 14
P_LOOP,
	CALL GET_MORE
	IF (I .EQ. 0) GOTO FA_RETURN
	NUMARA = I
	DLINE = '  INV-NO  INV-DATE  SHP-DATE  INV-AMOUNT  JOB-NO     PO-NO'
	IF (SHIPTO .NE. BLANKS) DLINE (43,70) = 'SHIP-TO'
;                12345678901234567890123456789012345678901234567890
;                         1         2         3         4         5
	XCALL POP2 (POP)
	IF (P_ACTION .EQ. 4) GOTO P_LOOP	;NEXT PAGE
FA_RETURN,
	XCALL MESAG (' ',1)
	GOTO DISPLA
;=============================================================

GET_MORE,
	CLEAR I
FA_LOOP,
	XCALL IOS (CHN184,SLHHDR,READ,LOKCTL)
	IF (LOKCTL .NE. 0) RETURN
	IF (OCUSNO .NE. CUST) RETURN
	IF (JOB.NE.BLANKS)
		BEGIN
		IF (.NOT. %INSTR(1,OJOBNO,JOB(1,%TRIM(JOB)),IS) ) 
		THEN	GOTO FA_LOOP
		ELSE	ALPHA(1,10) = OJOBNO
		END
	ALPHA(1,10) = OJOBNO
	IF (PO.NE.BLANKS)
		BEGIN
		IF (.NOT. %INSTR(1,OPONO,PO(1,%TRIM(PO)),IS) ) 
		THEN	GOTO FA_LOOP
		ELSE	ALPHA(12,21) = OPONO
		END
	ALPHA(12,21) = OPONO
	IF (SHIPTO.NE.BLANKS)
		BEGIN
		IF (.NOT. %INSTR(1,OSHPNM,SHIPTO(1,%TRIM(SHIPTO)),IS) ) 
		THEN	GOTO FA_LOOP
		ELSE	ALPHA = OSHPNM
		END

	TOTSAL = OSALE + OMISC + OTAX(1) + OTAX(2) + OTAX(3) + OFRGHT
	IF (MAXARA .EQ. 999)
	THEN	INVTOT = INVTOT + TOTSAL
	ELSE	BEGIN
		DLINE (1,6) = OINVNO,	'ZZZZZX'
		DLINE (9,16) = OINVDT,	'XX/XX/XX'
		DLINE(19,26) = OSHDAT,	'XX/XX/XX'
		DLINE(29,39) = TOTSAL,	'ZZZ,ZZX.XX-'
		DLINE(41,70) = ALPHA
		INCR I
		PARRY(I) = DLINE
		IF (I .GE. MAXARA) RETURN
		END

	GOTO FA_LOOP

;-------------------------------------------------------

ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('AR:ARMENU',1)
	STOP

INPUT,
	XCALL WINPT(W_ID,ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL)
	RETURN

OPENS,
	CLEAR OPNOK
	SWITCH = 5
	XCALL FILES (1,'SI',184,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN184 = 1

	XCALL FILES (2,'SI',001,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN001 = 2
	
;;;	XCALL FILES (3,'I',002,SWITCH)
;;;	IF (SWITCH .EQ. 9) RETURN
;;;	CHN002 = 3

;;;	XCALL IO (CHN001,CUSMAS,1,READ,LOKCTL)
;;;	BSEND = ORG001

	OPNOK = 1
	RETURN

CLOSE,
	IF (CHN184) XCALL FILES (CHN184,'SI',184,4)
	CLOSE CHN001
;;;	CLOSE CHN002
	RETURN
		

INIT_WINDOW,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL TNMBR (WN_TNMBR)
	XCALL W_INIT(POOLSIZE,WNDCHNL,MAXWINS)
	XCALL W_PROC(WP_FIND,WND_1,WN_NAME)
	IF (WND_1.EQ.0)
		BEGIN
		XCALL W_PROC(WP_CREATE,WND_1,WN_NAME,22,78)
		END
	XCALL W_BRDR(WND_1,WB_TITLE,'CUST RELEASE INFO',
&			WB_TPOS,WBT_TOP,WBT_CENTER)
	XCALL W_PROC(WP_PLACE,WND_1,2,2)	
	XCALL W_DISP(WND_1,WD_CLEAR)

	W_ID = WND_1
;; POP info...

	MAXARA = 20
	PLEN = 70
	NUMROW = 15
	WX = 8
	WY = 4
	POP_WID(1,5) = "JCPOP"
	POP_WID(6,8) = WN_TNMBR,	'XXX'
	POP_TITLE = "CUSTOMER INVOICES"
	RETURN
;-----------------------------------------------------------------

