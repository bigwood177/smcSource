; ORGCUS / NEW
;
;	REORGANIZE CUSMAS TO PHYSICALLY REMOVE DELETED RECORDS
;
;	ALSO REBUILDS CUSIDX IF CORRUPTED
;
RECORD CUSMAS
		.INCLUDE 'DEF:RD001A.DEF'
RECORD CUSCTL
		.INCLUDE 'DEF:RD001B.DEF'
RECORD CUSIDX
		.INCLUDE 'DEF:RD002A.DEF'
RECORD SRTMSG
		,A9,'AR:CUSCNT'
	RCNT	,D5
	OCNT	,D5
RECORD
		.INCLUDE 'DEF:RD002S.DEF'
	V	,D1
	SWITCH	,D1
	WRREC	,D5
	RDREC	,D5
	BLOCKS	,D4
	READ	,D1,0
	WRITE	,D1,1
	LOKCTL	,D1
	MSGCTL	,D1
	PROGNM	,A9
	BLANKS	,A10
PROC
	XCALL TERID (V)
	XCALL OUTPT (2,1,2,'REORGANIZE CUSTOMER FILES',1)
	SWITCH = 1
	XCALL SNMSG (PROGNM,SWITCH)	;get msg
	IF (SWITCH.EQ.9) GO TO OPEN1
	SWITCH = 3
	XCALL SNMSG(BLANKS,SWITCH)	;clear msg
	BLANKS =
	GOTO OPEN2
OPEN1,
	SWITCH = 3
	IF (PROGNM.NE.BLANKS) SWITCH = 5
	XCALL FILES (6,'I',002,SWITCH)		;FILE 002 - CUSIDX
	IF (SWITCH.NE.9) GOTO OPEN2
	GOTO ABORT
OPEN2,
	SWITCH = 2
	IF (PROGNM.NE.BLANKS) SWITCH = 5
	XCALL FILES (5,'U',001,SWITCH)		;FILE 001 - CUSMAS
	IF (SWITCH.NE.9) GOTO RDHDR
	CALL CLOSE1
	GOTO ABORT
CLOSES,
	XCALL FILES (5,'I',001,4)
CLOSE1,
	XCALL FILES (6,'I',002,4)
	RETURN
RDHDR,
	XCALL WATE (4,2)
	LOKCTL = 1
	XCALL IO (5,CUSCTL,1,READ,LOKCTL)
	BLOCKS = (((SIZ002+2)*(MAX001+1))/512) + 1
	XCALL OFILE (6,002,BLOCKS,SIZ002,SWITCH)
	CUSIDX =
	RDREC = 1
	LOKCTL = 1
	XCALL IOS (6,CUSIDX,WRITE,LOKCTL)
	WRREC = 1
RDLOOP,
	INCR RDREC
	LOKCTL = 1
	XCALL IO (5,CUSMAS,RDREC,READ,LOKCTL)
	IF (CUSMAS.EQ.']]]]]]]]]]') GOTO BRACK
	IF (NAME.EQ.']]]DEL') GOTO RDLOOP
	INCR WRREC
	ICUSNO = CUSNO
	IRC001 = WRREC
	LOKCTL = 1
	XCALL IOS (6,CUSIDX,WRITE,LOKCTL)
	IF (WRREC.EQ.RDREC) GOTO RDLOOP
	LOKCTL = 1
	XCALL IO (5,CUSMAS,WRREC,WRITE,LOKCTL)
	EAPNA = ']]]DEL'
	LOKCTL = 1
	XCALL IO (5,CUSMAS,RDREC,WRITE,LOKCTL)
	GOTO RDLOOP
BRACK,
	LOKCTL = 1
	XCALL IO (5,CUSCTL,1,READ,LOKCTL)
	ORG001 = 1
	REC001 = WRREC
	DEL001 =
	LOKCTL = 1
	XCALL IO (5,CUSCTL,1,WRITE,LOKCTL)
	CUSIDX = CUSMAS
BRAKLP,
	INCR WRREC
	LOKCTL = 1
	XCALL IOS (6,CUSIDX,WRITE,LOKCTL)
	IF (WRREC.GT.MAX001) GOTO EOF
	IF (WRREC.GE.RDREC) GOTO BRAKLP
	LOKCTL = 1
	XCALL IO (5,CUSMAS,WRREC,WRITE,LOKCTL)
	GOTO BRAKLP
EOF,
	CLOSE 5
	IF (PROGNM.EQ.BLANKS) XCALL FILES (5,'I',001,4)
	CLOSE 6
	RCNT = REC001
	OCNT = 1
	MSGCTL = 5
	XCALL SNMSG (SRTMSG,MSGCTL)
	IF (PROGNM.NE.BLANKS)
	BEGIN
	  MSGCTL = 2
	  XCALL SNMSG (PROGNM,MSGCTL)
	END
	XCALL PGCHN ('AR:SRTCID',1)
ABORT,
	XCALL PGCHN ('AR:ARMENU',1)
END	
