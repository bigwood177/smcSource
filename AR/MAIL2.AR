;MAIL2.AR

RECORD	CUSMAS
	CUSNO	,D6  	; CUSTOMER NUMBER
	NAME	,A25  	; CUSTOMER NAME
	ADD1	,A25	; ADDRESS LINE 1
	ADD2	,A21  	; ADDRESS LINE 2
	CITY	,A15  	; CITY
	STATE	,A2	; STATE - POST OFFICE CODE
	ZIP	,A10	; ZIP CODE (XXXXX-XXXX)
	
RECORD	VARS
	ENTRY	,A30
	INXCTL	,D1
	WHATNO	,D2
	CNGCTL	,D1
	KCUST	,D6
	LOKCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	STORE	,D1,2
	SELECT	,D1
	V	,D1

PROC
	XCALL TERID (V)
	OPEN (1,SU,'SMC:NONCUS.ISM')
DISPLA,
	XCALL OUTPT (1,1,2,'NON-CUSTOMER DATA',1)
	XCALL OUTPT ( 4,5,0,'   NUMBER',1)
	XCALL OUTPT ( 6,5,0,'1. NAME',1)
	XCALL OUTPT ( 8,5,0,'2. ADD1',1)
	XCALL OUTPT (10,5,0,'3. ADD2',1)
	XCALL OUTPT (12,5,0,'4. CITY',1)
	XCALL OUTPT (14,5,0,'5. STATE',1)
	XCALL OUTPT (16,5,0,'6. ZIP',1)
CUSNO,
	XCALL INPUT (4,16,06,00,'#E',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	KCUST = ENTRY(1,6)
	CALL GETKEY
	GOTO (ANYCNG),SELECT-1
NAME,
	XCALL INPUT (6,16,25,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	NAME = ENTRY(1,25)
	GOTO (ANYCNG),CNGCTL
ADD1,
	XCALL INPUT (8,16,25,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	ADD1 = ENTRY(1,25)
	GOTO (ANYCNG),CNGCTL
ADD2,
	XCALL INPUT (10,16,21,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	ADD2 = ENTRY(1,21)
	GOTO (ANYCNG),CNGCTL
CITY,
	XCALL INPUT (12,16,15,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	CITY = ENTRY(1,15)
	GOTO (ANYCNG),CNGCTL
STATE,
	XCALL INPUT (14,16,02,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	STATE = ENTRY(1,2)
	GOTO (ANYCNG),CNGCTL
ZIP,
	XCALL INPUT (16,16,10,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	ZIP = ENTRY(1,10)
	GOTO (ANYCNG),CNGCTL

ANYCNG,
	CLEAR CNGCTL
	XCALL OUTPT (24,1,1,'FIELD:       <TAB> = DELETE',1)
	XCALL INPUT (24,8,01,00,'AT',ENTRY,INXCTL,1)
	IF (INXCTL .EQ. 3) 
		BEGIN
		XCALL OUTPT (24,1,1,'DELETE, ARE YOU SURE',1)
		XCALL INPUT (24,22,01,01,'YN',ENTRY,INXCTL,1)
		GOTO (ANYCNG),INXCTL-1
		DELETE (1,CUSNO)
		GOTO DISPLA
		END
	WHATNO = ENTRY(1,2)
	IF (WHATNO .EQ. 0) GOTO PROCES
	CNGCTL = 1
	GOTO (CUSNO,NAME,ADD1,ADD2,CITY,STATE,ZIP),WHATNO
	GOTO ANYCNG
PROCES,
	CASE SELECT OF
	BEGINCASE
	1:	BEGIN
		CUSNO = KCUST
		XCALL ISIO (1,CUSMAS,CUSNO,STORE,LOKCTL)
		END
	2:	XCALL ISIO (1,CUSMAS,CUSNO,WRITE,LOKCTL)
	ENDCASE
	GOTO DISPLA

;============================================================
GETKEY,
	XCALL ISIO (1,CUSMAS,KCUST,READ,LOKCTL)
	IF (LOKCTL .EQ. 0)
	THEN	BEGIN
		SELECT = 2		;CHANGE MODE
		CALL DSPREC
		END
	ELSE	BEGIN
		SELECT = 1		;ADD MODE
		READ (1,CUSMAS,^LAST)
		KCUST = CUSNO + 1
		ENTRY(1,6) = KCUST,	'ZZZZZX'[LEFT]
		XCALL OUTPT (4,16,1,ENTRY(1,6),1)
		END
	RETURN
;--------------------------------------------------

DSPREC,
	XCALL OUTPT (6,16,1,NAME,1)
	XCALL OUTPT (8,16,1,ADD1,1)
	XCALL OUTPT (10,16,1,ADD2,1)
	XCALL OUTPT (12,16,1,CITY,1)
	XCALL OUTPT (14,16,1,STATE,1)
	XCALL OUTPT (16,16,1,ZIP,1)

	RETURN
;--------------------------------------------------
ENDOFF,
	CLOSE 1
	XCALL PGCHN ('AR:MAIL0',1)
	END



