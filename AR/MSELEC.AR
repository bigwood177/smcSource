;MSELEC.AR
;
;	SELECT CUSTOMERS TO EXPORT TO MAILING LIST FILES.
; 	5-21-18: ssq convert cusmas to isam
;
RECORD	MAIL
	M_CUSNO		,D6  	; CUSTOMER NUMBER
	M_NAME		,A25  	; CUSTOMER NAME
	M_ADD1		,A25	; ADDRESS LINE 1
	M_ADD2		,A21  	; ADDRESS LINE 2
	M_CITY		,A15  	; CITY
	M_STATE		,A2	; STATE - POST OFFICE CODE
	M_ZIP		,A10	; ZIP CODE (XXXXX-XXXX)
	M_PHONE		,D10	; PHONE NUMBER (XXX-XXX-XXXX)
	M_SLSMAN	,D2   	; SALESMAN NUMBER
	M_TERR		,A2	; TERRITORY
	M_CUSCD		,A2	; CUSTOMER TYPE - USER DEFINED
	M_SALYTD	,D10  	; SALES YEAR-TO-DATE  ($XX,XXX,XXX.XX-)
	M_ESTART	,D6	; ACOUNT OPEN DATE
	M_ELAST		,D6	; LAST SALE DATE
	M_CONTAC	,4A30	; ROLODEX CONTACTS

RECORD	CUSMAS
	.INCLUDE 'DEF:RD001A.DEF'

RECORD	ROLO
	.INCLUDE 'DEF:ROLO.DEF'

RECORD
		,A14,	'CUSTOMER #    '
		,A14,	'CUSTOMER TYPE '
		,A14,	'SALESMAN      '
		,A14,	'STATE         '
		,A14,	'ZIPCODE       '
		,A14,	'TERRITORY     '
		,A14,	'AREA CODE     '
		,A14,	'DATE LAST SALE'
		,A14,	'DATED STARTED '
		,A14,	'YTD SALES $   '
		,A6,	'001:06'	;M_CUSNO - CUSTOMER NUMBER
		,A6,	'119:02'	;M_CUSCD - CUSTOMER TYPE - USER DEFINED
		,A6,	'115:02'	;M_SLSMAN - SALESMAN NUMBER
		,A6,	'093:02'	;M_STATE - STATE - POST OFFICE CODE
		,A6,	'095:10'	;M_ZIP - ZIP CODE (XXXXX-XXXX)
		,A6,	'117:02'	;M_TERR - TERRITORY
		,A6,	'105:10'	;M_PHONE - PHONE NUMBER (XXX-XXX-XXXX)
		,A6,	'139:08'	;M_ELAST - LAST SALE DATE
		,A6,	'131:08'	;M_ESTART - ACOUNT OPEN DATE
		,A6,	'121:10'	;M_SALYTD - SALES YEAR-TO-DATE  ($XX,XXX,XXX.XX-)

RECORD	ARAS,X
	PROMPT	,10A14
	FIELDS	,10A6

RECORD	ARA2
	NUMARA	,D4,	0010
	SORTF	,10D2		;HOLDS ORDER FIELDS WILL BE SORTED IN
	
RECORD	TMPLT
	TN	,A3
		,A1
	TD	,A14

RECORD	MSORT
	STR	,A256
	STR2	,A6		;pos:len
	
RECORD	CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2

RECORD	CHANNEL
	CHN001	,D2
	CHNROL	,D2
	CHNMAL	,D2

RECORD	RANGES
	STCUS	,D6
	ENCUS	,D6
	STTYP	,A2
	ENTYP	,A2
	STMAN	,D2
	ENMAN	,D2
	STSTA	,A2
	ENSTA	,A2
	STZIP	,A10
	ENZIP	,A10
	STTER	,A2
	ENTER	,A2
	STAC	,D3
	ENAC	,D3
	STLST	,D8	;DATE LAST SALE
	ENLST	,D8	
	STSTR	,D8	;DATE STARTED
	ENSTR	,D8
	STSLS	,D10	;YTD SALES
	ENSLS	,D10

RECORD	YEAR
	CC	,D2
	YY	,D2

RECORD	VARS
	OPNOK	,D1
	INXCTL	,D1
	CNGCTL	,D1
	LOKCTL	,D1
	ENTRY	,A30
	WHATNO	,D2
	READ	,D1,0
	WRITE	,D1,1
	XDATE	,D8
	DATE6	,D6
	AREA	,D3	;AREA CODE
	NUM	,D2
	I	,D6
	J	,D6
	SWITCH	,D1
	V	,D1

;
PROC
	XCALL TERID(V)
	XCALL OUTPT (1,1,2,'SELECT CUSTOMERS FOR MAILING LIST',1)

	CALL OPENS
	IF (.NOT. OPNOK) GOTO ENDOFF

DISPLA,
	XCALL OUTPT (1,1,2,'SELECT CUSTOMERS FOR MAILING LIST',1)
	XCALL OUTPT (3,1,0,'SELECTION CRITERIA:',1)
	XCALL OUTPT (3,50,0,'SORT ORDER:',1)

	FOR I FROM 1 THRU NUMARA
		BEGIN
		TN = I,	'ZX.'
		TD = PROMPT(I)
		XCALL OUTPT(I+4,1,0,TMPLT,1)
		END
CUS,
	CTL = '05,26,06,00,#E'
	CALL INPUT
	GOTO (DISPLA,ENDOFF),INXCTL
	STCUS = ENTRY(1,6)
	IF (STCUS .EQ. 0)
		BEGIN
		ENCUS = 999999
		XCALL OUTPT (ROW,COL,1,'ALL',1)
		GOTO (ANYCNG),CNGCTL
		GOTO TYP
		END
	COL = 39
	CALL INPUT
	GOTO (DISPLA,ENDOFF),INXCTL
	ENCUS = ENTRY(1,6)
	IF (ENCUS .EQ. 0)
		BEGIN
		ENCUS = STCUS
		ENTRY(1,6) = ENCUS,'ZZZZZX' 
		XCALL OUTPT (ROW,COL,0,ENTRY(1,6),1)
		END
	GOTO (ANYCNG),CNGCTL

TYP,
	CTL = '06,30,02,00,A '
	CALL INPUT
	GOTO (DISPLA),INXCTL
	STTYP = ENTRY(1,2)
	IF (STTYP .EQ. '  ')
		BEGIN
		ENTYP = 'ZZ'
		XCALL OUTPT (ROW,COL,1,'ALL',1)
		GOTO (ANYCNG),CNGCTL
		GOTO MAN
		END
	COL = 43
	CALL INPUT
	GOTO (DISPLA),INXCTL
	ENTYP = ENTRY(1,2)
	IF (ENTYP .EQ. '  ')
		BEGIN
		ENTYP = STTYP
		XCALL OUTPT (ROW,COL,0,ENTYP,1)
		END
	GOTO (ANYCNG),CNGCTL
MAN,
	CTL = '07,30,02,00,# '
	CALL INPUT
	GOTO (DISPLA),INXCTL
	STMAN = ENTRY(1,2)
	IF (STMAN .EQ. 0)
		BEGIN
		ENMAN = 99
		XCALL OUTPT (ROW,COL,1,'ALL',1)
		GOTO (ANYCNG),CNGCTL
		GOTO STA
		END
	COL = 43
	CALL INPUT
	ENMAN = ENTRY(1,2)
	IF (ENMAN .EQ. 0)
		BEGIN
		ENMAN = STMAN
		ENTRY(1,2) = ENMAN,	'ZX' 
		XCALL OUTPT (ROW,COL,0,ENTRY(1,2),1)
		END
	GOTO (ANYCNG),CNGCTL
STA,
	CTL = '08,30,02,00,A '
	CALL INPUT
	GOTO (DISPLA),INXCTL
	STSTA = ENTRY(1,2)
	IF (STSTA .EQ. '  ')
		BEGIN
		ENSTA = 'ZZ'
		XCALL OUTPT (ROW,COL,1,'ALL',1)
		GOTO (ANYCNG),CNGCTL
		GOTO ZIP
		END
	COL = 43
	CALL INPUT
	ENSTA = ENTRY(1,2)
	IF (ENSTA .EQ. '  ')
		BEGIN
		ENSTA = STSTA
		XCALL OUTPT (ROW,COL,0,ENSTA,1)
		END
	GOTO (ANYCNG),CNGCTL
ZIP,
	CTL = '09,22,10,00,A '
	CALL INPUT
	GOTO (DISPLA),INXCTL
	STZIP = ENTRY(1,10)
	IF (STZIP .EQ. '    ')
		BEGIN
		ENZIP = 'ZZZZZZZZZZ'
		XCALL OUTPT (ROW,COL,1,'ALL',1)
		GOTO (ANYCNG),CNGCTL
		GOTO TER
		END
	COL = 35
	CALL INPUT
	GOTO (DISPLA),INXCTL
	ENZIP = ENTRY(1,10)
	IF (ENZIP .EQ. '  ')
		BEGIN
		ENZIP = STZIP
		XCALL OUTPT (ROW,COL,0,ENZIP,1)
		END
	GOTO (ANYCNG),CNGCTL
TER,
	CTL = '10,30,02,00,A '
	CALL INPUT
	GOTO (DISPLA),INXCTL
	STTER = ENTRY(1,2)
	IF (STTER .EQ. '  ')
		BEGIN
		ENTER = 'ZZ'
		XCALL OUTPT (ROW,COL,1,'ALL',1)
		GOTO (ANYCNG),CNGCTL
		GOTO AC
		END
	COL = 43
	CALL INPUT
	GOTO (DISPLA),INXCTL
	ENTER = ENTRY(1,2)
	IF (ENTER .EQ. '  ')
		BEGIN
		ENTER = STTER
		XCALL OUTPT (ROW,COL,0,ENTER,1)
		END
	GOTO (ANYCNG),CNGCTL
AC,
	CTL = '11,29,03,00,# '
	CALL INPUT
	GOTO (DISPLA),INXCTL
	STAC = ENTRY(1,3)
	IF (STAC .EQ. 0)
		BEGIN
		ENAC = 999
		XCALL OUTPT (ROW,COL,1,'ALL',1)
		GOTO (ANYCNG),CNGCTL
		GOTO LST
		END
	COL = 42
	CALL INPUT
	GOTO (DISPLA),INXCTL
	ENAC = ENTRY(1,3)
	IF (ENAC .EQ. 0)
		BEGIN
		ENAC = STAC
		ENTRY(1,3) = ENAC,	'ZZZ' 
		XCALL OUTPT (ROW,COL,0,ENTRY(1,3),1)
		END
	GOTO (ANYCNG),CNGCTL
LST,
	CTL = '12,22,08,00,D '
	CALL INPUT
	GOTO (DISPLA),INXCTL
	STLST = ENTRY(1,8)
	IF (STLST .EQ. 0)
		BEGIN
		ENLST = 99999999
		XCALL OUTPT (ROW,COL,1,'ALL',1)
		GOTO (ANYCNG),CNGCTL
		GOTO STR
		END
	COL = 35
	CALL INPUT
	GOTO (DISPLA),INXCTL
	ENLST = ENTRY(1,8)
	IF (ENLST .EQ. 0)
		BEGIN
		ENLST = STLST
		XDATE(1,4) = ENLST(5,8)
		XDATE(5,8) = ENLST(1,4)
		ENTRY(1,10) = XDATE,	'XX/XX/XXXX'
		XCALL OUTPT(ROW,COL,0,ENTRY(1,10),1)
		END
	GOTO (ANYCNG),CNGCTL
STR,
	CTL = '13,22,08,00,D '
	CALL INPUT
	GOTO (DISPLA),INXCTL
	STSTR = ENTRY(1,8)
	IF (STSTR .EQ. 0)
		BEGIN
		ENSTR = 99999999
		XCALL OUTPT(ROW,COL,1,'ALL',1)
		GOTO (ANYCNG),CNGCTL
		GOTO SLS
		END
	COL = 35
	CALL INPUT
	GOTO (DISPLA),INXCTL
	ENSTR = ENTRY(1,8)
	IF (ENSTR .EQ. 0)
		BEGIN
		ENSTR = STSTR
		XDATE(1,4) = ENSTR(5,8)
		XDATE(5,8) = ENSTR(1,4)
		ENTRY(1,10) = XDATE,	'XX/XX/XXXX'
		XCALL OUTPT(ROW,COL,0,ENTRY(1,10),1)
		END
	GOTO (ANYCNG),CNGCTL
SLS,
	CTL = '14,20,09,00,$ '
	CALL INPUT
	GOTO (DISPLA),INXCTL
	STSLS = ENTRY(1,9)
	IF (STSLS .EQ. 0)
		BEGIN
		ENSLS = 9999999999
		XCALL OUTPT (ROW,COL,1,'ALL',1)
		GOTO (ANYCNG),CNGCTL
		GOTO ANYCNG
		END
	COL = 33
	CALL INPUT
	GOTO (DISPLA),INXCTL
	ENSLS = ENTRY(1,9)
	IF (ENSLS .EQ. 0)
		BEGIN
		ENSLS = STSLS
		ENTRY(1,11) = ENSLS,	'ZZZZ,ZZX.XX'
		XCALL OUTPT (ROW,COL,0,ENTRY(1,11),1)
		END
	GOTO (ANYCNG),CNGCTL
ANYCNG,
	XCALL ANYCN (CNGCTL,WHATNO)
	GOTO (PROCES,CNGBR),CNGCTL + 1
CNGBR,
	GOTO (CUS,TYP,MAN,STA,ZIP,TER,AC,LST,STR,SLS),WHATNO
	GOTO ANYCNG
PROCES,

DISPL2,
	CLEAR TD
	FOR I FROM 1 THRU NUMARA
		BEGIN
		TN = (I+11),	'ZX.'
		XCALL OUTPT(I+4,50,0,TMPLT,1)
		END

	CTL = '05,54,02,00,#E'
	FOR I FROM 1 THRU NUMARA
		BEGIN
		CALL GET_SORT
		GOTO (DISPL2,ANYCN2),INXCTL
		IF (NUM .EQ. 0) EXITLOOP
		END	
ANYCN2,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (PROCE2,CNGBR2),CNGCTL + 1
CNGBR2,
	CALL GET_SORT
	GOTO ANYCN2

PROCE2,
	READ (CHN001,CUSMAS,1)
LOOP,
	READS (CHN001,CUSMAS,EOF)
;;;	IF (CUSMAS .EQ. ']]]]]]') GOTO EOF
	IF (NAME .EQ. ']]]DEL') GOTO LOOP
	IF (CUSNO.LT.STCUS .OR. CUSNO.GT.ENCUS) GOTO LOOP
	IF (CUSCD.LT.STTYP .OR. CUSCD.GT.ENTYP) GOTO LOOP
	IF (SLSMAN.LT.STMAN .OR. SLSMAN.GT.ENMAN) GOTO LOOP
	IF (STATE.LT.STSTA .OR. STATE.GT.ENSTA) GOTO LOOP
	IF (ZIP.LT.STZIP .OR. ZIP.GT.ENZIP) GOTO LOOP
	IF (TERR.LT.STTER .OR. TERR.GT.ENTER) GOTO LOOP
	IF (SALYTD.LT.STSLS .OR. SALYTD.GT.ENSLS) GOTO LOOP

	IF(PHONE.GT.9999999)
	THEN	AREA = PHONE(1,3)
	ELSE	AREA =
	IF (AREA.LT.STAC .OR. AREA.GT.ENAC) GOTO LOOP

	DATE6 = ESTART
	CALL MAKE_DATE
	M_ESTART = XDATE
	IF(XDATE.LT.STSTR .OR. XDATE.GT.ENSTR) GOTO LOOP

	DATE6 = ELAST
	CALL MAKE_DATE
	M_ELAST = XDATE
	IF(XDATE.LT.STLST .OR. XDATE.GT.ENLST) GOTO LOOP	

	M_CUSNO = CUSNO		; CUSTOMER NUMBER
	M_NAME = NAME		; CUSTOMER NAME
	M_ADD1 = ADD1		; ADDRESS LINE 1
	M_ADD2 = ADD2		; ADDRESS LINE 2
	M_CITY = CITY		; CITY
	M_STATE = STATE		; STATE - POST OFFICE CODE
	M_ZIP = ZIP		; ZIP CODE (XXXXX-XXXX)
	M_PHONE = PHONE		; PHONE NUMBER (XXX-XXX-XXXX)
	M_SLSMAN = SLSMAN	; SALESMAN NUMBER
	M_TERR = TERR		; TERRITORY
	M_CUSCD = CUSCD		; CUSTOMER TYPE - USER DEFINED
	M_SALYTD = SALYTD	; SALES YEAR-TO-DATE  ($XX,XXX,XXX.XX-)
	

;rolo 
	
	WRITES(CHNMAL,MAIL)

	GOTO LOOP

MAKE_DATE,	;;;;;;;;;;;;;;;;;;;
	YY = DATE6(5,6)
	IF (YY .GT. 50)
	THEN	CC = 19
	ELSE	CC = 20
	XDATE(1,4) = YEAR
	XDATE(5,8) = ESTART(1,4)
	RETURN
;------------------------------------
EOF,
	CLOSE CHNMAL
	IF (SORTF(1) .EQ. 0) SORTF(1) = 1	;FORCE AT LEAST 1 SORT FIELD
	STR = 'KEY=('
	FOR I FROM 1 THRU NUMARA
		BEGIN
		J = SORTF(I)		;NUMBER OF NEXT SORT FIELD
		IF (J.GT.0) STR = STR(1,%TRIM(STR)) + FIELDS(J) + ','
		END
	J = %TRIM(STR)
	STR(J,J) = ')'
	SORT(INPUT='DATA.MAL', RECORD=MAIL, OPTIONS=STR)

ENDOFF,
	CALL CLOSE
	STOP

GET_SORT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	ROW = I + 4
ASK_SORT,
	CALL INPUT
	IF (INXCTL .NE. 0) RETURN		;ABORT,END
	NUM = ENTRY(1,2)
	IF (NUM .EQ. 0) RETURN
	IF (NUM.LT.1 .OR. NUM.GT.NUMARA) GOTO ASK_SORT
	XCALL OUTPT (ROW,57,0,PROMPT(NUM),1)
	SORTF(I) = NUM
	RETURN
;------------------------------------------------------------

INPUT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL INPUT(ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,1)
	RETURN
;-----------------------------------------------------------

OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK
	SWITCH = 1
	XCALL FILES (1,'SI',01,SWITCH)		;01 CUSMAS
	IF (SWITCH .EQ. 9) RETURN
	CHN001 =1 

	OPEN(2,O,'DATA.MAL')		;WILL CHANGE THIS LATER
	CHNMAL = 2

	OPEN(3,SI,'SMC:ROLO.SMM')
	CHNROL = 3

	OPNOK = 1
	RETURN
;--------------------------------------------

CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (CHN001) CLOSE CHN001
	IF (CHNMAL) CLOSE CHNMAL
	RETURN
;--------------------------------------------


