;  FNCMNT / AR
;
;
;		::PCPYAR.DEF::
;******************************************************************************
;		ACCOUNTS RECEIVABLE 
;
;		RELEASED: AUGUST 1, 1984 (d70s10)
;******************************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;		PROGRAM TO MAINTAIN THE FINANCIAL CONTROL FILE
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD FINCTL	
		.INCLUDE 'DEF:RD173A.DEF'
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD
	CNGCTL	,D1
	ENTRY	,A15
	INXCTL	,D1
	LOKCTL	,D1
	NEW	,D1
	READ	,D1	,0
	RECCNT  ,D5
	SRCCTL	,D1
	SWITCH	,D1
	V       ,D1
	WHATNO  ,D2
	WRITE	,D1	,1
PROC
	XCALL TERID(V)
MENU,
	NEW =
	XCALL OUTPT (1,1,2,'FINANCE CHARGE CONTROL FILE MAINTENANCE',V)
	XCALL OUTPT (2,1,0,'ADD,CHANGE/INQUIRE',1)
OPEN,
	SWITCH = 3
	XCALL FILES (1,'U',173,SWITCH)		;FILE 173 - FINCTL
	IF (SWITCH.EQ.9) GO TO INU173
	SWITCH = 5
	XCALL FILES (1,'U',173,SWITCH)		;FILE 173 - FINCTL
	IF (SWITCH.EQ.9) GO TO CLO173
	GO TO DISPLA
;**********************************************************************
CLO173,
	XCALL FILES (1,'U',173,4)
INU173,
	XCALL PGCHN ('AR:ARSFMN',1)
;**********************************************************************
DISPLA,
	XCALL OUTPT (6,20,0,'1. FINANCE CHARGE % - LEVEL 1',1)
	XCALL OUTPT (8,20,0,'2. LEVEL 1 CUTOFF AMOUNT',1)
	XCALL OUTPT (10,20,0,'3. FINANCE CHARGE % - LEVEL 2',1)
	XCALL OUTPT (12,20,0,'4. AGE PAST DUE BY INV OR DUE DATE',1)
	XCALL OUTPT (14,20,0,'5. # DAYS PAST DUE FOR FIN CHG',1)
	XCALL OUTPT (16,20,0,'6. MINIMUM FINANCE CHARGE',1)
	CNGCTL =
	LOKCTL = 1
	XCALL IO (1,FINCTL,1,READ,LOKCTL)
	IF (AGEFLG.EQ.']') GO TO FNCHG1
	CALL DSPREC
	CNGCTL = 6
	GO TO ANYCNG
FNCHG1,
	CTL = '06,56,05,00,#E'
	CALL INPUT
	GO TO (DISPLA,END), INXCTL
	FNCHG1 = ENTRY
	XCALL DSPLY (5,6,56,FNCHG1,4,1)
	GO TO (ANYCNG), CNGCTL
CUTOFF,
	CTL = '08,56,05,00,# '
	CALL INPUT
	GO TO (DISPLA,END), INXCTL
	CUTOFF = ENTRY
	XCALL DSPLY (5,8,56,CUTOFF,1,1)
	IF (CUTOFF.EQ.0) GO TO NOCHRG
FNCHG2,
	IF (CNGCTL .AND. CUTOFF.EQ.0) GO TO BADCHG
	CTL = '10,56,05,00,# '
	CALL INPUT
	GO TO (DISPLA,END), INXCTL
	FNCHG2 = ENTRY
NOCHRG,
	IF (CUTOFF.EQ.0) FNCHG2 = 0
	XCALL DSPLY (5,10,56,FNCHG2,4,1)
	GO TO (ANYCNG), CNGCTL
AGEFLG,
	CTL = '12,56,01,01,A '
	CALL INPUT
	GO TO (DISPLA,END), INXCTL
	AGEFLG = ENTRY
	IF (AGEFLG.NE.'I'.AND.AGEFLG.NE.'D') GO TO AGEFLG
	GO TO (ANYCNG), CNGCTL
PSTDUE,
	CTL = '14,56,03,00,# '
	CALL INPUT
	GO TO (DISPLA,END), INXCTL
	PSTDUE = ENTRY
	XCALL DSPLY (3,14,56,PSTDUE,1,1)
	GO TO (ANYCNG), CNGCTL
MINCHG,
	CTL = '16,56,04,00,# '
	CALL INPUT
	GO TO (DISPLA,END), INXCTL
	MINCHG = ENTRY
	XCALL DSPLY (5,16,56,MINCHG,3,1)
ANYCNG,
	XCALL ANYCN (CNGCTL,WHATNO)
	GO TO (END,CNGBR), CNGCTL + 1
	GO TO END
CNGBR,
	GO TO (FNCHG1,CUTOFF,FNCHG2,AGEFLG,PSTDUE,MINCHG), WHATNO
	CNGCTL = 3
	GO TO ANYCNG
BADCHG,
	XCALL MESAG ('LEVEL 1 CUTOFF MUST BE NON-ZERO',1)
	GO TO ANYCNG 
;**********************************************************************
DSPREC,
	XCALL DSPLY (5,6,56,FNCHG1,4,1)
	XCALL DSPLY (5,8,56,CUTOFF,1,1)
	XCALL DSPLY (5,10,56,FNCHG2,4,1)
	XCALL OUTPT (12,56,0,AGEFLG,1)
	XCALL DSPLY (3,14,56,PSTDUE,1,1)
	XCALL DSPLY (4,16,56,MINCHG,3,1)
	RETURN
;**********************************************************************
INPUT,
	XCALL INPUT(ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,1)
	RETURN
;**********************************************************************
END,	
	LOKCTL = 1
	XCALL IO (1,FINCTL,1,WRITE,LOKCTL)
	GO TO CLO173
END
