;FRTMNT.AR
;
;	MAINTAIN AND PRINT FREIGHT CLAIMS TABLE.
;
RECORD	FRTCLM
	.INCLUDE 'DEF:RD023A.DEF'

RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'

RECORD	ORDHDR
	.INCLUDE 'DEF:RD044A.DEF'

RECORD	CHANNEL
	CHN023	,D2
	CHN182	,D2
	CHN184	,D2

RECORD	PRINT
	TITLE	,A*,	'FREIGHT CLAIMS'
	HD	,A6,	'NO HDR'
	LG	,A9,	'NO LEGEND'
	PLINE	,A80
	PRNTON	,D1
	LINCNT	,D2,60
	PGCNT	,D6
	LPSW	,D2
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	PRTCTL	,D3,080
	LPARG	,D1
	PRNTSW	,D1
	PRTCTR	,D1


RECORD	HD1
		,A*,	'FC-ORD CARR CUST # NAME                  '
		,A*,	'FC-INV-DAT INV-AMOUNT ORIG #  DATE-PAID'

RECORD	KEY2
	K2_SCAC		,A4
	K2_ORDER	,D6

RECORD	VARS
	REDFIL	,A14
	ORDFIL	,A14
	ORDFND	,D1
	SCFND	,D1
	ALPHA	,A12
	OPNOK	,D1
	BLANKS	,A15
	ORDER	,D6
	ST_SCAC	,A4
	EN_SCAC	,A4
	XDATE	,D8
	TODAY	,D6
	YEAR	,D2
	PGM	,D1
	ROW	,D2
	ENTRY	,A30
	INXCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	STORE	,D1,2
	DELETE	,D1,3
	LOKCTL	,D1
	WHATNO	,D2
	SELECT	,D1
	CNGCTL	,D1
	I	,D3
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'FREIGHT CLAIMS',1)
	XCALL RDATE(TODAY)
	YEAR = TODAY(5,6)
	YEAR = YEAR - 1
	CALL OPENS
	IF (.NOT. OPNOK) GOTO CLOSE

MENU,
	XCALL OUTPT (1,1,2,'FREIGHT CLAIMS',1)
	XCALL OUTPT (3,9,0,'PLEASE SELECT APPLICATION',1)
	XCALL OUTPT (5,15,0,'1. FREIGHT CLAIMS MAINTENANCE',1)
	XCALL OUTPT (6,15,0,'2. PRINT FREIGHT CLAIMS',1)
MINPUT,
	XCALL INPUT (3,36,1,1,'#E',ENTRY,INXCTL,1)
	GOTO (MINPUT,ENDOFF), INXCTL
	PGM = ENTRY(1,1)
	GOTO (DISPLA,PRINT_TABLE),PGM
	GOTO MINPUT

DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,'FREIGHT CLAIMS MAINTENANCE',1)
	XCALL OUTPT ( 4,4,0,'1. FC ORDER #',1)
	XCALL OUTPT ( 6,4,0,'2. ORIG SCAC',1)
	XCALL OUTPT ( 8,4,0,'3. FC INVOICE DATE',1)
	XCALL OUTPT (10,4,0,'4. FC INVOICE AMT',1)
	XCALL OUTPT (12,4,0,'5. ORIG CUST',1)
	XCALL OUTPT (14,4,0,'6. ORIG ORDER #',1)
	XCALL OUTPT (16,4,0,'7. FC INVOICE PD DATE',1)

OORDNO,
	XCALL INPUT (4,28,06,00,'#E',ENTRY,INXCTL,1)
	GOTO (DISPLA,MENU),INXCTL
	ORDER = ENTRY(1,6)
	XCALL ISIO (CHN023,FRTCLM,ORDER,READ,LOKCTL)
	IF (LOKCTL .EQ. 0)
		BEGIN
		SELECT = 2
		CALL DSPREC
		GOTO ANYCNG
		END

	SELECT = 1			;ADD MODE
	CLEAR FRTCLM, CNGCTL
SCAC,
	XCALL INPUT (6,28,04,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	FC_SCAC = ENTRY(1,4)
	CALL GET_SCAC
	IF (.NOT. SCFND) GOTO SCAC
	GOTO (ANYCNG),CNGCTL
OINVDT,
	XCALL INPUT (8,28,08,00,'D ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	FC_OINVDT = ENTRY(1,8)
	GOTO (ANYCNG),CNGCTL
OSALE,
	XCALL INPUT (10,28,08,00,'$ ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	FC_OSALE = ENTRY(1,8)
	GOTO (ANYCNG),CNGCTL
CUST,
	IF (SELECT .EQ. 2) GOTO ANYCNG
	XCALL INPUT (12,28,06,00,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	FC_CUST = ENTRY(1,6)
	GOTO (ANYCNG),CNGCTL
ORIG,
	XCALL INPUT (14,28,06,00,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	FC_ORIG = ENTRY(1,6)
	CALL GET_ORD
	IF (.NOT. ORDFND) GOTO ORIG
	GOTO (ANYCNG),CNGCTL
DATPD,
	XCALL INPUT (16,28,08,00,'D ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	FC_DATPD = ENTRY(1,8)
	GOTO ANYCNG

ANYCNG,
	XCALL OUTPT (24,1,1,'FIELD # TO CHANGE            <TAB> = DELETE',1)
	XCALL INPUT (24,20,02,00,'#T',ENTRY,INXCTL,1)
	IF (INXCTL .EQ. 3)
		BEGIN
		XCALL OUTPT (24,1,1,'DELETE, ARE YOU SURE ?',1)
		XCALL INPUT (24,24,01,01,'YN',ENTRY,INXCTL,1)
		GOTO (ANYCNG),INXCTL-1
		XCALL ISIO (CHN023,FRTCLM,ORDER,DELETE,LOKCTL)
		GOTO DISPLA
		END

	WHATNO = ENTRY(1,2)
	IF (WHATNO .EQ. 0)
	THEN 	CLEAR CNGCTL
	ELSE 	CNGCTL = 1

	GOTO (PROCES,CNGBR),CNGCTL+1
CNGBR,
	GOTO (OORDNO,SCAC,OINVDT,OSALE,CUST,ORIG,DATPD),WHATNO
	GOTO ANYCNG

PROCES,
	CASE SELECT OF
	BEGINCASE
	1:	BEGIN
		FC_OORDNO = ORDER
		XCALL ISIO (CHN023,FRTCLM,ORDER,STORE,LOKCTL)
		END
	2:	XCALL ISIO (CHN023,FRTCLM,ORDER,WRITE,LOKCTL)
	ENDCASE
	GOTO DISPLA

ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('AR:FCMMNU',1)
	STOP


GET_ORD,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		;;; LOOK UP IN CURRENT & PREV YEARS HISTORY
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	ORDFND = 1		;ASSUME FOUND
	ORDFIL = REDFIL	
	ORDFIL(14,14) = 'M'
	OPEN (CHN184, SI, ORDFIL)		;SLHHDR
	XCALL ISIO (CHN184, ORDHDR, FC_ORIG, READ, LOKCTL)
	IF (LOKCTL .EQ. 0) 
		BEGIN
		CLOSE CHN184
		CALL SET_CUST
		RETURN
		END

	CLOSE CHN184
	ORDFIL(9,10) = YEAR,	'XX'
	OPEN(CHN184,SI,ORDFIL)
	XCALL ISIO (CHN184, ORDHDR, FC_ORIG, READ, LOKCTL)
	IF (LOKCTL .EQ. 0) 
		BEGIN
		CLOSE CHN184
		CALL SET_CUST
		RETURN
		END


	ORDFND = 0		;NOT FOUND
	CLOSE CHN184
	RETURN

SET_CUST,
	FC_CUST = OCUSNO
	FC_NAME = OCUSNM
	ENTRY(1,6) = FC_CUST,	'ZZZZZX' [LEFT]
	XCALL OUTPT (12,28,1,ENTRY(1,6),1)

	XCALL OUTPT (12,36,0,FC_NAME,1)

	FC_SCAC = OSCAC
	CALL GET_SCAC
	IF (SCFND) 
		BEGIN
		XCALL OUTPT (6,28,1,FC_SCAC,1)
		XCALL OUTPT (6,36,1,SC_NAME,1)
		END
	RETURN
;-------------------------------------------

GET_SCAC,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		;;; COP TABLE SCAC LOOK-UP
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR TBL_KEY
	TBLCOD = 'SC'
	TBLKEY = FC_SCAC
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .EQ. 0) 
	THEN	BEGIN
		SCFND = 1
		XCALL OUTPT (6,36,1,SC_NAME,1)
		END
	ELSE	BEGIN
		SCFND = 0		
		XCALL MESAG('TRUCKLINE NOT FOUND',1)
		END
	RETURN
;-----------------------------------------------------

DSPREC,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; DISPLAY RECORD
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	ENTRY(1,6) = FC_OORDNO,	'ZZZZZX' [LEFT]
	XCALL OUTPT (4,28,1,ENTRY(1,6),1)

	XCALL OUTPT (6,28,1,FC_SCAC,1)

	XDATE(1,4) = FC_OINVDT(5,8)
	XDATE(5,8) = FC_OINVDT(1,4)
	ENTRY(1,10) = XDATE,	'XX/XX/XXXX'
	XCALL OUTPT (8,28,1,ENTRY(1,10),1)

	ENTRY(1,10) = FC_OSALE	,'ZZZ,ZZX.XX' [LEFT]
	XCALL OUTPT (10,28,1,ENTRY(1,10),1)

	ENTRY(1,6) = FC_CUST,	'ZZZZZX' [LEFT]
	XCALL OUTPT (12,28,1,ENTRY(1,6),1)

	XCALL OUTPT (12,36,0,FC_NAME,1)

	ENTRY(1,6) = FC_ORIG,	'XXXXXX'
	XCALL OUTPT (14,28,1,ENTRY(1,6),1)

	XDATE(1,4) = FC_DATPD(5,8)
	XDATE(5,8) = FC_DATPD(1,4)
	ENTRY(1,10) = XDATE,	'ZZ/ZZ/ZZZZ'
	XCALL OUTPT (16,28,1,ENTRY(1,10),1)

	RETURN
;-----------------------------------



;===================================================================
PRINT_TABLE,
;===================================================================
	LINCNT = 66
	PGCNT  = 0
	
PDISP,
	CNGCTL = 
	XCALL OUTPT (1,1,2,'PRINT FREIGHT CLAIMS',1)
	XCALL OUTPT (4,4,0,'1. STARTING TRUCKLINE',1)
	XCALL OUTPT (6,4,0,'2. ENDING   TRUCKLINE',1)
ST_SCAC,
	XCALL INPUT (4,30,04,00,'AE',ENTRY,INXCTL,1)
	GOTO (PDISP,PDONE),INXCTL
	ST_SCAC = ENTRY(1,4)
	IF (ST_SCAC .EQ. BLANKS)
		BEGIN
		EN_SCAC = 'ZZZZ'
		XCALL OUTPT (4,30,1,'ALL',1)
		XCALL OUTPT (6,30,1,' ',1)
		GOTO P_ANY
		END
	GOTO (P_ANY),CNGCTL
EN_SCAC,
	XCALL INPUT (6,30,04,00,'A ',ENTRY,INXCTL,1)
	GOTO (PDISP),INXCTL
	EN_SCAC = ENTRY(1,4)
	IF (EN_SCAC .EQ. BLANKS)
		BEGIN
		EN_SCAC = ST_SCAC
		XCALL OUTPT (6,25,0,EN_SCAC,1)
		END
P_ANY,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (P_PRINT,P_CNGBR),CNGCTL + 1
P_CNGBR,
	GOTO (ST_SCAC,EN_SCAC),WHATNO
	GOTO P_ANY

P_PRINT,
	CLEAR KEY2
	K2_SCAC = ST_SCAC
	FIND (CHN023,FRTCLM,KEY2,KRF=1)[ERR=PLOOP]

PLOOP,
	XCALL IOS (CHN023,FRTCLM,READ,LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF
	IF (FC_SCAC .LT. ST_SCAC) GOTO PLOOP
	IF (FC_SCAC .GT. EN_SCAC) GOTO EOF


	PLINE(1,6) = FC_OORDNO,	'ZZZZZX'
	PLINE(8,11) = FC_SCAC
	PLINE(13,18) = FC_CUST,	'ZZZZZX'
	PLINE(20,40) = FC_NAME
	XDATE(1,4) = FC_OINVDT(5,8)
	XDATE(5,8) = FC_OINVDT(1,4)
	PLINE(42,51) = XDATE,	'XX/XX/XXXX'
	PLINE(53,62) = FC_OSALE,'ZZZ,ZZX.XX'
	PLINE(64,69) = FC_ORIG,	'ZZZZZX'
	XDATE(1,4) = FC_DATPD(5,8)
	XDATE(5,8) = FC_DATPD(1,4)
	PLINE(71,80) = XDATE	,'XX/XX/XXXX'

	CALL PRINT
	GOTO PLOOP

EOF,
	IF (PRNTON.EQ.1)  XCALL AR_LPOFF(LPSW,SPLFIL,PGCNT)
	PRNTON  = 0

PDONE,
	GOTO MENU

PRINT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (PRNTON .EQ. 0) CALL PRNTON
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HD1,HD,HD
&		,LG,LG,LG,0,080,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;-------------------------------------------------------------
PRNTON,
	SPLFIL (5,6) = 'EF'
	LPSW = 1		;PRINT,SPOOL, OR DISPLAY
	XCALL AR_LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTON = 1
	RETURN
;-------------------------------------------------------------
;===================================================================


OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (1,'SU',023,SWITCH)		;FRTCLM
	IF (SWITCH .EQ. 9) RETURN
	CHN023 = 1

	SWITCH = 5
	XCALL FILES (2,'SI',182,SWITCH)		;COPTBL
	IF (SWITCH .EQ. 9) RETURN
	CHN182 = 2

	XCALL FFILE (184,REDFIL,SWITCH)		;SLHHDR
	CHN184 = 4

	OPNOK = 1
	RETURN
;----------------------------------------------------

CLOSE,
	CLOSE CHN023
	CLOSE CHN182
	RETURN
;----------------------------------------------------
	
	
;FC-ORD CARR CUST # NAME                  FC-INV-DAT INV-AMOUNT ORIG #  DATE-PAID
;ZZZZZX AAAA ZZZZZX AAAAAAAAAAAAAAAAAAAAA XX/XX/XXXX ZZZ,ZZX.XX ZZZZZX XX/XX/XXXX
;12345678901234567890123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5         6         7         8


