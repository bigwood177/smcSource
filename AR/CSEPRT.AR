;  CSEPRT / AR 
;
;
;      CREATED BY : DIGITAL COMPUTER SOFTWARE, INC
;	     SUB  : JR			   
;      PURPOSE    : REPORT FOR EXTENDED CUSTOMER INFORMATION
;
;

RECORD CSEMAS		; 
		.INCLUDE 'DEF:RD135A.DEF'
RECORD CSECTL	,X	; 
		.INCLUDE 'DEF:RD135B.DEF'
RECORD CSEIDX
		.INCLUDE 'DEF:RD136A.DEF'
RECORD HDR1
		,A50,'CUST NO                     A/P PHONE             '
		,A50,'      MKT PHONE                         START     '
		,A7,' LST SL'
RECORD HDR2
		,A50,'NAME                            CONTACT           '
		,A50,'          CONTACT & ADDRESS             DATE      '
		,A5,' DATE'
RECORD TITLE
		,A39,	'EXTENDED CUSTOMER INFORMATION PRINT OUT'
RECORD
	ARRPOS	,D2
	ARRSIZ	,D2	,10		;CHANGE VALUE HERE TO MATCH ARRAY SIZE
	RECNO	,D5
	CUSCNT	,D5
	STRTNO	,A6
	ENDNO	,A6
	STRNUM	,D6
	ENDNUM	,D6
	STXCTL	,D1
	LINCNT	,D2,	60
	PGCNT	,D3,	000
	PLINE	,A132
	PRTCTL	,D3
	LPSW	,D1
	ERRRET	,D1
	KEY   	,A6
	BSMID	,D5
	BSEND	,D5
	SRCCTL	,D1
	ENTRY	,A2
	INXCTL	,D1
	YESNO	,2A1,	'Y','N'
	SWITCH	,D1,	1
	V	,D1
	RPTNUM	,D3
	PRTTYP	,A1
	PRNTSW	,D1
	SPLFIL	,A14
	LPARG	,D1
	READ	,D1,	0
	WRITE	,D1,	1
	LOKCTL	,D1
PROC
	XCALL TERID (V)
BEGIN,
	IF (PRNTSW) CALL LPOFF
	XCALL OUTPT (1,1,2,'E-CUSTOMER MASTER FILE MAINTENANCE',V)
	XCALL OUTPT (2,1,1,'PRINT OUT',1)
	XCALL WATE (4,V)
	SWITCH = 1
	XCALL FILES (6,'I',136,SWITCH)		;FILE # 136 -- CSEIDX FILE
	IF (SWITCH.EQ.9) XCALL PGCHN ('AR:ARMENU',1)
	SWITCH = 1
	XCALL FILES (5,'I',135,SWITCH)		;FILE # 135 -- CSEMAS FILE
	IF (SWITCH.EQ.9) XCALL FILES (5,'I',135,4)
	IF (SWITCH.EQ.9) XCALL PGCHN ('AR:ARMENU',1)
	LOKCTL = 1
	XCALL IO (5,CSEMAS,1,READ,LOKCTL)
	BSEND = ORG135
	ENDNUM = 999999
	STXCTL = 1
	XCALL STENO (STRTNO,ENDNO,'CUSTOMER #',6,STXCTL,V)
	GO TO (END), STXCTL
	ON ERROR NOTNUM
	STRNUM = STRTNO
	ENDNUM = ENDNO
NOTNUM,
	IF (STRTNO.EQ.ENDNO) GO TO BSERCH
	RECNO = 1
READ,
	INCR RECNO
	LOKCTL = 1
	XCALL IO (6,CSEIDX,RECNO,READ,LOKCTL)
	IF (CSEIDX.EQ.']]]]]') GO TO LSTREC
	IF (IECUSN.LT.STRNUM) GO TO READ
	IF (IECUSN.GT.ENDNUM) GO TO LSTREC
	IF (IRC135.EQ.0) GO TO READ
PRTSET,
	LOKCTL = 1
	XCALL IO (5,CSEMAS,IRC135,READ,LOKCTL)
	PLINE (1,6) = ECUSNO
	PLINE (29,40) = EAPPHO, 'ZZZ-ZZZ-ZZZZ'
	PLINE (57,68) = ECOPHO,  'ZZZ-ZZZ-ZZZZ'
	PLINE (91,98) = ESTART,  'XX/XX/XX'
	PLINE (102,109) = ELAST, 'XX/XX/XX'
	CALL PRINT
	PLINE (1,26) = ENAME
	PLINE (29,53)= EAPNA
	PLINE (57,60)= ECONT1
	PLINE (62,71)= ECONT2
	PLINE (73,87)= ECONT3
	CALL PRINT
	PLINE (57,87)= ECOADD
	CALL PRINT
	PLINE (57,71)=ECOCIT
	PLINE (73,74)=ECOST
	PLINE (76,85)=ECOZIP
	CALL PRINT
FINPRT,
	XCALL LINFD (1)
	INCR LINCNT
	IF (STRTNO.EQ.ENDNO) RETURN
	IF (ENDNO.EQ.'[[[') INCR CUSCNT
	GO TO READ
PRINT,
	IF (PRNTSW.EQ.0) CALL LPON
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,'NO HDR',
&	    'NO LEGEND','NO LEGEND',' ',0,132,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
LPON,
	LPSW = 1
	SPLFIL (5,6) = 'AA'
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO END
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTSW = 1
	RETURN
LPOFF,
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	PGCNT =
	LINCNT = 60
	PRNTSW =
	RETURN
BSERCH,
	KEY = ENDNUM, 'XXXXXX'
	XCALL SERCH (6,CSEIDX,KEY,1,6,BSEND,BSMID,SRCCTL,4,7,11,0,0,0,0)
;	ABOVE LINE CHANGED AS PER MCBA PATCH AR10-DIBOL-7
;
	GO TO (NOFIND), SRCCTL
	CALL PRTSET
	CALL CLOSES
	GO TO BEGIN
NOFIND,
	XCALL MESAG ('CUSTOMER NOT ON FILE',1)
	CALL CLOSES
	GO TO BEGIN
LSTREC,
	IF (ENDNO.EQ.'[[[') GO TO PRNT1
	IF (PRNTSW.EQ.0) XCALL MESAG ('NO CUSTOMERS FIT THESE PARAMETERS',1)
	CALL CLOSES
	GO TO BEGIN
PRNT1,
	IF (PRNTSW.EQ.0) XCALL MESAG ('CSEMAS FILE EMPTY',1)
	IF (PRNTSW.EQ.0) GO TO END
	PLINE (1,6) = CUSCNT, 'ZZ,ZZX'
	PLINE (8,24) = 'CUSTOMERS ON FILE'
	XCALL LINFD (2)
	CALL PRINT
END,
	IF (PRNTSW) CALL LPOFF
	XCALL WATE (3,V)
	CALL CLOSES
	XCALL PGCHN ('AR:CSEMNT',1)
CLOSES,
	XCALL FILES (5,'I',135,4)
	XCALL FILES (6,'I',136,4)
	RETURN
END


;;;;;;; MASK
CUST NO                     A/P PHONE                   MKT PHONE                         START      LST SL
NAME                            CONTACT                     CONTACT & ADDRESS             DATE       DATE

XXXXXX                      XXX/XXX/XXXX                XXX/XXX/XXXX
XXXXXXXXXXXXXXXXXXXXXXXXX   XXXXXXXXXXXXXXXXXXXXXXXXX   XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX   XX/XX/XX   XX/XX/XX 
                                                        XXXXXXXXXXXXXXXXXXXXXXXXX
                                                        XXXXXXXXXXXXXXXXXXXXXXXXXXXXX
