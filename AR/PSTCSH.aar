;  PSTCSH / AR 
;
;
;
;		POSTS CASH RECEIPTS TRX TO AROPEN FILE
;		post and sort
;
; 10-01-09 ssq: remove calamt calact


RECORD CASH		; 
		.INCLUDE 'DEF:RD006A.DEF'
RECORD AROPEN		; 
		.INCLUDE 'DEF:RD003A.DEF'
RECORD	DUMARO		; 
		.INCLUDE 'DEF:RD003B.DEF'

RECORD	FFILE
	FILNUM	,D3
	CLCTL	,D1
	REDFIL	,A14
 
RECORD	HEADER
		,A7,	'DBSORT '
	HFILE	,A14

RECORD	VARS
	V	,D1
	READ	,D1,	0
	WRITE	,D1,	1
	LOKCTL	,D1

PROC
	XCALL TERID (V)
	XCALL FLAGS(10000000)
	XCALL OUTPT (2,1,1,'UPDATE A/R OPEN ITEM FILE',1)
	XCALL FILES (6,'I',06,5)		;FILE # 06 -- CASH00 FILE
	LOKCTL = 1
	XCALL IOS (6,CASH,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO ENDCSH

;-
	filnum = 3
	XCALL FFILE (FILNUM,REDFIL,CLCTL)	;GET FILE SPEC
;;;	XCALL FILES (3,'U',03,5)		;FILE # 03 -- AROPEN FILE
open_aro,
	open (3, U, redfil, share:0) [err=in_use]	;exclusive use
	goto not_in_use
in_use,
	xcall chkf ('aropen')		;who has aropen?
	goto open_aro

not_in_use,
;-
	LOKCTL = 1
	XCALL IO (3,DUMARO,1,READ,LOKCTL)

LOOP,
	LOKCTL = 1
	XCALL IOS (6,CASH,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO ENDCSH
	IF (CNAME.EQ.']]]]]]') GO TO ENDCSH
	IF (CNAME.EQ.'000000'.OR.CARCSH.EQ.'N') GO TO LOOP
	IF (CAMRCD.EQ.0.AND.CDISAL.EQ.0) GO TO LOOP

	CLEAR AROPEN

	ADOCNO = CCHKNO
	ADOCTP = 2
	ADOCDT = CRCTDT
	ACUSNO = CCUSNO
	AAMT = CAMRCD
	AOTHER = CDISAL ;;;>+ CALAMT
	AAPLNO = CAPLNO

	ACMPNY = CCMPNY		;SSQ 10-26-04
	INCR REC003
	LOKCTL = 1
	XCALL IO (3, AROPEN, REC003, WRITE, LOKCTL)

	GO TO LOOP
ENDCSH,
	IF (REC003 .GE. MAX003)
		BEGIN
		MAX003 = REC003 + 1
		XCALL FILL (']', AROPEN)
		XCALL IO (3, AROPEN, MAX003, WRITE, LOKCTL)
		END

	ORG003 = REC003
	LOKCTL = 1
	XCALL IO (3,DUMARO,1,WRITE,LOKCTL)
	CLOSE 3

;;; SORT CODE
	FILNUM = 3		
	XCALL FFILE (FILNUM,REDFIL,CLCTL)	;GET FILE SPEC
	HFILE = REDFIL
 
	XCALL OUTPT (2,1,2,HEADER,1)
 
	SORT (IN=REDFIL,RECORD=AROPEN,
&		KEY = (ACUSNO,AAPLNO,ADOCDT,ADOCTP,ADOCNO))

;;; SORT CODE

ENDOFF,
END,
	CLOSE 3
	CLOSE 6
	XCALL PGCHN ('AR:CLRCSH',1)
END
