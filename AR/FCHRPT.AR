;  FCHRPT / AR 
;
;
RECORD FINCHG		; 
		.INCLUDE 'DEF:RD097A.DEF'
RECORD TITLE
	RPTNAM	,A22,	'FINANCE CHARGES REPORT'
RECORD HDR1
		,A1
		,A4,	'CUST'
		,A3
		,A4,	'NAME'
		,A25
		,A8,	'PAST DUE'
		,A5
		,A7,	'FINANCE'
RECORD HDR2
		,A2
		,A2,	'NO'
		,A34
		,A6,	'AMOUNT'
		,A7
		,A6,	'CHARGE'
RECORD LEGND
		,A27,	'FOR THE FINANCE CHARGE DATE'
		,A2
	LGNDTE	,A10
		,A4
		,A23,	'FINANCE CHARGES ACCT = '
	LFCHAC	,A8
		,A4
		,A11,	'A/R ACCT = '
	LARACT	,A8

RECORD	VARS
	XDATE	,D8
	MASK	,A12,	'Z,ZZZ,ZZZ.XX'
	MASK2	,A8,	'XXXX-XXX'
	TOTFCH	,D8
	TOTODU	,D9
	ENTRY	,A4
	INXCTL	,D1
	LINCNT	,D2,	60
	LPARG	,D1
	LPSW	,D1
	PGCNT	,D3
	PLINE 	,A132
	RPTNUM	,D3
	PRTTYP	,A1
	SPLFIL	,A14
	V	,D1
	PRNTSW	,D1
	READ	,D1,	0
	WRITE	,D1,	1
	LOKCTL	,D1
PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'FINANCE CHARGES REPORT',V)
	XCALL FILES (5,'I',97,5)
READ,
	LOKCTL = 1
	XCALL IOS (5,FINCHG,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOFFCH
	XDATE(1,4) = FDOCDT(5,8)
	XDATE(5,8) = FDOCDT(1,4)
	LGNDTE = XDATE, 'XX/XX/XXXX'
;;;	LGNDTE = FDOCDT, 'XX/XX/XXXX'
	IF (FCHACT.EQ.0) LEGND (40,93) =
	IF (FCHACT.EQ.0) GO TO SKPACT
	LFCHAC = FCHACT, MASK2
	LARACT = FARACT, MASK2
SKPACT,
	PLINE (1,6) = FCUSNO
	PLINE (9,33) = FNAME
	PLINE (36,45) = FODUAM, MASK
	PLINE (49,57) = FCHAMT, MASK
	CALL PRINT
	TOTODU = TOTODU + FODUAM
	TOTFCH = TOTFCH + FCHAMT
	GO TO READ
EOFFCH,
	IF (PRNTSW.EQ.0) XCALL MESAG ('FINCHG FILE IS EMPTY',2)
	IF (PRNTSW.EQ.0) GO TO END1
	XCALL LINFD (2)
	PLINE (26,32) = 'TOTALS:'
	PLINE (34,45) = TOTODU, MASK
	PLINE (46,57) = TOTFCH, MASK
	CALL PRINT
OK2PST,
	IF (PRNTSW) CALL LPOFF
	CLOSE 5
	XCALL OUTPT (1,1,2,'FINANCE CHARGES REPORT',V)
	XCALL OUTPT (6,10,0,
&	'DO YOU WISH TO POST THESE CHARGES TO THE SALES TRX FILE ?',V)
	XCALL INPUT (06,69,01,00,'YN',ENTRY,INXCTL,V)
	GO TO (PRTRPT,END1), INXCTL
PRTRPT,
	XCALL PGCHN ('AR:PSTFCH',1)
END1,
	XCALL FILES (5,'I',97,7)
END,
	XCALL PGCHN ('AR:ARMENU',1)
PRINT,
	IF (PRNTSW.EQ.0) CALL LPON
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,'NO HDR',
&		LEGND,'NO LEGEND',' ',0,0,132,1,LPSW,RPTNUM,PRTTYP)
	RETURN
LPON,
	LPSW = 1
	SPLFIL (5,6) = 'AM'
	XCALL AR_LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO OK2PST
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTSW = 1
	RETURN
LPOFF,
	XCALL AR_LPOFF (LPSW,SPLFIL,PGCNT)
	PRNTSW =
	RETURN
END
