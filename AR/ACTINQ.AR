;  ACTINQ / AR 
;
;
;		ALLOWS AGED ACCOUNT INQUIRY THROUGH CRT
;	
;	19-AUG-02 SSQ: DOCTP 6 = credit card
;
; 	5-7-18: ssq convert cusmas to isam
;	8-14-18: ssq convert aropen to isam

	.DEFINE POOLSIZE	,25000
	.DEFINE WNDCHNL		,15
	.DEFINE MAXWINS		,10
	.INCLUDE 'DEF:WINDOWS.DEF'



RECORD	D_STUFF
	D_IN	,D8		;DATE-IN, ANY FORMAT
	D_OUT	,D6		;RETURN VALUE MMDDYY
	D_OUTR	,D8		;RETURN VALUE CCYYMMDD
	D_FMT	,A10		;RETURN VALUE MM/DD/CCYY
	D_SW	,A2		;"99" = DATE CONVERSION ERROR

RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'

RECORD	FUNKEY
	.INCLUDE 'DEF:FUNKEY.DEF'
RECORD AROPEN		; 
		.INCLUDE 'DEF:RD003A.DEF'
RECORD ,X		; 
		.INCLUDE 'DEF:RD003B.DEF'
RECORD ARMACH		; 
		.INCLUDE 'DEF:RD003F.DEF'
RECORD CUSMAS		; 
		.INCLUDE 'DEF:RD001A.DEF'
RECORD CUSCTL	,X	; 
		.INCLUDE 'DEF:RD001B.DEF'

RECORD AGETOT
		,A6,	'AGED: '
	CUR	,A12
		,A7,	'/CUR   '
	THIRTY	,A12
		,A7,	'/30+   '
	SIXTY	,A12
		,A7,	'/60+   '
	NINETY	,A12
		,A4,	'/90+'
RECORD FCHYMD
	FCHYY	,D2
	FCHMM	,D2
	FCHDD	,D2

RECORD AGEYMD
	AGEYY	,D2
	AGEMM	,D2
	AGEDD	,D2
RECORD
	TODAY	,D8
RECORD	,X
	CC	,D2
	YY	,D2
	MM	,D2
	DD	,D2

RECORD
	MONTH	,12A3,	'JAN','FEB','MAR','APR','MAY','JUN','JUL','AUG','SEP',
&			'OCT','NOV','DEC'
	ASTER	,A50,	'**************************************************'

RECORD	WN_NAME
	WN_NAM	,A5
	WN_TNMBR,D4

RECORD	CHANNEL
	CHN001	,D2
	CHN002	,D2
	CHN003	,D2
	CHN182	,D2

RECORD	VARS
	TDATE	,D6
	F_KEY	,D3
	OPNOK	,D1
	WND_1	,D4
	WND_2	,D4
	STAT	,D1
	ALPH	,A3
	DEC	,D3

RECORD
	SAVRFA	,A6
	CUSRFA	,A6
	DISPLY	,A80
	AGEDTE	,D8
	ARCNT	,D5
	CNT	,D5
	BEGCNT	,D5
	CSAGED	,4D9
	CSOTHR	,D8
	CSSALE	,D9
	DMY	,D2
	DMY1	,D2
	LSTCUS	,D6
	MASK	,A12,	'ZZZZ,ZZZ.XX-'
	NODAYS	,D6
	ENTRY	,A7
	INXCTL  ,D1
	STXCTL	,D1
	KEY   	,A6
	BSN001	,D5
	BSN135	,D5
	BSND1	,D5
	BSMID	,D5
	SRCCTL	,D1
	CNGCTL	,D1
	WHATNO	,D1
	TOTAL	,D8
	GTOTAL	,D9
	ROW	,D2
	I	,D2
	SAVCUS	,A6
	SAVAPL	,D6,	-000002
	TCHAR	,D3
	CRTOT	,D9
	GRNTOT	,A13
	MASK1	,A13,	'Z,ZZZ,ZZZ.XX-'
	MASK2	,A6,	'ZZZZZX'
	MASK3	,A8,	'ZZZ,ZZX-'
	PHMASK	,A12,	'ZZZ-XXX-XXXX'
	DTMASK	,A8,	'XX/XX/XX'
	DT	,D1
	ENDCHR	,D3
;;;	DOCTYP	,5A7,	'INVOICE','PAYMENT','CR MEMO','FIN CHG','DR MEMO'
;;;	DOCTYP	,5A3,	'INV','PAY','C/M','F/C','D/M'
	DOCTYP	,6A3,	'INV','PAY','C/M','F/C','D/M','CC '
	ENDSW	,D1
	FLAG	,D1
	SWITCH	,D1,	1
	SYSTEM	,D1
	V	,D1
	ROW2	,D2
	READ	,D1,	0
	WRITE	,D1,	1
	LOKCTL	,D1
	ALPHA	,A12
	SELECT	,A1
	PLINE	,A80
	TLINE	,A80
	SCALE	,D7
	HIGHCR	,D7
PROC
	XCALL TERID (V)
	XCALL FLAGS (80000000,0)	;TEST

;;;;	XCALL ENVRN(SYSTEM)
	XCALL RDATE (TDATE)
	XCALL DATE8(TDATE, D_OUT, TODAY, D_FMT, D_SW)

	CALL INIT_WINDOW

	CALL OPENS
	IF (.NOT. OPNOK) GOTO ENDOFF

BEGIN,
	IF (WND_2)	XCALL W_PROC(WP_REMOVE,WND_2)
	CALL CLEAR_TOTALS

	XCALL W_DISP(WND_1,WD_CLR,WDC_WND)
	XCALL W_DISP(WND_1,WD_POS,6,15,'PLEASE ENTER THE CUSTOMER #')
	XCALL W_UPDT
	XCALL WINPT (WND_1,6,44,06,01,'#E',ENTRY,INXCTL,F_KEY)
	GO TO (BEGIN,ENDS), INXCTL
	CASE F_KEY OF
	BEGINCASE
	079:	GOTO ENDS		;<END> KEY
	133:	GOTO ENDS		;<F11> KEY
	ENDCASE

	CUSNO = ENTRY (1,6)
	KEY = CUSNO, 'XXXXXX'

	LOKCTL = 1
	XCALL ISIO (1, CUSMAS, CUSNO, READ, LOKCTL)
	IF (LOKCTL .EQ. 0) GOTO VERIFY


	XCALL MESAG ('CUSTOMER NOT ON FILE',2)
	GO TO BEGIN
	
VERIFY,
	XCALL W_DISP(WND_1,WD_POS,6,51,NAME)
	SAVCUS = CUSNO, MASK2
SELECT,
	XCALL W_DISP(WND_1,WD_POS,7,15,'<C>REDIT HISTORY OR <A>CCOUNT INQUIRY')
	XCALL WINPT (WND_1,7,55,01,00,'A ',ENTRY,INXCTL)
	GOTO (BEGIN), INXCTL
	IF (ENTRY.EQ.' ') 
		BEGIN
		ENTRY(1,1) = 'A'
		XCALL W_DISP(WND_1,WD_POS,7,55,ENTRY(1,1))
		XCALL W_UPDT
		END
	SELECT = ENTRY
	IF (SELECT.NE.'C'.AND.SELECT.NE.'A') GOTO SELECT
	CNGCTL = 2
	XCALL ANYCN (CNGCTL,WHATNO)
	GO TO (BEGIN), CNGCTL
	IF (SELECT.EQ.'C') GOTO CRHIST
	
	LOKCTL = 1
	XCALL ISIO (3, AROPEN, CUSNO, READ, LOKCTL)
	IF (LOKCTL .NE. 0) GOTO NO_AR
;;;	IF (LOKCTL .EQ. 0) GOTO FOUND
	FIND (3, AROPEN, CUSNO) [ERR=NO_AR]
	GOTO FOUND
NO_AR,

;;;	XCALL IO (3,AROPEN,1,READ,LOKCTL)
;;;	BSND1 = ORG003
;;;	XCALL SERCH (3,AROPEN,KEY,16,21,BSND1,BSMID,SRCCTL,1,8,13,0,0,0,0)
;;;	GO TO (FOUND), SRCCTL + 1

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;	XCALL MESAG ('NOTHING IN ACCOUNT',2)
	XCALL W_DISP(WND_1,WD_POS,24,1,WD_CLR,WDC_LIN,'NOTHING IN ACCOUNT')
	XCALL W_UPDT
REDO,
	CALL INP
	CASE F_KEY OF
	BEGINCASE
	8:	GOTO BEGIN
	13:	GOTO BEGIN
	133:	GOTO BEGIN
	79:	GOTO BEGIN
	27:	GOTO BEGIN
	59:	BEGIN		;F1 KEY

		xcall c_note(cusno)
	;;;	XCALL CUSNT(CUSNO,0,'Y',2,03,19,CUSMAS)
		GOTO BEGIN
		END
	ENDCASE

	GOTO REDO
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

FOUND,
	SAVCUS = ACUSNO, MASK2
	CALL DSPTOP
	ARCNT = BSMID
	LSTCUS = ACUSNO
	BEGCNT = ARCNT
LOOP,
	INCR ROW
	CALL BUCKET
	FLAG =
	IF (ENDSW) GO TO ENDTOT
	CALL DSPLAY
	GO TO (LOOP,BEGIN), FLAG + 1
DSPTOP,
	CLEAR TBL_KEY
	TBLCOD = 'EM'
	EM_CUST = CUSNO
	XCALL ISIO (CHN182, COPTBL, TBL_KEY, READ, LOKCTL)
	IF (LOKCTL .NE. 0) CLEAR COPTBL

	XCALL W_DISP(WND_1,WD_POS,1,1,WD_CLR,WDC_WND)
	XCALL W_DISP(WND_1,WD_POS,1,1,EM_EMAIL(1,37))
	XCALL W_DISP(WND_1,WD_POS,1,38,'CUST: ')
	XCALL W_DISP(WND_1,WD_POS,1,44,SAVCUS)	;44
	XCALL W_DISP(WND_1,WD_POS,1,52,NAME)	;52
	XCALL W_DISP(WND_1,WD_POS,2,52,CITY)
	XCALL W_DISP(WND_1,WD_POS,2,69,STATE)
	ALPHA = EAPPHO,PHMASK
	IF (EAPPHO(1,3).EQ.0) ALPHA(1,4) =
	XCALL W_DISP(WND_1,WD_POS,3,64,'V) ')
	XCALL W_DISP(WND_1,WD_POS,3,67,ALPHA)
	XCALL W_DISP(WND_1,WD_POS,3,36,EAPNA)
	ALPHA(1,6) = CRDLMT,'ZZZZZX'
	ALPHA(7,8) = ECOMNT,'ZX'
	XCALL W_DISP(WND_1,WD_POS,3,1,'CREDIT CODE - AR:         TRW:   ')
	XCALL W_DISP(WND_1,WD_POS,3,19,ALPHA(1,6))
	XCALL W_DISP(WND_1,WD_POS,3,32,ALPHA(7,8))
	XCALL W_DISP(WND_1,WD_POS,4,1,'STARTED: ')
	ALPHA(1,8) = ESTART,DTMASK
	XCALL W_DISP(WND_1,WD_POS,4,10,ALPHA(1,8))
	ALPHA = EFAX,PHMASK
	IF (EFAX(1,3).EQ.0) ALPHA(1,4) =
	XCALL W_DISP(WND_1,WD_POS,4,64,'F) ')
	XCALL W_DISP(WND_1,WD_POS,4,67,ALPHA)


	CALL WINDOW_2
	TLINE = 'DOC-#  TYP     DATE      AMOUNT     OTHER-AMT    TOTAL-AMT   APPLY-TO  AGE'
	XCALL W_DISP(WND_2,WD_POS,1,1,TLINE(1,74) )
	ROW = 2
	RETURN
ENDTOT,
	IF (ROW.GT.12) CALL SHIFT
	GO TO (BEGIN), FLAG
	ENDSW =
	GRNTOT = GTOTAL, MASK1
	XCALL W_DISP(WND_2,WD_POS,14,1,WD_CLR,WDC_LIN)
	XCALL W_DISP(WND_2,WD_POS,14,38,'TOTAL:')
	XCALL W_DISP(WND_2,WD_POS,14,47,GRNTOT)
	IF (BALMTH.EQ.'B') CALL ADJUST
	CUR = CSAGED (1), MASK
	THIRTY = CSAGED (2), MASK
	SIXTY = CSAGED (3), MASK
	NINETY = CSAGED (4), MASK
	XCALL W_DISP(WND_1,WD_POS,22,1,AGETOT)
RED1,
	XCALL WINPT (WND_2,14,01,01,00,'AE',ENTRY,INXCTL,F_KEY)
;;;	CALL INP
	CASE f_key OF
	BEGINCASE
	8:	GOTO BEGIN
	23:	GOTO BEGIN
	27:	GOTO BEGIN
	133:	GOTO BEGIN
	79:	GOTO BEGIN
	59:	BEGIN
		xcall c_note(cusno)
	;;;	XCALL CUSNT(CUSNO,0,'Y',2,03,19,CUSMAS)
		CALL CLEAR_TOTALS
		ACUSNO = SAVCUS
		GOTO FOUND
		END
	ENDCASE

	GOTO RED1


;===================================================================
INP,
	XCALL W_DISP(WND_1,WD_POS,22,70)
;;;	XCALL WINPT (WND_1,21,1,01,00,'AE',ENTRY,INXCTL,F_KEY)
	XCALL WINPT (WND_1,22,70,01,00,'AE',ENTRY,INXCTL,F_KEY)
	return

;==================================================


BUCKET,
	READS (3, AROPEN, ENDIT, GETRFA:SAVRFA)
;;;	LOKCTL = 1
;;;	XCALL IO (3,AROPEN,ARCNT,READ,LOKCTL)
	IF (AROPEN.EQ.']]]]]]'.OR.ACUSNO.NE.LSTCUS) GO TO ENDIT
	INCR ARCNT
	IF (ADOCDT.EQ.0) GO TO BUCKET
	AGEDTE = ADOCDT
	IF (AAPLNO.NE.SAVAPL) CALL NEWAPL
	IF (ADOCTP.EQ.2.OR.ADOCTP.EQ.3) CALL CREDIT
	IF (ADOCTP.EQ.5) CALL DRMEMO
	TOTAL = AAMT + AOTHER
	GTOTAL = GTOTAL + TOTAL
	IF (BALMTH.EQ.'B'.AND.(ADOCTP.EQ.2.OR.ADOCTP.EQ.3)) GO TO ADDCR

	XCALL BDAT8 (TODAY,AGEDTE,DMY,DMY1,NODAYS)
	IF (TODAY.LT.AGEDTE) NODAYS = NODAYS *(-1)

	IF (NODAYS.GT.30) DT = 2
	IF (NODAYS.GT.60) DT = 3
	IF (NODAYS.GT.90) DT = 4
	IF (NODAYS.LE.30) DT = 1
	CSAGED (DT) = CSAGED (DT) + AAMT + AOTHER
	RETURN
ADDCR,
	CRTOT = CRTOT - AAMT - AOTHER
	RETURN
ENDIT,
	ENDSW = 1
	RETURN
NEWAPL,
	SAVAPL = AAPLNO
	CUSRFA = SAVRFA
;;;	BEGCNT = ARCNT - 1
	RETURN
DSPLAY,
	PLINE(1,6) = ADOCNO,	'ZZZZZX'
	PLINE(8,10) = DOCTYP(ADOCTP)

	XCALL DATE8(ADOCDT, D_OUT, D_OUTR, D_FMT, D_SW)
	PLINE(12,19) = D_OUT,	'ZX/XX/XX'
	PLINE(21,32) = AAMT,	MASK
	PLINE(35,46) = AOTHER,	MASK
	PLINE(48,59) = (AAMT + AOTHER),	MASK
	PLINE(62,68) = AAPLNO,	'ZZZZZX-'
	PLINE(72,74) = NODAYS,	'ZZX'

	CALL SETAPL

	IF (ROW.GT.12) CALL SHIFT
	IF (FLAG.EQ.1) RETURN
	XCALL W_DISP(WND_2,WD_POS,ROW,1,WD_CLR,WDC_LIN,PLINE)
	RETURN
SETAPL,
	IF (BALMTH.EQ.'B') GO TO BALFWD
	PLINE(62,68) = AAPLNO,	'ZZZZZX-'
	RETURN
BALFWD,
	RETURN
SHIFT,
	XCALL W_DISP(WND_2,WD_POS,14,1,
&		'TYPE RETURN TO SEE NEXT PAGE',WD_CLR,WDC_EOL)
	FLAG =
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
RED2,
;;;	CALL INP
	XCALL WINPT (WND_2,14,31,01,00,'AE',ENTRY,INXCTL,F_KEY)
	CASE F_KEY OF
	BEGINCASE
	59:	BEGIN
		xcall c_note(cusno)
	;;;	XCALL CUSNT(CUSNO,0,'Y',2,03,19,CUSMAS)
		CALL CLEAR_TOTALS
		ACUSNO = SAVCUS
		GOTO FOUND
		END
	79:	GOTO DONE
	133:	GOTO DONE
	ENDCASE

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	ROW = 3
	XCALL W_DISP(WND_2,WD_POS,1,1,WD_CLR,WDC_EOW)
	XCALL W_DISP(WND_2,WD_POS,1,1,TLINE(1,74) )
	RETURN

DONE,
	FLAG = 1
	RETURN
ADJUST,
	I = 5
ADJ1,
	I = I - 1
	IF (I.EQ.0) GO TO TSTOCR
	IF (CRTOT.GT.CSAGED(I)) GO TO SET0
	CSAGED (I) = CSAGED (I) - CRTOT
	RETURN
SET0,
	CRTOT = CRTOT - CSAGED (I)
	CSAGED (I) =
	GO TO ADJ1
TSTOCR,
	IF (CRTOT.GT.0) CSAGED (1) = (-1) * CRTOT
	RETURN
CREDIT,
	AAMT = AAMT * (-1)
	AOTHER = AOTHER * (-1)
DRMEMO,
	IF (AAPLNO.EQ.0.OR.BALMTH.EQ.'B') RETURN
	FIND (33, ARMACH, RFA:CUSRFA)
;;;	CNT = BEGCNT - 1
MACHAR,
	LOKCTL = 1
	XCALL IOS (33, ARMACH, READ, LOKCTL)

;;;	INCR CNT
;;;	LOKCTL = 1
;;;	XCALL IO (3,ARMACH,CNT,READ,LOKCTL)
	IF (ACUSNO.NE.MCUSNO) GO TO OUTMCH
	IF (AAPLNO.NE.MAPLNO) GO TO OUTMCH
	IF (AAPLNO.NE.MDOCNO) GO TO MACHAR
	IF (MDOCTP.NE.1.AND.MDOCTP.NE.4.AND.MDOCTP.NE.6) GO TO MACHAR
	IF (MDOCDT.EQ.0) GO TO MACHAR
	AGEDTE = MDOCDT
OUTMCH,
	RETURN


;===================================
CLEAR_TOTALS,
	ENDSW =
	TOTAL =
	GTOTAL =
	CRTOT =
	ROW = 2
	CSAGED(1) =
	CSAGED(2) =
	CSAGED(3) =
	CSAGED(4) =
	RETURN
;----------------------------------
;;;
;;;--- CREDIT HISTORY DISPLAY
;;;
CRHIST,
	IF (SRCCTL) 
	BEGIN
	  XCALL MESAG ('NO CREDIT HISTORY ON FILE FOR THIS CUSTOMER',1)
	  GOTO BEGIN
	END
	IF (WND_2)
		BEGIN
		XCALL W_PROC(WP_REMOVE,WND_2)
		XCALL W_PROC(WP_DELETE,WND_2)
		CLEAR WND_2
		END
	XCALL W_DISP(WND_1,WD_POS,3,1,WD_CLR,WDC_EOW)
;;;;;;
	XCALL W_DISP(WND_1,WD_POS,1,38,'CUST: ')
	XCALL W_DISP(WND_1,WD_POS,1,44,SAVCUS)	;44
	XCALL W_DISP(WND_1,WD_POS,1,50,'  ')	;50
	XCALL W_DISP(WND_1,WD_POS,1,52,NAME)	;52
	XCALL W_DISP(WND_1,WD_POS,2,52,CITY)
	XCALL W_DISP(WND_1,WD_POS,2,69,STATE)
	ALPHA = EAPPHO,PHMASK
	IF (EAPPHO(1,3).EQ.0) ALPHA(1,4) =
	XCALL W_DISP(WND_1,WD_POS,3,64,'V) ')
	XCALL W_DISP(WND_1,WD_POS,3,67,ALPHA)
	XCALL W_DISP(WND_1,WD_POS,3,36,EAPNA)
	ALPHA(1,6) = CRDLMT,'ZZZZZX'
	ALPHA(7,8) = ECOMNT,'ZX'
	XCALL W_DISP(WND_1,WD_POS,3,1,'CREDIT CODE - AR:         TRW:   ')
	XCALL W_DISP(WND_1,WD_POS,3,19,ALPHA(1,6))
	XCALL W_DISP(WND_1,WD_POS,3,32,ALPHA(7,8))
	XCALL W_DISP(WND_1,WD_POS,4,1,'STARTED: ')
	ALPHA(1,8) = ESTART,DTMASK
	XCALL W_DISP(WND_1,WD_POS,4,10,ALPHA(1,8))
	ALPHA = EFAX,PHMASK
	IF (EFAX(1,3).EQ.0) ALPHA(1,4) =
	XCALL W_DISP(WND_1,WD_POS,4,64,'F) ')
	XCALL W_DISP(WND_1,WD_POS,4,67,ALPHA)

	XCALL W_DISP(WND_1,WD_ATTR,ATTR_LOAD+ATTR_RVRS)
	CLEAR PLINE
	XCALL W_DISP(WND_1,WD_POS,5,1,PLINE)
	XCALL W_DISP(WND_1,WD_ATTR,ATTR_CLR+ATTR_RVRS)

	XCALL W_DISP(WND_1,WD_POS,6,1,'------ CREDIT HISTORY -----')
	XCALL W_DISP(WND_1,WD_POS,7,1,'            HIGH   AVG DAYS')
	XCALL W_DISP(WND_1,WD_POS,8,1,'PERIOD     CREDIT    TO PAY')
	XCALL W_DISP(WND_1,WD_POS,9,1,'------    --------   ------')
	HIGHCR =

	FOR I FROM 1 THRU 12
		BEGIN
		IF (EHIGHC(I).GT.HIGHCR) HIGHCR = EHIGHC(I)
		END

	SCALE = ((HIGHCR)+(HIGHCR)#2)/50
	ROW = 10
	PLINE (1,3) = MONTH(MM)		;;;>,'ZX' [LEFT]
	PLINE (5,6) = YY	,'XX'
	PLINE (11,18) = EHIGHC(MM),MASK3
	IF (EPAYNO(MM)) PLINE (24,27) = (EPAYDA(MM) / EPAYNO(MM)),MASK3
 	XCALL W_DISP(WND_1,WD_POS,ROW,1,PLINE)
	PLINE =
	PLINE (1,3) = MONTH(MM)		;;;>,'ZX' [LEFT]
	PLINE (5,6) = YY-1	,'XX'
	PLINE (11,18) = EHIGHC(13),MASK3
	IF (EPAYNO(13)) PLINE (24,27) = (EPAYDA(13) / EPAYNO(13)),MASK3
	XCALL W_DISP(WND_1,WD_POS,ROW,33,PLINE)
	PLINE =
;;;	FOR I = 1 STEP 1 UNTIL 12
	FOR I FROM 1 THRU 12
	BEGIN
	  PLINE (1,3) = MONTH(I)	;;;>,'ZX' [LEFT]
	  PLINE (5,6) = YY		,'XX'
	  IF (I.GT.MM) PLINE (5,6) = YY - 1	,'XX'
	  PLINE (11,18) = EHIGHC(I),MASK3
	  IF (EPAYNO(I)) PLINE (24,27) = (EPAYDA(I) / EPAYNO(I)),MASK3
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	  IF (SCALE .AND. (EHIGHC(I)/SCALE.GT.0) ) 
&	  	PLINE (30,80) = ASTER(1,EHIGHC(I)/SCALE)

;;;	     IF (HIGHCR.GT.0)
;;;	     BEGIN
;;;	     SCALE = EHIGHC(I)/HIGHCR
;;;	     IF (SCALE.GT.0) PLINE (30,80) = ASTER(1,SCALE)
;;;	     END
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	  IF (I.LT.MM) XCALL W_DISP(WND_1,WD_POS,11+MM-I,1,WD_CLR,WDC_EOL,PLINE)
	  IF (I.GT.MM) XCALL W_DISP(WND_1,WD_POS,23+MM-I,1,WD_CLR,WDC_EOL,PLINE)
	  PLINE =
	END

;;-
	XCALL W_DISP(WND_1,WD_POS,22,1,WD_CLR,WDC_EOL,'CR TO CONINUE')
	CALL INP
	CASE F_KEY OF
	BEGINCASE
	59:	BEGIN
		xcall c_note(cusno)
	;;;	XCALL CUSNT(CUSNO,0,'Y',2,03,19,CUSMAS)
		CALL CLEAR_TOTALS
		ACUSNO = SAVCUS
		GOTO CRHIST
		END
	ENDCASE
;;-
;;	XCALL MESAG (' ',2)
	GOTO BEGIN
ENDOFF,
ENDS,
	CALL CLOSE
EXIT,
	XCALL PGCHN ('AR:ARMENU',1)

OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

;;;	SWITCH = 1
;;;	XCALL FILES (2,'I',02,SWITCH)		;FILE # 02 -- CUSIDX FFILE
;;;	IF (SWITCH.EQ.9) RETURN
;;;	CHN002 = 2

	XCALL FILES (3,'SI',03,SWITCH)		;FILE # 03 -- AROPEN FILE
	IF (SWITCH.EQ.9) RETURN
	CHN003 = 3

	XCALL FILES (33,'SI',03,5)		;FOR ARMACH
	IF (SWITCH .EQ. 9) RETURN
	
;;;	XCALL FILES (1,'U',01,SWITCH)		;FILE # 01 -- CUSMAS FILE
	XCALL FILES (1,'SI',01,SWITCH)		;ssq 9-7-04
	IF (SWITCH.EQ.9) RETURN
	CHN001 = 1

	XCALL FILES (18, 'SI', 182, SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN182 = 18


	OPNOK = 1
	RETURN
;-------------------------------------------------------------

CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (CHN001) XCALL FILES (CHN001,'SI',001,4)
	IF (CHN003) XCALL FILES (CHN003,'SI',003,4)
	IF (CHN182) CLOSE CHN182
	CLOSE 33
	RETURN
;-------------------------------------------------------------


INIT_WINDOW,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL TNMBR (WN_TNMBR)
	WN_NAM = "ACINQ"
	XCALL W_INIT(POOLSIZE,WNDCHNL,MAXWINS)
	XCALL W_PROC(WP_FIND,WND_1,WN_NAME)
	IF (WND_1.EQ.0)
		BEGIN
		XCALL W_PROC(WP_CREATE,WND_1,WN_NAME,22,78)
		END
	XCALL W_BRDR(WND_1,WB_TITLE,'CRT ACCOUNT INQUIRY',
&			WB_TPOS,WBT_TOP,WBT_CENTER)
	XCALL W_PROC(WP_PLACE,WND_1,2,2)	
	XCALL W_DISP(WND_1,WD_CLEAR)
	RETURN

WINDOW_2,
	IF (WND_2)
		BEGIN
		XCALL W_PROC(WP_REMOVE,WND_2)
		XCALL W_PROC(WP_DELETE,WND_2)
		END
	WN_NAM = "ACI01"
	XCALL W_PROC(WP_FIND,WND_2,WN_NAME)
	IF (WND_2.EQ.0)
		BEGIN
		XCALL W_PROC(WP_CREATE,WND_2,WN_NAME,14,76)
		END
	XCALL W_PROC(WP_PLACE,WND_2,7,3)	
	XCALL W_DISP(WND_2,WD_CLEAR)
	CLEAR PLINE
	RETURN
;-----------------------------------------------------------------

END
;------ CREDIT HISTORY -----
;            HIGH   AVG DAYS
;PERIOD     CREDIT    TO PAY
;------    --------   ------
;JUL 88    XXX,XXX-     XXX      JUL 87    XXX,XXX-    XXX
;
;MON 88    XXX,XXX-     XXX 
;MON 88    XXX,XXX-     XXX
;MON 88    XXX,XXX-     XXX
;MON 88    XXX,XXX-     XXX
;MON 88    XXX,XXX-     XXX
;MON 88    XXX,XXX-     XXX
;MON 87    XXX,XXX-     XXX
;MON 87    XXX,XXX-     XXX
;MON 87    XXX,XXX-     XXX
;MON 87    XXX,XXX-     XXX
;MON 87    XXX,XXX-     XXX
;
;DOC-#  TYP     DATE      AMOUNT     OTHER-AMT    TOTAL-AMT   APPLY-TO  AGE
;XXXXXX AAA XX/XX/XX ZZZZ,ZZX.XX-  ZZZZ,ZZX.XX- ZZZZ,ZZX.XX-  XXXXXX    ZZX
;1234567890123456789012345678901234567890123456789012345678901234567890123456
;         1         2         3         4         5         6         7

