NO LONGER USED
;  CNVCUS / AR 
;
;
;		::PCPYAR.DEF::
;******************************************************************************
;		ACCOUNTS RECEIVABLE 
;
;		RELEASED: AUGUST 1, 1984 (d70s10)
;******************************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;		PROGRAM TO CONVERT CUSMAS FILE FOR USE WITH AR RELEASE 1.
;
;***************************************************************************
;
;
;	PATCHED AS PER MCBA PATCH AR14-DIBOL-7		30-AUG-85
;
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD NEWMAS
	      	,A217
RECORD OLDMAS
		; ::CRD001A.DEF::
		; CUSTOMER MASTER FILE - RD001A.DEF
		; RECORD SIZE = 215
	CUSNO	,D6  	; CUSTOMER NUMBER
		,A25  	; CUSTOMER NAME
		,A25	; ADDRESS LINE 1
		,A21  	; ADDRESS LINE 2
		,A15  	; CITY
		,A2	; STATE - POST OFFICE CODE
		,A10	; ZIP CODE (XXXXX-XXXX)
		,D10	; PHONE NUMBER (XXX-XXX-XXXX)
		,D2   	; SALESMAN NUMBER
	TERR	,A2	; STATE ;USED FOR SALES ANALYSIS
		,A2	; INVENTORY LOCATION
		,A2	; CUSTOMER TYPE - USER DEFINED
		,D2	; CUSTOMER SHIP VIA CODE ( WAS CUSTOMER CODE 2 - UNUSED
		,D8	; SALES MONTH-TO-DATE ($XXX,XXX.XX-)
		,D8   	; SALES YEAR-TO-DATE ($XXX,XXX.XX-)
		,D8	; COST MONTH-TO-DATE ($XXX,XXX.XX-)
		,D8	; COST YEAR-TO-DATE ($XXX,XXX.XX-)
	TAXFLG	,D1	; TAX FLAG (1=TAXABLE  2=NONTAXABLE)
		,D6	; CREDIT LIMIT ($XXX,XXX.-)
		,D8	; ACCOUNT BALANCE ($XXX,XXX.XX-)
		,D1	; TERMS - USER DEFINED
	OBALMTH	,A1	; ACCOUNT BALANCE METHOD
			;   (O=OPEN ITEM  B=BALANCE FORWARD)
		,D1	; STATEMENT FLAG
			;   (1=GETS A STATEMENT    2=DOES NOT GET A STATEMENT)
		,A1	; EXTERNAL FLAG - UNUSED
		,10A2	; PRODUCT CODE ARRAY
		,10D2	; DISCOUNT PERCENT ARRAY (XX%)
;:
RECORD,X
	TCUSNO	,A6
		,A201
	MAX001	,D5
		,A3

RECORD TMPMAS
	FRONT	,A156
	TXCODE	,A3
	CSEND	,A58
RECORD	,X
		; CUSTOMER MASTER FILE - RD001A.DEF
		; RECORD SIZE = 217
		,D6  	; CUSTOMER NUMBER
		,A25  	; CUSTOMER NAME
		,A25	; ADDRESS LINE 1
		,A21  	; ADDRESS LINE 2
		,A15  	; CITY
		,A2	; STATE - POST OFFICE CODE
		,A10	; ZIP CODE (XXXXX-XXXX)
		,D10	; PHONE NUMBER (XXX-XXX-XXXX)
		,D2   	; SALESMAN NUMBER
		,A2	; TERRITORY
		,A2	; INVENTORY LOCATION
		,A2	; CUSTOMER TYPE - USER DEFINED
		,A2	; CUSTOMER CODE 2 - UNUSED
		,D8	; SALES MONTH-TO-DATE ($XXX,XXX.XX-)
		,D8   	; SALES YEAR-TO-DATE ($XXX,XXX.XX-)
		,D8	; COST MONTH-TO-DATE ($XXX,XXX.XX-)
		,D8	; COST YEAR-TO-DATE ($XXX,XXX.XX-)
		,A3	; TAX FLAG ( CODES FOUND IN 'ARTCDE.XXX' )
		,D6	; CREDIT LIMIT ($XXX,XXX.-)
		,D8	; ACCOUNT BALANCE ($XXX,XXX.XX-)
		,A1	; TERMS - ( CODES FOUND IN 'ARTERM.XXX' )
	NBALMTH	,A1	; ACCOUNT BALANCE METHOD
			;   (O=OPEN ITEM  B=BALANCE FORWARD)
		,D1	; STATEMENT FLAG
			;   (1=GETS A STATEMENT    2=DOES NOT GET A STATEMENT)
		,A1	; EXTERNAL FLAG - UNUSED
		,10A2	; PRODUCT CODE ARRAY
		,10D2	; DISCOUNT PERCENT ARRAY (XX%)
;:

RECORD NEWCTL
		,A2
	CTLREC	,A215
RECORD 
	NCUSMS	,A14,	'   :CUSMAS.111'
	OCUSMS  ,A14,	'   :CUSMAS.   '
	UTFILE	,A14
RECORD BRACKS
		,A50,	']]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]'
		,A50,	']]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]'
		,A50,	']]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]'
		,A50,	']]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]'
		,A17,	']]]]]]]]]]]]]]]]]'
RECORD COMPNY
			.INCLUDE 'DEF:CMP001.DEF'
RECORD DEVICE
			.INCLUDE 'DEF:DEV001.DEF'
RECORD
	CNGCTL  ,D1
	CMPNTR	,D1
	CNT1	,D2
	CNT2	,D2
	DEFCOD	,A3
	ENTRY	,A3
	ERROR	,D5
	FILBL	,A14
	INXCTL	,D1
	LOKCTL	,D1
	READ    ,D1,    0
	WRITE   ,D1,    1
	READS   ,D1,    0
	SIZE	,D5
	SWITCH	,D1
	WRITES	,D1, 	1
	TERM    ,A1
	TSTOLD  ,D3,	215
	TSTSIZ  ,A300
	WHATNO  ,D2
	V	,D1
PROC (3)
	V = -1			 ;RUN LOGGED OUT OF SECURITY SYSTEM.
	XCALL TERID (V)
BEGIN,
	XCALL OUTPT (1,1,2,'CONVERT CUSTOMER MASTER FILE',V)
	UTFILE = 'UT:COMPNY.DDF'
	XCALL OPENF (11,'I',UTFILE,ERROR,0,SWITCH)
	IF (SWITCH) GOTO ERRCO
	LOKCTL = 1
	XCALL IO (11,COMPNY,1,READ,LOKCTL)
	CLOSE 11
	UTFILE = 'UT:DEVICE.DDF'
	XCALL OPENF (11,'U',UTFILE,ERROR,0,SWITCH)
	IF (SWITCH) GOTO ERRDEV
	XCALL OUTPT (6,17,0,'1. PLEASE ENTER COMPANY CODE',V)
	XCALL OUTPT (7,17,0,'2. PLEASE ENTER DEFAULT TAX CODE',V)
COMCOD,
	XCALL INPUT (6,47,3,3,'AE',ENTRY,INXCTL,V)
	GO TO (CLOS11,END), INXCTL
	OCUSMS(12,14) = ENTRY
	CMPNTR =
FINDCO,
	INCR CMPNTR
	IF (CMPNTR.GT.8) GO TO COMPNF
	IF (COMCOD(CMPNTR).NE.ENTRY) GO TO FINDCO
	GO TO (ANYCNG), CNGCTL
DEFCOD,
	XCALL INPUT (7,50,3,3,'AE',ENTRY,INXCTL,V)
	GO TO (CLOS11,END), INXCTL
	DEFCOD = ENTRY
ANYCNG,
	XCALL ANYCN (CNGCTL,WHATNO)
	GO TO (OPEN,CNGNBR), CNGCTL + 1
CNGNBR,
	GO TO (COMCOD,DEFCOD), WHATNO
	GO TO ANYCNG
COMPNF,
	XCALL MESAG ('INVALID COMPANY CODE',1)
	GO TO COMCOD
OPEN,
	XCALL WATE (4,V)
	NEWMAS =
	TSTOLD =
	LOKCTL = 1
	XCALL IO (11,DEVICE,001,READ,LOKCTL)
	OCUSMS(1,3) = DEVNAM(CMPNTR)
	CNT1 =
	CNT2 =
	FILBL =
LOOP1,
	IF (CNT1.GE.14) GO TO SQZNEW
	INCR CNT1
	IF (OCUSMS(CNT1,CNT1).EQ.' ') GO TO LOOP1
	INCR CNT2
	FILBL(CNT2,CNT2) = OCUSMS(CNT1,CNT1)
	GO TO LOOP1
SQZNEW,
	OCUSMS = FILBL
	NCUSMS(1,3) = DEVNAM(CMPNTR)
	CNT1 =
	CNT2 =
	FILBL =
LOOP2,
	IF (CNT1.GE.14) GO TO FINSQZ
	INCR CNT1
	IF (NCUSMS(CNT1,CNT1).EQ.' ') GO TO LOOP2
	INCR CNT2
	FILBL(CNT2,CNT2) = NCUSMS(CNT1,CNT1)
	GO TO LOOP2
FINSQZ,
	NCUSMS = FILBL
	IF (STATUS(CMPNTR).NE.0) GO TO OPNERR
	XCALL OPENF (1,'I',OCUSMS,ERROR,0,SWITCH)
	IF (SWITCH) GOTO FILENF
	STATUS(CMPNTR) = 99
	LOKCTL = 1
	XCALL IO (11,DEVICE,001,WRITE,LOKCTL)
	LOKCTL = 1
	XCALL IOS (1,TSTSIZ,READS,LOKCTL)
	XCALL RSTAT (TSTOLD,TERM)
	IF (TSTOLD.NE.215) XCALL MESAG ('INCORRECT CUSMAS RECORD LENGTH',1)
	IF (TSTOLD.NE.215) GO TO EOFILE
	LOKCTL = 1
	XCALL IO (1,OLDMAS,1,READ,LOKCTL)
;	SIZE = (((MAX001 + 1) * (217 + 2))/512) + 1
;	ABOVE LINE REMOVED AS PER MCBA PATCH AR14-DIBOL-7
;
	SIZE = (((MAX001 + 2) * (217 + 2))/512) + 1
;	ABOVE LINE ADDED AS PER MCBA PATCH AR14-0DIBOL-7
;
	XCALL OPENF (2,'O',NCUSMS,SIZE,217,SWITCH)
	IF (SWITCH) GO TO NOROOM
	XCALL OUTPT (12,1,0,'PROCESSING CUSTOMER NUMBER',V)
	CTLREC = OLDMAS
	NEWMAS = NEWCTL
	LOKCTL = 1
	XCALL IOS (2,NEWMAS,WRITE,LOKCTL)
PROCES,
	NEWMAS =
	LOKCTL = 1
	XCALL IOS (1,OLDMAS,READS,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOFILE
	IF (TCUSNO.EQ.']]]]]]') GO TO WRTBRK
	XCALL OUTPT (12,29,0,CUSNO,V)
	FRONT = OLDMAS(1,156)
	IF (OLDMAS(157,157).EQ.'2') TXCODE =
;	IF (OLDMAS(157,157).EQ.'1') TXCODE = DEFCOD
	IF (OLDMAS(157,157).EQ.'1') TXCODE = TERR	;
	CSEND = OLDMAS(158,215)
	NBALMTH = 'O'
	NEWMAS = TMPMAS
	LOKCTL = 1
	XCALL IOS (2,NEWMAS,WRITES,LOKCTL)
	GO TO PROCES
WRTBRK,
	NEWMAS = BRACKS
	LOKCTL = 1
	XCALL IOS (2,NEWMAS,WRITES,LOKCTL)
	LOKCTL = 1
	XCALL IOS (1,OLDMAS,READS,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOFILE
	GO TO WRTBRK
EOFILE,
	LOKCTL = 1
	XCALL IO (11,DEVICE,001,READ,LOKCTL)
	STATUS(CMPNTR) =
	LOKCTL = 1
	XCALL IO (11,DEVICE,001,WRITE,LOKCTL)
	CLOSE 11
	CLOSE 1
	CLOSE 2
	IF (TSTOLD.NE.215) GO TO END
	XCALL RENAM (3,OCUSMS,NCUSMS)
	XCALL OUTPT (2,1,2,'\',V)
	XCALL MESAG ('CONVERSION COMPLETED',2)
	GO TO BEGIN
;***********************************************************************
ERRCO,
	XCALL MESAG ('FILE ''COMPNY.DDF'' NOT FOUND ON DEVICE UT',1)
	GO TO END
ERRDEV,
	XCALL MESAG ('FILE ''DEVICE.DDF'' NOT FOUND ON DEVICE UT',1)
	GO TO END
;**********************************************************************
NOROOM,
	IF (SWITCH.EQ.3) GO TO FILERR
	XCALL MESAG ('NO ROOM ON DEVICE FOR NEW CUSMAS FILE',1)
	CLOSE 11
	GO TO BEGIN
FILERR,
	ENTRY = SIZE
	XCALL OUTPT (24,1,1,'DIBOL ERROR # ',1)
	XCALL OUTPT (0,0,0,ENTRY,1)
	XCALL OUTPT (0,0,0,' ON OPENING CUSMAS FILE',1)
	CLOSE 11
	GO TO BEGIN
OPNERR,
	XCALL MESAG ('FILE IN USE',1)
	CLOSE 11
	GO TO BEGIN
FILENF,
	XCALL MESAG ('FILE NOT FOUND WITH REQUESTED COMPANY CODE EXTENSION',1)
	CLOSE 11
	GO TO BEGIN
CLOS11,
	CLOSE 11
	GO TO BEGIN
;**********************************************************************
END,
	XCALL PGMND
END
