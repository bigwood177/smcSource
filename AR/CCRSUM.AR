;CCRSUM
;	SPLIT CASH AND SALES POSTING BATCHES.
;  DSTSUM / AR 
;
; 11-18-13: converted to isam
RECORD	COMP14
	ESC_11	,A1		;<ESC>
		,a4,	"&l1O"	;landscape mode
	ESC_12	,A1		;<ESC>
		,A4,	"&l8D"	;vertical spacing, 8 lines/inch
	ESC_13	,A1		;<ESC>
	C14V	,A8,	"(s13.00H"	;pitch 14 CPI


RECORD	FFILE
	FILNUM	,D3
	CLCTL	,D1
	REDFIL	,A14
 
RECORD,X
	PROGRM	,A9
 
RECORD	HEADER
		,A7,	'DBSORT '
	HFILE	,A14

RECORD SLSDST		; 
		.INCLUDE 'DEF:RD005A.DEF'
RECORD ARACCT		; 
		.INCLUDE 'DEF:RD007A.DEF'
;RECORD	,X		; 
;		.INCLUDE 'DEF:RD007B.DEF'
RECORD HDR1
		,A2
		,A9,	'ACCT-TYPE'
		,A6
		,A6,	'ACCT-#'
		,A3
		,A11,	'DESCRIPTION'
		,A24
		,A10,	'AMT-POSTED'
RECORD TITLE
		,A*,	'CC - CASH DISTRIBUTION SUMMARY'


RECORD	VARS
	LINCNT	,D2,	60
	LINCN4	,D2,	60
	PGCNT	,D3,	000
	PGCN4	,D3,	000
	PLINE	,A132
	PLIN4	,A132
	PRTCTL	,D3
	RPTNUM	,D3
	PRTTYP	,A1
	SPLFIL	,A14
	PDFFIL	,A*,	'C:\SMC\SPOOL\CCRSUM.PDF'
	LPSW	,D1
	LPARG	,D1
	MSGCTL	,D1
	BSEND	,D5
	BSMID	,D5
	SRCCTL	,D1
	KEY	,A7
	MASK	,A14,	'ZZ,ZZZ,ZZZ.XX-'
	MASK2	,A8,	'XXXX-XXX'
	ACTTOT	,D10
	SUBTOT	,D10
	GRDTOT	,D10
	SAVTYP	,D1,	-1
	SAVACT	,D7
	ENDFLG	,D1
	V	,D1
	READ	,D1,	0
	WRITE	,D1,	1
	LOKCTL	,D1
	CTOT	,D10,	0
PROC
	XCALL TERID (V)
	XCALL OUTPT (2,1,1,'CREDIT CARD PRINT DISTRIBUTION SUMMARY',1)	

	FILNUM = 005				;SLSDST
	XCALL FFILE (FILNUM,REDFIL,CLCTL)	;GET FILE SPEC
	HFILE = REDFIL
 
	XCALL OUTPT (2,1,2,HEADER,1)
 
	SORT (IN=REDFIL,
&		RECORD=SLSDST,KEY = (SDSTYP,SDSACT,SDSDAT,SDSCUS,SDSDOC))
 
RESTRT,
	LPSW = 4
	SPLFIL (5,6) = 'AE'
	XCALL AR_LPON (LPSW,SPLFIL)
;;;	IF (LPSW.EQ.0) GO TO NOPRNT
	IF (LPSW.EQ.0) 
	BEGIN
	 XCALL MESAG ('CAN NOT ABORT THIS PRINTOUT-PLEASE RESELECT PRINTER',2)
	 GOTO RESTRT
	END

	open (44,o,pdffil)		;12-15-10

	xcall ascii (27, esc_11)
	set esc_12, esc_13 = esc_11
	display (44, comp14)


	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	XCALL FILES (5,'I',5,5)			;FILE # 05 -- SLSDST FILE
	LOKCTL = 1
	XCALL IOS (5,SLSDST,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOF
	XCALL FILES (7,'SI',07,5)
READ,
	LOKCTL = 1
	XCALL IOS (5,SLSDST,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOF
	IF (SLSDST.EQ.']]]]]]') GO TO EOF
	IF (SDSTYP.NE.SAVTYP) GO TO NEWTYP
	IF (SDSACT.NE.SAVACT) GO TO NEWACT
ADD,
	ACTTOT = ACTTOT + SDSAMT
	SUBTOT = SUBTOT + SDSAMT
	IF (SDSTYP.EQ.0.OR.SDSTYP.EQ.6.OR.SDSTYP.EQ.7) SDSAMT = (-1) * SDSAMT
	GRDTOT = GRDTOT + SDSAMT
	GO TO READ
NEWACT,
	IF (SAVTYP.EQ.-1) GO TO FSTACT
	PLINE (17,24) = SAVACT, MASK2
	PLINE (27,56) = ARACDS
	PLINE (59,72) = ACTTOT, MASK
;;;
;	SECTION ADDED TO SUBTOT COUNTIES ON DST SUM REPORT 12/7/93
;;;
	IF (SAVTYP.EQ.3)
	BEGIN
	  IF (SAVACT(5,7).NE.0) CTOT = CTOT + ACTTOT
	  IF (SDSACT(1,4).NE.SAVACT(1,4).AND.CTOT.NE.0)
	  BEGIN
	    PLINE (83,86) = SAVACT(1,4)
	    PLINE (87,96) = 'SUBTOTAL: '
	    PLINE (97,107) = CTOT,MASK
	    CTOT =
	  END
	END
;;;
;
;;;
	CALL PRINT
	    
FSTACT,
	IF (ENDFLG.EQ.1) RETURN
	
	SAVACT = SDSACT
	ACTTOT =
	SRCCTL = 0
	XCALL ISIO (7, ARACCT, SAVACT, READ, SRCCTL)
	IF (SRCCTL .NE. 0) SRCCTL = 1
	IF (SRCCTL.EQ.1) ARACDS = '** ACCOUNT NOT ON FILE **'
	IF (SAVTYP.NE.SDSTYP) RETURN
	GO TO ADD
NEWTYP,
	CALL NEWACT
	IF (SAVTYP.EQ.-1) GO TO FSTTYP
	XCALL LINFD (1)
	INCR LINCNT
	PLINE (48,57) = 'SUB-TOTAL:'
	PLINE (59,72) = SUBTOT, MASK
	CALL PRINT
	XCALL LINFD (2)
	LINCNT = LINCNT + 2
FSTTYP,
	IF (ENDFLG.EQ.1) RETURN
	SAVTYP = SDSTYP
	SUBTOT =
	IF (SAVTYP.EQ.0) PLINE (1,15) = 'DEBIT TO A/R'
	IF (SAVTYP.EQ.1) PLINE (1,15) = 'SALES'
	IF (SAVTYP.EQ.2) PLINE (1,15) = 'OTHER CHARGES'
	IF (SAVTYP.EQ.3) PLINE (1,15) = 'SALES TAX'
	IF (SAVTYP.EQ.4) PLINE (1,15) = 'FREIGHT'
	IF (SAVTYP.EQ.5) PLINE (1,15) = 'FINANCE CHARGES'
	IF (SAVTYP.EQ.6) PLINE (1,15) = 'CASH RECEIVED'
	IF (SAVTYP.EQ.7) PLINE (1,15) = 'DISC & ALLOW'
	IF (SAVTYP.EQ.8) PLINE (1,15) = 'CREDIT TO A/R'
	IF (SAVTYP.EQ.9) PLINE (1,15) = 'MISC CASH'
	GO TO ADD
PRINT,
	PLIN4 = PLINE

	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,'NO HDR','NO HDR',
&		'NO LEGEND',' ',' ',0,132,PRTCTL,0,LPSW,RPTNUM,PRTTYP)

	XCALL LP44 (LINCN4,PGCN4,PLIN4,TITLE,HDR1,'NO HDR','NO HDR',
&		'NO LEGEND',' ',' ',0,132,PRTCTL,0,LPSW,RPTNUM,PRTTYP)

	RETURN
EOF,
	IF (SAVTYP.EQ.-1) GO TO END2
	ENDFLG = 1
	CALL NEWTYP
	XCALL LINFD (2)
	LINCNT = LINCNT + 2
	PLINE (44,57) = 'CONTROL TOTAL:'
	PLINE (59,72) = GRDTOT, MASK
	CALL PRINT
END2,
	CLOSE 44
	XCALL JNL (PDFFIL, 'Credit_Card_Distribution_Summary.pdf')

	CLOSE 5
	CLOSE 7
	XCALL AR_LPOFF (LPSW,SPLFIL,PGCNT)		;ssq 11-03-14
;;;	XCALL AR_LPOFF (LPSW,SPLFIL,PGCNT,2)	;ssq 7-13-06
	IF (GRDTOT.NE.0) GO TO OFFBAL
NOPRNT,
	XCALL WATE (4,V)
	XCALL PGCHN ('AR:CCRDST',1)
;;;	XCALL PGCHN ('AR:PSTDST',1)
OFFBAL,
	XCALL OUTPT (3,1,2,'\',1)
	XCALL OUTPT (10,15,2,
&		'The distributions are out of balance.  The posting',1)
	XCALL OUTPT (12,15,0,
&		'run is being terminated at this point to allow you',1)
	XCALL OUTPT (14,15,0,
&		'to correct any errors before any files are updated.',1)
	XCALL OUTPT (16,15,0,
&		'You can then run the posting application as usual.',1)
	XCALL MESAG (' ',2)
	
	XCALL PGCHN ('AR:UNPRCC',0)

END


