;MAKCCT.AR
;
;	EXTRACT CC TRANSACTIONS (IF ANY) FROM SALES0
;
;		SLSPRP->MAKCCT->SRTSLS

;	8-6-18: ssq make aropen isam

RECORD	CCARD
	CC_CUSNO	,D6
	CC_NAME		,A25
	CC_DATE		,D8
	CC_DOCN		,D6
	CC_AMT		,D8
	CC_APLNO	,D6
	CC_CSHACT	,D7
	CC_ARACT	,D7

RECORD	CUSCTL
	.INCLUDE 'DEF:RD001B.DEF'

RECORD	SALES
	.INCLUDE 'DEF:RD004A.DEF'
RECORD,X
	.INCLUDE 'DEF:RD004B.DEF'

RECORD	SLSDST
	.INCLUDE 'DEF:RD005A.DEF'

RECORD	DSTCTL
	.INCLUDE 'DEF:RD005B.DEF'

RECORD	AROPEN
	.INCLUDE 'DEF:RD003A.DEF'

RECORD	AROCTL
	.INCLUDE 'DEF:RD003B.DEF'

RECORD	CCTRAN
	.INCLUDE 'DEF:RD138A.DEF'

RECORD	HDR1
,A*,'------------CUSTOMER------------       DATE DOC NO       AMOUNT  APPLY-TO'

RECORD	PRINT
	TITLE	,A*,	'CREDIT CARD JOURNAL'
	LINFD	,D2
	LGNCTL	,D1
	LINCNT	,D2
	LPARG   ,D1
	LPONSW	,D1
	PRNTSW	,D1
	LPSW	,D1
	PGCNT	,D3
	PLINE	,A80
	PRTCNT	,D5			;AR29
	PRTTYP  ,A1
	RPTNUM  ,D3
	SPLFIL  ,A14
	HDR	,A6,	'NO HDR'
	LEG	,A9,	'NO LEGEND'


RECORD	CHANNEL
	CHN001	,D2
	CHN003	,D2
	CHN004	,D2
	CHN005	,D2
	CHN138	,D2

RECORD	VARS
	OPNOK	,D1
	TOTAMT	,D8
	TOTTAX	,D8
	XDATE	,D8
	LOKCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	STORE	,D1,2
	SWITCH	,D1
	V	,D1
;
PROC
	XCALL TERID(V)
	V = 1

	CALL OPENS
	IF (.NOT. OPNOK) GOTO ABORT

	XCALL IOS (CHN004, SALES, READ, LOKCTL)
LOOP,
	XCALL IOS (CHN004, SALES, READ, LOKCTL)
	IF (SNAME .EQ. ']]]]]]') GOTO EOF
	IF (LOKCTL .NE. 0) GOTO EOF

	TOTTAX = STAX(1) + STAX(2) + STAX(3)
	IF (SNAME.EQ.'000000' .OR. SSLAMT+SFRGHT+SMISC+TOTTAX .EQ. 0 
&		.OR. SCUSNO.EQ.999999) GOTO LOOP

; check for cctran record, if none exists, skip
	XCALL ISIO (CHN138, CCTRAN, SDOCNO, READ, LOKCTL)
	IF (LOKCTL .NE. 0) GOTO LOOP

	CC_CUSNO = SCUSNO
	CC_NAME = SNAME
	CC_DATE = SDOCDT
	CC_DOCN(1,2) = 99
	CC_DOCN(3,6) = CT_NUMBR(13,16)		;LAST 4 DIG
	CC_AMT = SSLAMT + SFRGHT + SMISC + TOTTAX
	CC_APLNO = SDOCNO
	CC_CSHACT = DEFCSH
	CC_ARACT = DEFAR

	CALL UPD_SLSDST
	CALL UPD_AROPEN
	CALL PRT_JNL
	GOTO LOOP

EOF,
	LOKCTL = 1
;;;	XCALL IO (CHN003, AROCTL, 1, WRITE, LOKCTL)
	XCALL IO (CHN005, DSTCTL, 1, WRITE, LOKCTL)

	CALL CLOSE
;;;	CLOSE 14
	XCALL AR_LPOFF (LPSW,SPLFIL,PGCNT)
	XCALL PGCHN ('AR:SRTSLS',1)
;==========================================================
ABORT,
	CALL CLOSE
	XCALL MESAG ('MAKCCT - POSTING DID NOT COMPLETE - CALL SUPPORT',1)
	STOP

UPD_SLSDST,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR SLSDST
	
	SDSTYP	= 6	; DISTRIBUTION TYPE
	SDSACT	= CC_CSHACT	; DISTRIBUTION ACCOUNT NUMBER (XXXX-XXX)
	SDSDAT	= CC_DATE	; DISTRIBUTION DATE (CCYYMMDD)
	SDSCUS	= CC_CUSNO	; CUSTOMER NUMBER
	SDSDOC	= -CC_DOCN	; DOCUMENT NUMBER (INVOICE # OR CHECK #)
	SDSAMT	= CC_AMT	; DISTRIBUTION AMOUNT ($XXX,XXX.XX-)
	SDTFLG	= 	; SHIP-TO TAX CODE FROM COP
	SDSFLG	= 	; DISTRIBUTION FLAG (NOT USED)
	CALL WRTDST

	SDSTYP = 8	;CREDIT TO AR
	SDSACT = CC_ARACT
	CALL WRTDST
	RETURN

WRTDST,
	INCR REC005
	LOKCTL = 1
	XCALL IO (CHN005, SLSDST, REC005, WRITE, LOKCTL)

	RETURN
;-------------------------------------------------

UPD_AROPEN,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR AROPEN
	ADOCNO = -CC_DOCN	;NEG WILL INDICATE CC TRANSACTION
	ADOCTP = 2		;CASH
	ADOCDT = CC_DATE
	ACUSNO = CC_CUSNO
	AAMT = CC_AMT
	AAPLNO = CC_APLNO
	AARACT = CC_ARACT

;;;	INCR REC003
	LOKCTL = 1
	XCALL ISIO (CHN003, AROPEN, ACUSNO, STORE, LOKCTL)
;;;	XCALL IO (CHN003, AROPEN, REC003, WRITE, LOKCTL)

	RETURN
;-------------------------------------------------

PRT_JNL,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	PLINE(1,6) = CC_CUSNO,	'ZZZZZX'
	PLINE(8,22) = CC_NAME
	XDATE(1,4) = CC_DATE(5,8)
	XDATE(5,8) = CC_DATE(1,4)
	PLINE(34,43) = XDATE,	'XX/XX/XXXX'
	PLINE(45,50) = CC_DOCN,	'XXXXXX'
	PLINE(54,63) = CC_AMT,	'ZZZ,ZZX.XX'
	PLINE(68,73) = CC_APLNO,'XXXXXX'

;------------CUSTOMER------------       DATE DOC NO       AMOUNT  APPLY-TO
;ZZZZZX AAAAAAAAAAAAAAAAAAAAAAAAA XX/XX/XXXX XXXXXX   ZZZ,ZZX.XX    XXXXXX
;12345678901234567890123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5         6         7         8

	CALL PRINT
	RETURN
;-------------------------------------------------

PRINT,
	IF (PRNTSW.EQ.0) CALL LPON
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR,HDR,
&		LEG,LEG,LEG,0,080,080,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;-------------------------------------------------
LINFD,
	XCALL LINFD (LINFD)
	LINCNT = LINCNT + LINFD
	RETURN
;-------------------------------------------------
LPON,
	LPSW = 1
	SPLFIL (5,6) = 'AR'
	XCALL AR_LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO ABORT
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTSW = 1
	LINCNT = 66
	RETURN
;-------------------------------------------------

OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK
;;;	SWITCH = 5
;;;	XCALL FILES (1,'I',001,SWITCH)
;;;	IF (SWITCH .EQ. 9) RETURN
;;;	CHN001 = 1

	SWITCH = 5
	XCALL FILES (3,'SU',003,SWITCH)		;AROPEN
	IF (SWITCH .EQ. 9) RETURN
	CHN003 = 3

	SWITCH = 5
	XCALL FILES (4,'I',004,SWITCH)		;SALES
	IF (SWITCH .EQ. 9) RETURN
	CHN004 = 4

	SWITCH = 5
	XCALL FILES (8,'U',005,SWITCH)		;SLSDST
	IF (SWITCH .EQ. 9) RETURN
	CHN005 = 8

	SWITCH = 5
	XCALL FILES(38,'SI',138,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN138 = 38

;;;	LOKCTL = 1
;;;	XCALL IO(CHN001, CUSCTL, 1, READ, LOKCTL)

;;;	XCALL IO(CHN003, AROCTL, 1, READ, LOKCTL)

	XCALL IO(CHN005, DSTCTL, 1, READ, LOKCTL)

	OPNOK = 1	
	RETURN
;-------------------------------------------------

CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;	IF (CHN001) CLOSE CHN001
	IF (CHN003) CLOSE CHN003
	IF (CHN004) CLOSE CHN004
	IF (CHN005) CLOSE CHN005
	IF (CHN138) CLOSE CHN138

	RETURN
;-------------------------------------------------
;------------CUSTOMER------------       DATE DOC NO       AMOUNT  APPLY-TO
;ZZZZZX AAAAAAAAAAAAAAAAAAAAAAAAA XX/XX/XXXX XXXXXX   ZZZ,ZZX.XX    XXXXXX
;12345678901234567890123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5         6         7         8
