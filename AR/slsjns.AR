;SLSJNS.AR	SALES TO SMC SLSJNL
;  SLSJNL / AR 
;
;	SMC CUST: 65545
;	12-18-06: ROCKFORD ONLY CUST: 63340
;	03-06-08: 39 distributions
; 12-12-13: converted to isam

RECORD	D_STUFF
	D_IN	,D8		;DATE-IN, ANY FORMAT
	D_OUT	,D6		;RETURN VALUE MMDDYY
	D_OUTR	,D8		;RETURN VALUE CCYYMMDD
	D_FMT	,A10		;RETURN VALUE MM/DD/CCYY
	D_SW	,A2		;"99" = DATE CONVERSION ERROR

RECORD	COMP14
	ESC_11	,A1		;<ESC>
		,a4,	"&l1O"	;landscape mode
	ESC_12	,A1		;<ESC>
		,A4,	"&l8D"	;vertical spacing, 8 lines/inch
	ESC_13	,A1		;<ESC>
	C14V	,A8,	"(s13.00H"	;pitch 14 CPI


RECORD SALES		; 
		.INCLUDE 'DEF:RD004A.DEF'
RECORD SLSCTL	,X	; 
		.INCLUDE 'DEF:RD004B.DEF'
RECORD ARACCT		; 
		.INCLUDE 'DEF:RD007A.DEF'
;RECORD	,X		; 
;		.INCLUDE 'DEF:RD007B.DEF'
RECORD HDR1
		,A6,	'CUST-#'
		,A2
		,A4,	'NAME'
		,A23
		,A5,	'DOC-#'
		,A3
		,A3,	'TYP'
		,A1
		,A8,	'APPLY-TO'
		,A3
		,A8,	'SALE-AMT'
		,A4
		,A8,	'OTH-CHGS'
		,A3
		,A9,	'SALES-TAX'
		,A4
		,A7,	'FREIGHT'
		,A5
		,A9,	'TOTAL-AMT'
		,A5
;;;		,A8,	'COMM-AMT'
RECORD HDR2
		,A34
		,A8,	'DOC-DATE'
		,A6
		,A5,	'SLSMN'
		,A7
		,A4,	'COST'
		,A7
		,A6,	'ACCT-#'
		,A6
		,A6,	'ACCT-#'
		,A5
		,A6,	'ACCT-#'
		,A6
		,A9,	'AR-ACCT-#'
		,A5
;;;		,A8,	'DUE DATE'
		,A8,	'TAX CODE'
RECORD TITLE
		,A19,	'SALES JOURNAL - SMC'
RECORD LEGEND
		,A91,'   DOCUMENT TYPES:   1 = SALE (INVOICE)   3 = CR MEMO
&   4 = FINANCE CHARGE   5 = DEBIT MEMO'


RECORD	ARA
	CDATES	,11A10
	CSLAMT	,11D10
	CMISC	,11D10
	CTAX	,11D10
	CFRGHT	,11D10
	CCNT	,11D6
	CIDX	,D6
	CMAX	,D6

RECORD	VARS
	i	,d6
	j	,d6
	savcnt	,d6
	savdat	,a10
	savamt	,d10
	savmis	,d10
	savtax	,d10
	savfrt	,d10	
	blanks	,a10
	CMPCOD	,A3
	LINCNT	,D2,	60
	PGCNT	,D3,	000
	PLINE	,A132
	PRTCTL	,D3
	BSEND	,D5
	BSMID	,D5
	SRCCTL	,D1
	KEY	,A7
	CRCOST	,D10
	CRFRGH	,D8
	CRMISC	,D8
	CRSAMT	,D10
	CRTAX	,D8
	CRCOMM	,D8
	DRCOST	,D10
	DRFRGH	,D8
	DRMISC	,D8
	DRSAMT	,D10
	DRTAX	,D8
	DRCOMM	,D8
	DSTFLG	,A1
	DISTIX	,D2
	FCCOST	,D9
	FCFRGH	,D7
	FCMISC	,D7
	FCSAMT	,D9
	FCTAX	,D8
	FCCOMM	,D8
	LPSW	,D1
	LPARG	,D1
	MSGCTL	,D1
	NTXSLS	,D10
	NTMISC	,D8
	NTFRGH	,D8
	NTCOST	,D10
	NTCOMM	,D8
	NUMDST	,D2
	RPTNUM	,D3
	PRTTYP	,A1
	SLFRGH	,D8
	SLMISC	,D8
	SLSAMT	,D10
	SLSCST	,D10
	SLTAX	,D8
	SLCOMM	,D8
	pdffil	,a*,	'c:\smc\spool\slsjns.spl'
	SPLFIL	,A14
	MASK	,A14,	'ZZ,ZZZ,ZZZ.XX-'
	MASK2	,A8,	'XXXX-XXX'
	TFRGHT	,D8
	TMISC	,D8
	TRXCNT	,D4
	TSLAMT	,D10
	TTAX	,D8
	TCOST	,D10
	TCOMM	,D8
	V	,D1
	READ	,D1,	0
	WRITE	,D1,	1
	LOKCTL	,D1
	WRKTAX	,D7
	TAXIDX	,D1
PROC
	XCALL TERID (V)
	XCALL OUTPT (2,1,1,'PRINT SALES JOURNAL',1)
	XCALL WHO (CMPCOD)

	USING CMPCOD SELECT
	('ROC'),	NOP
	('CAT'),	NOP
	(),		GOTO NOPRNT
	ENDUSING

	for cidx from 1 thru 10 
		begin
		cdates(cidx) =
		cslamt(cidx) =
		cmisc(cidx) =
		ctax(cidx) =
		cfrght(cidx) =
		ccnt(cidx) =
		end
	clear cmax


	open (14,o,pdffil)
	
	xcall ascii (27, esc_11)
	set esc_12, esc_13 = esc_11
	display (14, comp14)

	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	XCALL FILES (4,'I',4,5)			;FILE # 04 -- SALES0 FILE
	LOKCTL = 1
	XCALL IOS (4,SALES,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOF
	DSTFLG = SDETDS
	IF (DSTFLG.EQ.'Y') GO TO OPEN7
	HDR2 (71,115) =
	GO TO READ
OPEN7,
	XCALL FILES (7,'SI',07,5)		;FILE # 07 -- ARACCT FILE
READ,
	LOKCTL = 1
	XCALL IOS (4,SALES,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOF
	IF (SNAME.EQ.']]]]]]') GO TO EOF
	IF (SNAME.EQ.'000000') GO TO READ
	IF (SCUSNO .NE. 65545) GOTO READ	;SMC ONLY
BUFF,
	IF (DSTFLG.NE.'Y') GO TO SKPCNT
	CALL CNTDST
	IF (LINCNT+NUMDST+3.GT.60) LINCNT = 60
SKPCNT,
	PLINE (1,6) = SCUSNO
	PLINE (9,33) = SNAME
	PLINE (36,41) = SDOCNO
	PLINE (45,45) = SDOCTP
	PLINE (49,54) = SAPLNO
	IF (SAPLNO.EQ.-1) PLINE (49,54) = 'BAL-FW'
	WRKTAX = STAX(1) + STAX(2) + STAX(3)
	GO TO (SALES,CONTIN,CRMEMO,FINCHG,DRMEMO,SALES), SDOCTP
;;;	GO TO (SALES,CONTIN,CRMEMO,FINCHG,DRMEMO), SDOCTP
CONTIN,
	PLINE (57,67) = SSLAMT, MASK
	PLINE (70,79) = SMISC, MASK
	PLINE (82,91) = WRKTAX, MASK
	PLINE (93,102) = SFRGHT, MASK
	PLINE (106,116) = SSLAMT + SMISC + WRKTAX + SFRGHT, MASK
	IF (STAXFL.EQ.'   ') PLINE (120,130) = '* WARNING *'
	TSLAMT = TSLAMT + SSLAMT
	TMISC = TMISC + SMISC
	TTAX = TTAX + WRKTAX
	TFRGHT = TFRGHT + SFRGHT
	TCOST = TCOST + SCOST
	TCOMM = TCOMM + SCOMM
	CALL PRINT

	XCALL DATE8(SDOCDT, D_OUT, D_OUTR, D_FMT, D_SW)
	PLINE (33,42) = D_FMT

	for cidx from 1 thru 10
		begin
		if (d_fmt .eq. cdates(cidx) )
			begin
			cslamt(cidx) = cslamt(cidx)  + sslamt
			cmisc(cidx) = cmisc(cidx)  + smisc
			ctax(cidx) = ctax(cidx)  + wrktax
			cfrght(cidx) = cfrght(cidx)  + sfrght
			incr ccnt(cidx)

			exitloop
			end

		if (cdates(cidx) .eq. blanks)
			begin
			cdates(cidx) = d_fmt
			cslamt(cidx) = sslamt
			cmisc(cidx) = smisc
			ctax(cidx) = wrktax
			cfrght(cidx) = sfrght
			ccnt(cidx) = 1
			incr cmax

			exitloop
			end
		end

	PLINE (51,52) = SSLMAN
	IF (DSTFLG.NE.'Y') GO TO SKIP1
	IF (SMISC.NE.0) PLINE (71,78) = SMSACT, MASK2
	IF (STAX(1).NE.0) PLINE (83,90) = STXACT(1), MASK2
	IF (STAX(2).NE.0 .OR. STAX(3).NE.0) PLINE (83,90) = 
	IF (SFRGHT.NE.0) PLINE (94,101) = SFRACT, MASK2
	PLINE (108,115) = SARACT, MASK2
SKIP1,
	PLINE (120,123) = STAXFL
	IF (STAXFL.EQ.'   ') PLINE (120,130) = '*NO TXCODE*'
	CALL PRINT
	IF (NUMDST.LE.0.OR.DSTFLG.NE.'Y') GO TO SKPDST
;
	PLINE (12,31) = 'SALES DISTRIBUTIONS:'
	CALL PRTDST
SKPDST,
	IF (STAX(2).EQ.0 .AND. STAX(3).EQ.0) GO TO SKPTAX
	PLINE (12,31) = '  TAX DISTRIBUTIONS:'
	CALL PRTTAX
SKPTAX,
	XCALL LINFD (1)
	INCR LINCNT
	INCR TRXCNT
	IF (NUMDST.EQ.0.OR.DSTFLG.NE.'Y') GO TO READ
	XCALL LINFD (1)
	INCR LINCNT
	GO TO READ
SALES,
	IF (WRKTAX.EQ.0) GO TO NOTAX
	SLSAMT = SLSAMT + SSLAMT
	SLMISC = SLMISC + SMISC
	SLTAX = SLTAX + WRKTAX
	SLFRGH = SLFRGH + SFRGHT
	SLSCST = SLSCST + SCOST
	SLCOMM = SLCOMM + SCOMM
	GO TO CONTIN
NOTAX,
	NTXSLS = NTXSLS + SSLAMT
	NTMISC = NTMISC + SMISC
	NTFRGH = NTFRGH + SFRGHT
	NTCOST = NTCOST + SCOST
	NTCOMM = NTCOMM + SCOMM
	GO TO CONTIN
CRMEMO,
	IF (SSLAMT.NE.0) SSLAMT = (-1) * SSLAMT
	IF (SMISC.NE.0) SMISC = (-1) * SMISC
	IF (WRKTAX.NE.0) WRKTAX = (-1) * WRKTAX
	IF (SFRGHT.NE.0) SFRGHT = (-1) * SFRGHT
	IF (SCOST.NE.0) SCOST = (-1) * SCOST
	IF (SCOMM.NE.0) SCOMM = (-1) * SCOMM
	CRSAMT = CRSAMT + SSLAMT
	CRMISC = CRMISC + SMISC
	CRTAX = CRTAX + WRKTAX
	CRFRGH = CRFRGH + SFRGHT
	CRCOST = CRCOST + SCOST
	CRCOMM = CRCOMM + SCOMM
	GO TO CONTIN
FINCHG,
	FCSAMT = FCSAMT + SSLAMT
	FCMISC = FCMISC + SMISC
	FCTAX = FCTAX + WRKTAX
	FCFRGH = FCFRGH + SFRGHT
	FCCOST = FCCOST + SCOST
	FCCOMM = FCCOMM + SCOMM
	GO TO CONTIN
DRMEMO,
	DRSAMT = DRSAMT + SSLAMT
	DRMISC = DRMISC + SMISC
	DRTAX = DRTAX + WRKTAX
	DRFRGH = DRFRGH + SFRGHT
	DRCOST = DRCOST + SCOST
	DRCOMM = DRCOMM + SCOMM
	GO TO CONTIN
CNTDST,
	DISTIX =
	NUMDST =
CNTDS1,
	INCR DISTIX
	IF (DISTIX.GT.39) RETURN
	IF (SDAMTS(DISTIX).NE.0) INCR NUMDST
	GO TO CNTDS1
PRTDST,
	XCALL LINFD (1)
	INCR LINCNT
	DISTIX =
PRTDS1,
	INCR DISTIX
	IF (DISTIX.GT.39) RETURN
	IF (SDAMTS(DISTIX).EQ.0) GO TO PRTDS1
	PLINE (34,41) = SDACTS (DISTIX), MASK2
	KEY = SDACTS (DISTIX), 'XXXXXXX'
	
	SRCCTL = 1
	XCALL ISIO (7, ARACCT, KEY, READ, SRCCTL)
	IF (SRCCTL.GT.1) SRCCTL = 1
;	XCALL SERCH (7,ARACCT,KEY,1,7,BSEND,BSMID,SRCCTL,4,32,37,0,0,0,0)
	IF (SRCCTL.EQ.1) ARACDS = '** ACCOUNT NOT ON FILE **'
	PLINE (44,73) = ARACDS
	IF (SDOCTP.EQ.3) SDAMTS (DISTIX) = (-1) * SDAMTS (DISTIX)
	PLINE (76,87) = SDAMTS (DISTIX), MASK
	CALL PRINT
	GO TO PRTDS1
PRTTAX,
	IF (NUMDST.EQ.0) XCALL LINFD(1)
	IF (NUMDST.EQ.0) INCR LINCNT
	TAXIDX = 
PRTTX1,
	INCR TAXIDX
	IF (TAXIDX.GT.3) RETURN
	IF (STAX(TAXIDX).EQ.0) GO TO PRTTX1
	PLINE (34,41) = STXACT(TAXIDX), MASK2
	KEY = STXACT(TAXIDX), 'XXXXXXX'
	
	SRCCTL = 1
	XCALL ISIO (7, ARACCT, KEY, READ, SRCCTL)
	IF (SRCCTL.GT.1) SRCCTL = 1
;	XCALL SERCH (7,ARACCT,KEY,1,7,BSEND,BSMID,SRCCTL,4,32,37,0,0,0,0)	
	IF (SRCCTL.EQ.1) ARACDS = '** ACCOUNT NOT ON FILE **'
	PLINE (44,73) = ARACDS
	IF (SDOCTP.EQ.3) STAX(TAXIDX) = (-1) * STAX(TAXIDX)
	PLINE (76,87) = STAX(TAXIDX), MASK
	CALL PRINT
	INCR NUMDST
	GO TO PRTTX1
PRINT,
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,'NO HDR',
&		LEGEND,'NO LEGEND2',' ',0,132,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN

print_totals,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	HDR1 (1,55) =
	HDR2 (1,55) =
	HDR2 (65,128) =
	LINCNT = 60
	PLINE (30,52) = 'TAXED SALES SUB TOTALS:'
	PLINE (54,67) = SLSAMT, MASK
	PLINE (81,91) = SLTAX, MASK
	PLINE (103,116) = SLSAMT + SLFRGH + SLMISC + SLTAX, MASK
	PLINE (69,79) = SLMISC, MASK
	PLINE (92,102) = SLFRGH, MASK
	CALL PRINT
	PLINE (54,67) = SLSCST, MASK
	CALL PRINT
	XCALL LINFD (1)
	PLINE (26,52) = 'NON-TAXED SALES SUB TOTALS:'
	PLINE (54,67) = NTXSLS, MASK
	PLINE (88,90) = '.00'
	PLINE (103,116) = NTXSLS + NTFRGH + NTMISC, MASK
	PLINE (69,79) = NTMISC, MASK
	PLINE (92,102) = NTFRGH, MASK
	CALL PRINT
	CALL PRINT
	XCALL LINFD (1)
	PLINE (34,52) = 'CR MEMO SUB TOTALS:'
	PLINE (54,67) = CRSAMT, MASK
	PLINE (81,91) = CRTAX, MASK
	PLINE (103,116) = CRSAMT + CRFRGH + CRMISC + CRTAX, MASK
	PLINE (69,79) = CRMISC, MASK
	PLINE (92,102) = CRFRGH, MASK
	CALL PRINT
	CALL PRINT
	XCALL LINFD (1)
	PLINE (27,52) = 'FINANCE CHARGE SUB TOTALS:'
	PLINE (54,67) = FCSAMT, MASK
	PLINE (81,91) = FCTAX, MASK
	PLINE (103,116) = FCSAMT + FCFRGH + FCMISC + FCTAX, MASK
	PLINE (69,79) = FCMISC, MASK
	PLINE (92,102) = FCFRGH, MASK
	CALL PRINT
	CALL PRINT
	XCALL LINFD (1)
	PLINE (34,52) = 'DR MEMO SUB TOTALS:'
	PLINE (54,67) = DRSAMT, MASK
	PLINE (81,91) = DRTAX, MASK
	PLINE (103,116) = DRSAMT + DRFRGH + DRMISC + DRTAX, MASK
	PLINE (69,79) = DRMISC, MASK
	PLINE (92,102) = DRFRGH, MASK
	CALL PRINT
	CALL PRINT
	XCALL LINFD (4)
	PLINE (1,4) = TRXCNT
	PLINE (6,12) = 'ENTRIES'
	PLINE (40,52) = 'GRAND TOTALS:'
	PLINE (54,67) = TSLAMT, MASK
	PLINE (81,91) = TTAX, MASK
	PLINE (103,116) = TSLAMT + TFRGHT + TMISC + TTAX, MASK
	PLINE (69,79) = TMISC, MASK
	PLINE (92,102) = TFRGHT, MASK
	CALL PRINT
	CALL PRINT

; date totals:
	call bubble_sort		;;sort by date

	for cidx from 1 thru cmax
		begin
		if (cdates(cidx) .eq. blanks) exitloop
		pline (1,4) = ccnt(cidx), 'ZZZX'
		pline (6,12) = 'entries'
		pline (40,52) = cdates(cidx)
		pline (54,67) = cslamt(cidx), mask
		pline (69,79) = cmisc(cidx), mask
		pline (81,91) = ctax(cidx), mask
		pline (92,102) = cfrght(cidx), mask
		pline (103,116) = cslamt(cidx) + cfrght(cidx) + 
&	cmisc(cidx) + ctax(cidx), mask
		call print
		call print
		
		end

	return

bubble_sort,

;	BUBBLE SORT ACCREC ARRAY
;
	FOR J FROM 1 THRU cmax
	 BEGIN
	  FOR I FROM 1 THRU cmax - 1
	   BEGIN
	    IF (CDATES(I).GT.CDATES(I+1))
	      BEGIN
		savcnt = ccnt(i+1)
	    	SAVDAT = CDATES(I+1)
		savamt = cslamt(I+1)
		savmis = cmisc(I+1)
		savtax = ctax(i+1)
		savfrt = cfrght(i+1)

		ccnt(i+1) = ccnt(i)
		cdates(i+1) = cdates(i)
		cslamt(i+1) = cslamt(i)
		cmisc(i+1) = cmisc(i)
		ctax(i+1) = ctax(i)
		cfrght(i+1) = cfrght(i)

		ccnt(i) = savcnt
		cdates(i) = savdat
		cslamt(i) = savamt
		cmisc(i) = savmis
		ctax(i) = savtax
		cfrght(i) = savfrt
	      END
	   END
	 END

	RETURN
;--------------------------------------------------------


EOF,
	call print_totals
	close 14
	xcall jnl (pdffil, 'smc_sales_jnl.pdf')

restrt,
	LPSW = 4	; MAY BE AUTO-SPOOLED
	SPLFIL (5,6) = 'AC'
	XCALL AR_LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) 
	BEGIN
	 XCALL MESAG ('CAN NOT ABORT THIS PRINTOUT-PLEASE RESELECT PRINTER',2)
	 close 14		;10-17-01 ssq
	 GOTO RESTRT
	END

	lincnt = 66
	pgcnt = 0
	call print_totals


	XCALL AR_LPOFF (LPSW,SPLFIL,PGCNT)
	CLOSE 4
	CLOSE 7
NOPRNT,
	XCALL WATE(4,V)
	IF (DSTFLG.EQ.'N') XCALL PGCHN ('AR:ACMSLS',1)
	XCALL OUTPT (2,1,1,'\',1)

	XCALL PGCHN ('AR:SRTDST',0)
;;;	XCALL PGCHN ('AR:SLSJNM',0)	;4-26-20 smp
;;;	XCALL PGCHN ('AR:SRTDST',0)
END

