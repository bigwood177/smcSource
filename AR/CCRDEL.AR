;  CCRDEL / AR 
;
;
;		DELETION OF CC - CASH TRANSACTIONS
;		STANDARD TRX MAINTENANCE MODULE
;
;
; 10-01-09 ssq: remove calamt calact
; 11-18-13: converted to isam


RECORD	D_STUFF
	D_IN	,D8		;DATE-IN, ANY FORMAT
	D_OUT	,D6		;RETURN VALUE MMDDYY
	D_OUTR	,D8		;RETURN VALUE CCYYMMDD
	D_FMT	,A10		;RETURN VALUE MM/DD/CCYY
	D_SW	,A2		;"99" = DATE CONVERSION ERROR


RECORD CASH		; 
		.INCLUDE 'DEF:RD024A.DEF'
RECORD CCRCTL		; 
		.INCLUDE 'DEF:RD024B.DEF'
RECORD ARACCT		; 
		.INCLUDE 'DEF:RD007A.DEF'
;RECORD	,X		; 
;		.INCLUDE 'DEF:RD007B.DEF'
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD
	BALMTH	,A1
	DEFACT	,D7
	OPTION	,D1
	ENTRY	,A26
	INXCTL	,D1
	ROW2	,D2
	WHATNO	,D2
	DECMAL	,D18
	KCUSNO	,D6
	KCHKNO	,D6
	KAPLNO	,D6
	LSTCUS	,D6
	LSTCHK	,D6
	DFLTAR	,D7
	DFALAC	,D7
	SAVCHK	,D6
	ORGCUS	,D5
	ORGARA	,D5
	ORGARO	,D5
	RECNO	,D5
	BLANKS	,A8
	MASK	,A7,	'XXXXXXX'
	MASK2	,A8,	'XXXX-XXX'
	ALPHA	,A8
	KEY	,A6
	KEY1	,A7
	BSEND	,D5
	BSMID	,D5
	SRCCTL	,D1
	TODAY	,A8
	MAXCNT	,D5
	TCHAR	,D3
	SYSTEM	,D1
	SWITCH	,D1,1
	V	,D1
	READ	,D1,	0
	WRITE	,D1,	1
	LOKCTL	,D1
PROC
	XCALL TERID (V)
	XCALL ENVRN (SYSTEM)
	V = 1
	SWITCH = 1
	XCALL FILES (6,'U',24,SWITCH)		;FILE # 24 -- CCRTRX FILE
	IF (SWITCH.EQ.9) GO TO END1
	XCALL FILES (7,'SI',07,5)		;FILE # 07 -- ARACCT FILE
	XCALL FILES (13,'I',24,5)
	LOKCTL = 1
	XCALL IO (6,CCRCTL,1,READ,LOKCTL)
	LOKCTL = 1
;	XCALL IO (7,ARACCT,1,READ,LOKCTL)
;	ORGARA = ORG007
	MAXCNT = MAX024
DISPLA,
	CASH =
	UNLOCK 6
	XCALL OUTPT (4,3,2,'*1. CUSTOMER #',V)
	XCALL OUTPT (5,27,0,'BAL METHOD:',V)
	XCALL OUTPT (6,3,0,'*2. CHECK #',V)
	XCALL OUTPT (8,4,0,'3. RECPT DATE',V)
	XCALL OUTPT (10,4,0,'4. A/R CASH ?',V)
	IF (CDETDS.EQ.'Y') XCALL OUTPT (10,33,0,'ACCT-#',V)
	XCALL OUTPT (14,4,0,'6. AMT PAID',V)
	IF (CDETDS.EQ.'Y') XCALL OUTPT (14,33,0,'ACCT-#',V)
	XCALL OUTPT (16,4,0,'7. DISCOUNT',V)
	IF (CDETDS.EQ.'Y') XCALL OUTPT (16,33,0,'ACCT-#',V)
;;;	XCALL OUTPT (18,4,0,'8. ALLOWANCE',V)
;;;	IF (CDETDS.EQ.'Y') XCALL OUTPT (18,33,0,'ACCT-#',V)
CUSTNO,
	CTL = '04,19,06,00,#E'
	CALL INPUT
	GO TO (DISPLA,ENDDEL), INXCTL
	KCUSNO = ENTRY (1,6)
	IF (KCUSNO.NE.0) GO TO FNDCUS
	CALL SAMCUS
	IF (KCUSNO.EQ.0) GO TO CUSTNO
FNDCUS,
	XCALL WATE (3,2)
	RECNO = 1
	LOKCTL = 1
	XCALL IO (13,CASH,MAXCNT,READ,LOKCTL)
RDCUST,
	INCR RECNO
	LOKCTL = 1
	XCALL IO (13,CASH,RECNO,READ,LOKCTL)
	IF (CNAME.EQ.']]]]]]') GO TO NOFIND
	IF (CNAME.EQ.'000000') GO TO RDCUST
	IF (CCUSNO.NE.KCUSNO) GO TO RDCUST
	XCALL OUTPT (24,1,1,'\',V)
	DECMAL = CCUSNO
	LSTCUS = CCUSNO
	CTL = '04,19,06'
	CALL DSPNUM
	BALMTH = 'O'				;IN ADD MODE, APPLY-TO IS ALWAYS
	IF (CAPLNO.EQ.-1) BALMTH = 'B'		;SET TO -1 FOR BAL FWD CUSTOMERS
	IF (CCUSNO.EQ.999999) BALMTH =
	IF (BALMTH.EQ.'O') XCALL OUTPT (5,39,0,'OPEN ITEM',V)
	IF (BALMTH.EQ.'O') XCALL OUTPT (12,3,0,'*5. APPLY-TO',V)
	IF (BALMTH.EQ.'B') XCALL OUTPT (5,39,0,'BAL FWD',V)
CHEKNO,
	CTL = '06,19,06,00,#E'
	CALL INPUT
	GO TO (DISPLA,ENDDEL), INXCTL
	KCHKNO = ENTRY (1,6)
	IF (KCHKNO.NE.0) GO TO APLYTO
	CALL SAMCHK
	IF (KCHKNO.EQ.0) GO TO CHEKNO
APLYTO,
	IF (BALMTH.NE.'O') GO TO SKPAPL
	CTL = '12,19,06,00,#E'
	CALL INPUT
	GO TO (DISPLA,ENDDEL), INXCTL
	KAPLNO = ENTRY (1,6)
	GO TO FNDTRX
SAMCUS,
	KCUSNO = LSTCUS
	DECMAL = KCUSNO
	CALL DSPNUM
	IF (KCUSNO.EQ.0) XCALL MESAG(' ',3)
	RETURN
SAMCHK,
	KCHKNO = LSTCHK
	DECMAL = KCHKNO
	CALL DSPNUM
	IF (KCHKNO.EQ.0) XCALL MESAG(' ',3)
	RETURN
SKPAPL,
	KAPLNO = -1
FNDTRX,
	IF (CCHKNO.EQ.KCHKNO.AND.CAPLNO.GE.KAPLNO) GO TO DSPTRX
	XCALL WATE (3,2)
RDNEXT,
	INCR RECNO
	LOKCTL = 1
	XCALL IO (13,CASH,RECNO,READ,LOKCTL)
	IF (CNAME.EQ.']]]]]]') GO TO NOFIND
	IF (CNAME.EQ.'000000') GO TO RDNEXT
	IF (CCUSNO.NE.KCUSNO) GO TO RDNEXT
	IF (CCHKNO.NE.KCHKNO) GO TO RDNEXT
	IF (CAPLNO.LT.KAPLNO) GO TO RDNEXT
DSPTRX,
	LOKCTL = 0
	XCALL IO (6,CASH,RECNO,READ,LOKCTL)
	IF (LOKCTL) GO TO DISPLA
	XCALL OUTPT (4,27,0,CNAME,V)
	DECMAL = CCHKNO
	LSTCHK = CCHKNO
	CTL = '06,19,06'
	CALL DSPNUM
	DECMAL (1,8) = CRCTDT
	CTL = '08,19,06'
	CALL DSPDTE
	XCALL OUTPT (10,19,0,CARCSH,V)
	IF (CDETDS.NE.'Y') GO TO DSP2
	DEFACT = CARACT
	ROW = 10
	CALL DSPACT
DSP2,
	XCALL OUTPT (12,19,1,'\',V)
	IF (CARCSH.EQ.'Y'.AND.BALMTH.EQ.'O') CALL DSPAPL
	CALL DSPBOT
	XCALL OUTPT (24,1,1,'RIGHT TRX ?',V)
	CTL = '24,14,01,00,YN'
	CALL INPUT
	GO TO (RDNEXT), INXCTL - 1
	XCALL OUTPT (24,1,1,'OK TO DELETE TRX ?',V)
	CTL = '24,21,01,00,YN'
	CALL INPUT
	GO TO (DELETE), INXCTL
	GO TO DISPLA
DELETE,
	CNAME = '000000'
DELTRX,
	LOKCTL = 1
	XCALL IO (6,CASH,RECNO,WRITE,LOKCTL)
	LOKCTL = 1
	XCALL IO (6,CCRCTL,1,READ,LOKCTL)
	INCR DEL024
	LOKCTL = 1
	XCALL IO (6,CCRCTL,1,WRITE,LOKCTL)
FREBUF,
	LOKCTL = 1
	XCALL IO (6,CCRCTL,MAXCNT,READ,LOKCTL)
	LOKCTL = 1
	XCALL IO (6,CCRCTL,1,READ,LOKCTL)
	XCALL MESAG ('TRX DELETED',1)
	GO TO DISPLA
NOFIND,
	XCALL MESAG ('TRX NOT FOUND',1)
	GO TO DISPLA
DSPAPL,
	IF (CAPLNO.EQ.0) GO TO DSPOCR
	DECMAL = CAPLNO
	CTL = '12,19,06'
	CALL DSPNUM
	RETURN
DSPOCR,
	XCALL OUTPT (12,19,0,'** OPEN CREDIT **',V)
	RETURN
DSPBOT,
	DECMAL = CAMRCD
	CTL = '14,19,08'
	CALL DSPDLR
	XCALL OUTPT (14,41,1,'\',V)
	IF (CDETDS.NE.'Y'.OR.CAMRCD.EQ.0) GO TO SKPAC1
	DEFACT = CSHACT
	ROW = 14
	CALL DSPACT
SKPAC1,
	DECMAL = CDISAL
	CTL = '16,19,07'
	CALL DSPDLR
	XCALL OUTPT (16,41,1,'\',V)
	IF (CDETDS.NE.'Y'.OR.CDISAL.EQ.0) GO TO SKPAC2
	DEFACT = CDSACT
	CALL DSPACT
SKPAC2,
;;;	DECMAL = CALAMT
;;;	CTL = '18,19,07'
;;;	CALL DSPDLR
;;;	XCALL OUTPT (18,41,1,'\',V)
;;;	IF (CDETDS.NE.'Y'.OR.CALAMT.EQ.0) RETURN
;;;	DEFACT = CALACT
;;;	CALL DSPACT
	RETURN
DSPACT,
	LOKCTL = 0
	XCALL ISIO (7, ARACCT, DEFACT, READ, LOKCTL)
	IF (LOKCTL .NE. 0) ARACDS = '** ACCOUNT NOT ON FILE **'
;	BSEND = ORGARA
;	KEY1 = DEFACT, MASK
;	XCALL SERCH (7,ARACCT,KEY1,1,7,BSEND,BSMID,SRCCTL,4,32,37,0,0,0,0)
;	IF (SRCCTL.EQ.1) ARACDS = '** ACCOUNT NOT ON FILE **'
	ALPHA = DEFACT, MASK2
	XCALL OUTPT (ROW,41,0,ALPHA,V)
	XCALL OUTPT (ROW,51,0,ARACDS,V)
	RETURN
ENDDEL,
	XCALL WATE (3,2)
	CALL CLOSE
END1,
	XCALL PGCHN ('AR:CCRENT',1)
CLOSE,
	XCALL FILES (6,'U',24,4)
	RETURN
INPUT,
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,V)
	RETURN
DSPNUM,
	OPTION = 1
	GO TO CALDSP
DSPDTE,
	XCALL DATE8(DECMAL(1,8), D_OUT, D_OUTR, D_FMT, D_SW)
	XCALL OUTPT (ROW, COL, 0, D_FMT, V)
	RETURN

;;;	OPTION = 2
;;;	GO TO CALDSP
DSPDLR,
	OPTION = 3
CALDSP,
	XCALL DSPLY (MAX,ROW,COL,DECMAL,OPTION,V)
	RETURN
END
