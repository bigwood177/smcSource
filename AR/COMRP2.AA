;COMRPT.AR - NEW VERSION
;COMRP2
;		INVOICES AND ESTIMATES
;
RECORD	ORDHDR
	.INCLUDE 'DEF:RD044A.DEF'
RECORD	ORDLIN
	.INCLUDE 'DEF:RD045A.DEF'

RECORD SALMAN		;
		.INCLUDE 'DEF:RD054A.DEF'
;
RECORD	CEST
	CE_SLSMAN	,D2
	CE_NUMINV	,D6
	CE_DLRINV	,D10
	CE_NUMEST	,D6
	CE_DLREST	,D10

RECORD	ARRAYS
	A_MAN	,30D2		;SALESMEN
	A_NUM	,30D5		;COUNT
	A_DLR	,30D10		;DOLLARS
	MAXARA	,D2,	30
	AI	,D4		;INDEX

RECORD	CHANNEL
	CHN044	,D2
	CHN045	,D2
	CHN184	,D2
	CHNWRK	,D2

RECORD	DIS
	II	,D6

RECORD	PRINT
	TITLE	,A*,	'SALES AND QUOTES BY SALESMEN'
	HDR1	,A*,	'-----------SALESMAN-----------            NO        AMOUNT'
	HDR	,A*,	'NO HDR'
	LEG	,A*,	'NO LEGEND'
	PLINE	,A132
	LINCNT	,D2,	60
	LPARG	,D1
	LPSW	,D1
	PGCNT	,D6
	PRTTYP	,A1
	PRGFIL	,A1
	PRNTSW	,D1
	PRTRPT	,A1
	RPTNUM	,D3
	SPLFIL	,A14

RECORD	VARS
	I	,D6
	OPNOK	,D1
	ENDAT	,D8
	TODAY	,D8
	XDATE	,D8
	AMT	,D10
	MTD	,A1	;M=MTD, Y=YTD
	WRKFIL	,A14,	'DEV:CESTXX.ISM'
	ENTRY	,A30
	INXCTL	,D1
	CNGCTL	,D1
	LOKCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	STORE	,D1,2
	WHATNO	,D2
	SWITCH	,D1
	V	,D1
;
PROC
	XCALL TERID(V)
	XCALL OUTPT (1,1,2,'COMMISSIONS REPORT WITH ESTIMATES',1)
	CALL OPENS
	IF (.NOT. OPNOK) GOTO ENDOFF
	XCALL RDAT8(TODAY)
DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,'COMMISSIONS REPORT WITH ESTIMATES',1)
	XCALL OUTPT (4,4,0,'1. CUT-OFF DATE',1)
	XCALL OUTPT (6,4,0,'2. MTD or YTD <M>',1)
ENDAT,
	XCALL INPUT (4,22,08,00,'DE',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	ENDAT = ENTRY(1,8)
	IF (ENDAT .EQ. 0) 
		BEGIN
		ENDAT = TODAY
		XDATE(1,4) = ENDAT(5,8)
		XDATE(5,8) = ENDAT(1,4)
		ENTRY(1,10) = XDATE,	'XX/XX/XXXX'
		XCALL OUTPT(4,22,0,ENTRY(1,10),1)
		END
	GOTO (ANYCNG),CNGCTL
MTD,
	XCALL INPUT (6,22,01,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	MTD = ENTRY(1,1)
	IF (MTD.NE.'M' .AND. MTD.NE.'Y') GOTO MTD
ANYCNG,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (PROCES,CNGBR),CNGCTL+1
CNGBR,
	GOTO (ENDAT,MTD),WHATNO
	GOTO ANYCNG
PROCES,
	CALL GET_INV			;INVOICED ORDERS
	CALL GET_EST			;ESTIMATES
	CALL PRT_REPORT			;PRINT OUT DATA

ENDOFF,
	CALL CLOSE
	XCALL FLAGS(7000000)
	STOP
;===========================================================
;===========================================================

GET_INV,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR II
	CALL CLRARA
	FIND (CHN184,ORDHDR,^FIRST) [ERR=GI_LOOP]
GI_LOOP,
	INCR II
	IF (II/500*500 .EQ. II) DISPLAY(15,$SCR_POS(1,70),DIS)
	XCALL IOS (CHN184,ORDHDR,READ,LOKCTL)
	IF (LOKCTL .NE. 0) GOTO GI_EOF
	IF (MTD.EQ.'M' .AND. OINVDT(1,6).NE.ENDAT(1,6) )GOTO GI_LOOP
	IF (OSLMAN.LT.1 .OR. OSLMAN.GT.25) OSLMAN = MAXARA
	INCR A_NUM(OSLMAN)
	A_DLR(OSLMAN) = A_DLR(OSLMAN) + (OSALE-OFRGHT-OTAX)
	GOTO GI_LOOP	
GI_EOF,
	FOR I FROM 1 THRU MAXARA
		BEGIN
		CLEAR CEST
		CE_SLSMAN = I
		CE_NUMINV = A_NUM(I)
		CE_DLRINV = A_DLR(I)
		STORE(CHNWRK, CEST, CE_SLSMAN)
		END	

	RETURN
;-------------------------------------------

GET_EST,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR II
	CALL CLRARA
	FIND (CHN044,ORDHDR,^FIRST) [ERR=GE_LOOP]
GE_LOOP,
	INCR II
	IF (II/500*500 .EQ. II) DISPLAY(15,$SCR_POS(2,70),DIS)
	XCALL IOS (CHN044,ORDHDR,READ,LOKCTL)
	IF (LOKCTL .NE. 0) GOTO GE_EOF
	IF (OLOC .NE. 'E') GOTO GE_LOOP
	IF (OORDDT(1,4) .NE. TODAY(1,4)) GOTO GE_LOOP	;CURRENT YEAR
	IF (MTD.EQ.'M' .AND. OORDDT(1,6).NE.ENDAT(1,6) )GOTO GE_LOOP
	IF (OSLMAN.LT.1 .OR. OSLMAN.GT.25) OSLMAN = MAXARA

	CALL GET_LINES

	INCR A_NUM(OSLMAN)
	A_DLR(OSLMAN) = A_DLR(OSLMAN) + (OSALE-OFRGHT-OTAX)
	GOTO GE_LOOP	
GE_EOF,
	FOR I FROM 1 THRU MAXARA
		BEGIN
		CE_SLSMAN = I
		XCALL ISIO (CHNWRK, CEST, CE_SLSMAN, READ, LOKCTL) 
		IF (LOKCTL .NE. 0)
			BEGIN
			CLEAR CEST
			CE_SLSMAN = I
			END
		CE_NUMEST = A_NUM(I)
		CE_DLREST = A_DLR(I)
		IF (LOKCTL .EQ. 0)
		THEN	WRITE (CHNWRK, CEST, CE_SLSMAN)
		ELSE	STORE (CHNWRK, CEST, CE_SLSMAN)
		END	
	RETURN
;-------------------------------------------
GET_LINES,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OSALE, OTAX, OFRGHT
	FIND (CHN045, ORDLIN, OORDNO) [ERR=GL_LOOP]
GL_LOOP,
	XCALL IOS (CHN045,ORDLIN,READ,LOKCTL)
	IF (LORDNO .NE. OORDNO) RETURN
	AMT = (LQTYOR * LPRICE)#1
	OSALE = OSALE + AMT
	GOTO GL_LOOP
GL_EOF,

	RETURN
;-------------------------------------------

PRT_REPORT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	FIND (CHNWRK, CEST, ^FIRST)
LOOP,
	READS (CHNWRK, CEST, EOF)
	IF (CE_NUMINV.LE.0 .AND. CE_NUMEST.LE.0) GOTO LOOP
	IF (CE_SLSMAN.GT.0) XCALL IO (1,SALMAN,CE_SLSMAN,READ,LOKCTL)
	PLINE (1,2) = CE_SLSMAN, 'ZX'
	PLINE (6,30) = SLSNM
	PLINE (33,38) = 'SALES'
	PLINE (41,45) = CE_NUMINV,	'ZZZX-'
	PLINE (47,59) = CE_DLRINV,	'Z,ZZZ,ZZX.XX-'
	CALL PRINT
	PLINE (33,38) = 'QUOTES'
	PLINE (41,45) = CE_NUMEST,	'ZZZX-'
	PLINE (47,59) = CE_DLREST,	'Z,ZZZ,ZZX.XX-'
	CALL PRINT
	CALL PRINT
	GOTO LOOP

;-----------SALESMAN-----------            NO        AMOUNT
;ZX   AAAAAAAAAAAAAAAAAAAAAAAAA  SALES   ZZZX  Z,ZZZ,ZZX.XX-
;1234567890123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5         6         7
EOF,	
	IF (PRNTSW .GT. 0) CALL LPOFF
	RETURN
;-------------------------------------------

CLRARA,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	FOR I FROM 1 THRU MAXARA
		BEGIN
		CLEAR A_MAN(I)
		CLEAR A_NUM(I)
		CLEAR A_DLR(I)
		END
	RETURN
;-------------------------------------------
PRINT,
	IF (PRNTSW.EQ.0) CALL LPON
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR,HDR,LEG,
&		LEG,LEG,0,0,080,1,LPSW,RPTNUM,PRTTYP)
	RETURN
LPON,
	LPSW = 1
	SPLFIL (5,6) = 'AN'
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTSW = 1
	RETURN
LPOFF,
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	PRNTSW =
	RETURN
;-------------------------------------------

OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (1,'I',54,SWITCH)
	IF (SWITCH .EQ. 9) RETURN

	SWITCH = 5
	XCALL FILES (4,'SI',44,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN044 = 4

	SWITCH = 5
	XCALL FILES (5,'SI',45,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN045 = 5

	SWITCH = 5
	XCALL FILES (8,'SI',184,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN184 = 8

	XCALL FFILE(44,WRKFIL,INXCTL)
	WRKFIL(5,14) = 'CESTXX.ISM'
	XCALL ISAMC (WRKFIL, 34, 1, 'START=1, LENGTH=2, DUPS, ASCEND')
	OPEN (33, SU, WRKFIL)
	CHNWRK = 33
	
	OPNOK = 1
	RETURN
;-------------------------------------------

CLOSE,
	IF (CHN044) CLOSE CHN044
	RETURN
;-------------------------------------------
