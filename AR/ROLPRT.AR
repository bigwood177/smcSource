;ROLPRT.AR
;
RECORD	CUSNAM
	N_CUST	,D6
	N_NAME	,A25
;
RECORD	ROLLDX
;; RD181A.DEF:	CSNOTE - CUSTOMER NOTES

	AR_CUST	,D6
	AR_TYPE	,D2
	AR_SEQ	,D4
	AR_NOTE	,A60
RECORD,X
	AR_KEY	,D8

RECORD	CHANNEL
	CHNNAM	,D2
	CHNROL	,D2

RECORD	PRINT
	HD	,A6,	'NO HDR'
	LG	,A9,	'NO LEGEND'
	PLINE	,A80
	PGCNT	,D4
	LINCNT	,D2
	TITLE	,A*,	'NO TITLE'
	LPSW	,D1
	RPTNUM	,D3
	PRTTYP	,D3
	SPLFIL	,A14

RECORD	VARS
	VALTYP	,5A8,	'SALES   ','SHIPPING','GENERAL ','CREDIT  ','ALL     '
	OPNOK	,D1
	STNO	,D6
	ENNO	,D6
	ENTRY	,A30
	INXCTL	,D1
	CNGCTL	,D1
	WHATNO	,D2
	SAVTYP	,D1
	SWITCH	,D1
	V	,D1
PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'PRINT ROLODEX',1)
	CALL OPENS
	IF (.NOT. OPNOK) GOTO ENDOFF
DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,'PRINT ROLODEX',1)
	XCALL OUTPT (5,5,0,'1. START CUST #',1)
	XCALL OUTPT (7,5,0,'2. END   CUST #',1)
STNO,	
	XCALL INPUT (5,22,06,00,'#E',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	STNO = ENTRY(1,6)
	IF (STNO .EQ. 0)
		BEGIN
		ENNO = 999999
		XCALL OUTPT (5,22,1,'ALL',1)
		XCALL OUTPT (7,22,1,' ',1)
		END
ENNO,
	XCALL INPUT (7,22,06,00,'# ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	ENNO = ENTRY(1,6)
	IF (ENNO .EQ. 0)
		BEGIN
		ENNO = STNO
		ENTRY(1,6) = ENNO,	'ZZZZZZ' [LEFT]
		XCALL OUTPT (7,22,1,ENTRY(1,6),1)	
		END
ANYCNG,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (DISPLA),CNGCTL

	N_CUST = -1
	XCALL AR_LPON (LPSW,SPLFIL)
	FIND (CHNROL,ROLLDX,STNO) [ERR=LOOP]
LOOP,
	READS (CHNROL,ROLLDX,EOF)
	IF (AR_CUST .LT. STNO) GOTO LOOP
	IF (AR_CUST .GT. ENNO) GOTO EOF
	IF (AR_CUST .NE. N_CUST) CALL NEWCUS
	IF (AR_TYPE .NE. SAVTYP) CALL NEWTYP
	PLINE (1,60) = AR_NOTE
	CALL PRINT
	GOTO LOOP

NEWCUS,
	PLINE = '------------------------------------------'
	CALL PRINT
	PLINE (1,6) = AR_CUST,	'ZZZZZX'
	PLINE (8,23) = '** NOT ON FILE **'
	READ (CHNNAM,CUSNAM,AR_CUST) [ERR=BADCUS]
	PLINE (8,23) = N_NAME
BADCUS,
	CALL PRINT
NEWTYP,
	CALL PRINT
	PLINE (5,20) = VALTYP(AR_TYPE)
	CALL PRINT
	SAVTYP = AR_TYPE
	RETURN

EOF,
	XCALL AR_LPOFF (LPSW,SPLFIL,PGCNT)
ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('AR:ARMENU',1)
	STOP

PRINT,
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HD,HD,HD,
&		LG,LG,LG,0,80,80,0,LPSW,RPTNUM,PRTTYP)
	RETURN


OPENS,
	CLEAR OPNOK
	SWITCH = 5
	XCALL FILES (1,'SI',190,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHNROL = 1

	OPEN (2,SI,'SMC:CUSNAM')
	CHNNAM = 2

	OPNOK = 1
	RETURN

CLOSE,
	IF (CHNROL) CLOSE CHNROL
	IF (CHNNAM) CLOSE CHNNAM
	RETURN

;XXXXXX AAAAAAAAAAAAAAAAAAAAAAAAA
;    AAAAAAAAAAAAAA
;AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA
;123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5          6
