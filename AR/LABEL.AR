; LABEL.AR
;		PROGRAM TO PRINT ROLODEX CARD OR LABELS
;
;	AUTHOR  JAN TRIDEN
;
;	DIGITAL COMPUTER SOFTWARE, INC.
;
;
;
RECORD CUSMAS
		.INCLUDE 'DEF:RD001A.DEF'
;:
RECORD CUSCTL
		.INCLUDE 'DEF:RD001B.DEF'
;:
RECORD CUSIDX
		.INCLUDE 'DEF:RD002A.DEF'
;:
;;;RECORD CSEMAS
;;;		.INCLUDE 'DEF:RD135A.DEF'
;:
;;;RECORD CSECTL	,X
;;;		.INCLUDE 'DEF:RD135B.DEF'
;:
;;;RECORD CSEIDX
;;;		.INCLUDE 'DEF:RD136A.DEF'
;:
RECORD TMPIDX
	TZIPCD	,A10
	TCUSNO	,D6
	TRECNO	,D5
RECORD SRTMSG
	NXTPRG	,A9
	RCNT	,D5
	OCNT	,D5
RECORD

	V	,D1
	SWITCH	,D1
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	INXCTL	,D1
	ENTRY	,A1
	YN	,A1
	LABROL	,D1
	LINCNT	,D5
	PGCNT	,D5
	PLINE	,A50
	LPSW	,D2
	RPTNUM	,D3
	TYPSW	,A1
	STRTNO	,D6
	ENDNO	,D6
	SPLFIL	,A14
	PGCNG	,D3
	STXCTL	,D1
	WHATNO	,D1
	CNGCTL	,D1
	RECNO	,D5
	COPY	,D2
	COPCNT	,D2
	BSN135	,D5
	BSMID	,D5
	SRCCTL	,D1
	KEY	,A6
	WRCNT	,D5
	MSGCTL	,D1
	BLOCK	,D5
	TODAY	,D6
PROC
	XCALL TERID (V)
OPEN,
	XCALL FILES (1,'I',01,SWITCH)
	IF (SWITCH.EQ.9)GOTO END
OPEN1,
	XCALL FILES (2,'I',02,SWITCH)
	IF (SWITCH.NE.9) GOTO BEGIN
	GOTO CLOSE1

LPOFF,
	PGCNT = -1
	XCALL AR_LPOFF (LPSW,SPLFIL,PGCNT)
	GOTO BEGIN

CLOSE,
	IF (LABROL.NE.6) XCALL FILES (2,'I',02,4)
	IF (LABROL.EQ.6) XCALL FILES (2,'I',10,4)
CLOSE1,
	XCALL FILES (1,'I',01,4)

END,
	XCALL PGCHN ('AR:ARMENU',1)

BEGIN,
 	LOKCTL = 1
	XCALL IO (1,CUSCTL,1,READ,LOKCTL)

BEGIN1,
	RECNO =
	XCALL OUTPT (1,1,2,'PRINT CUSTOMER LABELS',V)
	XCALL OUTPT (4,15,0,'PLEASE SELECT APPLICATION  .',V)
	XCALL OUTPT (5,20,0,'1.  PRINT LABELS',V)
	XCALL OUTPT (6,20,0,'2.  PRINT ROLODEX CARDS',V)
	XCALL OUTPT (7,20,0,'3.  PLAIN PAPER ROLODEX FORMAT (3 TO A PAGE)',V)
	XCALL OUTPT (8,20,0,'4.  PRINT LABELS IN ZIP CODE ORDER',V)
	XCALL OUTPT (9,20,0,'5.  PRINT ROLODEX CARDS IN ZIP CODE ORDER',V)
	XCALL OUTPT (10,20,0,'6.  PRINT LABELS FOR OD ACCTS (FROM AGING)',V)
	XCALL OUTPT (11,20,0,'7.  PRINT LABELS FOR LAST YEAR ACCTS',V)
	XCALL INPUT (4,42,01,01,'#E',ENTRY,INXCTL,V)
	GOTO (BEGIN1,CLOSE), INXCTL
	LABROL = ENTRY(1,1)
	IF (LABROL.GT.7) GOTO BEGIN1
	CALL ANYCNG
	IF (CNGCTL) GOTO BEGIN1
	IF (LABROL.EQ.4.OR.LABROL.EQ.5) GOTO ZIPIDX
	IF (LABROL.EQ.6)
	BEGIN
	  CLOSE 2
	  SWITCH = 1
	  XCALL FILES (2,'I',10,SWITCH)
	  IF (SWITCH.NE.9)
	  BEGIN
	    XCALL FILES (8,'I',02,4)
	    STRTNO = 
	    ENDNO = 999999
	    RECNO = 1
	    COPY = 1
	    GOTO OPENLP
	  END
	  XCALL FILES (2,'I',02,5)
	  XCALL MESAG ('TMPIDX FILE IN USE - PLEASE TRY LATER',1)
	  GOTO BEGIN1
	END
	IF (LABROL.EQ.7)
	BEGIN
	  XCALL RDATE (TODAY)
;;;	  XCALL FILES (5,'I',135,5)
;;;	  XCALL FILES (6,'I',136,5)
;;;	  LOKCTL = 1
;;;	  XCALL IO (5,CSECTL,1,READ,LOKCTL)
;;;	  BSN135 = ORG135
	END
OPENLP,
	LPSW = 2
	XCALL AR_LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO CLOSE
	IF (LABROL.EQ.6) GOTO RDLOOP

SELECT,
	RECNO = 1
	LOKCTL = 1
	XCALL IO (1,CUSCTL,1,READ,LOKCTL)
	XCALL OUTPT (2,1,2,'\',V)
	STXCTL = 1
	XCALL STENO (STRTNO,ENDNO,'CUSTOMER',6,STXCTL,V)
	GOTO (LPOFF), STXCTL
	XCALL OUTPT (8,20,0,'PRINT PHONE NUMBER <N> ?   ',V)
	XCALL INPUT (8,46,01,00,'YN',ENTRY,INXCTL,V)
	YN = ENTRY(1,1)
	XCALL OUTPT (10,20,0,'NUMBER OF CARDS <1> ? ',V)
	XCALL INPUT (10,46,01,00,'# ',ENTRY,INXCTL,V)
	IF (INXCTL) GOTO SELECT
	IF (ENTRY.EQ.' ') ENTRY (1,1) = '1'
	COPY = ENTRY
	CALL ANYCNG
	IF (CNGCTL) GOTO SELECT

	IF (STRTNO.EQ.ENDNO)
	BEGIN
	  KEY = STRTNO,'XXXXXX'
	  XCALL SERCH (2,CUSIDX,KEY,1,6,ORG001,BSMID,SRCCTL,4,7,11,0,0,0,0)
	  IF (SRCCTL)
	  BEGIN
	    XCALL MESAG ('CUSTOMER NOT FOUND',2)
	    GOTO SELECT
	  END
	  LOKCTL = 1
	  XCALL IO (1,CUSMAS,IRC001,READ,LOKCTL)
	  GOTO PLINE
	END

RDLOOP,
	INCR RECNO
	LOKCTL = 1
	XCALL IO (2,CUSIDX,RECNO,READ,LOKCTL)
	IF (IRC001.EQ.00000)GOTO RDLOOP
	IF (CUSIDX.EQ.']]]]')GOTO SELECT
	IF (ICUSNO.LT.STRTNO)GOTO RDLOOP
	IF (ICUSNO.GT.ENDNO) GOTO SELECT
	LOKCTL = 1
	XCALL IO (1,CUSMAS,IRC001,READ,LOKCTL)
	IF (LABROL.EQ.7)
	BEGIN
;;;	  CALL GETCSE
	  IF (SRCCTL) GOTO PLINE
	  IF (ELAST(5,6).LT.TODAY(5,6)-1) GOTO RDLOOP
	END
PLINE,
	XCALL LINFD (1)			;;;1
	PLINE (1,6) = CUSNO
	CALL PRINT			;;;2
	PLINE (1,26) = NAME
	IF (YN.EQ.'Y') PLINE (32,45) = PHONE,'(ZZZ) XXX-XXXX'
	CALL PRINT			;;;3
	PLINE (1,26) = ADD1
	CALL PRINT			;;;4
	PLINE (1,22) = ADD2
	IF (ADD2.NE.'    ') CALL PRINT	;;;5 MAYBE
	PLINE (1,15) = CITY
	PLINE (16,16) = ','
	PLINE (18,19) = STATE
	PLINE (21,31) = ZIP
	CALL PRINT				;;;5 OR 6 MAYBE
	IF (ADD2.EQ.'     ') XCALL LINFD (1)	;;;6 MAYBE

	IF (LABROL.EQ.2.OR.LABROL.EQ.3)
	BEGIN
		XCALL LINFD (1)			;;;7
		PLINE (1,7) = 'CONTACT'
		PLINE (21,24) = 'NAME'
		PLINE (34,38) = 'TITLE'
		CALL PRINT			;;;8
		XCALL LINFD (2)			;;;9-10
		PLINE (1,3) = 'A/P'
		CALL PRINT			;;;11
		XCALL LINFD (2)			;;;12-13
		PLINE (1,13) = 'SPECIAL INSTR.'
		CALL PRINT			;;;14
		XCALL LINFD (4)			;;;15-16-17-18
	END
	IF (LABROL.EQ.3) XCALL LINFD (4)	;;;19-20-21-22
	IF (COPY.GT.1.AND.COPCNT.LT.COPY-1)
	BEGIN
	  INCR COPCNT
	  GOTO PLINE
	END
	COPCNT =
	IF (STRTNO.EQ.ENDNO) GOTO SELECT
	GOTO RDLOOP

PRINT,
	LINCNT =
	XCALL LPOUT (LINCNT,PGCNT,PLINE,'NO TITLE','NO HDR',' ',' ','NO LEGEND',' ',' ',0,50,50,0,LPSW,RPTNUM,TYPSW)
	RETURN

ANYCNG,
	CNGCTL = 2
	XCALL ANYCN (CNGCTL,WHATNO)
	RETURN
;
; SECTION ADDED TO BUILD INDEX FOR ZIP CODE ORDER
;
ZIPIDX,
	XCALL OUTPT (2,1,1,'BUILD INDEX FOR ZIP SORT',1)
	XCALL WATE (4,2)
	SWITCH = 3
	XCALL FILES (3,'I',68,SWITCH)
	IF (SWITCH.EQ.9) GOTO INUSE
	BLOCK = ( ( ( REC001 + 1 ) * 23 ) / 512 ) + 1
	XCALL OFILE (3,68,BLOCK,22,SWITCH)
	TMPIDX =
	TRECNO = 1
	LOKCTL = 1
	XCALL IOS (3,TMPIDX,WRITE,LOKCTL)
	WRCNT = 1
ZIPLP1,
	INCR TRECNO
	LOKCTL = 1
	XCALL IO (1,CUSMAS,TRECNO,READ,LOKCTL)
	IF (CUSMAS.EQ.']]]]]]]]]]') GOTO ZIPLP2
	IF (NAME.EQ.']]]DEL') GOTO ZIPLP1
	TZIPCD = ZIP
	TCUSNO = CUSNO
	LOKCTL = 1
	XCALL IOS (3,TMPIDX,WRITE,LOKCTL)
	INCR WRCNT
	GOTO ZIPLP1
ZIPLP2,
	TMPIDX = CUSMAS
	LOKCTL = 1
	XCALL IOS (3,TMPIDX,WRITE,LOKCTL)
	CLOSE 3
	XCALL FILES (3,'U',68,5)
	LOKCTL = 1
	XCALL IO (3,TMPIDX,1,READ,LOKCTL)
	TRECNO = WRCNT
	LOKCTL = 1
	XCALL IO (3,TMPIDX,1,WRITE,LOKCTL)
	CLOSE 3
	XCALL FILES (1,'I',01,4)
	XCALL FILES (2,'I',02,4)
	RCNT = WRCNT
	OCNT = 1
	IF (LABROL.EQ.4) NXTPRG = 'AR:ZIPLAB'
	IF (LABROL.EQ.5) NXTPRG = 'AR:ZIPRDX'
	MSGCTL = 5
	XCALL SNMSG (SRTMSG,MSGCTL)
	XCALL PGCHN ('AR:SRTZIP',1)
INUSE,
	XCALL MESAG ('TMPIDX FILE IN USE - TRY AGAIN LATER',2)
	GOTO BEGIN1
;;;GETCSE,
;;;	KEY = CUSNO, 'XXXXXX'
;;;	XCALL SERCH (6,CSEIDX,KEY,1,6,BSN135,BSMID,SRCCTL,4,7,11,0,0,0,0)
;;;	IF (SRCCTL) 
;;;	BEGIN
;;;	  CSEMAS =
;;;	  RETURN
;;;	END
;;;	LOKCTL = 1
;;; 	XCALL IO (5,CSEMAS,IRC135,READ,LOKCTL)
;;;	RETURN
END
