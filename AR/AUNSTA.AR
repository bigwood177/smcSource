;AUNSTA.AR
;newsta.ar
;NEWTER.AR
; 	5-7-18: ssq convert cusmas to isam

;
;	NEW VERSION OF SALES ANALYSIS BY TERITORRY
;
;	8-28-06: SKIP ROCKFORD
;

RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'

RECORD	COMP14
	O_CHAR	,A1		;<ESC>
		,a4,	"&l1O"	;landscape mode
	ESC_12	,A1		;<ESC>
		,A4,	"&l8D"	;vertical spacing, 8 lines/inch
	ESC_13	,A1		;<ESC>
;;;		,A4,	"&k2S"	;pitch mode = 2 = 16.5-16.7 (compressed)
		,A8,	"(s13.00H"	;pitch 14 CPI
RECORD CUSMAS		; 
		.INCLUDE 'DEF:RD001A.DEF'
RECORD ,X		; 
		.INCLUDE 'DEF:RD001B.DEF'

RECORD	SALMAN
		.INCLUDE 'DEF:RD054A.DEF'

RECORD SAINDX		; 
		.INCLUDE 'DEF:RD009X.DEF'

RECORD	SA_KEY
	SA_MAN	,D2
	SA_TER	,A2
	SA_CUS	,D6


RECORD	TMPFIL
		,A4,	'SPL:'
		,A6,	'SAINDX'
		,A4,	'.ISM'

RECORD TITLE
		,A27,	'SALES ANALYSIS BY TERRITORY'

RECORD HDR1
	,	A*,	'TR  ------------CUSTOMER------------     MTD SALES'
	,	A*,	'  %-SLS     YTD SALES  %-SLS'

RECORD	GR_CLEAR	;GRAND TOTALS
	GR_TMTD	,D10
	GR_TYTD	,D10
	GR_CNT	,D6

RECORD	SM_CLEAR	;SALESMAN TOTALS
	SM_TMTD	,D10
	SM_TYTD	,D10
	SM_CNT	,D6

RECORD	TR_CLEAR	;TERRITORY TOTALS
	TR_TMTD	,D10
	TR_TYTD	,D10
	TR_CNT	,D6

RECORD	CHANNEL
	CHN001	,D2
	CHN002	,D2
	CHN009	,D2
	CHN054	,D2
	CHN182	,D2

RECORD	PRINT
	LINFD	,D2
	LGNCTL	,D1
	LINCNT	,D2
	LPARG   ,D1
	LPONSW	,D1
	PRNTSW	,D1
	LPSW	,D1
	PGCNT	,D3
	PLINE	,A80
	PRTCNT	,D5			;AR29
	PRTTYP  ,A1
	RPTNUM  ,D3
	SPLFIL	,A*,	'c:\smc\spool\automu.spl'
	HDR	,A6,	'NO HDR'
	LEG	,A9,	'NO LEGEND'

RECORD	RPTDIR
;;;		,A*,	'RP2:'
		,a*,	'\\server01\smc\Reports\smc\'
	RPTMON	,A3
		,A1,	'\'
	RPTRPT	,A60

RECORD
	MON,	12A3,	'JAN','FEB','MAR','APR','MAY','JUN',
&			'JUL','AUG','SEP','OCT','NOV','DEC'

RECORD	VARS
	TL	,D3
	WMM	,D2
	MONDAT	,D8
	p_copies	,d5
	SUMMRY	,D1
	OPNOK	,D1
	EOF	,D1
	PCT	,D5
	STTER	,A2
	ENTER	,A2
	SAVTER	,A2
	MASK1	,A14,	 'ZZ,ZZZ,ZZZ.XX-'
	MASK2	,A5,	'ZZZ.X'
	RECNO	,D5
	ENTRY	,A2
	INXCTL	,D1
	CNGCTL	,D1
	WHATNO	,D2
	READ	,D1,	0
	WRITE	,D1,	1
	LOKCTL	,D1
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	V = 1
	XCALL OUTPT (1,1,2,'SALES ANALYSIS BY TERRITORY',V)
	XCALL WATE (4,V)

;========================================================
	CALL OPENS
	IF (.NOT. OPNOK) GOTO ALL_DONE

	CALL BUILD_INDEX
	STTER =
	ENTER = 'ZZ'
	SUMMRY = 1

PROCES,
	CLEAR GR_CLEAR, SM_CLEAR, TR_CLEAR
	LINCNT = 66
	EOF = 0
	SAVTER = '-1'
	CALL LPON		;SSQ 12-9-03

	FIND (CHN009, SAINDX, ^FIRST) [ERR=ACM_LOOP]
ACM_LOOP,
	LOKCTL = 1
	XCALL IOS (CHN009, SAINDX, READ, LOKCTL)
	IF (LOKCTL .NE. 0) GOTO ACM_EOF
	
	GR_TMTD = GR_TMTD + C_SALMTD
	GR_TYTD = GR_TYTD + C_SALYTD
	GOTO ACM_LOOP
ACM_EOF,
	FIND (CHN009, SAINDX, ^FIRST) [ERR=AC2_LOOP]
;--------------------------------------------------------
AC2_LOOP,
	LOKCTL = 1
	XCALL IOS (CHN009, SAINDX, READ, LOKCTL)
	IF (LOKCTL .NE. 0) GOTO AC2_EOF
	IF (SASTAT .LT. STTER) GOTO AC2_LOOP
	IF (SASTAT .GT. ENTER) GOTO AC2_EOF

	IF (SASTAT .NE. SAVTER) CALL NEW_TERR
	
	TR_TMTD = TR_TMTD + C_SALMTD
	TR_TYTD = TR_TYTD + C_SALYTD
	GOTO AC2_LOOP
AC2_EOF,
	GOTO ALL_DONE

;--------------------------------------------------------
NEW_TERR,;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (SAVTER .EQ. '-1') GOTO OUTTER
	CALL PRINT_TERR				;PRINT TERR DETAIL
;;;	GR_TMTD = GR_TMTD + TR_TMTD
;;;	GR_TYTD = GR_TYTD + TR_TYTD
	GR_CNT = GR_CNT + TR_CNT
OUTTER,
	SAVTER = SASTAT
	CLEAR TR_CLEAR
	RETURN
;--------------------------------------------------------

ALL_DONE,
	EOF = 1
	CALL NEW_TERR

	LINFD = 1
	CALL LINFD

	CLEAR PLINE				;SSQ 12-9-03
	PLINE (4,9) = GR_CNT, 'ZZ,ZZX'
	PLINE (11,33) = 'CUSTOMERS FOR REPORT'
	CALL PRINT

	PCT = 1000
	PLINE (5,60) = 'REPORT TOTALS:'
	PLINE (38,51) = GR_TMTD		,MASK1
	PLINE (53,57) = PCT		,MASK2
	PLINE (59,72) = GR_TYTD		,MASK1
	PLINE (74,78) = PCT		,MASK2
	CALL PRINT

	CLOSE 14
	RPTMON = MON(WMM)	;for reports directory
	RPTRPT = 'Sales_by_Territory_Summary.pdf'
	TL = %TRIM(RPTDIR)
	xcall mkpdf (splfil,rptdir(1,tl))

ENDOFF,
	CALL CLOSE
	XCALL PGCHN('AR:SASEL',1)

;--------------------------------------------------------
;========================================================
;--------------------------------------------------------
PRINT_TERR,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		;;; PRINT ALL CUSTOMERS IN CURRENT TERR.
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CLEAR SA_KEY
	SA_MAN = 
	SA_TER = SAVTER
	FIND (CHN009, SAINDX, SA_KEY) [ERR=PRT_LOOP]
PRT_LOOP,
	LOKCTL = 1
	XCALL IOS (CHN009, SAINDX, READ, LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF_PRT
	IF (SASTAT .NE. SAVTER) GOTO EOF_PRT

	PLINE (1,2) = SAVTER
	PLINE (5,10) = SACUNO
	PLINE (12,36) = C_NAME
	PLINE (38,51) = C_SALMTD	,MASK1

	IF(TR_TMTD.EQ.0)
	THEN	PCT = 0
	ELSE	PCT = (C_SALMTD*10000/TR_TMTD)#1

	PLINE (53,57) = PCT		,MASK2
	PLINE (59,72) = C_SALYTD	,MASK1

	IF(TR_TYTD.EQ.0)
	THEN	PCT = 0
	ELSE	PCT = (C_SALYTD*10000/TR_TYTD)#1

	PLINE (74,78) = PCT		,MASK2

	IF(SUMMRY .EQ. 0) CALL PRINT	;SSQ 12-9-03
	INCR TR_CNT
	GOTO PRT_LOOP

EOF_PRT,
	LINFD = 1
	CALL LINFD

	CLEAR PLINE			;SSQ 12-9-03
	PLINE (4,9) = TR_CNT, 'ZZ,ZZX'
	PLINE (11,33) = 'CUSTOMERS FOR TERRITORY'
	PLINE (35,36) = SAVTER
	CALL PRINT

	PCT = 1000
	PLINE (5,60) = 'TERRITORY XX TOTALS:'
	PLINE (15,16) = SAVTER
	PLINE (38,51) = TR_TMTD		,MASK1

	IF (GR_TMTD .EQ. 0)
	THEN	PCT = 0
	ELSE	PCT = (TR_TMTD*10000/GR_TMTD)#1
	PLINE (53,57) = PCT		,MASK2

	IF (GR_TYTD .EQ. 0)
	THEN	PCT = 0
	ELSE	PCT = (TR_TYTD*10000/GR_TYTD)#1
	PLINE (59,72) = TR_TYTD		,MASK1
	PLINE (74,78) = PCT		,MASK2
	CALL PRINT

	XCALL FILL ('-',PLINE)
	CALL PRINT
	LINFD = 1
	CALL LINFD

	SM_TMTD = SM_TMTD + TR_TMTD
	SM_TYTD = SM_TYTD + TR_TYTD
	SM_CNT = SM_CNT + TR_CNT
;;;	LINCNT = 66			;SSQ 12-9-03
	RETURN
;--------------------------------------------------------

BUILD_INDEX,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL OUTPT (2,1,0,'BUILD INDEX',1)
	LOKCTL = 1
	XCALL IO (1,CUSMAS,1,READ,LOKCTL)	;HEADER
READ,
	LOKCTL = 1
	XCALL IOS (1,CUSMAS,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOF1
;;;	IF (CUSMAS.EQ.']]]]]]') GO TO EOF1
	INCR RECNO
	IF (NAME.EQ.']]]DEL') GO TO READ
	IF (CUSNO .EQ. 63340) GOTO READ		;SKIP ROCK 8-29-06

	SASTAT = TERR
	SASLMN =		;DON'T CARE SALESMAN # IN CUSMAS
;;;	SASLMN = SLSMAN
	SACUNO = CUSNO
	C_NAME = NAME
	C_SALMTD = SALMTD
	C_SALYTD = SALYTD
	C_COSMTD = COSMTD
	C_COSYTD = COSYTD
	LOKCTL = 1
	STORE (3,SAINDX, SAINDX(1,10) )
;;;	XCALL ISIO (3,SAINDX,SAINDX(1,10),STORE,LOKCTL)
	GO TO READ

EOF1,
;;;	CLOSE CHN009
	RETURN
;-----------------------------------------------


PRINT,
	IF (PRNTSW.EQ.0) CALL LPON
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR,HDR,
&		LEG,LEG,LEG,0,080,080,0,LPSW,RPTNUM,PRTTYP)
	RETURN
LINFD,
	XCALL LINFD (LINFD)
	LINCNT = LINCNT + LINFD
	RETURN
LPON,
	OPEN (14, O, SPLFIL)
	DISPLAY (14, COMP14)

	LPSW = 1
	PRNTSW = 1
	RETURN

OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

;Create work file...
	XCALL ISAMC (TMPFIL, 75, 1, 'START=1, LENGTH=10, DUPS, ASCEND')
	OPEN (3, SU, TMPFIL)
	CHN009 = 3

	SWITCH = 1
;;;	XCALL FILES (2,'I',02,SWITCH)		;FILE # 02 -- CUSIDX FILE
;;;	IF (SWITCH.EQ.9) RETURN
	CHN002 = 2

	XCALL FILES (1,'SI',01,SWITCH)		;FILE # 01 -- CUSMAS FILE
	IF (SWITCH.EQ.9) RETURN
	CHN001 = 1

	SWITCH = 5
	XCALL FILES (4,'SI',182,SWITCH)
	IF(SWITCH.EQ.9) RETURN
	CHN182 = 4

	XCALL FILES (5,'I',054,SWITCH)
	IF(SWITCH.EQ.9) RETURN
	CHN054 = 5
	XCALL OUTPT (12,1,1,'\',V)
	LOKCTL = 1
	CUSNO = 0
	XCALL ISIO (1,CUSMAS,CUSNO,READ,LOKCTL)

	SWITCH = 5
	XCALL FILES (18,'SU',182,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN182 = 18
	
	CLEAR COPTBL
	TBLCOD = "EM"
	LOKCTL = 1
	XCALL ISIO (CHN182, COPTBL, TBL_KEY, READ, LOKCTL)
	IF (LOKCTL .NE. 0) RETURN
	WMM = ME_DATE(5,6)	;MONTH
	IF (WMM.LE.0 .OR. WMM.GT.12) WMM = 1

	CLOSE CHN182
	
	XCALL ASCII (27,O_CHAR)
	ESC_12 = O_CHAR
	ESC_13 = O_CHAR

	OPNOK = 1
	RETURN
;------------------------------------------------

CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (CHN001)
		BEGIN
		XCALL FILES (CHN001, 'SI', 001, 4)
		;;;XCALL FILES (CHN002, 'I', 002, 4)
		END

	CLOSE CHN009
	CLOSE CHN182
	XCALL DELET (TMPFIL)
	RETURN
;-------------------------------------------------

END

;TR  ------------CUSTOMER------------     MTD SALES  %-SLS     YTD SALES  %-SLS
;AA  ZZZZZX AAAAAAAAAAAAAAAAAAAAAAAAA ZZ,ZZZ,ZZX.XX- ZZZ.X ZZ,ZZZ,ZZX.XX- ZZZ.X
;12345678901234567890123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5         6         7


