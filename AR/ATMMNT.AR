;  ATMMNT / AR
;
; 4-30-18 ssq: add short description

RECORD ARTERM
		.INCLUDE 'DEF:RD170A.DEF'

RECORD ,X
		.INCLUDE 'DEF:RD170C.DEF'

RECORD ARTCTL	
		.INCLUDE 'DEF:RD170B.DEF'

RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD SRTMSG
	PRGNAM	,A9,	'AR:ATMCNT'
	RCNT	,D5
	OCNT	,D5
RECORD
	NXTPGM	,A9
	SRTPGM	,A9,	'AR:SRTATM'
	ORGPGM	,A9,	'AR:ORGATM'
	PRTPGM	,A9,	'AR:ATMPRT'
RECORD
	ALPHA   ,A2
	BS170   ,D5
	BSEND	,D5
	BSMID	,D5
	CNGCTL	,D1
	ENTRY	,A15
	INXCTL	,D1
	KEY     ,A4
	LOKCTL	,D1
	OPNFLG  ,D1
	PAYDEP  ,D2
	READ	,D1	,0
	RECCNT  ,D5
	SELECT  ,D1
	SORT    ,A1
	SRCCTL	,D1
	SWITCH	,D1
	TMPCOD	,A1
	V       ,D1
	WHATNO  ,D2
	WRITE	,D1	,1
	WRKDEP  ,A2
	WRKSAV  ,A2
PROC
	XCALL TERID(V)
MENU,
	XCALL OUTPT (1,1,2,'TERMS CODE FILE MAINTENANCE',V)
	XCALL OUTPT (4,20,0,'PLEASE SELECT APPLICATION',V)
	XCALL OUTPT (5,25,0,'1. ADD TERMS CODES',V)
	XCALL OUTPT (6,25,0,'2. CHANGE/INQUIRE TERMS CODES',V)
	XCALL OUTPT (7,25,0,'3. DELETE TERMS CODES',V)
	XCALL OUTPT (8,25,0,'4. PRINT TERMS CODES LIST',V)
	CTL = '08,47,01,01,#E'
	CALL INPUT
	GO TO (MENU,ENDOFF), INXCTL
	SELECT = ENTRY
	IF (SELECT.LT.1 .OR. SELECT.GT.4) GO TO MENU
	IF (SELECT.EQ.4) GO TO ASKSRT
	IF (OPNFLG.EQ.0) GO TO OPEN
	IF (SELECT.EQ.1 .OR. SELECT.EQ.2 .OR. SELECT.EQ.3) GO TO DISPLA
OPEN,
	XCALL WATE (3,V)
	SWITCH = 3
	XCALL FILES (1,'U',170,SWITCH)		;FILE 170 ARTERM
	IF (SWITCH.EQ.9) GO TO INU170
	SWITCH = 5
	XCALL FILES (1,'U',170,SWITCH)		;FILE 170 ARTERM
	IF (SWITCH.EQ.9) GO TO CLO170
	LOKCTL = 0
	XCALL IO (1,ARTCTL,1,READ,LOKCTL)
	RECCNT = REC170
	BS170 = ORG170
	UNLOCK 1
	OPNFLG = 1
	GO TO DISPLA
;**********************************************************************
CLO170,
	XCALL FILES (1,'U',170,4)
INU170,
	XCALL PGCHN ('AR:ARSFMN',1)
;**********************************************************************
ASKSRT,
	XCALL OUTPT (1,1,2,'TERMS CODE FILE MAINTENANCE',1)
	XCALL OUTPT (2,1,0,'PRINT TERMS CODE LIST',1)
	XCALL OUTPT (12,20,0,'SORT BEFORE PRINTING ?',1)
SRTQRY,
	CTL = '12,44,01,00,A '
	CALL INPUT
	IF (ENTRY.NE.'Y' .AND. ENTRY.NE.'N') ENTRY = 'N'
	XCALL OUTPT (6,44,0,ENTRY,V)
	SORT = ENTRY
ENDOFF,
	XCALL WATE(4,V)
	XCALL FILES (1,'U',170,4)
	IF (SORT.EQ.'Y') GO TO ORGNIZ
	IF (DEL170.GE.95.AND.SELECT.NE.4) GO TO ORGNIZ
	IF (REC170-ORG170.GE.50.AND.SELECT.NE.4) GO TO ORGNIZ
PRTEND,
	IF (SELECT.EQ.4) XCALL PGCHN ('AR:ATMPRT',1)
	XCALL PGCHN ('AR:ARSFMN',1)
ORGNIZ,
	NXTPGM = 'AR:ARSFMN'
	IF (SELECT.EQ.4) NXTPGM = 'AR:ATMPRT'
	SWITCH = 2
	XCALL FILES (1,'U',170,SWITCH)
	IF (SWITCH.EQ.9) XCALL PGCHN (NXTPGM,0)
	LOKCTL = 1
	XCALL IO (1,ARTCTL,1,READ,LOKCTL)
	UNLOCK 1
	CLOSE 1
	RCNT = REC170
	OCNT = ORG170
	SWITCH = 5
	XCALL SNMSG (' ',SWITCH)
	IF ((RCNT-OCNT.GE.50) .OR. SORT.EQ.'Y') XCALL SNMSG (SRTMSG,2)
	IF (DEL170.GE.95) XCALL SNMSG (ORGPGM,2)
	IF (SELECT.EQ.4) XCALL SNMSG (PRTPGM,2)
	IF ((RCNT-OCNT.GE.50) .OR. SORT.EQ.'Y') XCALL PGCHN (SRTPGM,0)
	XCALL PGCHN (ORGPGM,0)
;**********************************************************************
DISPLA,
	UNLOCK 1
	CNGCTL =
	XCALL OUTPT (1,1,2,'TERMS CODE FILE MAINTENANCE',1)
	IF (SELECT.EQ.1) XCALL OUTPT (2,1,1,'ADD',1)
	IF (SELECT.EQ.1 .AND. REC170.GE.MAX170) GO TO FULL
	IF (SELECT.EQ.2) XCALL OUTPT (2,1,1,'CHANGE/INQUIRE',1)
	IF (SELECT.EQ.2 .OR. SELECT.EQ.3) XCALL OUTPT (8,13,1,'*',1)
	IF (SELECT.EQ.3) XCALL OUTPT (2,1,1,'DELETE',1)
	XCALL OUTPT (8,15,0,'1.  TERMS CODE',1)
	XCALL OUTPT (10,15,0,'2.  DESCRIPTION',1)
	XCALL OUTPT (12,15,0,'3.  DUE DAYS',1)
	XCALL OUTPT (14,15,0,'4.  DISCOUNT DAYS',1)
	XCALL OUTPT (16,15,0,'5.  DISCOUNT PCT',1)
	XCALL OUTPT (18,15,0,'6.  SHORT DESC',1)

WORK,
	IF (CNGCTL.EQ.1 .AND. SELECT.NE.1) GO TO BADCNG
	CTL = '08,35,01,01,AE'
	CALL INPUT
	GO TO (DISPLA,MENU), INXCTL
	TMPCOD = ENTRY
	IF (ENTRY(1,1).EQ.' ') GO TO WORK
	BSEND = BS170
	SRCCTL = 2
	XCALL SERCH (1,ARTERM,ENTRY,1,1,BSEND,BSMID,SRCCTL,1,2,7,0,0,0,0)
	IF (SRCCTL.EQ.0.AND.SELECT.EQ.1) XCALL MESAG ('RECORD ALREADY EXISTS',1)
	IF (SRCCTL.EQ.0 .AND. SELECT.EQ.1) GO TO WORK
	IF (SRCCTL.EQ.1 .AND. SELECT.NE.1) XCALL MESAG ('RECORD NOT FOUND',1)
	IF (SRCCTL.EQ.1 .AND. SELECT.NE.1) GO TO WORK
	WRKSAV = BSMID
	IF (SELECT.EQ.2 .OR. SELECT.EQ.3) GO TO DSPREC
DESC,
	ARTRCD = TMPCOD
	CTL = '10,35,15,00,A '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	ARTRDS = ENTRY
	XCALL OUTPT (10,35,0,ARTRDS,1)
	GO TO (ANYCNG), CNGCTL
DUEDAY,
	CTL = '12,35,03,00,# '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	ARTRDD = ENTRY
	XCALL DSPLY (3,12,35,ARTRDD,1,1)
	GO TO (ANYCNG), CNGCTL
DISDAY,
	CTL = '14,35,03,00,# '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	ARTRDY = ENTRY
	XCALL DSPLY (3,14,35,ARTRDY,1,1)
	GO TO (ANYCNG), CNGCTL
DISPCT,
	CTL = '16,35,04,00,# '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	ARTRDP = ENTRY
	XCALL DSPLY (4,16,35,ARTRDP,3,1)
	GO TO (ANYCNG), CNGCTL
ARTDS2,
	CTL = '18,35,07,00,A '
	CALL INPUT
	ARTDS2 = ENTRY(1,7)
	GOTO (ANYCNG),CNGCTL

ANYCNG,
	XCALL ANYCN (CNGCTL,WHATNO)
	GO TO (ADD,CNGBR,DISPLA), CNGCTL + 1
CNGBR,
	GO TO (DESC,DUEDAY,DISDAY,DISPCT,ARTDS2), WHATNO-1
	CNGCTL = 3
	GO TO ANYCNG
BADCNG,
	XCALL MESAG ('CAN''T CHANGE KEY FIELD',1)
	GO TO ANYCNG
;**********************************************************************
DSPREC,
	LOKCTL = 1
	XCALL IO (1,ARTERM,WRKSAV,READ,LOKCTL)
	XCALL OUTPT (8,35,0,ARTRCD,1)
	XCALL OUTPT (10,35,0,ARTRDS,1)
	XCALL DSPLY (3,12,35,ARTRDD,1,1)
	XCALL DSPLY (3,14,35,ARTRDY,1,1)
	XCALL DSPLY (4,16,35,ARTRDP,3,1)
	XCALL OUTPT (18,35,0,ARTDS2,1)
	IF (SELECT.EQ.2) GO TO ANYCNG
	GO TO DELETE
ADD,
	IF (SELECT.EQ.2) GO TO CHANGE
	LOKCTL = 1
	XCALL IO (1,ARTCTL,1,READ,LOKCTL)
	INCR REC170
	RECCNT = REC170
	LOKCTL = 1
	XCALL IO (1,ARTCTL,1,WRITE,LOKCTL)
	LOKCTL = 1
	XCALL IO (1,ARTERM,RECCNT,WRITE,LOKCTL)
	ARTERM =
	GO TO DISPLA
	
FULL,
	UNLOCK 1
	XCALL OUTPT (23,1,0,'ARTERM FILE IS NOW FULL. USE EXPAND',1)
	XCALL MESAG ('OPTION IN SYSTEM FUNCTIONS',2)
	GO TO MENU
CHANGE,
	IF (SELECT.EQ.3) GO TO DELETE
	LOKCTL = 1
	XCALL IO (1,ARTERM,WRKSAV,WRITE,LOKCTL)
	GO TO DISPLA
DELETE,
	XCALL OUTPT (24,1,1,'OKAY TO DELETE  ?',1)
	CTL = '24,20,01,01,YN'
	CALL INPUT
	IF (INXCTL.NE.1) GO TO DISPLA
	XCALL WATE(3,V)
	LOKCTL = 1
	XCALL IO (1,ARTCTL,1,READ,LOKCTL)
	INCR DEL170
	LOKCTL = 1
	XCALL IO (1,ARTCTL,1,WRITE,LOKCTL)
	DFL170 = ']]]DEL'
	ARTERM(9,26) =
	LOKCTL = 1
	XCALL IO (1,ARTERM,WRKSAV,WRITE,LOKCTL)
	XCALL MESAG ('RECORD DELETED',2)
	IF (DEL170.GE.95) GO TO PURGE
	GO TO DISPLA
PURGE,
	XCALL MESAG ('DELETED RECORDS MUST BE PURGED', 1)
	XCALL WATE(4,V)
	GO TO ENDOFF
;**********************************************************************
INPUT,
	XCALL INPUT(ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,1)
	RETURN
END
