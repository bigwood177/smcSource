no longer used
; CSEMNT / AR
;
;	EXTENDED CUSTOMER MASTER FILE MAINTENANCE
;
RECORD CSEMAS
		.INCLUDE 'DEF:RD135A.DEF'
RECORD CSECTL
		.INCLUDE 'DEF:RD135B.DEF'
RECORD CSEIDX
		.INCLUDE 'DEF:RD136A.DEF'
RECORD CUSMAS
		.INCLUDE 'DEF:RD001A.DEF'
RECORD CUSCTL	,X
		.INCLUDE 'DEF:RD001B.DEF'
RECORD CUSIDX
		.INCLUDE 'DEF:RD002A.DEF'
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD SRTMSG
		,A9 ,'AR:CSECNT'
	RCNT	,D5
	OCNT	,D5
	NXTPRG	,A9

RECORD TRWCOM
	TCOM	,A28
RECORD
	MSGCTL	,D1
	V	,D1
	SWITCH	,D1
	DECMAL	,D10
	OPTION	,D1
	ENTRY	,A30
	INXCTL	,D1
	SELECT	,D1
	LOKCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	KCUSNO	,D6
	KEY	,A6
	BSMID	,D5
	SRCCTL	,D1
	CNGCTL	,D1
	WHATNO	,D2
	ALPHA	,A12
	ACCEPT	,D1
	TCHAR	,D3
	SYSTEM	,D1
	DCHAR	,D3
	TODAY	,D6
	ORGCUS	,D5
PROC
	XCALL TERID (V)
	XCALL ENVRN (SYSTEM)
	XCALL OUTPT (1,1,2,'EXTENDED CUSTOMER MASTER FILE MAINTENANCE',1)
	XCALL RDATE (TODAY)
 OPEN1,
	SWITCH = 1
	XCALL FILES (6,'U',136,SWITCH)		;FILE 136 - CSEIDX
	IF (SWITCH.NE.9) GOTO OPEN2
	GOTO ABORT
OPEN2,
	XCALL FILES (5,'U',135,SWITCH)		;FILE 135 - CSEMAS
	IF (SWITCH.NE.9) GOTO OPEN3
	CALL CLOSE1
	GOTO ABORT
OPEN3,
	XCALL FILES (1,'I',01,SWITCH)		;FILE 01 - CUSMAS
	IF (SWITCH.NE.9) GOTO OPEN4
	CALL CLOSE2
	GOTO ABORT
OPEN4,
	XCALL FILES (2,'I',02,SWITCH)		;FILE 02 - CUSIDX
	IF (SWITCH.NE.9) GOTO RDHD2
	CALL CLOSE3
	GOTO ABORT
CLOSES,
	CLOSE 3
	XCALL FILES (2,'I',02,4)
CLOSE3,
	XCALL FILES (1,'I',01,4)
CLOSE2,
	XCALL FILES (5,'I',135,4)
CLOSE1,
	XCALL FILES (6,'I',136,4)
	RETURN
RDHD2,
	OPEN (3,I,'SMC:TRWCOM.SMC')
RDHDR,
	LOKCTL = 1
	XCALL IO (5,CSECTL,1,READ,LOKCTL)
	LOKCTL = 1
	XCALL IO (1,CUSCTL,1,READ,LOKCTL)
	ORGCUS = ORG001
	UNLOCK 5
	UNLOCK 6
	XCALL MMENU ('EXTENDED CUSTOMER MASTER FILE MAINTENANCE','CUSTOMER',SELECT,V)
	GOTO (DISPLA,DISPLA,DISPLA,PRINT,PRINT), SELECT
ENDOFF,
	CALL CLOSES
	IF (DEL135.GT.20) GOTO REORG
	IF (ORG135.EQ.REC135) GOTO ABORT
	NXTPRG = 		;;;defaults to 'AR:ARMENU'
	GOTO SORT
PRINT,
	CALL CLOSES
	IF (SELECT.EQ.4) XCALL PGCHN ('AR:CSEPRT',1)
	NXTPRG = 'AR:CSEPRT'
	GOTO SORT
SORT,
	SWITCH = 3
	XCALL FILES (2,'I',136,SWITCH)
	IF (SWITCH.EQ.9) GOTO ABORT
	RCNT = REC135
	OCNT = ORG135
	MSGCTL = 5
	XCALL SNMSG (SRTMSG,MSGCTL)
	MSGCTL = 2
	XCALL SNMSG (NXTPRG,MSGCTL)
	XCALL PGCHN ('AR:SRTCSE',1)
REORG,
	XCALL OUTPT (6,10,2,'The CSEMAS file needs to be reorganized.  This',1)
	XCALL OUTPT (8,10,0,'process can take a long time depending on file',1)
	XCALL OUTPT (10,10,0,'size and number of deleted records.  You might',1)
	XCALL OUTPT (12,10,0,'consider running this process over lunch or at',1)
	XCALL OUTPT (14,10,0,'night.  DO YOU WISH TO REORGANIZE NOW <N> ?',1)
	CTL = '14,55,01,00,YN'
	CALL INPUT
	IF (INXCTL.EQ.1) XCALL PGCHN ('AR:ORGCSE',1)
ABORT,
	XCALL PGCHN ('AR:ARMENU',1)
;
DISPLA,
	UNLOCK 5
	UNLOCK 6
	CSEMAS =
	CSEIDX =
	INXCTL =
	CNGCTL =
	XCALL OUTPT ( 4,20,2,'   CUSTOMER NUMBER ',1)
	XCALL OUTPT ( 6,20,0,'1. A/P CONTACT',1)
	XCALL OUTPT ( 7,20,0,'2.     PHONE',1)
	XCALL OUTPT ( 7,55,0,'3. FAX PHONE',1)
	XCALL OUTPT ( 9,20,0,'4. MKT CONTACT',1)
	XCALL OUTPT (10,20,0,'5.     ADDRESS',1)
	XCALL OUTPT (11,20,0,'6.     CITY, ST ZIP',1)
	XCALL OUTPT (12,20,0,'7.     PHONE',1)
	XCALL OUTPT (14,20,0,'8. DATE STARTED',1)
	XCALL OUTPT (15,20,0,'9. LAST SALE DT',1)
	XCALL OUTPT (16,19,0,'10. TRW COMMENT CODE',1)
ECUSNO,
	IF (CNGCTL.AND.SELECT.NE.1) GOTO ANYCNG
	CTL = '04,40,06,01,#E'
	CALL INPUT
	GOTO (DISPLA,RDHDR), INXCTL
	KCUSNO = ENTRY
	IF (KCUSNO.EQ.0) GOTO ECUSNO
	KEY = KCUSNO,'XXXXXX'
	XCALL SERCH (2,CUSIDX,KEY,1,6,ORGCUS,BSMID,SRCCTL,1,7,11,0,0,0,0)
	IF (SRCCTL.EQ.1) 
	BEGIN
	  XCALL MESAG ('INVALID CUSTOMER NUMBER',2)
	  GOTO ECUSNO
	END
	LOKCTL = 1
	XCALL IO (1,CUSMAS,IRC001,READ,LOKCTL)
	XCALL OUTPT (04,50,0,NAME,1)
	KEY = KCUSNO,'XXXXXX'
	XCALL SERCH (6,CSEIDX,KEY,1,6,ORG135,BSMID,SRCCTL,1,7,11,0,0,0,0)
	GOTO (DSPREC,DSPREC), SELECT - 1
	IF (SRCCTL.EQ.0) GOTO NODUPS
	ECUSNO = KCUSNO
	ENAME = NAME
	GOTO (ANYCNG), CNGCTL
EAPNA,
	CTL = '06,40,25,00,A '
	CALL INPUT
	GOTO (DISPLA), INXCTL
	EAPNA = ENTRY
	GOTO (ANYCNG), CNGCTL
EAPPHO,
	CTL = '07,40,10,00,# '
	CALL INPUT
	GOTO (DISPLA), INXCTL
	EAPPHO = ENTRY
	IF (EAPPHO(1,3).EQ.0.AND.ENTRY.NE.'   ') EAPPHO(1,3) = 612
	ALPHA = EAPPHO,'ZZZ-ZZZ-ZZZZ'
	XCALL OUTPT (ROW,COL,0,ALPHA,1)
	GOTO (ANYCNG), CNGCTL
EFAX,
	CTL = '07,68,10,00,# '
	CALL INPUT
	GOTO (DISPLA), INXCTL
	EFAX = ENTRY
	IF (EFAX(1,3).EQ.0.AND.ENTRY.NE.'   ') EFAX(1,3) = 612
	ALPHA = EFAX,'ZZZ-ZZZ-ZZZZ'
	XCALL OUTPT (ROW,COL,0,ALPHA,1)
	GOTO (ANYCNG), CNGCTL
ECONT1,
	CTL = '09,40,04,00,L '
	IF (CNGCTL)
	BEGIN
	  CALL ACCEPT
	  GOTO (DISPLA,ECONT2), ACCEPT
	END
	CALL INPUT
	GOTO (DISPLA), INXCTL
	ECONT1 = ENTRY
ECONT2,
	CTL = '09,47,10,00,L '
	IF (CNGCTL)
	BEGIN
	  CALL ACCEPT
	  GOTO (DISPLA,ECONT3), ACCEPT
	END
	CALL INPUT
	GOTO (DISPLA), INXCTL
	ECONT2 = ENTRY
ECONT3,
	CTL = '09,60,15,00,L '
	IF (CNGCTL)
	BEGIN
	  CALL ACCEPT
	  GOTO (DISPLA,ANYCNG), ACCEPT
	END
	CALL INPUT
	GOTO (DISPLA), INXCTL
	ECONT3 = ENTRY
	GOTO (ANYCNG), CNGCTL
ECOADD,
	CTL = '10,40,25,00,L '
	CALL INPUT
	GOTO (DISPLA), INXCTL
	ECOADD = ENTRY
	GOTO (ANYCNG), CNGCTL
ECOCIT,
	CTL = '11,40,15,00,L '
	IF (CNGCTL)
	BEGIN
	  CALL ACCEPT
	  GOTO (DISPLA,ECOST), ACCEPT
	END
	CALL INPUT
	GOTO (DISPLA), INXCTL
	ECOCIT = ENTRY
ECOST,
	CTL = '11,58,02,00,L '
	IF (CNGCTL)
	BEGIN
	  CALL ACCEPT
	  GOTO (DISPLA,ECOZIP), ACCEPT
	END
	CALL INPUT
	GOTO (DISPLA), INXCTL
	ECOST = ENTRY
ECOZIP,
	CTL = '11,63,10,00,A '
	CALL INPUT
	GOTO (DISPLA), INXCTL
	ECOZIP = ENTRY
	GOTO (ANYCNG), CNGCTL
ECOPHO,
	CTL = '12,40,10,00,# '
	CALL INPUT
	GOTO (DISPLA), INXCTL
	ECOPHO = ENTRY
	IF (ECOPHO(1,3).EQ.0.AND.ENTRY.NE.'   ') ECOPHO(1,3) = 612
	ALPHA = ECOPHO,'ZZZ-ZZZ-ZZZZ'
	XCALL OUTPT (ROW,COL,0,ALPHA,1)
	GOTO (ANYCNG), CNGCTL
ESTART,
	CTL = '14,40,06,00,D '
	CALL INPUT
	GOTO (DISPLA), INXCTL
	ESTART = ENTRY
	IF (ESTART.EQ.0)
	BEGIN
	  ESTART = TODAY
	  DECMAL(1,6) = ESTART
	  CALL DSPDTE
	END
	GOTO (ANYCNG), CNGCTL
ELAST,
	CTL = '15,40,06,00,D '
	CALL INPUT
	GOTO (DISPLA), INXCTL
	ELAST = ENTRY
	IF (ELAST.EQ.0)
	BEGIN
	  DECMAL(1,6) = ELAST
	  CALL DSPDTE
	END
	GOTO (ANYCNG), CNGCTL
ECOMNT,
	XCALL OUTPT (16,40,1,'\',1)
	CTL = '16,40,02,00,# '
	CALL INPUT
	GOTO (DISPLA), INXCTL
	ECOMNT = ENTRY
	IF (ECOMNT.LT.0.OR.ECOMNT.GT.89) GOTO ECOMNT
	READ (3,TRWCOM,ECOMNT+1)
	XCALL OUTPT (16,40,0,TCOM,1)
	GOTO ANYCNG
DSPREC,
	IF (SRCCTL) GOTO NOFIND
	LOKCTL = 1
	XCALL IO (5,CSEMAS,IRC135,READ,LOKCTL)
	CTL = '06,40,25,00,A '
	XCALL OUTPT (ROW,COL,0,EAPNA,1)
	CTL = '07,40,10,00,# '
	ALPHA = EAPPHO,'ZZZ-ZZZ-ZZZZ'
	XCALL OUTPT (ROW,COL,0,ALPHA,1)
	CTL = '07,68,10,00,# '
	ALPHA = EFAX,'ZZZ-ZZZ-ZZZZ'
	XCALL OUTPT (ROW,COL,0,ALPHA,1)
	CTL = '09,40,04,00,L '
	XCALL OUTPT (ROW,COL,0,ECONT1,1)
	CTL = '09,47,10,00,L '
	XCALL OUTPT (ROW,COL,0,ECONT2,1)
	CTL = '09,60,15,00,L '
	XCALL OUTPT (ROW,COL,0,ECONT3,1)
	CTL = '10,40,25,00,A '
	XCALL OUTPT (ROW,COL,0,ECOADD,1)
	CTL = '11,40,15,00,A '
	XCALL OUTPT (ROW,COL,0,ECOCIT,1)
	CTL = '11,58,02,00,A '
	XCALL OUTPT (ROW,COL,0,ECOST,1)
	CTL = '11,63,10,00,A '
	XCALL OUTPT (ROW,COL,0,ECOZIP,1)
	CTL = '12,40,10,00,# '
	ALPHA = ECOPHO,'ZZZ-ZZZ-ZZZZ'
	XCALL OUTPT (ROW,COL,0,ALPHA,1)
	CTL = '14,40,06,00,D '
	DECMAL(1,6) = ESTART
	CALL DSPDTE
	CTL = '15,40,06,00,D '
	DECMAL(1,6) = ELAST
	CALL DSPDTE
	IF (ECOMNT.EQ.0) ECOMNT =
	READ (3,TRWCOM,ECOMNT+1)
	XCALL OUTPT (16,40,0,TCOM,1)
	IF (SELECT.EQ.3) GOTO DELETE
ANYCNG,
	XCALL ANYCN (CNGCTL,WHATNO)
	IF (CNGCTL)
	BEGIN 
	  GOTO (EAPNA,EAPPHO,EFAX,ECONT1,ECOADD,ECOCIT,ECOPHO,ESTART,ELAST,
&		ECOMNT), WHATNO
	  CNGCTL = 3
	  GOTO ANYCNG
	END
	IF (SELECT.EQ.2) GOTO CHANGE
ADD,
	LOKCTL = 1
	XCALL IO (5,CSECTL,1,READ,LOKCTL)
	INCR REC135
	IF (REC135.GE.MAX135) GOTO FULL
	LOKCTL = 1
	XCALL IO (5,CSECTL,1,WRITE,LOKCTL)
	LOKCTL = 1
	XCALL IO (5,CSEMAS,REC135,WRITE,LOKCTL)
	IECUSN = ECUSNO
	IRC135 = REC135
	LOKCTL = 1
	XCALL IO (6,CSEIDX,REC135,WRITE,LOKCTL)
	GOTO FREBUF
CHANGE,
	LOKCTL = 1
	XCALL IO (5,CSEMAS,IRC135,WRITE,LOKCTL)
	GOTO FREBUF
DELETE,
	XCALL OUTPT (24,1,0,'DELETE REQUIRES PASSWORD: ',1)
	CTL = '24,30,06,01,# '
	CALL INPUT
	DECMAL = ENTRY
	IF (DECMAL.NE.TODAY)
	BEGIN
	  XCALL MESAG ('INVALID PASSWORD - SWITCHING TO CHANGE MODE',1)
	  SELECT = 2
	  XCALL OUTPT (2,1,0,'CHANGE/INQUIRE',1)
	  GOTO ANYCNG
	END
	XCALL OUTPT (24,1,1,'OK TO DELETE <N> ?',1)
	CTL = '24,20,01,00,YN'
	CALL INPUT
	IF (INXCTL.NE.1) GOTO DISPLA
	EAPNA = ']]]DEL'
	LOKCTL = 1
	XCALL IO (5,CSEMAS,IRC135,WRITE,LOKCTL)
	IRC135 =
	LOKCTL = 1
	XCALL IO (6,CSEIDX,BSMID,WRITE,LOKCTL)
	LOKCTL = 1
	XCALL IO (5,CSECTL,1,READ,LOKCTL)
	INCR DEL135
	LOKCTL = 1
	XCALL IO (5,CSECTL,1,WRITE,LOKCTL)
	XCALL MESAG ('E-CUSTOMER NUMBER DELETED',2)
FREBUF,
	LOKCTL = 1
	XCALL IO (6,CSEIDX,MAX135,READ,LOKCTL)
	LOKCTL = 1
	XCALL IO (5,CSEMAS,MAX135,READ,LOKCTL)
	LOKCTL = 1
	XCALL IO (6,CSEIDX,1,READ,LOKCTL)
	LOKCTL = 1
	XCALL IO (5,CSECTL,1,READ,LOKCTL)
	GOTO DISPLA
FULL,
	XCALL MESAG ('THE RINVMS FILE IS NOW FULL AND NEEDS TO BE EXPANDED',2)
	CALL CLOSES
	GOTO ABORT
NOFIND,
	XCALL MESAG ('E-CUSTOMER NUMBER NOT FOUND',1)
	GO TO DISPLA
NODUPS,
	XCALL MESAG ('E-CUSTOMER NUMBER ALREADY ON FILE',1)
	GO TO DISPLA
INPUT,
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,1)
	RETURN
ACCEPT,
	XCALL FLAGS (00010000)			;DISABLE CHARACTER ECHOING
	XCALL OUTPT (ROW,COL,0,'\',1)
	ACCEPT (15,TCHAR)
	IF (SYSTEM.NE.1.AND.TCHAR.EQ.13) ACCEPT (15,DCHAR)
	XCALL FLAGS (00000000)			;RE-ENABLE CHARACTER ECHOING
	ACCEPT =
	IF (TCHAR.EQ.23) ACCEPT = 1	;CTRL/W
	IF (TCHAR.EQ.13) ACCEPT = 2	;RETURN
	IF (TCHAR.EQ.9) ACCEPT = 3	;TAB
	IF (ACCEPT.EQ.0) GOTO ACCEPT
	RETURN
DSPDTE,
	OPTION = 2
	GOTO CALDSP
DSPNUM,
	OPTION = 1
CALDSP,
	XCALL DSPLY (MAX,ROW,COL,DECMAL,OPTION,1)
	RETURN
END
