;  SACUST / AR 
;
; 	5-21-18: ssq convert cusmas to isam
;

RECORD CUSMAS		; 
		.INCLUDE 'DEF:RD001A.DEF'
RECORD SAINDX		; 
		.INCLUDE 'DEF:RD009A.DEF'
RECORD TITLE
		,A26,	'SALES ANALYSIS BY CUSTOMER'
RECORD HDR1
		,A32,	'------------CUSTOMER------------'
		,A2
		,A2,	'SL'
		,A2
		,A16,	'----------------'
		,A13,	'MONTH-TO-DATE'
		,A16,	'----------------'
		,A3
		,A16,	'----------------'
		,A12,	'YEAR-TO-DATE'
		,A17,	'-----------------'
RECORD HDR2
		,A2
		,A2,	'NO'
		,A3
		,A4,	'NAME'
		,A23
		,A2,	'MN'
		,A2
		,A10,	'COST-SALES'
		,A5
		,A5,	'SALES'
		,A2
		,A5,	'%-SLS'
		,A5
		,A6,	'PROFIT'
		,A2
		,A5,	'%-PFT'
		,A3
		,A10,	'COST-SALES'
		,A5
		,A5,	'SALES'
		,A2
		,A5,	'%-SLS'
		,A5
		,A6,	'PROFIT'
		,A2
		,A5,	'%-PFT'
RECORD
	PLINE	,A132
	LINCNT	,D2,	60
	LPARG	,D1
	LPSW	,D1
	PGCNT	,D3,	000
	PRTTYP	,A1
	PRNTSW	,D1
	PRTCTL	,D3
	RPTNUM	,D3
	SPLFIL	,A14
	CUSCNT	,D5
	MASK	,A14,	'ZZ,ZZZ,ZZZ.XX-'
	MASK1A	,A13,	'ZZZZZ,ZZZ.XX-'
	MASK2	,A6,	'ZZZ.X-'
	MTDPCT	,D4
	PPCMTD	,D4
	PPCYTD,	D4
	TCSMTD	,D10
	TCSYTD	,D10
	TOTMTD	,D10
	TOTYTD	,D10
	TPCMTD	,D4
	TPCYTD	,D4
	TPPMTD	,D4
	TPPYTD	,D4
	YTDPCT	,D4
	SWITCH	,D1
	V	,D1
	PRTCTR	,D1
	READ	,D1,	0
	WRITE	,D1,	1
	LOKCTL	,D1
PROC
	XCALL TERID (V)
	XCALL OUTPT (2,1,1,'\',1)
	SWITCH = 1
	XCALL FILES (1,'SI',01,SWITCH)		;FILE # 01 -- CUSMAS FILE
	IF (SWITCH.EQ.9) GO TO INU001
	XCALL FILES (3,'I',09,5)		;FILE # 09 -- SAINDX FILE
	LOKCTL = 1
	XCALL IOS (1,CUSMAS,READ,LOKCTL)	;HEADER
	IF (LOKCTL.EQ.2) GO TO EOFIDX
CALBAS,
	LOKCTL = 1
	XCALL IOS (1,CUSMAS,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO RDIDX
	IF (NAME.EQ.']]]DEL') GO TO CALBAS
	IF (STATE.EQ.']]') GO TO RDIDX
	TOTMTD = TOTMTD + SALMTD
	TOTYTD = TOTYTD + SALYTD
	TCSMTD = TCSMTD + COSMTD
	TCSYTD = TCSYTD + COSYTD
	GO TO CALBAS
RDIDX,
	LOKCTL = 1
	XCALL IOS (3,SAINDX,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOFIDX
	LOKCTL = 1
	XCALL ISIO (1,CUSMAS,SACUNO,READ,LOKCTL)
	PLINE (1,6) = CUSNO
	PLINE (8,32) = NAME
	PLINE (35,36) = SLSMAN
	PLINE (38,48) = COSMTD, MASK
	PLINE (50,60) = SALMTD, MASK
	MTDPCT =
	ON ERROR NOMTD
	MTDPCT = (SALMTD*10000/TOTMTD) #1
NOMTD,
	OFF ERROR
	PLINE (62,66) = MTDPCT, MASK2
	PLINE (68,78) = (SALMTD-COSMTD), MASK
	PPCMTD =
	ON ERROR ZERO
	PPCMTD = ((SALMTD-COSMTD)*10000/(TOTMTD-TCSMTD)) #1
ZERO,
	PLINE (80,84) = PPCMTD, MASK2
	PLINE (86,96) = COSYTD, MASK
	PLINE (97,108) = SALYTD, MASK1A
	YTDPCT =
	ON ERROR NOYTD
	YTDPCT = (SALYTD*10000/TOTYTD) #1
NOYTD,
	OFF ERROR
	PLINE (110,114) = YTDPCT, MASK2
	PLINE (115,126) = (SALYTD-COSYTD), MASK1A
	PPCYTD =
	ON ERROR ZEROA
	PPCYTD = ((SALYTD-COSYTD)*10000/(TOTYTD-TCSYTD)) #1
ZEROA,
	PLINE (128,132) = PPCYTD, MASK2
	CALL PRINT
	TPCMTD = TPCMTD + MTDPCT
	TPCYTD = TPCYTD + YTDPCT
	TPPMTD = TPPMTD + PPCMTD
	TPPYTD = TPPYTD + PPCYTD
	INCR CUSCNT
	XCALL LINFD (1)
	INCR LINCNT
	GO TO RDIDX
EOFIDX,
	XCALL LINFD (1)
	PLINE (1,6) = CUSCNT, 'ZZ,ZZX'
	PLINE (8,16) = 'CUSTOMERS'
	PLINE (26,32) = 'TOTALS:'
	PLINE (35,48) = TCSMTD, MASK
	PLINE (61,66) = TPCMTD, MASK2
	PLINE (79,84) = TPPMTD, MASK2
	PLINE (95,108) = TOTYTD, MASK
	PLINE (113,126) = (TOTYTD-TCSYTD), MASK
	CALL PRINT
	PLINE =
	PLINE (47,60) = TOTMTD, MASK
	PLINE (65,78) = (TOTMTD-TCSMTD), MASK
	PLINE (83,96) = TCSYTD, MASK
	PLINE (109,114) = TPCYTD, MASK2
	PLINE (127,132) = TPPYTD, MASK2
	CALL PRINT
	CALL LPOFF
ENDOFF,
	CLOSE 3
	XCALL FILES (1,'I',01,4)
INU001,
	XCALL PGCHN ('AR:SASEL',1)
PRINT,
	IF (PRNTSW.EQ.0) CALL LPON
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,'NO HDR',
&		'NO LEGEND',' ',' ',0,132,132,0,LPSW,RPTNUM,PRTTYP)
	RETURN
LPON,
	LPSW = 1
	SPLFIL (5,6) = 'AO'
	XCALL AR_LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTSW = 1
	RETURN
LPOFF,
	XCALL AR_LPOFF (LPSW,SPLFIL,PGCNT)
	PRNTSW =
	RETURN
END
