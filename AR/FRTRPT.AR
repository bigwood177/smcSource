;FRTRPT.AR
;
;	PRINT FREIGHT CLAIMS REPORT
; 5-16-18: ssq convert cusmas to isam
;
RECORD	FRTCLM
	.INCLUDE 'DEF:RD023A.DEF'

RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'

RECORD	ORDHDR
	.INCLUDE 'DEF:RD044A.DEF'

RECORD	CUSMAS
	.INCLUDE 'DEF:RD001A.DEF'
RECORD,X
	.INCLUDE 'DEF:RD001B.DEF'

;;;RECORD	CUSIDX
;;;	.INCLUDE 'DEF:RD002A.DEF'

RECORD	CHANNEL
	CHN001	,D2
	CHN002	,D2
	CHN023	,D2
	CHN182	,D2
	CHN184	,D2

RECORD	PRINT
	TITLE	,A*,	'FREIGHT CLAIMS'
	HD	,A6,	'NO HDR'
	LG	,A9,	'NO LEGEND'
	PLINE	,A80
	PRNTON	,D1
	LINCNT	,D2,60
	PGCNT	,D6
	LPSW	,D2
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	PRTCTL	,D3,080
	LPARG	,D1
	PRNTSW	,D1
	PRTCTR	,D1


RECORD	HD1
	,A*,	'FC-ORD CARR CUST # NAME                   '
	,A*,	'INV-DAT INV-AMOUNT ORIG #  DATE-PD'

RECORD	KEY2
	K2_SCAC		,A4
;;;	K2_ORDER	,D6

RECORD	VARS
	STDAT	,D8
	ENDAT	,D8
	STSCAC	,A4
	ENSCAC	,A4
	STCUS	,D6
	ENCUS	,D6
	REDFIL	,A14
	ORDFIL	,A14
	ORDFND	,D1
	CUSFND	,D1
	SCFND	,D1
	ALPHA	,A12
	OPNOK	,D1
	BLANKS	,A15
	ORDER	,D6
	XDATE	,D6
	XDAT8	,D8
	XCUST	,D6
	TODAY	,D6
	YEAR	,D2
	ROW	,D2
	KEY	,A6
	BSEND	,D5
	BSMID	,D5
	SRCCTL	,D1
	ENTRY	,A30
	INXCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	STORE	,D1,2
	DELETE	,D1,3
	LOKCTL	,D1
	WHATNO	,D2
	SELECT	,D1
	CNGCTL	,D1
	I	,D3
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'FREIGHT CLAIMS REPORT',1)
	CALL OPENS
	IF (.NOT. OPNOK) GOTO CLOSE

DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,'FREIGHT CLAIMS REPORT',1)
	XCALL OUTPT ( 4,4,0,'1. START FC INVOICE DATE',1)
	XCALL OUTPT ( 5,4,0,'2. END   FC INVOICE DATE',1)
	XCALL OUTPT ( 7,4,0,'3. START SCAC CODE',1)
	XCALL OUTPT ( 8,4,0,'4. END   SCAC CODE',1)
	XCALL OUTPT (10,4,0,'5. START CUST NO',1)
	XCALL OUTPT (11,4,0,'6. END   CUST NO',1)

STDAT,
	XCALL INPUT(4,30,08,00,'DE',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	STDAT = ENTRY(1,8)
	IF (STDAT .EQ. 0)
		BEGIN
		ENDAT = 99999999
		XCALL OUTPT (4,30,1,'ALL',1)
		XCALL OUTPT (5,30,1,' ',1)
		GOTO(ANYCNG),CNGCTL
		GOTO STSCAC
		END
	GOTO (ANYCNG),CNGCTL
ENDAT,
	XCALL INPUT (5,30,08,00,'DE',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	ENDAT = ENTRY(1,8)
	IF (ENDAT .EQ. 0)
		BEGIN
		ENDAT = STDAT
		XDAT8(1,4) = ENDAT(5,8)
		XDAT8(5,8) = ENDAT(1,4)
		ENTRY(1,10) = XDAT8,	'ZX/XX/XXXX'[LEFT]
		XCALL OUTPT (5,30,1,ENTRY(1,10),1)
		END
	GOTO (ANYCNG),CNGCTL
STSCAC,
	XCALL INPUT (7,24,04,00,'AE',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	STSCAC = ENTRY(1,4)
	IF (STSCAC .EQ. BLANKS)
		BEGIN
		ENSCAC = 'ZZZZ'
		XCALL OUTPT (7,24,1,'ALL',1)
		XCALL OUTPT (8,24,1,' ',1)
		GOTO (ANYCNG),CNGCTL
		GOTO STCUS
		END
	CALL GET_SCAC
	IF (.NOT. SCFND) GOTO STSCAC
	XCALL OUTPT (7,29,1,SC_NAME,1)
	GOTO (ANYCNG),CNGCTL
ENSCAC,
	XCALL INPUT (8,24,04,00,'AE',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	ENSCAC = ENTRY(1,4)
	IF (ENSCAC .EQ. BLANKS)
		BEGIN
		ENSCAC = STSCAC
		XCALL OUTPT (8,24,1,ENSCAC,1)
		END
	CALL GET_SCAC
	IF (.NOT. SCFND) GOTO STSCAC
	XCALL OUTPT (8,29,1,SC_NAME,1)
	GOTO (ANYCNG),CNGCTL
STCUS,
	XCALL INPUT (10,24,06,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	STCUS = ENTRY(1,6)
	IF (STCUS .EQ. 0)
		BEGIN
		ENCUS = 999999
		XCALL OUTPT (10,24,1,'ALL',1)
		XCALL OUTPT (11,24,1,' ',1)
		GOTO ANYCNG
		END
	CALL GET_CUST
	IF (.NOT. CUSFND) GOTO STCUS
	XCALL OUTPT (10,31,1,NAME,1)
	GOTO (ANYCNG),CNGCTL
ENCUS,
	XCALL INPUT(11,24,06,00,'A ',ENTRY,INXCTL,1)
	GOTO (DISPLA),INXCTL
	ENCUS = ENTRY(1,6)
	IF (ENCUS .EQ. 0)
		BEGIN
		ENCUS = STCUS
		ENTRY(1,6) = ENCUS,	'ZZZZZX' [LEFT]
		XCALL OUTPT (11,24,1,ENTRY(1,6),1)
		GOTO ANYCNG
		END	
	CALL GET_CUST
	IF (.NOT. CUSFND) GOTO ENCUS

	XCALL OUTPT (11,31,1,NAME,1)

ANYCNG,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (PROCES,CNGBR),CNGCTL+1
CNGBR,
	GOTO (STDAT,ENDAT,STSCAC,ENSCAC,STCUS,ENCUS),WHATNO
	GOTO ANYCNG

PROCES,

	CALL P_PRINT

ENDOFF,
	CALL CLOSE
	XCALL PGCHN ('AR:FCMMNU',1)
	STOP

GET_ORD,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		;;; LOOK UP IN CURRENT & PREV YEARS HISTORY
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	ORDFND = 1		;ASSUME FOUND
	ORDFIL = REDFIL	
	ORDFIL(14,14) = 'M'
	OPEN (CHN184, SI, ORDFIL)		;SLHHDR
	XCALL ISIO (CHN184, ORDHDR, FC_ORIG, READ, LOKCTL)
	IF (LOKCTL .EQ. 0) 
		BEGIN
		CLOSE CHN184
		CALL SET_CUST
		RETURN
		END

	CLOSE CHN184
	ORDFIL(9,10) = YEAR,	'XX'
	OPEN(CHN184,SI,ORDFIL)
	XCALL ISIO (CHN184, ORDHDR, FC_ORIG, READ, LOKCTL)
	IF (LOKCTL .EQ. 0) 
		BEGIN
		CLOSE CHN184
		CALL SET_CUST
		RETURN
		END


	ORDFND = 0		;NOT FOUND
	CLOSE CHN184
	RETURN

SET_CUST,
	FC_CUST = OCUSNO
	FC_NAME = OCUSNM
	RETURN
;-------------------------------------------
;---------------------------------------------------

GET_SCAC,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		;;; COP TABLE SCAC LOOK-UP
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR TBL_KEY
	TBLCOD = 'SC'
	TBLKEY = ENTRY(1,4)
	XCALL ISIO (CHN182,COPTBL,TBL_KEY,READ,LOKCTL)
	IF (LOKCTL .EQ. 0) 
	THEN	SCFND = 1
	ELSE	BEGIN
		SCFND = 0		
		XCALL MESAG('TRUCKLINE NOT FOUND',1)
		END
	RETURN
;-----------------------------------------------------

GET_CUST,
	XCALL ISIO (CHN001, CUSMAS, XCUST, READ, LOKCTL)
	IF (LOKCTL .NE. 0)
	THEN	CUSFND = 0
	ELSE	CUSFND = 1
	
;;;	XCUST = ENTRY(1,6)
;;;	KEY = XCUST,'XXXXXX'
;;;	SRCCTL = 3
;;;	XCALL SERCH (CHN002,CUSIDX,KEY,1,6,BSEND,BSMID,SRCCTL,1,7,11,0,0,0,0)
;;;	IF(SRCCTL.NE.0 .OR. IRC001.LE.0)
;;;	THEN	CUSFND = 0			;NOT FOUND
;;;	ELSE	BEGIN
;;;		CUSFND = 1			;FOUND
;;;		XCALL IO(CHN001,CUSMAS,IRC001,READ,LOKCTL)
;;;		END

	RETURN
;-----------------------------------------------------

;===================================================================
P_PRINT,
;===================================================================
	
	LINCNT = 66
	PGCNT  = 0
	CLEAR KEY2
	K2_SCAC = STSCAC
	FIND (CHN023,FRTCLM,KEY2,KRF=1)[ERR=PLOOP]
;;;	read (CHN023,FRTCLM,KEY2,KEYNUM:1)[ERR=PLOOP]

PLOOP,
	XCALL IOS (CHN023,FRTCLM,READ,LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF
	IF (FC_SCAC .LT. STSCAC) GOTO PLOOP
	IF (FC_SCAC .GT. ENSCAC) GOTO EOF

	IF (FC_OINVDT.LT.STDAT .OR. FC_OINVDT.GT.ENDAT) GOTO PLOOP
	IF (FC_CUST.LT.STCUS .OR. FC_CUST.GT.ENCUS) GOTO PLOOP

	PLINE(1,6) = FC_OORDNO,	'ZZZZZX'
	PLINE(8,11) = FC_SCAC
	PLINE(13,18) = FC_CUST,	'ZZZZZX'
	IF (FC_SHTO .GT. 0)PLINE(19,19) = '*'
	PLINE(20,40) = FC_NAME
	XDATE(1,4) = FC_OINVDT(5,8)
	XDATE(5,6) = FC_OINVDT(3,4)
	PLINE(42,49) = XDATE,	'XX/XX/XX'
	PLINE(51,60) = FC_OSALE,'ZZZ,ZZX.XX'
	PLINE(62,67) = FC_ORIG,	'ZZZZZX'
	XDAT8(1,4) = FC_DATPD(5,8)
	XDAT8(5,8) = FC_DATPD(3,4)
	PLINE(69,76) = XDAT8	,'XX/XX/XX'
	IF (FC_FLAG .EQ. 'S') PLINE(78,79) = 'CR'

	CALL PRINT
	GOTO PLOOP

EOF,
	IF (PRNTON.EQ.1)  XCALL AR_LPOFF(LPSW,SPLFIL,PGCNT)
	PRNTON  = 0

PDONE,
	RETURN
;------------------------------------------------------------

PRINT,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (PRNTON .EQ. 0) CALL PRNTON
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HD1,HD,HD
&		,LG,LG,LG,0,080,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;-------------------------------------------------------------
PRNTON,
	SPLFIL (5,6) = 'EF'
	LPSW = 1		;PRINT,SPOOL, OR DISPLAY
	XCALL AR_LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTON = 1
	RETURN
;-------------------------------------------------------------
;===================================================================


OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

	SWITCH = 5
	XCALL FILES (1,'SU',023,SWITCH)		;FRTCLM
	IF (SWITCH .EQ. 9) RETURN
	CHN023 = 1

	SWITCH = 5
	XCALL FILES (2,'SI',182,SWITCH)		;COPTBL
	IF (SWITCH .EQ. 9) RETURN
	CHN182 = 2

	XCALL FFILE (184,REDFIL,SWITCH)		;SLHHDR
	CHN184 = 4

	SWITCH = 5
	XCALL FILES (5,'SI',001,SWITCH)
	IF (SWITCH .EQ. 9) RETURN
	CHN001 = 5

;;;	SWITCH = 5
;;;	XCALL FILES (6,'I',002,SWITCH)
;;;	IF (SWITCH .EQ. 9) RETURN
;;;	CHN002 = 6

;;;	LOKCTL = 1
;;;	XCALL IO (CHN001,CUSMAS,1,READ,LOKCTL)
;;;	BSEND = ORG001

	OPNOK = 1
	RETURN
;----------------------------------------------------

CLOSE,
	CLOSE CHN023
	CLOSE CHN182
	IF (CHN001) CLOSE CHN001
;;;	IF (CHN002) CLOSE CHN002
	RETURN
;----------------------------------------------------
	
	
;FC-ORD CARR CUST # NAME                   INV-DAT INV-AMOUNT ORIG #  DATE-PD
;ZZZZZX AAAA ZZZZZX AAAAAAAAAAAAAAAAAAAAA XX/XX/XX ZZZ,ZZX.XX ZZZZZX XX/XX/XX AA
;12345678901234567890123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5         6         7         8


