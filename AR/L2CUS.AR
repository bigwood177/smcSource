SUBROUTINE L2CUS

;
RECORD	ICUST
	I_CUST	,D6
	I_NUM	,D2
RECORD,X
	I_KEY	,D8

;
RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'

RECORD	ARRAY
	A_CUST	,500D6
	MAXARA	,D3,	500
	NUMARA	,D6

RECORD	CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2

RECORD	VARS
	SELECT	,D1
	CHN182	,D2,	17
	CHNWRK	,D2,	22
	CUR_NOTE	,D2
	CLR_NOTE	,D2		;NOTE TO CLEAR FROM FILE
	GOT_NOTES	,D1
	NOTE2	,5A40
	NN	,D6
	NUMASK	,A6,	'ZZZZZZ'
	I	,D6
	J	,D6
	DCHAR	,D3
	ACHAR	,A1
	DEC3	,D3
	TTWAIT	,D1
	ENTRY	,A30
	INXCTL	,D1
	WHATNO	,D2
	CNGCTL	,D1
	LOKCTL	,D1
	READ	,D1,0
	WRITE	,D1,1
	STORE	,D1,2

PROC
	SELECT = 1		;ASSUME ADD MODE

	ONERROR NO_FILE
	OPEN (CHNWRK, SU, 'SMC:LSRST2.ISM')
	OFFERROR
	GOT_NOTES = 1
	GOTO GET_NOTES

NO_FILE,
	OFFERROR
	GOT_NOTES = 0
	XCALL ISAMC ('SMC:LSRST2.ISM', 8, 1, 'START=1, LENGTH=8, DUPS, ASCEND')
	OPEN (CHNWRK, SU, 'SMC:LSRST2.ISM')

GET_NOTES,
	XCALL OUTPT (2,1,2,'CUSTOMER NOTES',1)
	XCALL OUTPT (4,4,1, 'NOTE #',1)
	XCALL INPUT (4, 13, 02, 00, '#E', ENTRY, INXCTL, 1)
	GOTO (GET_NOTES, ENDOFF),INXCTL
	CUR_NOTE = ENTRY(1,2)

	CLEAR COPTBL
	TBLCOD = 'N2'
	N2_NBR = CUR_NOTE
	read (chn182, coptbl, tbl_key) [err=chk_not]
chk_not,
	if (n2_nbr.ne.cur_note)
		begin
		XCALL MESAG ('NOTE NOT ON FILE',1)
		GOTO GET_NOTES
		end

	FOR I FROM 1 THRU 5 CLEAR NOTE2(I)

	NOTE2(1) = N2_NOTE
NO_TBL2,
	FOR I FROM 2 THRU 5
		BEGIN
		XCALL IOS (CHN182, COPTBL, READ, LOKCTL)
		IF (TBLCOD .NE. 'N2') GOTO DONE_N2
		IF (N2_NBR .NE. CUR_NOTE) GOTO DONE_N2
		NOTE2(I) = N2_NOTE
		END
DONE_N2,
	FOR I FROM 1 THRU 5
		BEGIN
		XCALL OUTPT (I+4, 13, 0, NOTE2(I), 1)
		END

OLD_NOTES,
	IF (.NOT. GOT_NOTES) GOTO DISPLA

	XCALL OUTPT (12,4,1,'USE CUSTOMERS FROM LAST RUN?',1)
	XCALL INPUT (12,35,01,00, 'AE', ENTRY,INXCTL,1)
	GOTO (OLD_NOTES, ENDOFF),INXCTL
	
	USING ENTRY(1,1) SELECT
	('Y'),	BEGIN
		SELECT = 2	;CHANGE
		CALL LOAD	;LOAD ARRAY FOR CURRENT NOTE
		END

	('N'),	BEGIN
		SELECT = 1	;ADD MODE
		CLR_NOTE = CUR_NOTE
		CALL CLEAR_LSRST2
		END
	(),	GOTO OLD_NOTES
	ENDUSING

DISPLA,
	XCALL OUTPT (10,1,2,'INPUT CUSTOMER NUMBERS',1)
;--AA
CUSLIS,
	IF (SELECT.EQ.2) GOTO DSPLLS
	CLEAR ARRAY
	MAXARA = 500

	CTL = '11,00,06,01,#E'
CSLSLP,
	COL = COL + 10
	IF (COL.GT.70)
	BEGIN
	  INCR ROW
	  IF (ROW.GT.22) 
	  BEGIN
	    XCALL MESAG ('CANNOT SELECT MORE THAN 105 CUSTOMERS',2)
	    GOTO ANYCAR
	  END
	  COL = 10
	END
	CALL INPUT
	GOTO (CUSLIS,ANYCAR), INXCTL
	I = (7 * (ROW - 11) ) + (COL/10)
	A_CUST(I) = ENTRY
	NUMARA = I
	GOTO CSLSLP	

DSPLLS,
	CTL = '11,00'
	FOR J FROM 1 THRU NUMARA
	BEGIN
	  COL = COL + 10
	  IF (COL.GT.70) 
	  BEGIN
	    INCR ROW
	    COL = 10
	  END
	  ENTRY(1,6) = A_CUST(J),NUMASK
	  XCALL OUTPT (ROW,COL,0,ENTRY(1,6),1)
	END

	GOTO ANYCAR
CNGARY,
	CTL = '11,00,06,01,# '
CARLP,
	COL = COL + 10
	IF (COL.GT.70) 
	BEGIN
	  INCR ROW
	  COL = 10
	END
ASK1,
	J = (7 * (ROW - 11) ) + (COL/10)
	XCALL OUTPT (ROW,COL-1,0,' ',1)
	XCALL OUTPT (ROW,COL-1,0,'\',1)
	ACCEPT (15,DCHAR)
	IF (DCHAR.EQ.13) ACCEPT (15,ACHAR)
	IF (DCHAR.EQ.13) GOTO CARLP
	IF (DCHAR.EQ.8.OR.DCHAR.EQ.24) GOTO DSPLLS
	IF (DCHAR.EQ.9) GOTO CNG1
;-
	IF (DCHAR.EQ.0) 
		BEGIN	
		CLEAR DEC3
		XCALL FLAGS (10000,1)
		XCALL TTSTS (TTWAIT)
		IF (TTWAIT)	ACCEPT (15,DEC3)
		XCALL FLAGS (10000,0)
		IF (DEC3.EQ.133) GOTO DSPLLS		;<F11>
		IF (DEC3.EQ.79)  GOTO DSPLLS		;<END>	
		END
;-
	GOTO ASK1
CNG1,
	CALL INPUT
	GOTO (DSPLLS), INXCTL
	A_CUST(J) = ENTRY
	IF (J .GT. NUMARA) NUMARA = J

	GOTO CARLP
ANYCAR,
	XCALL OUTPT (24,1,1,'CHANGE ARRAY <N> ?',1)
	XCALL INPUT (24,20,01,00,'YN',ENTRY,INXCTL,1)
	IF (INXCTL.EQ.1) GOTO CNGARY
;--AA
PROCES,
	CALL UNLOAD		;WRITE ARRAY TO DISK
	GOTO GET_NOTES

;===============================================================	

LOAD,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; LOAD ARRAY FROM DISK FOR CURRENT NOTE
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	I = 0

	FIND (CHNWRK, ICUST, ^FIRST) [ERR=L_LOOP]
L_LOOP,
	READS (CHNWRK, ICUST, L_EOF)
	IF (I_NUM .EQ. CUR_NOTE) 
		BEGIN
		INCR I
		A_CUST(I) = I_CUST
		END

	GOTO L_LOOP	
L_EOF,
	NUMARA = I
	
	RETURN
;--------------------------------------------------------------
UNLOAD,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;; WRITE CURRENT ARRAY TO DISK CURRENT NOTE
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CLR_NOTE = CUR_NOTE
	CALL CLEAR_LSRST2

	FOR I FROM 1 THRU NUMARA
		BEGIN
		I_CUST = A_CUST(I)
		I_NUM = CUR_NOTE
		STORE (CHNWRK, ICUST, I_KEY)
		END

	GOT_NOTES = 1

	RETURN
;--------------------------------------------------------------
CLEAR_LSRST2,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		;;; CLEAR SMC:LSRST2.ISM FILE
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	FIND (CHNWRK, ICUST, ^FIRST) [ERR=CL2_LOOP]
CL2_LOOP,
	READS (CHNWRK, ICUST, CL2_EOF)

	USING CLR_NOTE SELECT
	(0),		DELETE (CHNWRK)
	(.EQ. I_NUM),	DELETE (CHNWRK)
	ENDUSING

	GOTO CL2_LOOP
CL2_EOF,
	RETURN
;-----------------------------------------------------------

INPUT,
	XCALL INPUT (ROW, COL, MAX, MIN, TYPE, ENTRY, INXCTL, 1)
	RETURN
	
ENDOFF,
	CLOSE CHNWRK

	XRETURN

END

