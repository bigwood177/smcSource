;NEWTER.AR
;
;	NEW VERSION OF SALES ANALYSIS BY TERITORRY
; 	5-21-18: ssq convert cusmas to isam
;

RECORD CUSMAS		; 
		.INCLUDE 'DEF:RD001A.DEF'
RECORD ,X		; 
		.INCLUDE 'DEF:RD001B.DEF'

RECORD	SALMAN
		.INCLUDE 'DEF:RD054A.DEF'

RECORD SAINDX		; 
		.INCLUDE 'DEF:RD009X.DEF'

RECORD	SA_KEY
	SA_MAN	,D2
	SA_TER	,A2
	SA_CUS	,D6

RECORD	COPTBL
		.INCLUDE 'DEF:RD182A.DEF'

RECORD	TMPFIL
		,A4,	'SPL:'
		,A6,	'SAINDX'
		,A4,	'.ISM'

RECORD TITLE
		,A27,	'SALES ANALYSIS BY TERRITORY'
RECORD	HDR1
	,	A*,	'SALESMAN'
	,	A1
	HDR2_NO	,A2
	,	A1
	HDR2_NAME	,A25

RECORD HDR2
	,	A*,	'TR  ------------CUSTOMER------------     MTD SALES'
	,	A*,	'  %-SLS     YTD SALES  %-SLS'

RECORD	GR_CLEAR	;GRAND TOTALS
	GR_TMTD	,D10
	GR_TYTD	,D10
	GR_CNT	,D6

RECORD	SM_CLEAR	;SALESMAN TOTALS
	SM_TMTD	,D10
	SM_TYTD	,D10
	SM_CNT	,D6

RECORD	TR_CLEAR	;TERRITORY TOTALS
	TR_TMTD	,D10
	TR_TYTD	,D10
	TR_CNT	,D6

RECORD	CHANNEL
	CHN001	,D2
	CHN002	,D2
	CHN009	,D2
	CHN054	,D2
	CHN182	,D2

RECORD	PRINT
	LINFD	,D2
	LGNCTL	,D1
	LINCNT	,D2
	LPARG   ,D1
	LPONSW	,D1
	PRNTSW	,D1
	LPSW	,D1
	PGCNT	,D3
	PLINE	,A80
	PRTCNT	,D5			;AR29
	PRTTYP  ,A1
	RPTNUM  ,D3
	SPLFIL  ,A14
	HDR	,A6,	'NO HDR'
	LEG	,A9,	'NO LEGEND'

RECORD	VARS
	OPNOK	,D1
	EOF	,D1
	PCT	,D5
	STMAN	,D2
	ENMAN	,D2
	SAVMAN	,D2
	MASK1	,A14,	 'ZZ,ZZZ,ZZZ.XX-'
	MASK2	,A5,	'ZZZ.X'
	RECNO	,D5
	ENTRY	,A2
	INXCTL	,D1
	CNGCTL	,D1
	WHATNO	,D2
	READ	,D1,	0
	WRITE	,D1,	1
	LOKCTL	,D1
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	V = 1
	XCALL OUTPT (1,1,2,'NEW SALES ANALYSIS BY TERRITORY',V)
	XCALL WATE (4,V)

;========================================================
	CALL OPENS
	IF (.NOT. OPNOK) GOTO ALL_DONE

	CALL BUILD_INDEX
	XCALL OUTPT (2,1,0,'PRINT REPORT',1)
DISPLA,
	XCALL OUTPT (1,1,2,'NEW SALES ANALYSIS BY TERRITORY',V)
	XCALL OUTPT (2,1,0,'PRINT REPORT',1)
	XCALL OUTPT (4,4,0,'1. STARTING SALESMAN:',1)
	XCALL OUTPT (6,4,0,'2. ENDING   SALESMAN:',1)
STMAN,
	XCALL INPUT (4,27,02,00,'#E',ENTRY,INXCTL,1)
	GOTO(DISPLA,ENDOFF),INXCTL
	STMAN = ENTRY(1,2)
	IF(STMAN .EQ. 0)
		BEGIN
		XCALL OUTPT(4,27,1,'ALL',1)
		XCALL OUTPT(6,27,1,' ',1)
		ENMAN = 99
		GOTO ANYCNG
		END
	GOTO(ANYCNG),CNGCTL
ENMAN,
	XCALL INPUT(6,27,02,00,'#E',ENTRY,INXCTL,1)
	GOTO(DISPLA,ENDOFF),INXCTL
	ENMAN = ENTRY(1,2)
	IF(ENMAN .EQ. 0)
		BEGIN
		ENMAN = STMAN
		ENTRY(1,2) = ENMAN,'ZX' [LEFT]
		XCALL OUTPT(6,27,1,ENTRY(1,2),1)
		END
	GOTO(ANYCNG),CNGCTL

ANYCNG,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO(PROCES,CNGBR),CNGCTL+1
CNGBR,
	GOTO(STMAN,ENMAN),WHATNO
	GOTO ANYCNG
PROCES,
	CLEAR GR_CLEAR, SM_CLEAR, TR_CLEAR
	LINCNT = 66
	SAVMAN = -1
	EOF = 0
	CLEAR TBLKEY
	TBLCOD = 'TR'
	TR_SLSM = STMAN
	FIND (CHN182,COPTBL,TBL_KEY) [ERR=LOOP]

LOOP,
	LOKCTL = 1
	XCALL IOS (CHN182,COPTBL,READ,LOKCTL)
	IF (LOKCTL.NE.0 .OR. TBLCOD.NE.'TR') GOTO ALL_DONE
	IF (TR_SLSM.LT.STMAN)GOTO LOOP
	IF (TR_SLSM.GT.ENMAN)GOTO ALL_DONE

	IF (TR_SLSM .NE. SAVMAN) CALL NEWMAN
	CALL ACCUM_TERR				;ACCUMULATE TERR TOTALS
	CALL PRINT_TERR				;PRINT TERR DETAIL
	GOTO LOOP

ALL_DONE,
	EOF = 1
	CALL NEWMAN

	LINFD = 1
	CALL LINFD

	PLINE (4,9) = GR_CNT, 'ZZ,ZZX'
	PLINE (11,33) = 'CUSTOMERS FOR REPORT'
	CALL PRINT

	PCT = 1000
	PLINE (5,60) = 'REPORT TOTALS:'
	PLINE (38,51) = GR_TMTD		,MASK1
	PLINE (53,57) = PCT		,MASK2
	PLINE (59,72) = GR_TYTD		,MASK1
	PLINE (74,78) = PCT		,MASK2
	CALL PRINT
	IF(PRNTSW) CALL LPOFF
ENDOFF,
	CALL CLOSE
	XCALL PGCHN('AR:SASEL',1)
NEWMAN,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF(SAVMAN .EQ. -1) GOTO OUTMAN
	LINFD = 1
	CALL LINFD

	PLINE (4,9) = SM_CNT, 'ZZ,ZZX'
	PLINE (11,33) = 'CUSTOMERS FOR SALESMAN'
	PLINE (35,36) = SAVMAN,'ZX'
	CALL PRINT

	PCT = 1000
	PLINE (5,60) = 'SALESMAN XX TOTALS:'
	PLINE (14,15) = SAVMAN,'ZX' [LEFT]
	PLINE (38,51) = SM_TMTD		,MASK1
	PLINE (53,57) = PCT		,MASK2
	PLINE (59,72) = SM_TYTD		,MASK1
	PLINE (74,78) = PCT		,MASK2
	CALL PRINT

	XCALL FILL ('=',PLINE)
	CALL PRINT

	LINFD = 1
	CALL LINFD

	GR_TMTD = GR_TMTD + SM_TMTD
	GR_TYTD = GR_TYTD + SM_TYTD
	GR_CNT = GR_CNT + SM_CNT
OUTMAN,
	SAVMAN = TR_SLSM
	CLEAR SM_CLEAR
	RECNO = TR_SLSM
	IF(RECNO.LT.1 .OR. RECNO.GT.99) RECNO = 1
	XCALL IO (CHN054,SALMAN,RECNO,READ,LOKCTL)
	IF(LOKCTL) CLEAR SALMAN
	IF (.NOT. EOF) 
		BEGIN
		HDR2_NO = TR_SLSM,	'ZX'
		HDR2_NAME = SLSNM
		LINCNT = 66
		END
	RETURN
;--------------------------------------------------------
;========================================================
;--------------------------------------------------------
ACCUM_TERR,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		;;; GET TOTAL MTD & YTD SALES FOR TERR.
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR TR_CLEAR

	CLEAR SA_KEY
	SA_MAN = 
;;;	SA_MAN = TR_SLSM
	SA_TER = TR_TERR
	FIND (CHN009, SAINDX, SA_KEY) [ERR=ACM_LOOP]
ACM_LOOP,
	LOKCTL = 1
	XCALL IOS (CHN009, SAINDX, READ, LOKCTL)
	IF (LOKCTL .NE. 0) GOTO ACM_EOF
;;;	IF (SAINDX(1,4) .NE. SA_KEY(1,4)) GOTO ACM_EOF
	IF (SASTAT .NE. TR_TERR) GOTO ACM_EOF
	
	TR_TMTD = TR_TMTD + C_SALMTD
	TR_TYTD = TR_TYTD + C_SALYTD
	GOTO ACM_LOOP
ACM_EOF,
	RETURN
;--------------------------------------------------------
PRINT_TERR,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		;;; PRINT ALL CUSTOMERS IN CURRENT TERR.
		;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CLEAR SA_KEY
	SA_MAN = 
;;;	SA_MAN = TR_SLSM
	SA_TER = TR_TERR
	FIND (CHN009, SAINDX, SA_KEY) [ERR=PRT_LOOP]
PRT_LOOP,
	LOKCTL = 1
	XCALL IOS (CHN009, SAINDX, READ, LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF_PRT
;;;	IF (SAINDX(1,4) .NE. SA_KEY(1,4)) GOTO EOF_PRT
	IF (SASTAT .NE. TR_TERR) GOTO EOF_PRT

	PLINE (1,2) = TR_TERR
	PLINE (5,10) = SACUNO
	PLINE (12,36) = C_NAME
	PLINE (38,51) = C_SALMTD	,MASK1

	IF(TR_TMTD.EQ.0)
	THEN	PCT = 0
	ELSE	PCT = (C_SALMTD*10000/TR_TMTD)#1

	PLINE (53,57) = PCT		,MASK2
	PLINE (59,72) = C_SALYTD	,MASK1

	IF(TR_TYTD.EQ.0)
	THEN	PCT = 0
	ELSE	PCT = (C_SALYTD*10000/TR_TYTD)#1

	PLINE (74,78) = PCT		,MASK2

	CALL PRINT
	INCR TR_CNT
	GOTO PRT_LOOP

EOF_PRT,
	LINFD = 1
	CALL LINFD

	PLINE (4,9) = TR_CNT, 'ZZ,ZZX'
	PLINE (11,33) = 'CUSTOMERS FOR TERRITORY'
	PLINE (35,36) = TR_TERR
	CALL PRINT

	PCT = 1000
	PLINE (5,60) = 'TERRITORY XX TOTALS:'
	PLINE (15,16) = TR_TERR
	PLINE (38,51) = TR_TMTD		,MASK1
	PLINE (53,57) = PCT		,MASK2
	PLINE (59,72) = TR_TYTD		,MASK1
	PLINE (74,78) = PCT		,MASK2
	CALL PRINT

	XCALL FILL ('-',PLINE)
	CALL PRINT
	LINFD = 1
	CALL LINFD

	SM_TMTD = SM_TMTD + TR_TMTD
	SM_TYTD = SM_TYTD + TR_TYTD
	SM_CNT = SM_CNT + TR_CNT
	LINCNT = 66			;NEW PAGE
	RETURN
;--------------------------------------------------------

BUILD_INDEX,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	XCALL OUTPT (2,1,0,'BUILD INDEX',1)
	LOKCTL = 1
	CUSNO = 0
	XCALL ISIO (1,CUSMAS,CUSNO,READ,LOKCTL)	;HEADER
READ,
	LOKCTL = 1
	XCALL IOS (1,CUSMAS,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOF1
;;;	IF (CUSMAS.EQ.']]]]]]') GO TO EOF1
	INCR RECNO
	IF (NAME.EQ.']]]DEL') GO TO READ
	IF (CUSNO .EQ. 63340) GOTO READ		;ROCKFORD 2-10-04

	SASTAT = TERR
	SASLMN =		;DON'T CARE SALESMAN # IN CUSMAS
;;;	SASLMN = SLSMAN
	SACUNO = CUSNO
	C_NAME = NAME
	C_SALMTD = SALMTD
	C_SALYTD = SALYTD
	C_COSMTD = COSMTD
	C_COSYTD = COSYTD
	LOKCTL = 1
	STORE (3,SAINDX, SAINDX(1,10) )
;;;	XCALL ISIO (3,SAINDX,SAINDX(1,10),STORE,LOKCTL)
	GO TO READ

EOF1,
;;;	CLOSE CHN009
	RETURN
;-----------------------------------------------


PRINT,
	IF (PRNTSW.EQ.0) CALL LPON
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,HDR,
&		LEG,LEG,LEG,0,080,080,0,LPSW,RPTNUM,PRTTYP)
	RETURN
LINFD,
	XCALL LINFD (LINFD)
	LINCNT = LINCNT + LINFD
	RETURN
LPON,
	LPSW = 1
	SPLFIL (5,6) = 'AR'
	XCALL AR_LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTSW = 1
	RETURN
LPOFF,
	XCALL AR_LPOFF (LPSW,SPLFIL,PGCNT)
	PRNTSW =
	RETURN

OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLEAR OPNOK

;Create work file...
	XCALL ISAMC (TMPFIL, 75, 1, 'START=1, LENGTH=10, DUPS, ASCEND')
	OPEN (3, SU, TMPFIL)
	CHN009 = 3

;;;	SWITCH = 1
;;;	XCALL FILES (2,'I',02,SWITCH)		;FILE # 02 -- CUSIDX FILE
;;;	IF (SWITCH.EQ.9) RETURN
;;;	CHN002 = 2

	XCALL FILES (1,'SI',01,SWITCH)		;FILE # 01 -- CUSMAS FILE
	IF (SWITCH.EQ.9) RETURN
	CHN001 = 1

	SWITCH = 5
	XCALL FILES (4,'SI',182,SWITCH)
	IF(SWITCH.EQ.9) RETURN
	CHN182 = 4

	XCALL FILES (5,'I',054,SWITCH)
	IF(SWITCH.EQ.9) RETURN
	CHN054 = 5
	XCALL OUTPT (12,1,1,'\',V)
;;;	LOKCTL = 1
;;;	XCALL IO (1,CUSMAS,1,READ,LOKCTL)

	OPNOK = 1
	RETURN
;------------------------------------------------

CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	IF (CHN001)
		BEGIN
		XCALL FILES (CHN001, 'I', 001, 4)
	;;;	XCALL FILES (CHN002, 'I', 002, 4)
		END

	CLOSE CHN009
	CLOSE CHN182
	XCALL DELET (TMPFIL)
	RETURN
;-------------------------------------------------

END

;TR  ------------CUSTOMER------------     MTD SALES  %-SLS     YTD SALES  %-SLS
;AA  ZZZZZX AAAAAAAAAAAAAAAAAAAAAAAAA ZZ,ZZZ,ZZX.XX- ZZZ.X ZZ,ZZZ,ZZX.XX- ZZZ.X
;12345678901234567890123456789012345678901234567890123456789012345678901234567890
;         1         2         3         4         5         6         7
