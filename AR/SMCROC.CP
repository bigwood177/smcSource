;SMPROC.CP
;
;	SMC/ROCKFORD SALES
;
RECORD	ORDHDR
	.INCLUDE 'DEF:RD044A.DEF'

RECORD	ORDLIN
	.INCLUDE 'DEF:RD045A.DEF'
RECORD,X
	.INCLUDE 'DEF:RD045D.DEF'

RECORD	TOTALS
	S_SALES		,12D10	;SMC SALES BY MONTH - EXCLUDING 63340
	SR_SALES	,12D10	;SMC SALES TO ROCKFORD (63340)
	R_SALES		,12D10	;ROCKFORD SALES

RECORD	HD1
    	,A*,	'     ---MINNESOTA SALES---    ---ROCKFORD SALES---'
	,A*,	'   -ROCKFORD PURCHASES--   ----ROCKFORD NET---'

RECORD	HD2
	,A*,	'           MTD        YTD          MTD        YTD'
	,A*,	'          MTD        YTD         MTD        YTD'


RECORD	PRINT
	TITLE	,A*,	'SMC/ROCKFORD SALES SUMMARY'
	LG	,A9,	'NO LEGEND'
	HD	,A6,	'NO HDR'
	LINCNT	,D6
	LPSW	,D1
	SPLFIL	,A14
	LPONSW	,D1
	LPARG	,D1
	RPTNUM	,D3
	PRTTYP	,A1
	PGCNT	,D6,	000000
	PLINE	,A132
	PRTCTL	,D3


RECORD	DIS
	II	,D6

RECORD	CHANNEL
	CHNSLN	,D2	;SLHLIN.SMC
	CHNSHD	,D2	;SLHHDR.SMC
	CHNRLN	,D2	;SLHLIN.ROC
	CHNRHD	,D2	;SLHHDR.ROC

RECORD	VARS
	NUMCOP	,D2	;SSQ 9-12-06
	AMT	,D12
	OPNOK	,D1
	MONTH	,12A3,	'JAN','FEB','MAR','APR','MAY','JUN','JUL','AUG',
&			'SEP','OCT','NOV','DEC'
	CHNLIN	,D2
	ENTRY	,A30
	INXCTL	,D1
	CNGCTL	,D1
	WHATNO	,D2
	STDAT	,D8
	MASK	,A10,	'ZZ,ZZZ,ZZX'
	SMC	,D10
	SMCROC	,D10
	SUM1	,D10
	SUM2	,D10
	SUM3	,D10
	SUMR1	,D10
	SUMR2	,D10
	SUMR3	,D10
	CURM	,D2
	MN	,D2
	I	,D2
	TODAY	,D8
	SWITCH	,D1
	V	,D1
;
PROC
	XCALL TERID(V)
	XCALL OUTPT (1,1,2,'SMC/ROCKFORD SALES SUMMARY',1)
	
	CALL OPENS
	IF (.NOT. OPNOK) GOTO ENDOFF

	XCALL RDATE(TODAY)

DISPLA,
	XCALL OUTPT (1,1,2,'SMC/ROCKFORD SALES SUMMARY',1)
	XCALL OUTPT (5,5,0,'MONTH ENDING DATE:',1)
	XCALL INPUT (5,24,08,00,'DE',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	STDAT = ENTRY(1,8)

	CNGCTL = 2
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (DISPLA),CNGCTL

	CURM = STDAT(5,6)		;MAY

	CHNLIN = CHNSLN		;SMC
	CLEAR II
	DISPLAY(15,$SCR_POS(1,65), 'SMC:')
	FIND (4, ORDHDR, ^FIRST) [ERR=LOOP]
LOOP,
	INCR II
	IF (II/200*200 .EQ. II) DISPLAY(15,$SCR_POS(1,70),DIS)

	READS (4, ORDHDR, EOF)
	MN = OINVDT(5,6)

	CALL LINES

	S_SALES(MN) = S_SALES(MN) + SMC
	SR_SALES(MN) = SR_SALES(MN) + SMCROC

	GOTO LOOP

EOF,
	CHNLIN = CHNRLN		;ROC
	CLEAR II
	DISPLAY(15,$SCR_POS(2,65), 'ROC:')
	FIND (24, ORDHDR, ^FIRST) [ERR=LOOP2]
LOOP2,
	INCR II
	IF (II/200*200 .EQ. II) DISPLAY(15,$SCR_POS(2,70),DIS)

	READS (24, ORDHDR, EOF2)
	MN = OINVDT(5,6)

	CALL LINES

	R_SALES(MN) = R_SALES(MN) + SMC
	
	GOTO LOOP2
	
EOF2,

	PLINE = TITLE
	CALL PRINT
	CALL PRINT

	PLINE = HD1
	CALL PRINT

	PLINE = HD2
	CALL PRINT


	CLEAR SUM1, SUM2
	FOR I FROM 1 THRU CURM
		BEGIN
		SUM1 = SUM1 + S_SALES(I)#2		;SMC ONLY SALES
		SUM2 = SUM2 + SR_SALES(I)#2		;SALES TO ROCK
		SUM3 = S_SALES(I)#2 + SR_SALES(I)#2

		SUMR1 = SUMR1 + R_SALES(I)#2
		SUMR2 = SUMR2 + SR_SALES(I)#2
		SUMR3 = R_SALES(I)#2 - SR_SALES(I)#2

		PLINE(1,3) = MONTH(I)			;MONTH

		PLINE(4,14) = S_SALES(I)#2,	MASK	;SMC - MTD
		PLINE(16,25) = SUM1,		MASK	;SMC - YTD

		PLINE(29,38) = R_SALES(I)#2,	MASK	;ROC - MTD
		PLINE(40,49) = SUMR1,		MASK	;ROC - YTD

		PLINE(53,62) = SR_SALES(I)#2,	MASK	;ROC PURCHASES MTD
		PLINE(64,73) = SUMR2,		MASK	;ROC PURCHASES YTD

		PLINE(76,85) = SUMR3,		MASK	;ROC NET MTD
		PLINE(87,96) = (SUMR1 - SUMR2),	MASK	;ROC NET YTD

		CALL PRINT

		END


;    ---MINNESOTA SALES---    ---ROCKFORD SALES---   --ROCKFOR PURCHASES--
;           MTD        YTD          MTD        YTD          MTD        YTD
;AAA ZZ,ZZZ,ZZX ZZ,ZZZ,ZZX   ZZ,ZZZ,ZZX ZZ,ZZZ,ZZX   ZZ,ZZZ,ZZX ZZ,ZZZ,ZZX
;1234567890123456789012345678901234567890123456789012345678901234567890123
;         1         2         3         4         5         6         7         

;   -----ROCKFOR NET-----
;   ZZ,ZZZ,ZZX ZZ,ZZZ,ZZX
;456789012345678901234567890
;         8         9         

ENDOFF,

	IF (LPONSW) XCALL LPOFF (LPSW,SPLFIL,PGCNT,NUMCOP)
;;;	IF (LPONSW.EQ.1) XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	CALL CLOSE


	XCALL PGCHN ('CP:COPRPT',1)
	STOP

;=====================================================================
;=====================================================================

LINES,
	clear smc, smcroc

	AMT = OSALE ;;;>- OTAX(1) - OTAX(2) - OTAX(3) - OFRGHT
	IF (OCUSNO .EQ. 63340)
	THEN	SMCROC = AMT
	ELSE	SMC = AMT

	RETURN
;---------------------------------------------------
	CLEAR SMC, SMCROC
	FIND (CHNLIN, ORDLIN, OORDNO) [ERR=L_LOOP]

L_LOOP,
	READS (CHNLIN, ORDLIN, L_EOF)
	IF (LORDNO .NE. OORDNO) GOTO L_EOF
	IF (LTYPE .EQ. 'M') GOTO L_LOOP

	IF (OCUSNO .EQ. 63340)
	THEN	SMCROC = SMCROC + (LQTYSH*LPRICE)#1
	ELSE	SMC = SMC + (LQTYSH*LPRICE)#1
	GOTO L_LOOP
L_EOF,

	RETURN
;---------------------------------------

PRINT,
	IF (LPONSW.EQ.0) CALL PRNTON
	PRTCTL = 132
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HD1,HD2,HD,
&			'NO LEGEND',' ',' ',1,132,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
PRNTON,
	SPLFIL (5,6) = 'EL'
	LPSW = 1	; PRINT, SPOOL OR DISPLAY

	XCALL LPON (LPSW,SPLFIL,,,,NUMCOP)
;;;	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) CALL CLOSE
	IF (LPSW.EQ.0) XCALL PGCHN ('CP:CPMENU',1)
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	LPONSW = 1
	RETURN

OPENS,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CLEAR OPNOK

	OPEN (4, SI, 'SMC:SLHHDR.SMM')
	CHNSHD = 4

	OPEN (5, SI, 'SMC:SLHLIN.SMM')
	CHNSLN = 5

	OPEN (24,SI, 'ROC:SLHHDR.ROM')
	CHNRHD = 24

	OPEN (25,SI, 'ROC:SLHLIN.ROM')
	CHNRLN = 25

;;;	OPEN (14,O,'SPL:SMCROC.SPL')

	OPNOK = 1

	RETURN
;-------------------------------------------------

CLOSE,	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	IF (CHNSHD) CLOSE CHNSHD
	IF (CHNSLN) CLOSE CHNSLN
	IF (CHNRHD) CLOSE CHNRHD
	IF (CHNRLN) CLOSE CHNRLN

	RETURN
;-------------------------------------------------

